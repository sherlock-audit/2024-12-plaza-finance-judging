Shaggy Clay Cobra

High

# Bond holders can not redeem as expected

### Summary

The arithmetic underflow happens in collateral level calculation can cause the bond holders unable to redeem as expected

### Root Cause

The function [getRedeemAmount()](https://github.com/sherlock-audit/2024-12-plaza-finance/blob/main/plaza-evm/src/Pool.sol#L477-L525) computes the amount of reserve token to be redeemed by the users. 
```solidity
  function getRedeemAmount(
    TokenType tokenType,
    uint256 depositAmount,
    uint256 bondSupply,
    uint256 levSupply,
    uint256 poolReserves,
    uint256 ethPrice,
    uint8 oracleDecimals,
    uint256 marketRate
  ) public pure returns(uint256) {
    if (bondSupply == 0) {
      revert ZeroDebtSupply();
    }

    uint256 tvl = (ethPrice * poolReserves).toBaseUnit(oracleDecimals);
    uint256 assetSupply = bondSupply;
    uint256 multiplier = POINT_EIGHT;

    // Calculate the collateral level based on the token type
    uint256 collateralLevel;
    if (tokenType == TokenType.BOND) {
@>      collateralLevel = ((tvl - (depositAmount * BOND_TARGET_PRICE)) * PRECISION) / ((bondSupply - depositAmount) * BOND_TARGET_PRICE);
    } else {
      multiplier = POINT_TWO;
      assetSupply = levSupply;
      collateralLevel = (tvl * PRECISION) / (bondSupply * BOND_TARGET_PRICE);

      if (assetSupply == 0) {
        revert ZeroLeverageSupply();
      }
    }
    
    // Calculate the redeem rate based on the collateral level and token type
    uint256 redeemRate;
    if (collateralLevel <= COLLATERAL_THRESHOLD) {
      redeemRate = ((tvl * multiplier) / assetSupply);
    } else if (tokenType == TokenType.LEVERAGE) {
      redeemRate = ((tvl - (bondSupply * BOND_TARGET_PRICE)) / assetSupply) * PRECISION;
    } else {
      redeemRate = BOND_TARGET_PRICE * PRECISION;
    }

    if (marketRate != 0 && marketRate < redeemRate) {
      redeemRate = marketRate;
    }
    
    // Calculate and return the final redeem amount
    return ((depositAmount * redeemRate).fromBaseUnit(oracleDecimals) / ethPrice) / PRECISION;
  }
```
The problem arises when `tvl < depositAmount * BOND_TARGET_PRICE`, meaning that TVL is not enough for the redeem given that each Bond token priced `BOND_TARGET_PRICE` (the pricing is wrongly assumed here). In such case, it will be underflow when calculating `collateralLevel`

### Internal Pre-conditions

_No response_

### External Pre-conditions

_No response_

### Attack Path

_No response_

### Impact

- Bond holders can not redeem as expected, especially when reserve token price decreases

### PoC

Add the test to `Pool.t.sol`:

```solidity
  function testRedeem_LowTVL() public {
    // @audit poc: under collateral level cause underflow
    vm.startPrank(governance);
    Token rToken = Token(params.reserveToken);

    // Mint reserve tokens
    rToken.mint(governance, 1e18);
    rToken.approve(address(poolFactory), 1e18);


    // assumed 1 ETH ~ 3000 USD
    // 1 ETH is reserved
    // total 35 BondETH is in supply. 1 BondETH is expected to 100 USD (BOND_TARGET_PRICE)
    // redeem more than 30 bondETH will exceed TVL

    // Create pool and approve deposit amount
    Pool _pool = Pool(poolFactory.createPool(params, 1e18, 35e18, 1e18, "", "", "", "", false));
    rToken.approve(address(_pool), 35e18);

    uint256 amount = _pool.redeem(Pool.TokenType.BOND, 33e18, 33);
  }
```

Test failed
```bash
Failing tests:
Encountered 1 failing test in test/Pool.t.sol:PoolTest
[FAIL: panic: arithmetic underflow or overflow (0x11)] testRedeem_LowTVL() (gas: 1938834)
```

### Mitigation

Take the case when `tvl < depositAmount * BOND_TARGET_PRICE` into account when calculating collateral level