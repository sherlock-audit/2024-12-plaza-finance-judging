Itchy Wintergreen Newt

High

# Incorrect period used in `transferReserveToAuction` leads to complete failure of action system and loss of bidders' funds.

### Summary

A critical flaw in the Pool contract's `transferReserveToAuction` function causes all auctions to fail by checking auctions mapping against the wrong period number. The function uses the current period instead of the previous period to verify the auction's address, making it impossible for any auction to successfully complete as the authorization check will always fail.

### Root Cause

In [Pool::startAuction](https://github.com/sherlock-audit/2024-12-plaza-finance/blob/main/plaza-evm/src/Pool.sol#L530), the auction started is stored in the auctions[N] mapping but the function also calls [increaseIndexedAssetPeriod()](https://github.com/sherlock-audit/2024-12-plaza-finance/blob/main/plaza-evm/src/Pool.sol#L567C15-L567C41) which increments the global pool's currentPeriod to N + 1 in the [BondToken.sol:L225](https://github.com/sherlock-audit/2024-12-plaza-finance/blob/main/plaza-evm/src/BondToken.sol#L225)
```solidity
// Pool.sol
function startAuction() external whenNotPaused() { 
// ...
 @>   (uint256 currentPeriod,) = bondToken.globalPool();
          require(auctions[currentPeriod] == address(0), AuctionAlreadyStarted());
// ...
@>    auctions[currentPeriod] = Utils.deploy(
           address(new Auction()),
           abi.encodeWithSelector(
           Auction.initialize.selector,
// ...

@>    bondToken.increaseIndexedAssetPeriod(sharesPerToken);
```
```solidity
// BondToken.sol
function increaseIndexedAssetPeriod(uint256 sharesPerToken) ...
// ...
@> globalPool.currentPeriod++;
// ...
```

This means that an auction can never be ended because when [Auction::endAuction()](https://github.com/sherlock-audit/2024-12-plaza-finance/blob/main/plaza-evm/src/Auction.sol#L336) function is called it always REVERTS because the endAuction function makes a call to the Pool's transferReserveToAuction function which has the issue explained below;

[Pool::transferReserveToAuction](https://github.com/sherlock-audit/2024-12-plaza-finance/blob/main/plaza-evm/src/Pool.sol#L577) function uses the current period (N + 1) from BondToken's globalPool to verify the auction address, but this period is the  the incremented period and not the period which corresponds to the auction. This means the verification is performed against `auctions[N + 1]` which will always be empty i.e `address(0)`, while the actual auction contract was registered in `auctions[N]`.
```solidity
function transferReserveToAuction(uint256 amount) external virtual {
@>    (uint256 currentPeriod, ) = bondToken.globalPool();
         address auctionAddress = auctions[currentPeriod];
// @audit this will always revert with CallerIsNotAuction()
         require(msg.sender == auctionAddress, CallerIsNotAuction());
```

### Internal Pre-conditions
1. A pool must be created and initialized
2. An auction must be started via `startAuction()`
3. The auction must receive valid bids
4. The auction period must end
5. `endAuction()` must be called

### External Pre-conditions

_No response_

### Attack Path
1. Pool is created and auction starts for period N
2. `startAuction()` deploys new auction contract and stores it in `auctions[N]`
3. `startAuction()` calls `bondToken.increaseIndexedAssetPeriod()` which increments period to N+1
4. When auction ends, `endAuction()` is called which triggers `transferReserveToAuction()`
5. `transferReserveToAuction()` checks `auctions[N+1]` which is address(0)
6. The require check `msg.sender == auctionAddress` fails as auction contract address â‰  address(0)
7. Transaction reverts, making it impossible to complete any auction

### Impact
1. This issue completely breaks the core auction functionality of the protocol. No auctions can ever complete successfully because reserve tokens cannot be transferred to successful auctions.
2. Bidders cannot receive their reserveTokens they have won in the auction hence loss of funds.
3. The entire coupon token distribution mechanism becomes unusable.

### PoC
```solidity
// SPDX-License-Identifier: UNLICENSED
pragma solidity ^0.8.26;

import "forge-std/Test.sol";
import {Create3} from "@create3/contracts/Create3.sol";
import {PoolFactory} from "src/PoolFactory.sol";
import {Utils} from "src/lib/Utils.sol";
import {Deployer} from "src/utils/Deployer.sol";
import {BondToken} from "src/BondToken.sol";
import {PoolFactory} from "src/PoolFactory.sol";
import {Distributor} from "src/Distributor.sol";
import {LeverageToken} from "src/LeverageToken.sol";
import {Pool} from "src/Pool.sol";
import {UpgradeableBeacon} from "@openzeppelin/contracts/proxy/beacon/UpgradeableBeacon.sol";
import {IERC20} from "@openzeppelin/contracts/token/ERC20/ERC20.sol";
import {MockERC20} from "./MockERC20.sol";
import {OracleFeeds} from "src/OracleFeeds.sol";
import {MockPriceFeed} from "./mocks/MockPriceFeed.sol";
import {OracleFeeds} from "src/OracleFeeds.sol";
import {Auction} from "src/Auction.sol";

contract MyAuctionTest is Test {
    PoolFactory private poolFactory;
    PoolFactory.PoolParams private params;
    Pool private pool;
    MockPriceFeed private mockPriceFeed;
    OracleFeeds private oracleFeeds;

    address private minter = address(0x2);
    address private governance = address(0x3);
    address private securityCouncil = address(0x4);
    address private alice = address(0x5);
    address private bob = address(0x6);
    address private feeBeneficiary = address(0x7);
    address public constant ethPriceFeed = address(0x71041dddad3595F9CEd3DcCFBe3D1F4b0a16Bb70);
    address public bondPriceFeed = makeAddr("bond_price_feed");
    MockPriceFeed private bondMockPriceFeed;
    uint256 private constant distributionPeriod = 30 days;
    uint256 private constant auctionPeriod = 10 days;

    function setUp() public {
        params.fee = 0;
        params.reserveToken = address(new MockERC20("Wrapped ETH", "WETH"));
        params.distributionPeriod = distributionPeriod;
        params.couponToken = address(new MockERC20("USDC", "USDC"));
        params.sharesPerToken = 1e6;
        params.feeBeneficiary = feeBeneficiary;

        oracleFeeds = new OracleFeeds();
        address contractDeployer = address(new Deployer());

        address poolBeacon = address(new UpgradeableBeacon(address(new Pool()), governance));
        address bondBeacon = address(new UpgradeableBeacon(address(new BondToken()), governance));
        address levBeacon = address(new UpgradeableBeacon(address(new LeverageToken()), governance));
        address distributorBeacon = address(new UpgradeableBeacon(address(new Distributor()), governance));

        poolFactory = PoolFactory(
            Utils.deploy(
                address(new PoolFactory()),
                abi.encodeWithSelector(
                    PoolFactory.initialize.selector,
                    governance,
                    contractDeployer,
                    address(oracleFeeds),
                    poolBeacon,
                    bondBeacon,
                    levBeacon,
                    distributorBeacon
                )
            )
        );

        oracleFeeds.setPriceFeed(params.reserveToken, address(0), ethPriceFeed, 1 days);
        mockPriceFeed = new MockPriceFeed();
        bytes memory bytecode = address(mockPriceFeed).code;
        vm.etch(ethPriceFeed, bytecode);
        mockPriceFeed = MockPriceFeed(ethPriceFeed);

        deal(params.reserveToken, governance, 100e18, true);
        deal(params.reserveToken, alice, 10e18, true);
        deal(params.reserveToken, bob, 10e18, true);
        deal(params.couponToken, alice, 100e18, true);

        vm.startPrank(governance);
        // granting roles
        poolFactory.grantRole(poolFactory.POOL_ROLE(), governance);
        poolFactory.grantRole(poolFactory.SECURITY_COUNCIL_ROLE(), securityCouncil);

        // creating the pool with 100 WETH, and will be minted 10 of bondEth and LevEth each
        IERC20(params.reserveToken).approve(address(poolFactory), 100e18);
        pool = Pool(poolFactory.createPool(params, 100e18, 10e18, 10e18, "Bond ETH", "BE", "Lev ETH", "LE", false));
        vm.stopPrank();
    }

    function testTransferReserveToAuction() public {
        vm.prank(governance);
        // governance has GOV_ROLE to set the auctionPeriod
        pool.setAuctionPeriod(auctionPeriod);

        vm.warp(block.timestamp + distributionPeriod + auctionPeriod - 1);
        pool.startAuction();

        (uint256 currentPeriod,) = pool.bondToken().globalPool();
        // this is the address of the auction we started earlier
        address auction = pool.auctions(currentPeriod - 1);
        // get the whole coupon amt that the auction needs from bidders.
        uint256 bidAmt = Auction(auction).totalBuyCouponAmount();

        vm.startPrank(alice);
        IERC20(params.couponToken).approve(address(auction), bidAmt);
        // alice bids the whole the couponAmt to be distributed
        Auction(auction).bid(0.05 * 1e18, bidAmt);
        vm.stopPrank();

        vm.warp(block.timestamp + auctionPeriod);
        // any one can call endAuction when auction period finishes
        vm.expectRevert(Pool.CallerIsNotAuction.selector);
        Auction(auction).endAuction();
        // it always reverts
    }
}
```

### Mitigation
```diff
function transferReserveToAuction(uint256 amount) external virtual {
      (uint256 currentPeriod, ) = bondToken.globalPool();
-    address auctionAddress = auctions[currentPeriod];
+    address auctionAddress = auctions[currentPeriod - 1];
      require(msg.sender == auctionAddress, CallerIsNotAuction());
    
      IERC20(reserveToken).safeTransfer(msg.sender, amount);
  }
```