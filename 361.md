Itchy Wintergreen Newt

High

# Incorrect Redemption Rate Applied to Leverage Tokens Due to Unconstrained Market Rate Check

### Summary
The Pool contract contains a logic flaw in its redemption rate calculation that can cause users to lose a huge portion of their funds when redeeming leverage tokens. The issue occurs because the contract unconditionally applies the bond token's market rate to leverage token redemptions when it should only apply to bond token redemptions.

### Root Cause
The marketRate is for bondTokens as seen in `simulateRedeem` function, In [Pool.sol:L446](https://github.com/sherlock-audit/2024-12-plaza-finance/blob/main/plaza-evm/src/Pool.sol#L446), the marketRate gets oracle price for bondToken i.e `marketRate = getOraclePrice(address(bondToken), USD).no...`

BUT in contrast,

In [Pool.sol:L519](https://github.com/sherlock-audit/2024-12-plaza-finance/blob/main/plaza-evm/src/Pool.sol#L519), the market rate check is incorrectly applied to both bond and leverage token redemptions:

```solidity
function getRedeemAmount(...) {
// ... snip ...
    // @audit both token type redemptions i.e LEVERAGE and BOND go through the check below.
    if (marketRate != 0 && marketRate < redeemRate) {
        redeemRate = marketRate;
   }
```
This causes leverage token holders to receive funds based on the bond token's market rate rather than the leverage token's calculated redemption rate, resulting in significant losses.


### Internal Pre-conditions
1. A pool must be created with initial reserve tokens, bond tokens and leverage tokens
2. The bond token must have an oracle price feed configured.
3. The bond token's market rate must be lower than the calculated redemption rate which is mostly always the case when dealing with leverage tokens.

### External Pre-conditions

_No response_

### Attack Path
1. User deposits 10 ETH worth $2000 each (total $20,000) to mint leverage tokens
2. User attempts to redeem their leverage tokens
3. Due to the incorrect market rate check, the redemption uses the bond token's market rate ($100) instead of the proper leverage token redemption rate
4. User receives only ~5 ETH worth $10,000 back, losing almost 50% of their funds

### Impact
Users redeeming leverage tokens can lose a huge portion of their deposited funds due to the incorrect application of the bond token's market rate to leverage token redemptions.

### PoC
put the file in test folder and run `forge test` on it
```solidity
// SPDX-License-Identifier: UNLICENSED
pragma solidity ^0.8.26;

import "forge-std/Test.sol";
import {Create3} from "@create3/contracts/Create3.sol";
import {PoolFactory} from "src/PoolFactory.sol";
import {Utils} from "src/lib/Utils.sol";
import {Deployer} from "src/utils/Deployer.sol";
import {BondToken} from "src/BondToken.sol";
import {PoolFactory} from "src/PoolFactory.sol";
import {Distributor} from "src/Distributor.sol";
import {LeverageToken} from "src/LeverageToken.sol";
import {Pool} from "src/Pool.sol";
import {UpgradeableBeacon} from "@openzeppelin/contracts/proxy/beacon/UpgradeableBeacon.sol";
import {IERC20} from "@openzeppelin/contracts/token/ERC20/ERC20.sol";
import {MockERC20} from "./MockERC20.sol";
import {OracleFeeds} from "src/OracleFeeds.sol";
import {MockPriceFeed} from "./mocks/MockPriceFeed.sol";
import {OracleFeeds} from "src/OracleFeeds.sol";

contract CoolTest is Test {
    PoolFactory private poolFactory;
    PoolFactory.PoolParams private params;
    Pool private pool;
    MockPriceFeed private mockPriceFeed;
    OracleFeeds private oracleFeeds;

    address private minter = address(0x2);
    address private governance = address(0x3);
    address private securityCouncil = address(0x4);
    address private alice = address(0x5);
    address private bob = address(0x6);
    address private feeBeneficiary = address(0x7);
    address public constant ethPriceFeed = address(0x71041dddad3595F9CEd3DcCFBe3D1F4b0a16Bb70);
    address public bondPriceFeed = makeAddr("bond_price_feed");
    MockPriceFeed private bondMockPriceFeed;

    function setUp() public {
        params.fee = 0;
        params.reserveToken = address(new MockERC20("Wrapped ETH", "WETH"));
        params.distributionPeriod = 0;
        params.couponToken = address(new MockERC20("USDC", "USDC"));
        // params.sharesPerToken = 50 * 10 ** 18;
        params.feeBeneficiary = feeBeneficiary;

        oracleFeeds = new OracleFeeds();
        address contractDeployer = address(new Deployer());

        address poolBeacon = address(new UpgradeableBeacon(address(new Pool()), governance));
        address bondBeacon = address(new UpgradeableBeacon(address(new BondToken()), governance));
        address levBeacon = address(new UpgradeableBeacon(address(new LeverageToken()), governance));
        address distributorBeacon = address(new UpgradeableBeacon(address(new Distributor()), governance));

        poolFactory = PoolFactory(
            Utils.deploy(
                address(new PoolFactory()),
                abi.encodeWithSelector(
                    PoolFactory.initialize.selector,
                    governance,
                    contractDeployer,
                    address(oracleFeeds),
                    poolBeacon,
                    bondBeacon,
                    levBeacon,
                    distributorBeacon
                )
            )
        );

        oracleFeeds.setPriceFeed(params.reserveToken, address(0), ethPriceFeed, 1 days);
        mockPriceFeed = new MockPriceFeed();
        bytes memory bytecode = address(mockPriceFeed).code;
        vm.etch(ethPriceFeed, bytecode);
        mockPriceFeed = MockPriceFeed(ethPriceFeed);

        deal(params.reserveToken, governance, 100e18, true);
        deal(params.reserveToken, alice, 10e18, true);
        deal(params.reserveToken, bob, 10e18, true);

        vm.startPrank(governance);
        // granting roles
        poolFactory.grantRole(poolFactory.POOL_ROLE(), governance);
        poolFactory.grantRole(poolFactory.SECURITY_COUNCIL_ROLE(), securityCouncil);

        // creating the pool with 100 WETH, and will be minted 10 of bondEth and LevEth each
        IERC20(params.reserveToken).approve(address(poolFactory), 100e18);
        pool = Pool(poolFactory.createPool(params, 100e18, 10e18, 10e18, "Bond ETH", "BE", "Lev ETH", "LE", false));
        vm.stopPrank();

        // we are using a mock bondPriceFeed
        oracleFeeds.setPriceFeed(address(pool.bondToken()), address(0), bondPriceFeed, 1 days);
        bondMockPriceFeed = new MockPriceFeed();
        bytes memory bondBytecode = address(bondMockPriceFeed).code;
        vm.etch(bondPriceFeed, bondBytecode);
        bondMockPriceFeed = MockPriceFeed(bondPriceFeed);
    }

    function testRateForLevToken() public {
        // set WETH price to 2000 USD
        mockPriceFeed.setMockPrice(2000e8, 8);

        // sets bond Oracle price to 100 USDC/BondEth which is very reasonable
        bondMockPriceFeed.setMockPrice(100e6, 6);

        vm.startPrank(alice);
        IERC20(params.reserveToken).approve(address(pool), 10e18);
        pool.create(Pool.TokenType.LEVERAGE, 10e18, 0);
        // alice deposited 10 WETH (10e18) and she got back lTokenBal worth of lToken
        uint256 lTokenBal = IERC20(pool.lToken()).balanceOf(alice);

        IERC20(pool.lToken()).approve(address(pool), lTokenBal);
        // alice attempts to put back the lTokenBal so as to get back her ~ 10 WETH
        pool.redeem(Pool.TokenType.LEVERAGE, lTokenBal, 0);
        // alice gets back only 5.025 WETH instead of 10 WETH. it is HUGE LOSS!
        uint256 aliceReserveBalance = IERC20(params.reserveToken).balanceOf(alice);
        console.log(aliceReserveBalance); // 5.025 * 1e18
        assertEq(aliceReserveBalance, 5.025125628140703515 * 1e18);
        vm.stopPrank();
    }
}
```

### Mitigation
Modify the market rate check to only apply to bond token redemptions:
```solidity
if (tokenType == TokenType.BOND && marketRate != 0 && marketRate < redeemRate) {
    redeemRate = marketRate;
}
```