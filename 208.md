Shaggy Clay Cobra

High

# Exchange rates are vulnerable to collateral level changes

### Summary

The current mechanism to calculate the redeem amount/creation amount is flaw that an user can split the redeem/deposit amount to many additive amounts and finally get more returned tokens

### Root Cause

- The [function `Pool:getRedeemAmount()`](https://github.com/sherlock-audit/2024-12-plaza-finance/blob/main/plaza-evm/src/Pool.sol#L477-L525) calculates the amount of reserve token that can be redeemed by the derivative token holders. The redeem rate is dependant on the current collateral level. It can be seen that `COLLATERAL_THRESHOLD` is the threshold at which the redeem rate is calculated by different formulas. 
For example, for redeeming Leverage tokens, when the current collateral level is higher than the threshold, the redeem rate is `redeemRate = ((tvl - (bondSupply * BOND_TARGET_PRICE)) / assetSupply) * PRECISION`, but when it does not exceed the threshold, the rate is `redeemRate = ((tvl * multiplier) / assetSupply)`.
Note that the redemption of Leverage token does not affect the supply of Bond token, but it only decreases pool reserve, which effectively can decrease the collateral level. In short, redemption of Leverage token can decrease collateral level.
The problem arises when there is an Leverage redemption that can cause the collateral level (CL) to cross the `COLLATERAL_THRESHOLD`, such that before the redemption, `CL > COLLATERAL_THRESHOLD` but after the redemption, `CL <= COLLATERAL_THRESHOLD`. Let examine 2 approaches for an user to redeem `X` amount of Leverage tokens given that the condition that current collateral is higher than the threshold `CL > COLLATERAL_THRESHOLD`
1/ User redeem `X` amount, with redeem rate `redeemRate_1 = ((tvl_1 - (bondSupply * BOND_TARGET_PRICE)) / assetSupply_1) * PRECISION` and the redeemed reserve amount is `depositAmount * redeemRate_1` (for simplicity, ignore the decimals)
2/ User sequentially redeems `X_1` --> `X_2` such that `X_1 + X_2 = X (at case above)`. Assume that `X_1` is the amount that it pushes the collateral level to equal to the threshold. By this, the calculated reserve amount is for redeeming `X_1` is `X_1 * redeemRate_1` and for redeeming `X_2` is `X_2 * ((tvl_2 * multiplier) / assetSupply_2) = X_2 * redeemRate_2`. Note that `tvl_2 < tvl_1 && assetSupply_2 < assetSupply_1`.
So, with an amount of Leverage token, the user can have more than 1 redeem rate and there can be cases that approach 2 can result more reserve tokens than approach 1.

```solidity
  function getRedeemAmount(
    TokenType tokenType,
    uint256 depositAmount,
    uint256 bondSupply,
    uint256 levSupply,
    uint256 poolReserves,
    uint256 ethPrice,
    uint8 oracleDecimals,
    uint256 marketRate
  ) public pure returns(uint256) {
    if (bondSupply == 0) {
      revert ZeroDebtSupply();
    }

    uint256 tvl = (ethPrice * poolReserves).toBaseUnit(oracleDecimals);
    uint256 assetSupply = bondSupply;
    uint256 multiplier = POINT_EIGHT;

    // Calculate the collateral level based on the token type
    uint256 collateralLevel;
    if (tokenType == TokenType.BOND) {
      collateralLevel = ((tvl - (depositAmount * BOND_TARGET_PRICE)) * PRECISION) / ((bondSupply - depositAmount) * BOND_TARGET_PRICE);
    } else {
      multiplier = POINT_TWO;
      assetSupply = levSupply;
      collateralLevel = (tvl * PRECISION) / (bondSupply * BOND_TARGET_PRICE);

      if (assetSupply == 0) {
        revert ZeroLeverageSupply();
      }
    }
    
    // Calculate the redeem rate based on the collateral level and token type
    uint256 redeemRate;
    if (collateralLevel <= COLLATERAL_THRESHOLD) {
@>      redeemRate = ((tvl * multiplier) / assetSupply);
    } else if (tokenType == TokenType.LEVERAGE) {
@>      redeemRate = ((tvl - (bondSupply * BOND_TARGET_PRICE)) / assetSupply) * PRECISION;
    } else {
      redeemRate = BOND_TARGET_PRICE * PRECISION;
    }

    if (marketRate != 0 && marketRate < redeemRate) {
      redeemRate = marketRate;
    }
    
    // Calculate and return the final redeem amount
    return ((depositAmount * redeemRate).fromBaseUnit(oracleDecimals) / ethPrice) / PRECISION;
  }
```

- Similarly, the same problem happens with the derivative token creation when the deposits push the collateral level cross the threshold such that using the same amount of reserve token, an user can create more derivative tokens than expected.

### Internal Pre-conditions

_No response_

### External Pre-conditions

_No response_

### Attack Path

1. Assume that the collateral level is currently above the threshold, such that CL = 1.25
2. Alice firstly redeems 20 Leverage token, effectively push the CL to the value CL = 1.2
3. Alice continues to redeem 80 Leverage token.
The total reserve tokens redeemed can be more than redeeming 100 Leverage token at once

### Impact

- Users can redeem more reserve tokens than expected.
- Users can create more derivatives tokens than expected.

### PoC

Add the below tests to `Pool.t.sol`:

1/ For the redeem flow:
```solidity
  function testGetRedeemAmount_Additive_Lev() public {
    delete calcTestCases;

    // leverage token
    // beyond the threshold
    calcTestCases.push(CalcTestCase({
      assetType: Pool.TokenType.LEVERAGE,
      inAmount: 30 ether,
      ethPrice: 3000 * 10**8,
      TotalUnderlyingAssets: 12.2 ether,
      DebtAssets: 300 ether,
      LeverageAssets: 100 ether,
      expectedCreate: 0,
      expectedRedeem: 0,
      expectedSwap: 0
    }));
    
    vm.startPrank(governance);
    Token rToken = Token(params.reserveToken);

    for (uint256 i = 0; i < 1; i++) {
      rToken.mint(governance, calcTestCases[i].TotalUnderlyingAssets);
      rToken.approve(address(poolFactory), calcTestCases[i].TotalUnderlyingAssets);

      // Create salt to create the pool at a different address
      string memory salt = i.toString();

      Pool _pool = Pool(poolFactory.createPool(params, calcTestCases[i].TotalUnderlyingAssets, calcTestCases[i].DebtAssets, calcTestCases[i].LeverageAssets, "", salt, "", "", false));

      // aproach 1:
      // redeem full amount at once
      uint amount1 =  _pool.getRedeemAmount(
        calcTestCases[i].assetType, 
        calcTestCases[i].inAmount, 
        calcTestCases[i].DebtAssets,
        calcTestCases[i].LeverageAssets, 
        calcTestCases[i].TotalUnderlyingAssets, 
        calcTestCases[i].ethPrice,
        CHAINLINK_DECIMAL,
        0
      );


      // approach 2:
      // split to many redemptions
      uint total = 0;
      for(uint j = 0 ; j < 2; ++j ){
        uint256 amount1 = _pool.getRedeemAmount(
          calcTestCases[i].assetType, 
          calcTestCases[i].inAmount / 2, 
          calcTestCases[i].DebtAssets ,
          calcTestCases[i].LeverageAssets - (j*calcTestCases[i].inAmount / 2), 
          calcTestCases[i].TotalUnderlyingAssets - total,
          calcTestCases[i].ethPrice,
          CHAINLINK_DECIMAL,
          0
        );
        total+=amount1;

      }


      assertEq(amount1, total, "not equal");
    }
  }
```
Run the test and it fails
```bash
Failing tests:
Encountered 1 failing test in test/Pool.t.sol:PoolTest
[FAIL: not equal: 660000000000000000 != 748941175000000000] testGetRedeemAmount_Additive_Lev() (gas: 2079287)
```

2/ For the deposit flow:
```solidity
  function testGetCreateAmount_Additive_Lev() public {
    delete calcTestCases;

    // leverage token
    // CL is now below the threshold
    calcTestCases.push(CalcTestCase({
      assetType: Pool.TokenType.LEVERAGE,
      inAmount: 1 ether,
      ethPrice: 3000 * 10**8,
      TotalUnderlyingAssets: 11.9 ether,
      DebtAssets: 300 ether,
      LeverageAssets: 100 ether,
      expectedCreate: 0,
      expectedRedeem: 0,
      expectedSwap: 0
    }));
    
    vm.startPrank(governance);
    Token rToken = Token(params.reserveToken);

    for (uint256 i = 0; i < 1; i++) {
      rToken.mint(governance, calcTestCases[i].TotalUnderlyingAssets);
      rToken.approve(address(poolFactory), calcTestCases[i].TotalUnderlyingAssets);

      string memory salt = i.toString();
      Pool _pool = Pool(poolFactory.createPool(params, calcTestCases[i].TotalUnderlyingAssets, calcTestCases[i].DebtAssets, calcTestCases[i].LeverageAssets, "", salt, "", "", false));

      // Approach: split to 2 deposits
      uint256 amount1 = _pool.getCreateAmount(
        calcTestCases[i].assetType, 
        0.111 ether,
        calcTestCases[i].DebtAssets, // bond
        calcTestCases[i].LeverageAssets, // leverage
        calcTestCases[i].TotalUnderlyingAssets, // reserve
        calcTestCases[i].ethPrice,
        CHAINLINK_DECIMAL
      );

      uint amount2 = _pool.getCreateAmount(
        calcTestCases[i].assetType, 
        calcTestCases[i].inAmount - 0.111 ether,
        calcTestCases[i].DebtAssets, // bond
        calcTestCases[i].LeverageAssets + amount1, // leverage
        calcTestCases[i].TotalUnderlyingAssets + 0.111 ether, // reserve
        calcTestCases[i].ethPrice,
        CHAINLINK_DECIMAL
      );


      // approach 2:
      // deposit full amount at once
      uint256 amount3 = _pool.getCreateAmount(
        calcTestCases[i].assetType, 
        calcTestCases[i].inAmount,
        calcTestCases[i].DebtAssets, // bond
        calcTestCases[i].LeverageAssets, // leverage
        calcTestCases[i].TotalUnderlyingAssets, // reserve
        calcTestCases[i].ethPrice,
        CHAINLINK_DECIMAL
      );

 
      assertEq(amount1+amount2, amount3, "not equal");
      
    }
  }
```
Run the test and it fails:
```bash
Failing tests:
Encountered 1 failing test in test/Pool.t.sol:PoolTest
[FAIL: not equal: 50932476431364597838 != 42016806722689075630] testGetCreateAmount_Additive_Lev() (gas: 2077581)
```

### Mitigation

Handle the case that deposit/redeem amount can cause the collateral level to cross the threshold