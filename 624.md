Funny Midnight Lobster

High

# Auction End Blocked Due to Address Fetched for Wrong Period in `Pool::transferReserveToAuction`

### Summary

When a new auction is initiated via `Pool::startAuction`, a new `Auction` is created, and its address is assigned to `auctions[currentPeriod]`. Subsequently, the [`bondToken.increaseIndexedAssetPeriod(sharesPerToken)`](https://github.com/sherlock-audit/2024-12-plaza-finance/blob/main/plaza-evm/src/Pool.sol#L567) function is called, and the `currentPeriod` is incremented.  

When attempting to end the auction using `Auction::endAuction`, the function `Pool::transferReserveToAuction` is invoked. However, this function retrieves the `auctionAddress` using the updated `currentPeriod`. Since the `currentPeriod` was incremented at the end of `Pool::startAuction`, the address stored in `auctions[currentPeriod]` is `address(0)`.  

The next check to ensure that `msg.sender` matches the `auctionAddress` fails because `auctionAddress` is `address(0)`, causing the transaction to revert with the error `CallerIsNotAuction`. This effectively blocks the ability to end auctions and distribute rewards.  

### Root Cause

The issue lies in the [Pool::transferReserveToAuction](https://github.com/sherlock-audit/2024-12-plaza-finance/blob/main/plaza-evm/src/Pool.sol#L579)  function. Specifically, the function retrieves the auction address using the current value of `currentPeriod`, rather than the previous period where the auction was stored.  

### Internal Pre-conditions

1. An auction must be started by calling `Pool::startAuction`.  

### Attack Path

1. User/protocol initiates an auction by calling `Pool::startAuction`.  
2. Any attempt to end the auction by invoking `Auction::endAuction` fails, as the call to `Pool::transferReserveToAuction` reverts with the error `CallerIsNotAuction`.  

### Impact

The inability to end auctions prevents the distribution of rewards, disrupting the intended functionality of the system. This makes the auction mechanism unusable in its current state.  

### PoC

In test `setUp` disable `useMockPool(pool);`. Attempt to call `endAuction`. The transaction will revert with the error `CallerIsNotAuction`. 

```solidity
function testCantEndAuction() public {
  vm.startPrank(governance);
  Pool(pool).setAuctionPeriod(10 days);
  vm.stopPrank();
  
  vm.warp(95 days);
  Pool(pool).startAuction();
  
  (uint256 currentPeriod,) = Pool(pool).bondToken().globalPool();
  address auctionAddress = Pool(pool).auctions(currentPeriod-1);
  Auction _auction = Auction(auctionAddress);
  Token usdcToken = Token(Pool(pool).couponToken());
  uint256 totalBuyCouponAmount = _auction.totalBuyCouponAmount();
  
  vm.startPrank(bidder);
  usdcToken.mint(bidder, totalBuyCouponAmount);
  usdcToken.approve(auctionAddress, totalBuyCouponAmount);
  _auction.bid(1 ether, totalBuyCouponAmount);
  
  vm.warp(block.timestamp + 11 days);
  vm.expectRevert(Pool.CallerIsNotAuction.selector);
  _auction.endAuction();
}
```

```solidity
[PASS] testCantEndAuction() (gas: 2453821)
```

### Mitigation

To resolve this issue, modify the `Pool::transferReserveToAuction` function to fetch the auction address using the previous period (`currentPeriod - 1`) instead of the current period:

```diff
function transferReserveToAuction(uint256 amount) external virtual {
  (uint256 currentPeriod, ) = bondToken.globalPool();
- address auctionAddress = auctions[currentPeriod];
+ address auctionAddress = auctions[currentPeriod - 1];
  require(msg.sender == auctionAddress, CallerIsNotAuction());
  
  IERC20(reserveToken).safeTransfer(msg.sender, amount);
}
```