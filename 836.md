Future Linen Pelican

Medium

# Incorrect Market Rate Calculation for LEVERAGE Token Redemption

### Summary

A logic flaw in the _redeem function leads to incorrect application of the marketRate for LEVERAGE token redemptions. Specifically, the redeemRate calculation unintentionally uses a marketRate derived for BOND tokens, resulting in incorrect token valuation during redemptions. This issue adversely affects users and the protocol's economic integrity.



### Root Cause

The marketRate used here is derived from the BOND token oracle feed, as defined earlier in the code. However, this value is applied to both BOND and LEVERAGE tokens without differentiation. When the token type is LEVERAGE, the redeemRate is incorrectly overwritten using the marketRate for BOND tokens, leading to inaccurate redemption values for LEVERAGE tokens.

```solidity
  function simulateRedeem(TokenType tokenType, uint256 depositAmount) public view returns(uint256) {
    require(depositAmount > 0, ZeroAmount());

    uint256 bondSupply = bondToken.totalSupply()
                          .normalizeTokenAmount(address(bondToken), COMMON_DECIMALS);
    uint256 levSupply = lToken.totalSupply()
                          .normalizeTokenAmount(address(lToken), COMMON_DECIMALS);
    uint256 poolReserves = IERC20(reserveToken).balanceOf(address(this))
                          .normalizeTokenAmount(reserveToken, COMMON_DECIMALS);

    // Calculate and subtract fees from poolReserves
    poolReserves = poolReserves - (poolReserves * fee * (block.timestamp - lastFeeClaimTime)) / (PRECISION * SECONDS_PER_YEAR);

    if (tokenType == TokenType.LEVERAGE) {
      depositAmount = depositAmount.normalizeTokenAmount(address(lToken), COMMON_DECIMALS);
    } else {
      depositAmount = depositAmount.normalizeTokenAmount(address(bondToken), COMMON_DECIMALS);
    }

    uint8 oracleDecimals = getOracleDecimals(reserveToken, USD);

    uint256 marketRate;
    address feed = OracleFeeds(oracleFeeds).priceFeeds(address(bondToken), USD);
    if (feed != address(0)) {
@>>      marketRate = getOraclePrice(address(bondToken), USD)
@>>        .normalizeAmount(
@>>          getOracleDecimals(address(bondToken), USD), 
 @>>         oracleDecimals // this is the decimals of the reserve token chainlink feed
        );
    }

    return getRedeemAmount(
      tokenType,
      depositAmount,
      bondSupply,
      levSupply,
      poolReserves,
      getOraclePrice(reserveToken, USD),
      oracleDecimals,
      marketRate
    ).normalizeAmount(COMMON_DECIMALS, IERC20(reserveToken).safeDecimals());
  }

  /**
   * @dev Calculates the amount of reserve tokens to be redeemed for a given amount of bond or leverage tokens.
   * @param tokenType The type of derivative token being redeemed (BOND or LEVERAGE).
   * @param depositAmount The amount of derivative tokens being redeemed.
   * @param bondSupply The total supply of bond tokens.
   * @param levSupply The total supply of leverage tokens.
   * @param poolReserves The total amount of reserve tokens in the pool.
   * @param ethPrice The current ETH price from the oracle.
   * @param oracleDecimals The number of decimals used by the oracle.
   * @param marketRate The current market rate of the bond token.
   * @return amount of reserve tokens to be redeemed.
   */
  function getRedeemAmount(
    TokenType tokenType,
    uint256 depositAmount,
    uint256 bondSupply,
    uint256 levSupply,
    uint256 poolReserves,
    uint256 ethPrice,
    uint8 oracleDecimals,
    uint256 marketRate
  ) public pure returns(uint256) {
    if (bondSupply == 0) {
      revert ZeroDebtSupply();
    }

    uint256 tvl = (ethPrice * poolReserves).toBaseUnit(oracleDecimals);
    uint256 assetSupply = bondSupply;
    uint256 multiplier = POINT_EIGHT;

    // Calculate the collateral level based on the token type
    uint256 collateralLevel;
    if (tokenType == TokenType.BOND) {
      collateralLevel = ((tvl - (depositAmount * BOND_TARGET_PRICE)) * PRECISION) / ((bondSupply - depositAmount) * BOND_TARGET_PRICE);
    } else {
      multiplier = POINT_TWO;
      assetSupply = levSupply;
      collateralLevel = (tvl * PRECISION) / (bondSupply * BOND_TARGET_PRICE);

      if (assetSupply == 0) {
        revert ZeroLeverageSupply();
      }
    }
    
    // Calculate the redeem rate based on the collateral level and token type
    uint256 redeemRate;
    if (collateralLevel <= COLLATERAL_THRESHOLD) {
      redeemRate = ((tvl * multiplier) / assetSupply);
    } else if (tokenType == TokenType.LEVERAGE) {
      redeemRate = ((tvl - (bondSupply * BOND_TARGET_PRICE)) / assetSupply) * PRECISION;
    } else {
      redeemRate = BOND_TARGET_PRICE * PRECISION;
    }

@>>    if (marketRate != 0 && marketRate < redeemRate) {
@>>      redeemRate = marketRate;
@>>    }
    
    // Calculate and return the final redeem amount
    return ((depositAmount * redeemRate).fromBaseUnit(oracleDecimals) / ethPrice) / PRECISION;
  }
```

https://github.com/sherlock-audit/2024-12-plaza-finance/blob/main/plaza-evm/src/Pool.sol#L422-L525

### Internal Pre-conditions

_No response_

### External Pre-conditions

_No response_

### Attack Path

A user initiates a call to the redeem function with tokenType set to LEVERAGE and supplies a valid depositAmount.
The system calculates the marketRate using a BOND token oracle feed, irrespective of the token type.
The calculated redeemRate for LEVERAGE tokens is erroneously replaced by the marketRate derived for BOND tokens.
The redemption process uses the incorrect redeemRate, resulting in the user receiving fewer reserve tokens than expected for their LEVERAGE tokens.

### Impact

The incorrect application of marketRate results in under-compensation for users redeeming LEVERAGE tokens. This discrepancy leads to an economic loss for affected users and undermines trust in the redemption mechanism. Additionally, the protocol may face reputational risks and potential loss of user confidence.



### PoC

_No response_

### Mitigation

To fix the issue, the marketRate should only be applied to BOND tokens and ignored for LEVERAGE tokens or retrive also the LEVERAGE marketRate as well. Update the logic as follows:

