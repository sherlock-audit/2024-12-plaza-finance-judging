Sneaky Eggplant Pike

High

# Auctions will never be successfully finished due to period mismatch in post-auction validation

### Summary

When trying to end an auction via [`Auction::endAuction()`](https://github.com/sherlock-audit/2024-12-plaza-finance/blob/main/plaza-evm/src/Auction.sol#L336-L350), it invokes the `pool.transferReserveToAuction()` function to receive the reserve tokens from the pool to provide won bidders those tokens.

However, due to incorrect validation in the `Pool::transferReserveToAuction()` function, the ending auction will always fail and the bidders' funds will permanently be locked in the auction contract.

### Root Cause

[Pool.sol#L577-L583](https://github.com/sherlock-audit/2024-12-plaza-finance/blob/main/plaza-evm/src/Pool.sol#L577-L583)

```solidity
  function transferReserveToAuction(uint256 amount) external virtual {
    (uint256 currentPeriod, ) = bondToken.globalPool();
@>  address auctionAddress = auctions[currentPeriod];
@>  require(msg.sender == auctionAddress, CallerIsNotAuction());
    
    IERC20(reserveToken).safeTransfer(msg.sender, amount);
  }
```

As can be seen from the `transferReserveToAuction()` function, it checks if the caller is the auction. The auction address is fetched from `auctions[currentPeriod]`.

By the way, at this moment, `currentPeriod` is the value which is already increased when starting this auction. So `auctionAddress` here is always `adddress(0)`.

Consequently, ending the auction will always fail, even though the conditions for concluding the auction are met.

### Internal pre-conditions

*None*

### External pre-conditions

*None*

### Attack Path

Just calling `pool.endAuction()` will reveal this vulnerability and make the auction fail.

### Impact

Every auction will inevitably fail, thereby disrupting the entire protocol ecosystem.

### PoC

Here is an end-to-end auction test case

```solidity
// SPDX-License-Identifier: UNLICENSED
pragma solidity ^0.8.26;

import {console} from "forge-std/console.sol";
import "forge-std/Test.sol";

import {Pool} from "../src/Pool.sol";
import {Token} from "./mocks/Token.sol";
import {Auction} from "../src/Auction.sol";
import {Utils} from "../src/lib/Utils.sol";
import {MockPool} from "./mocks/MockPool.sol";
import {BondToken} from "../src/BondToken.sol";
import {TestCases} from "./data/TestCases.sol";
import {Decimals} from "../src/lib/Decimals.sol";
import {PoolFactory} from "../src/PoolFactory.sol";
import {Distributor} from "../src/Distributor.sol";
import {OracleFeeds} from "../src/OracleFeeds.sol";
import {Validator} from "../src/utils/Validator.sol";
import {OracleReader} from "../src/OracleReader.sol";
import {LeverageToken} from "../src/LeverageToken.sol";
import {MockPriceFeed} from "./mocks/MockPriceFeed.sol";
import {Deployer} from "../src/utils/Deployer.sol";
import {Strings} from "@openzeppelin/contracts/utils/Strings.sol";
import {UpgradeableBeacon} from "@openzeppelin/contracts/proxy/beacon/UpgradeableBeacon.sol";


contract PoolMarketRateTest is Test, TestCases {
  using Decimals for uint256;
  using Strings for uint256;

  PoolFactory public poolFactory;
  PoolFactory.PoolParams public params;
  Pool pool;

  MockPriceFeed public mockPriceFeed;
  MockPriceFeed public bondMockPriceFeed;
  address public oracleFeedsContract;

  address public deployer = address(0x1);
  address public minter = address(0x2);
  address public governance = address(0x3);
  address public securityCouncil = address(0x4);
  address public user = address(0x5);
  address public user2 = address(0x6);

  address public constant bondPriceFeed = address(0x5a1b1E88A4E0a7635c965DF8e312264e2d8C59a2);
  address public constant ethPriceFeed = address(0x71041dddad3595F9CEd3DcCFBe3D1F4b0a16Bb70);
  uint256 public constant CHAINLINK_DECIMAL_PRECISION = 10**8;
  uint8 public constant CHAINLINK_DECIMAL = 8;

  function setUp() public {
    vm.startPrank(deployer);

    address contractDeployer = address(new Deployer());
    oracleFeedsContract = address(new OracleFeeds());

    address poolBeacon = address(new UpgradeableBeacon(address(new Pool()), governance));
    address bondBeacon = address(new UpgradeableBeacon(address(new BondToken()), governance));
    address levBeacon = address(new UpgradeableBeacon(address(new LeverageToken()), governance));
    address distributorBeacon = address(new UpgradeableBeacon(address(new Distributor()), governance));

    poolFactory = PoolFactory(Utils.deploy(address(new PoolFactory()), abi.encodeCall(
      PoolFactory.initialize, 
      (governance, contractDeployer, oracleFeedsContract, poolBeacon, bondBeacon, levBeacon, distributorBeacon)
    )));

    params.fee = 0;
    params.feeBeneficiary = governance;
    params.reserveToken = address(new Token("Wrapped ETH", "WETH", false));
    params.sharesPerToken = 20e6;
    params.distributionPeriod = 10 days;
    params.couponToken = address(new Token("USDC", "USDC", false));
    
    OracleFeeds(oracleFeedsContract).setPriceFeed(params.reserveToken, address(0), ethPriceFeed, 1 days);

    // Deploy the mock price feed
    mockPriceFeed = new MockPriceFeed();

    // Use vm.etch to deploy the mock contract at the specific address
    bytes memory bytecode = address(mockPriceFeed).code;
    vm.etch(ethPriceFeed, bytecode);

    // Set oracle price
    mockPriceFeed = MockPriceFeed(ethPriceFeed);
    mockPriceFeed.setMockPrice(3000 * int256(CHAINLINK_DECIMAL_PRECISION), uint8(CHAINLINK_DECIMAL));
    
    vm.stopPrank();

    vm.startPrank(governance);
    poolFactory.grantRole(poolFactory.POOL_ROLE(), governance);
    poolFactory.grantRole(poolFactory.SECURITY_COUNCIL_ROLE(), securityCouncil);

    vm.stopPrank();
  }

  // @audit-poc
  function testAuctionEndToEnd() public {
    vm.startPrank(governance);

    Token rToken = Token(params.reserveToken);
    rToken.mint(governance, 1000e18);
    rToken.approve(address(poolFactory), 500e18);

    // Create pool and approve deposit amount
    pool = Pool(poolFactory.createPool(params, 450e18, 10000e18, 9000e18, "", "", "", "", false));
    pool.setAuctionPeriod(5 days);
    rToken.approve(address(pool), 1000e18);
    vm.stopPrank();

    vm.warp(block.timestamp + 11 days);

    pool.startAuction();

    (uint256 currentPeriod,) = pool.bondToken().globalPool();
    assertEq(currentPeriod, 1);
    
    Auction auction = Auction(pool.auctions(currentPeriod - 1)); // The auction should be from currentPeriod -1 because currentPeriod was already increased
    assertTrue(address(auction) != address(0));

    uint256 totalBuyCouponAmount = auction.totalBuyCouponAmount();

    vm.deal(user, 1 ether); // Provide gas
    deal(auction.buyCouponToken(), user, totalBuyCouponAmount);

    vm.startPrank(user);
    Token(auction.buyCouponToken()).approve(address(auction), totalBuyCouponAmount);
    auction.bid(100e18, totalBuyCouponAmount);
    vm.warp(block.timestamp + 5 days);
    vm.expectRevert(Pool.CallerIsNotAuction.selector); // Expect invalid caller error
    auction.endAuction(); // Try to end the auction
    
    vm.stopPrank();
  }
}
```

The logs after running the test:
```bash
Ran 1 test for test/Pool.poc.t.sol:PoolMarketRateTest
[PASS] testAuctionEndToEnd() (gas: 4289558)
Suite result: ok. 1 passed; 0 failed; 0 skipped; finished in 21.57ms (20.20ms CPU time)
```

### Mitigation

The auction address should be fetched from the previous period:

```diff
  function transferReserveToAuction(uint256 amount) external virtual {
    (uint256 currentPeriod, ) = bondToken.globalPool();
-   address auctionAddress = auctions[currentPeriod];
+   address auctionAddress = auctions[currentPeriod - 1];
    require(msg.sender == auctionAddress, CallerIsNotAuction());
    
    IERC20(reserveToken).safeTransfer(msg.sender, amount);
  }
```