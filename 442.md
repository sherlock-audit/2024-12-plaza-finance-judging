Spare Navy Boa

High

# Period Mismatch in Pool and Auction Contract Interaction

### Summary

A period synchronization mismatch in Pool.sol will cause auctions to fail as the auction contract will be unable to receive reserve tokens from the pool due to period misalignment, preventing coupon distribution from functioning properly.

### Root Cause

In Pool.sol, the [startAuction()](https://github.com/sherlock-audit/2024-12-plaza-finance/blob/14a962c52a8f4731bbe4655a2f6d0d85e144c7c2/plaza-evm/src/Pool.sol#L552-L567) function registers the auction at the current period and then immediately increases the period. 
However, when ending the auction with [endAuction()](https://github.com/sherlock-audit/2024-12-plaza-finance/blob/14a962c52a8f4731bbe4655a2f6d0d85e144c7c2/plaza-evm/src/Auction.sol#L345) it tries to receive reserve tokens through [pool.transferReserveToAuction()](https://github.com/sherlock-audit/2024-12-plaza-finance/blob/14a962c52a8f4731bbe4655a2f6d0d85e144c7c2/plaza-evm/src/Pool.sol#L577-L583), which checks against the current (now incremented) period where no auction is registered:

```solidity
function startAuction() external whenNotPaused() {
    // Check if auction for current period has already started
    (uint256 currentPeriod,) = bondToken.globalPool();
    require(auctions[currentPeriod] == address(0), AuctionAlreadyStarted());

    // Register auction at period N
    auctions[currentPeriod] = Utils.deploy(...);

    // Increase to period N+1
    bondToken.increaseIndexedAssetPeriod(sharesPerToken);
}

function transferReserveToAuction(uint256 amount) external virtual {
    (uint256 currentPeriod, ) = bondToken.globalPool();
    address auctionAddress = auctions[currentPeriod]; // Looks at period N+1
    require(msg.sender == auctionAddress, CallerIsNotAuction()); // @audit: this will revert since auctionAddress will be 0
    // ...
}
```



### Internal Pre-conditions

1. Pool distribution period has to pass
2. Pool calls start auction
3. Auction achieve totalBuyCouponAmount and end with State.SUCCEEDED

### External Pre-conditions

_No response_

### Attack Path

_No response_

### Impact

The auction contract cannot receive reserve tokens from the pool, breaking the core coupon distribution mechanism. This effectively prevents the protocol from functioning as designed since auctions cannot execute properly.

### PoC

Add this test as a individual test file in the test folder, since the Auction.t.sol that the protocol team provides deploys a auction contract independently instead of using the Pool.startAuction() in the setUp() function

run it with: `forge test --mt testAuditPeriodSynchronization -vvvv`


```solidity
// SPDX-License-Identifier: UNLICENSED
pragma solidity ^0.8.13;

import {Test, console} from "forge-std/Test.sol";

import {Pool} from "../src/Pool.sol";
import {Token} from "./mocks/Token.sol";
import {Utils} from "../src/lib/Utils.sol";
import {Auction} from "../src/Auction.sol";
import {MockPool} from "./mocks/MockPool.sol";
import {BondToken} from "../src/BondToken.sol";
import {PoolFactory} from "../src/PoolFactory.sol";
import {Distributor} from "../src/Distributor.sol";
import {OracleFeeds} from "../src/OracleFeeds.sol";
import {LeverageToken} from "../src/LeverageToken.sol";
import {Deployer} from "../src/utils/Deployer.sol";
import {IERC20} from "@openzeppelin/contracts/token/ERC20/IERC20.sol";
import {UpgradeableBeacon} from "@openzeppelin/contracts/proxy/beacon/UpgradeableBeacon.sol";
import {PausableUpgradeable} from "@openzeppelin/contracts-upgradeable/utils/PausableUpgradeable.sol";

contract AuctionPeriodMissMatchTest is Test {
    address public constant ethPriceFeed = address(0x71041dddad3595F9CEd3DcCFBe3D1F4b0a16Bb70);
    address deployer = address(0x1);
    address minter = address(0x2);
    address governance = address(0x3);
    address securityCouncil = address(0x4);
    address bidder = address(0x5);

    function testAuditPeriodSynchronization() public {
        // Deploy needed contracts first
        vm.startPrank(deployer);
        // Deploy tokens
        Token usdc = new Token("USDC", "USDC", false);
        Token weth = new Token("WETH", "WETH", false);

        // Deploy factory and its dependencies
        address contractDeployer = address(new Deployer());
        address oracleFeeds = address(new OracleFeeds());

        address poolBeacon = address(new UpgradeableBeacon(address(new Pool()), governance));
        address bondBeacon = address(new UpgradeableBeacon(address(new BondToken()), governance));
        address levBeacon = address(new UpgradeableBeacon(address(new LeverageToken()), governance));
        address distributorBeacon = address(new UpgradeableBeacon(address(new Distributor()), governance));

        // Setup factory
        PoolFactory poolFactory = PoolFactory(
            Utils.deploy(
                address(new PoolFactory()),
                abi.encodeCall(
                    PoolFactory.initialize,
                    (governance, contractDeployer, oracleFeeds, poolBeacon, bondBeacon, levBeacon, distributorBeacon)
                )
            )
        );
        vm.stopPrank();

        // Setup initial params
        vm.startPrank(governance);
        poolFactory.grantRole(poolFactory.POOL_ROLE(), governance);
        poolFactory.grantRole(poolFactory.SECURITY_COUNCIL_ROLE(), securityCouncil);

        PoolFactory.PoolParams memory params;
        params.fee = 0;
        params.reserveToken = address(weth);
        params.sharesPerToken = 50 * 10 ** 18;
        params.distributionPeriod = 90 days;
        params.couponToken = address(usdc);

        // Create pool
        weth.mint(governance, 1000 * 1e18);
        weth.approve(address(poolFactory), 1000 * 1e18);
        Pool pool = Pool(
            poolFactory.createPool(
                params,
                1000 * 1e18, // initialReserve
                100 * 1e18, // bondAmount
                50 * 1e18, // leverageAmount
                "",
                "",
                "",
                "",
                false
            )
        );

        pool.setAuctionPeriod(10 days);
        vm.stopPrank();

        (uint256 currentPeriod1,) = pool.bondToken().globalPool();
        console.log("Current Period before start auction:", currentPeriod1);

        // Warp to just past distribution period but within auction period
        vm.warp(91 days); // Just after 90 day distribution period
        pool.startAuction();

        (uint256 currentPeriod2,) = Pool(pool).bondToken().globalPool();
        console.log("Current Period after start auction:", currentPeriod2);

        // since the period is increased with startAuction, we need to decrease it by 1 to get the auction address
        address auctionAddress = Pool(pool).auctions(currentPeriod2 - 1);
        Auction _auction = Auction(auctionAddress);

        uint256 totalRequiredCouponAmount = _auction.totalBuyCouponAmount();
        console.log("Total buy coupon amount required:", totalRequiredCouponAmount);

        vm.startPrank(bidder);
        usdc.mint(bidder, totalRequiredCouponAmount); // mint the required amount
        usdc.approve(address(_auction), totalRequiredCouponAmount);
        _auction.bid(1, totalRequiredCouponAmount);

        console.log("Current coupon amount:", _auction.currentCouponAmount());

        vm.warp(block.timestamp + 15 days);
        
        vm.expectRevert(Pool.CallerIsNotAuction.selector);
        // this will call the transferReserveToAuction that will revert since it will try to get the auction address from the current period (not synchronized) that does not exist
        _auction.endAuction();
        vm.stopPrank();
    }
}
```



### Mitigation

Consider one of these approaches:

Look up the previous period's auction in transferReserveToAuction:

```solidity
address auctionAddress = auctions[currentPeriod - 1];
```

Move the period increase to after the auction completes, for example in the `distribute()` function.