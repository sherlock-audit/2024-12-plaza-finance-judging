Fit Alabaster Goblin

High

# Incorrect precision leads to uneffective market rate checks

### Summary

The `marketRate` variable is set to `oracleDecimals` instead of `PRECISION`, which is the correct precision used by the protocol to handle rate variables. Because of this, the `marketRate` functionality will never work.

### Root Cause

The market rate functionality allows Plaza to mitigate bad arbitrage. If the market rate is smaller than the computed redeem rate by the protocol, the final redeem rate will be set to the market rate, instead of the calculated rate, effectively preventing users from arbitraging redemptions.

In `simulateRedeem`, the `marketRate` for bond token is fetched from Aerodrome:

```solidity
// File: Pool.sol

function simulateRedeem(
        TokenType tokenType,
        uint256 depositAmount
    ) public view returns (uint256) {
        ...

        uint8 oracleDecimals = getOracleDecimals(reserveToken, USD);
        address feed = OracleFeeds(oracleFeeds).priceFeeds(
            address(bondToken),
            USD
        );

        
        uint256 marketRate;
        if (feed != address(0)) {
            marketRate = getOraclePrice(address(bondToken), USD)
                .normalizeAmount(
                    getOracleDecimals(address(bondToken), USD),
                    oracleDecimals 
                );
        }
```

As shown in the snippet, after fetching the oracle price, [the `normalizeAmount` function is called on the returned price](https://github.com/sherlock-audit/2024-12-plaza-finance/blob/main/plaza-evm/src/Pool.sol#L449), which will change the price decimals from `getOracleDecimals(address(bondToken), USD)` (the decimals of the bond oracle) to `oracleDecimals` (which is **the decimals of the reserve asset, NOT the bond token**).

The problem with this is that this will make `marketRate` be in the decimals returned by the reserve oracle’s decimals. However, during all computations in the protocol, **rates are always in `PRECISION` units, not in oracle units.** 

This can be seen in the `getRedeemAmount` function. As an example, when the `redeemRate` is calculated when the CL is below 1.2, `tvl` is in `COMMON_DECIMALS`, and `assetSupply` is also in `COMMON_DECIMALS`, while `multiplier` is in `PRECISION`. Because `tvl` and `assetSupply` ’s decimals cancel out when dividing, the result stored in `redeemRate` is in the units from `multiplier`, which are `PRECISION`:

```solidity
// File: Pool.sol

function getRedeemAmount(
        TokenType tokenType,
        uint256 depositAmount,
        uint256 bondSupply,
        uint256 levSupply,
        uint256 poolReserves,
        uint256 ethPrice,
        uint8 oracleDecimals,
        uint256 marketRate
    ) public pure returns (uint256) {
        ...

        uint256 redeemRate;
        if (collateralLevel <= COLLATERAL_THRESHOLD) {
            
            redeemRate = ((tvl * multiplier) / assetSupply); 
        } else if (tokenType == TokenType.LEVERAGE) {
            
            ...
        } else {
            ...
        }

       ...

        if (marketRate != 0 && marketRate < redeemRate) {
            redeemRate = marketRate;
        }
        
         return
            ((depositAmount * redeemRate).fromBaseUnit(oracleDecimals) /
                ethPrice) / PRECISION;
                
		}
```

This can also be seen in all other calculations for rates in the protocol.

However, because `marketRate` is in `oracleDecimals`, the check to see if `marketRate < redeemRate` will never pass, because `marketRate` will always be smaller than the computed `redeemRate`. For example, if `oracleDecimals` is 18 (which is true when reserve asset is a BPT token), the market rate check will **never pass**, even if the market rate is actually smaller than the redeem rate, given that marketRate will be in 18 decimals, while the `redeemRate` previously computed will only be scaled by `PRECISION`, which is only 1000000.

### Internal Pre-conditions

_No response_

### External Pre-conditions

_No response_

### Attack Path

1. A user wants to redeem some bond tokens. The reserve asset is a balancer pool token. Inside `simulateRedeem`, the price for the bond token is fetched, and it results in a price of $95. Because the price is scaled to `oracleDecimals` (which is 18, because the balancer oracle decimals is 18), `marketRate` will be set to 95e18.
2. Inside `getRedeemAmount`, the CL is > 1.2, so `redeemRate = BOND_TARGET_PRICE * PRECISION`, which gives a `redeemRate` of 100e6, or $100.
3. Then, the following check is performed:
    
    ```solidity
    // File: Pool.sol
    if (marketRate != 0 && marketRate < redeemRate) {
    		redeemRate = marketRate;
    }
    ```
    
    Although `marketRate` is actually $95 and `redeemRate` is $100, because of an incorrect precision the actual compared amounts are 95e18 < 100e6, so the `redeemRate` won’t be updated to the current market rate.

### Impact

The market rate check functionality is effectively nullified. This functionality is extremely relevant to the protocol, given that it avoids situations of bad arbitrage where the protocol is offering bond redemptions at a higher rate than the market, which leads to a loss of funds for the protocol, given that it is paying more than its real market price. For example, if the market price is $90 and CL > 1.2, the protocol will be paying $100 instead of $90, which constitutes a $10 payment surplus for each bond redemption.

Because this leads to a loss of funds and does not require big external limitations (the market rate check is always broken), it must be deemed high severity.

### PoC

_No response_

### Mitigation

Change the `marketRate` precision from `oracleDecimals` to `PRECISION`:

```diff
// Pool.sol

function simulateRedeem(
        TokenType tokenType,
        uint256 depositAmount
    ) public view returns (uint256) {
        ...
       
        uint256 marketRate;
        if (feed != address(0)) {
            marketRate = getOraclePrice(address(bondToken), USD)
                .normalizeAmount(
                    getOracleDecimals(address(bondToken), USD),
-                   oracleDecimals 
+                   PRECISION 
                );
        }
```