Quiet Seafoam Carp

High

# Tokens may stuck in BalancerRouter contract because of incorrect assumption about balancerVault.joinPool behavior

### Summary

[BalancerRouter:joinBalancerPool()](https://github.com/sherlock-audit/2024-12-plaza-finance/blob/main/plaza-evm/src/BalancerRouter.sol#L65) function assumes that the [balancerVault.joinPool()](https://github.com/balancer/balancer-v2-monorepo/blob/36d282374b457dddea828be7884ee0d185db06ba/pkg/vault/contracts/PoolBalances.sol#L44) will always use all values from the `maxAmountsIn` array. However, the `joinPool()` function does not guarantee that all specified `maxAmountsIn` values are used. As a result, unused tokens remain locked in the contract because the function lacks logic to return unused tokens to the user.

### Root Cause

In `BalancerRouter:joinBalancerPool()` function is missing return of unused tokens to user:
```Solidity
function joinBalancerPool(
    bytes32 poolId,
    IAsset[] memory assets,
    uint256[] memory maxAmountsIn,
    bytes memory userData
) internal returns (uint256) {
    // Transfer assets from user to this contractmaxAmountsIn
    for (uint256 i = 0; i < assets.length; i++) {
        //@audit all maxAmountsIn tokens transfered to this contract
        IERC20(address(assets[i])).safeTransferFrom(msg.sender, address(this), maxAmountsIn[i]);
        IERC20(address(assets[i])).safeIncreaseAllowance(address(balancerVault), maxAmountsIn[i]);
    }

    IVault.JoinPoolRequest memory request = IVault.JoinPoolRequest({
        assets: assets,
        maxAmountsIn: maxAmountsIn,
        userData: userData,
        fromInternalBalance: false
    });

    // Join Balancer pool
    uint256 balancerPoolTokenBalanceBefore = balancerPoolToken.balanceOf(address(this));
    //@audit it is not 100% that all maxAmountsIn will be used
    balancerVault.joinPool(poolId, address(this), address(this), request);
    uint256 balancerPoolTokenBalanceAfter = balancerPoolToken.balanceOf(address(this));

    return balancerPoolTokenBalanceAfter - balancerPoolTokenBalanceBefore;
}
```

Balancer's `PoolBalances.sol:joinPool()` function uses [_callPoolBalanceChange()](https://github.com/balancer/balancer-v2-monorepo/blob/36d282374b457dddea828be7884ee0d185db06ba/pkg/vault/contracts/PoolBalances.sol#L160) function to finalize `amountsInOrOut` and [_processJoinPoolTransfers()](https://github.com/balancer/balancer-v2-monorepo/blob/36d282374b457dddea828be7884ee0d185db06ba/pkg/vault/contracts/PoolBalances.sol#L214) to transfer it to the pool:

```Solidity
function _callPoolBalanceChange(
    PoolBalanceChangeKind kind,
    bytes32 poolId,
    address sender,
    address payable recipient,
    PoolBalanceChange memory change,
    bytes32[] memory balances
)
    private
    returns (
        bytes32[] memory finalBalances,
        uint256[] memory amountsInOrOut,
        uint256[] memory dueProtocolFeeAmounts
    )
{
    (uint256[] memory totalBalances, uint256 lastChangeBlock) = balances.totalsAndLastChangeBlock();

    IBasePool pool = IBasePool(_getPoolAddress(poolId));
    //@audit calculates amountsInOrOut
    (amountsInOrOut, dueProtocolFeeAmounts) = kind == PoolBalanceChangeKind.JOIN
        ? pool.onJoinPool(
            poolId,
            sender,
            recipient,
            totalBalances,
            lastChangeBlock,
            _getProtocolSwapFeePercentage(),
            change.userData
        )
        : pool.onExitPool(
            poolId,
            sender,
            recipient,
            totalBalances,
            lastChangeBlock,
            _getProtocolSwapFeePercentage(),
            change.userData
        );

    InputHelpers.ensureInputLengthMatch(balances.length, amountsInOrOut.length, dueProtocolFeeAmounts.length);

    // The Vault ignores the `recipient` in joins and the `sender` in exits: it is up to the Pool to keep track of
    // their participation.
    finalBalances = kind == PoolBalanceChangeKind.JOIN
        ? _processJoinPoolTransfers(sender, change, balances, amountsInOrOut, dueProtocolFeeAmounts)
        : _processExitPoolTransfers(recipient, change, balances, amountsInOrOut, dueProtocolFeeAmounts);
}

function _processJoinPoolTransfers(
    address sender,
    PoolBalanceChange memory change,
    bytes32[] memory balances,
    uint256[] memory amountsIn,
    uint256[] memory dueProtocolFeeAmounts
) private returns (bytes32[] memory finalBalances) {
    // We need to track how much of the received ETH was used and wrapped into WETH to return any excess.
    uint256 wrappedEth = 0;

    finalBalances = new bytes32[](balances.length);
    for (uint256 i = 0; i < change.assets.length; ++i) {
        uint256 amountIn = amountsIn[i];
        //@audit amountIn can be less then maxAmountIn
        _require(amountIn <= change.limits[i], Errors.JOIN_ABOVE_MAX);

        // Receive assets from the sender - possibly from Internal Balance.
        IAsset asset = change.assets[i];
        _receiveAsset(asset, amountIn, sender, change.useInternalBalance);

        if (_isETH(asset)) {
            wrappedEth = wrappedEth.add(amountIn);
        }

        uint256 feeAmount = dueProtocolFeeAmounts[i];
        _payFeeAmount(_translateToIERC20(asset), feeAmount);

        // Compute the new Pool balances. Note that the fee amount might be larger than `amountIn`,
        // resulting in an overall decrease of the Pool's balance for a token.
        finalBalances[i] = (amountIn >= feeAmount) // This lets us skip checked arithmetic
            ? balances[i].increaseCash(amountIn - feeAmount)
            : balances[i].decreaseCash(feeAmount - amountIn);
    }

    // Handle any used and remaining ETH.
    _handleRemainingEth(wrappedEth);
}
```

### Internal Pre-conditions

_No response_

### External Pre-conditions

_No response_

### Attack Path

_No response_

### Impact

Tokens that are not used during the `balancerVault.joinPool()` stuck in the `BalancerRouter` contract without a mechanism to return them to user.

### PoC

_No response_

### Mitigation

Change [BalancerRouter:joinBalancerPool()](https://github.com/sherlock-audit/2024-12-plaza-finance/blob/main/plaza-evm/src/BalancerRouter.sol#L65) function like this:

```Solidity
function joinBalancerPool(
    bytes32 poolId,
    IAsset[] memory assets,
    uint256[] memory maxAmountsIn,
    bytes memory userData
) internal returns (uint256) {
    // Transfer assets from user to this contract
    for (uint256 i = 0; i < assets.length; i++) {
        IERC20(address(assets[i])).safeTransferFrom(msg.sender, address(this), maxAmountsIn[i]);
        IERC20(address(assets[i])).safeIncreaseAllowance(address(balancerVault), maxAmountsIn[i]);
    }

    IVault.JoinPoolRequest memory request = IVault.JoinPoolRequest({
        assets: assets,
        maxAmountsIn: maxAmountsIn,
        userData: userData,
        fromInternalBalance: false
    });

    // Join Balancer pool
    uint256 balancerPoolTokenBalanceBefore = balancerPoolToken.balanceOf(address(this));
    balancerVault.joinPool(poolId, address(this), address(this), request);
    uint256 balancerPoolTokenBalanceAfter = balancerPoolToken.balanceOf(address(this));

    //@audit Send remaining tokens back to user
    for (uint256 i = 0; i < assets.length; i++) {
        uint256 currentAmount = IERC20(address(assets[i])).balanceOf(address(this));
        if (currentAmount != 0) {
            IERC20(address(assets[i])).safeTransfer(msg.sender, currentAmount);
        }
    }

    return balancerPoolTokenBalanceAfter - balancerPoolTokenBalanceBefore;
}
```