Joyful Yellow Raccoon

Medium

# Precision Loss in Create Amount Calculation in `getCreateAmount()` Function of Pool Contract Leads to Minting More Bond Tokens than Actual

### Summary

The `getCreateAmount()` function in the Pool contract calculates the create amount inaccurately. This is due to the improper order of operations, where the formula calculates the bond token amount intended to be minted. The `creationRate` (for the case of `LEVERAGE` token type) is calculated by dividing the `adjustedValue` by the `assetSupply`. This `creationRate` is then used as the denominator in another fraction to determine the bond minting amount. However, the initial division to calculate the `creationRate` causes a loss in precision, as Solidity rounds down the result of the division. This reduction in precision makes the denominator **smaller**, which in turn leads to an **inflated** bond minting amount.

### Root Cause

The function `getCreateAmount()` of the Pool contract calculates the amount of new bond tokens to be minted based on the current pool state and oracle price. The output of this function is directly be used in minting the bond tokens.

```Solidity
  function getCreateAmount(
    TokenType tokenType,
    uint256 depositAmount,
    uint256 bondSupply, 
    uint256 levSupply, 
    uint256 poolReserves, 
    uint256 ethPrice,
    uint8 oracleDecimals) public pure returns(uint256) {
    
    ...

    if (collateralLevel <= COLLATERAL_THRESHOLD) {
      if (tokenType == TokenType.LEVERAGE && assetSupply == 0) {
        revert ZeroLeverageSupply();
      }
      creationRate = (tvl * multiplier) / assetSupply;
    } else if (tokenType == TokenType.LEVERAGE) {
      if (assetSupply == 0) {
        revert ZeroLeverageSupply();
      }

      uint256 adjustedValue = tvl - (BOND_TARGET_PRICE * bondSupply);
      creationRate = (adjustedValue * PRECISION) / assetSupply;
    }
    
    return ((depositAmount * ethPrice * PRECISION) / creationRate).toBaseUnit(oracleDecimals);
  }
```
https://github.com/sherlock-audit/2024-12-plaza-finance/blob/main/plaza-evm/src/Pool.sol#L340-L343

As can be seen from the mentioned snippet, the minting amount is calculated by this formula: (for the case where the `tokenType` is of type `LEVERAGE`)

$$ amount_{minting} = \frac{amount_{deposit} \times price_{ETH} \times 10^6}{\large\frac{value_{adjusted} \times 10^6}{supply_{asset}}} $$

Now if we look at the current implementation we can see that there is a prior division during the calculation of the `creationRate` which can significantly reduce the precision and accuracy. This rounding down in the denominator will escalate and inflate the bond token minting amount as the denominator becomes smaller.

If we want to implement such a structure in Solidity, we should prioritize the multiplication and use this simplification instead of the abovementioned formula:

 $$ amount_{minting} = \frac{amount_{deposit} \times price_{ETH} \times 10^6 \times supply_{asset}}{value_{adjusted} \times 10^6} $$

Or if we want to keep the `creationRate` concept here, we should use the fixed-point math library to keep the precision here.

### Internal Pre-conditions

_No response_

### External Pre-conditions

_No response_

### Attack Path

_No response_

### Impact

This issue causes an unintended inflated minting of bond tokens, leading to an imbalance in the bond token supply.

### PoC

This test shows the discrepancy between the actual and accurate numbers: (these variables are for testing purposes)
```Solidity
    function test_precissionLoss() public {

        uint adjustedValue = 4.5123e17;
        uint assetSupply= 9.5126e22;
        uint depositAmount= 6.5132e15;
        uint ethPrice = 3.3541e8;

        uint creationRate = (adjustedValue * PRECISION) / assetSupply;
    
        uint actual = ((depositAmount * ethPrice * PRECISION) / creationRate) / 1e8;
        uint accurate = (depositAmount * ethPrice * PRECISION * assetSupply) / (adjustedValue * PRECISION * 1e8);
        
        console.log("Current Implementation: ", actual );
        console.log("Accurate Implementation: ", accurate);
    }
```

The result would be:

```Markdown

     Current Implementation:  5461481030000000000000
     Accurate Implementation:  4605445954034793785874
```

This shows the discrepancy which is 18.5% here.

### Mitigation

Consider using advanced fixed-point math libraries such as PRBMath or Solmate's fixed-point math to keep precision in `creationRate` calculation. 
(https://github.com/PaulRBerg/prb-math/blob/main/src/ud60x18/Math.sol)