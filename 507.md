Rural Burgundy Hawk

High

# Invalid `period` used in `Pool::transferReserveToAuction(...)` function leads to DoS of the `Auction` contract

### Summary

The `Plaza` protocol creates `Auctions` after each `Pool`'s distribution period, as a means to collect `couponTokens` which are the main incentive backing `bondETH` tokens. Users bid `couponTokens` to win some of the pool's `reserveToken`. Whenever an auction is created, it is added to the `Pool`'s `auctions` array based on the `currentPeriod`, after creation the period is [increased](https://github.com/sherlock-audit/2024-12-plaza-finance/blob/14a962c52a8f4731bbe4655a2f6d0d85e144c7c2/plaza-evm/src/Pool.sol#L567). Whenever an auction is successful, the `Auction` contract, will call the `Pool`'s `transferReserveToAuction(...)` function to get the required `reserveToken` amount so that users can claim their bids.  However, when the `transferReserveToAuction` function is called, it incorrectly queries the `Pool`'s [auctions]() array with the current period (which as we discussed will be the new increased period) resulting in an invalid address comparison, making it impossible to end the auction:

https://github.com/sherlock-audit/2024-12-plaza-finance/blob/14a962c52a8f4731bbe4655a2f6d0d85e144c7c2/plaza-evm/src/Pool.sol#L580
```solidity
  function transferReserveToAuction(uint256 amount) external virtual {
    (uint256 currentPeriod, ) = bondToken.globalPool();
@> address auctionAddress = auctions[currentPeriod]; // @audit - this will take a non-existing zero address
@> require(msg.sender == auctionAddress, CallerIsNotAuction()); // @audit - this check will fail
    
    IERC20(reserveToken).safeTransfer(msg.sender, amount);
  }
```

### Root Cause

Invalid period passed to the `Pool`'s `auctions` array in the `transferReserveToAuction(...)` function, leading to DoS of the `Auction` contract.

### Internal Pre-conditions

1. A `Pool` is created and users start minting plaza tokens.
2. The distribution period ends and a new auction is started.
3. Users start bidding in the auction until the `couponToken` threshold is met.

### External Pre-conditions

N/A

### Attack Path

1. Users try to call `Auction::endAuction()`
2. The auction is DoS-ed as `endAuction()` will always revert with `CallerIsNotAuction()`

### Impact

1. Inability to end a successful action, which in turn leads to the `Pool` being unable to distribute `couponTokens` to `bondETH` holders, breaking the main incentive of the `Plaza` protocol.

### PoC

The following is the used `setUp`:

<details>

<summary>setUp()</summary>

```solidity
function setUp() public {
        vm.startPrank(deployer);

        // Deploy mock tokens
        balancerPoolToken = new Token("Balancer Pool Token", "balancerPoolToken", false);
        asset1 = new Token("Test Token 1", "TT1", true);
        asset2 = new Token("Test Token 2", "TT2", true);
        address contractDeployer = address(new Deployer());
        address oracleFeeds = address(new OracleFeeds());

        address poolBeacon = address(new UpgradeableBeacon(address(new Pool()), governance));
        address bondBeacon = address(new UpgradeableBeacon(address(new BondToken()), governance));
        address levBeacon = address(new UpgradeableBeacon(address(new LeverageToken()), governance));
        address distributorBeacon = address(new UpgradeableBeacon(address(new Distributor()), governance));

        poolFactory = PoolFactory(
            Utils.deploy(
                address(new PoolFactory()),
                abi.encodeCall(
                    PoolFactory.initialize,
                    (governance, contractDeployer, oracleFeeds, poolBeacon, bondBeacon, levBeacon, distributorBeacon)
                )
            )
        );

        params.fee = 2000;
        params.reserveToken = address(balancerPoolToken);
        params.sharesPerToken = 2_500_000;
        params.distributionPeriod = 90 days;
        params.couponToken = address(new Token("USDC", "USDC", false));

        Token(params.couponToken).setDecimals(6);

        OracleFeeds(oracleFeeds).setPriceFeed(params.reserveToken, address(0), ethPriceFeed, 1000 days);

        // Deploy the mock price feed
        mockPriceFeed = new MockPriceFeed();

        // Use vm.etch to deploy the mock contract at the specific address
        bytes memory bytecode = address(mockPriceFeed).code;
        vm.etch(ethPriceFeed, bytecode);

        // Set oracle price
        mockPriceFeed = MockPriceFeed(ethPriceFeed);
        mockPriceFeed.setMockPrice(3000 * int256(CHAINLINK_DECIMAL_PRECISION), uint8(CHAINLINK_DECIMAL));

        balancerPoolToken.mint(governance, 1000000 ether);
        vm.stopPrank();

        vm.startPrank(governance);
        poolFactory.grantRole(poolFactory.POOL_ROLE(), governance);

        balancerPoolToken.approve(address(poolFactory), 1000000 ether);

        _pool = Pool(poolFactory.createPool(params, 1000 ether, 25000 ether, 1000 ether, "", "", "", "", false));
        distributor = Distributor(poolFactory.distributors(address(_pool)));
        vm.stopPrank();

        vm.startPrank(deployer);

        // Deploy mock contracts
        vault = new MockBalancerVault(balancerPoolToken);
        predeposit = PreDeposit(
            Utils.deploy(
                address(new PreDeposit()),
                abi.encodeCall(
                    PreDeposit.initialize,
                    (
                        params,
                        address(poolFactory),
                        block.timestamp,
                        block.timestamp + 1 hours,
                        1 ether,
                        "Bond ETH",
                        "bondETH",
                        "Leveraged ETH",
                        "levETH"
                    )
                )
            )
        );
        router = new BalancerRouter(address(vault), address(balancerPoolToken));

        // Setup initial token balances
        asset1.mint(user, 1000 ether);
        asset2.mint(user, 1000 ether);

        asset1.mint(user2, 1000 ether);
        asset2.mint(user2, 1000 ether);

        vm.stopPrank();
    }
```
</details>

The following is the PoC:

```solidity
    function testAuctionDos() public {
        deal(params.reserveToken, user, 5 ether);

        vm.startPrank(user);
        IERC20(params.reserveToken).approve(address(_pool), 5 ether);
        _pool.create(Pool.TokenType.BOND, 5 ether, 0);
        vm.stopPrank();

        vm.warp(block.timestamp + 91 days);

        vm.startPrank(governance);
        _pool.setAuctionPeriod(1 days);
        vm.stopPrank();

        _pool.startAuction();

        deal(params.couponToken, user, 62890625000);

        vm.startPrank(user);
        IERC20(params.couponToken).approve(address(_pool.auctions(0)), 62890625000);
        vm.stopPrank();
        Auction auction = Auction(_pool.auctions(0));

        vm.startPrank(user);
        auction.bid(2e9, 62890625000);
        vm.stopPrank();

        vm.warp(block.timestamp + 1 days);

        address storedAuctionAddress = _pool.auctions(0);
        address actualAuctionAddress = address(auction);

        vm.expectRevert(Pool.CallerIsNotAuction.selector);
        auction.endAuction();
    }
```

### Mitigation

Change the `transferReserveToAuction` function to use the previous period when ending an auction:

```solidity
    function transferReserveToAuction(uint256 amount) external virtual {
        (uint256 currentPeriod,) = bondToken.globalPool();
        address auctionAddress = auctions[currentPeriod - 1];
        require(msg.sender == auctionAddress, CallerIsNotAuction());

        IERC20(reserveToken).safeTransfer(msg.sender, amount);
    }
```