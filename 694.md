Refined Ultraviolet Cricket

Medium

# All bonds cannot be redeemed.

### Summary

CollateralLevel calculation may revert, which will block last user to redeem all of his bonds.

### Root Cause

In `Pool:getRedeemAmount():498` the collateral level is calculated. 

https://github.com/sherlock-audit/2024-12-plaza-finance/blob/main/plaza-evm/src/Pool.sol#L498

However, if `bondSupply == depositAmount`, which correspond to the case where all the bond are redeem, this calcul will revert, blocking user from withdrawing all his bonds.

### Internal Pre-conditions

_No response_

### External Pre-conditions

_No response_

### Attack Path

1. Alice owns 100% of the bonds
2. She wants to redeem 100% of them.
3. `bondSupply == depositAmount` so redeem revert.

### Impact

The last user may not be able to withdraw all of his funds.

### PoC

```solidity
// SPDX-License-Identifier: UNLICENSED
pragma solidity ^0.8.26;

import {Test, console} from "forge-std/Test.sol";

import {Pool} from "../../../src/Pool.sol";
import {Token} from "../../mocks/Token.sol";
import {Utils} from "../../../src/lib/Utils.sol";
import {Deployer} from "../../../src/utils/Deployer.sol";
import {BondToken} from "../../../src/BondToken.sol";
import {OracleFeeds} from "../../../src/OracleFeeds.sol";
import {PoolFactory} from "../../../src/PoolFactory.sol";
import {Distributor} from "../../../src/Distributor.sol";
import {MockPriceFeed} from "../../mocks/MockPriceFeed.sol";
import {LeverageToken} from "../../../src/LeverageToken.sol";
import {UpgradeableBeacon} from "@openzeppelin/contracts/proxy/beacon/UpgradeableBeacon.sol";

abstract contract Setup is Test {
    uint256 public constant ETH_PRICE = 3000;
    uint256 public constant CHAINLINK_DECIMAL = 8;
    uint256 public constant CHAINLINK_DECIMAL_PRECISION = 10 ** 8;
    uint256 public constant INITIAL_LEV_TOKEN = 1 ether;
    uint256 public constant INITIAL_BOND_TOKEN = 25 ether;
    uint256 public constant INITIAL_RESERVE_TOKEN = 1 ether;

    Pool public pool;
    Token public reserveToken;
    Token public couponToken;
    BondToken public bondToken;
    LeverageToken public levToken;
    PoolFactory public poolFactory;

    PoolFactory.PoolParams public params;
    MockPriceFeed public mockPriceFeed;

    address public ethPriceFeed;
    address public oracleFeedsContract;

    mapping(address => string) public names;
    address[] public users;
    address public alice;
    address public bobby;
    address public carol;
    address public david;

    address public governor;
    address public deployer;

    function setUp() public virtual {
        // 1. Setup realistic test environment.
        vm.warp(1000000000);
        vm.roll(1000000000);

        // 2. Create users.
        _createUsers();

        // 3. Deploy contracts.
        _deployContracts();

        // 4. Approvals.
        _approveTokens();

        // 5. Labels.
        _addLabels();
    }

    function _createUsers() internal {
        governor = makeAddr("governor");
        deployer = makeAddr("deployer");

        // EOAs
        alice = makeAddr("alice");
        bobby = makeAddr("bobby");
        carol = makeAddr("carol");
        david = makeAddr("david");

        // Add user to the list
        users.push(alice);
        users.push(bobby);
        users.push(carol);
        users.push(david);

        //
        ethPriceFeed = makeAddr("ethPriceFeed");
    }

    function _deployContracts() internal {
        vm.startPrank(deployer);

        address contractDeployer = address(new Deployer());
        oracleFeedsContract = address(new OracleFeeds());

        address poolBeacon = address(new UpgradeableBeacon(address(new Pool()), governor));
        address bondBeacon = address(new UpgradeableBeacon(address(new BondToken()), governor));
        address levBeacon = address(new UpgradeableBeacon(address(new LeverageToken()), governor));
        address distributorBeacon = address(new UpgradeableBeacon(address(new Distributor()), governor));

        poolFactory = PoolFactory(
            Utils.deploy(
                address(new PoolFactory()),
                abi.encodeCall(
                    PoolFactory.initialize,
                    (
                        governor,
                        contractDeployer,
                        oracleFeedsContract,
                        poolBeacon,
                        bondBeacon,
                        levBeacon,
                        distributorBeacon
                    )
                )
            )
        );

        params.fee = 0;
        params.feeBeneficiary = governor;
        params.reserveToken = address(new Token("Wrapped ETH", "WETH", false));
        params.sharesPerToken = 50 * 10 ** 18;
        params.distributionPeriod = 0;
        params.couponToken = address(new Token("USDC", "USDC", false));

        // Todo: create fake ehtPriceFeed
        OracleFeeds(oracleFeedsContract).setPriceFeed(params.reserveToken, address(0), ethPriceFeed, 1 days);

        // Deploy the mock price feed
        mockPriceFeed = new MockPriceFeed();

        // Use vm.etch to deploy the mock contract at the specific address
        bytes memory bytecode = address(mockPriceFeed).code;
        vm.etch(ethPriceFeed, bytecode);

        // Set oracle price
        mockPriceFeed = MockPriceFeed(ethPriceFeed);
        mockPriceFeed.setMockPrice(int256(ETH_PRICE) * int256(CHAINLINK_DECIMAL_PRECISION), uint8(CHAINLINK_DECIMAL));

        vm.stopPrank();

        vm.startPrank(governor);
        poolFactory.grantRole(poolFactory.POOL_ROLE(), governor);
        poolFactory.grantRole(poolFactory.SECURITY_COUNCIL_ROLE(), governor);
        vm.stopPrank();

        reserveToken = Token(params.reserveToken);
        createPool();

        levToken = LeverageToken(pool.lToken());
        bondToken = BondToken(pool.bondToken());
        couponToken = Token(params.couponToken);
        couponToken.setDecimals(6);
    }

    function _approveTokens() internal {
        for (uint256 i; i < users.length; i++) {
            vm.prank(users[i]);
            reserveToken.approve(address(pool), type(uint256).max);
        }
    }

    function _addLabels() internal {
        vm.label(address(reserveToken), "WETH");
        vm.label(address(couponToken), "USDC");
        vm.label(address(bondToken), "bondToken");
        vm.label(address(levToken), "levToken");
    }

    function createPool() public {
        vm.startPrank(governor);
        reserveToken.mint(governor, INITIAL_RESERVE_TOKEN);
        reserveToken.approve(address(poolFactory), INITIAL_RESERVE_TOKEN);

        // Create pool
        pool = Pool(
            poolFactory.createPool(
                params,
                INITIAL_RESERVE_TOKEN,
                INITIAL_BOND_TOKEN,
                INITIAL_LEV_TOKEN,
                "Bond Token",
                "BOND",
                "Leverage Token",
                "LEVE",
                false
            )
        );

        vm.stopPrank();
    }

    function min(uint256 a, uint256 b) internal pure returns (uint256) {
        return a < b ? a : b;
    }

    function max(uint256 a, uint256 b) internal pure returns (uint256) {
        return a > b ? a : b;
    }
}


import {Pool} from "../../../src/Pool.sol";
import {Setup} from "./Setup.sol";

contract Unit is Setup {

    function test_unit() public {
       // This view should not revert.
        pool.simulateRedeem(Pool.TokenType.BOND, bondToken.balanceOf(governor));
    }
}
```

### Mitigation

As we don't care about what is the collateral level, we are sure that is is bellow 1.2, so we can set it directly to 0. 

```solidity
    uint256 collateralLevel;
    if (tokenType == TokenType.BOND) {
      if (bondSupply == depositAmount) {
        collateralLevel = 0;
      } else {
        collateralLevel = ((tvl - (depositAmount * BOND_TARGET_PRICE)) * PRECISION) / ((bondSupply - depositAmount) * BOND_TARGET_PRICE);
      }
    } else
```