Crazy Carmine Corgi

Medium

# Pool#transferReserveToAuction is completely broken

### Summary

When starting an auction, an auction is created and mapped to the currentPeriod but then the period is immediately increment in bondToken.increaseIndexedAssetPeriod. The result is that it's impossible for that same auction to later call Pool#transferReserveToAuction, since it checks currentPeriod and not the previousPeriod (currentPeriod - 1).

[Pool.sol#L552-L567](https://github.com/sherlock-audit/2024-12-plaza-finance/blob/main/plaza-evm/src/Pool.sol#L552-L567)

    @>  auctions[currentPeriod] = Utils.deploy(
        address(new Auction()),
        abi.encodeWithSelector(
            Auction.initialize.selector,
            address(couponToken),
            address(reserveToken),
            couponAmountToDistribute,
            block.timestamp + auctionPeriod,
            1000,
            address(this),
            poolSaleLimit
        )
        );

        // Increase the bond token period
    @>  bondToken.increaseIndexedAssetPeriod(sharesPerToken);


We see that the auction is deployed and mapped the current epoch. Immediately after bondToken.increaseIndexedAssetPeriod is called.

[BondToken.sol#L217-L229](https://github.com/sherlock-audit/2024-12-plaza-finance/blob/main/plaza-evm/src/BondToken.sol#L217-L229)

        function increaseIndexedAssetPeriod(uint256 sharesPerToken) public onlyRole(DISTRIBUTOR_ROLE) whenNotPaused() {
            globalPool.previousPoolAmounts.push(
            PoolAmount({
                period: globalPool.currentPeriod,
                amount: totalSupply(),
                sharesPerToken: globalPool.sharesPerToken
            })
            );
    @>      globalPool.currentPeriod++;
            globalPool.sharesPerToken = sharesPerToken;

            emit IncreasedAssetPeriod(globalPool.currentPeriod, sharesPerToken);
        }

We see that the currentPeriod is incremented, moving to the next period.

[Pool.sol#L577-L583](https://github.com/sherlock-audit/2024-12-plaza-finance/blob/main/plaza-evm/src/Pool.sol#L577-L583)

        function transferReserveToAuction(uint256 amount) external virtual {
            (uint256 currentPeriod, ) = bondToken.globalPool();
            address auctionAddress = auctions[currentPeriod];
    @>      require(msg.sender == auctionAddress, CallerIsNotAuction());
            
            IERC20(reserveToken).safeTransfer(msg.sender, amount);
        }

This causes an issue when the auction is complete as msg.sender will not match auctions[currentPeriod] since there will be no auction created for the new period

### Root Cause

[Pool.sol#L579](https://github.com/sherlock-audit/2024-12-plaza-finance/blob/main/plaza-evm/src/Pool.sol#L579) checks auctions[currentPeriod] rather than auctions[currentPeriod - 1]

### Internal preconditions

None

### External preconditions

None

### Attack Path

N/A

### Impact

All auction depositors will permanently lose their funds

### POC

Tests for all vulnerabilities can be found [here](https://gist.github.com/IAm0x52/05589415ce45af83aa4f7a5f63afbf45).

Insert the following test into Pool.t.sol

    function testTransferReserveToAuction() public {
        //test setup
        Pool.TokenType bondTokenType = Pool.TokenType(0);
        Pool.TokenType levTokenType = Pool.TokenType(1);

        Token rToken = Token(params.reserveToken);
        Token cToken = Token(params.couponToken);

        rToken.mint(governance, 1e20);
        vm.prank(governance);
        rToken.approve(address(poolFactory), 1e20);

        string memory salt = "test";
        PoolFactory.PoolParams memory _params = params;
        
        // create pool
        vm.prank(governance);
        Pool _pool = Pool(poolFactory.createPool(_params, 1e20, 1e20, 1e20, "", salt, "", "", false));

        vm.prank(governance);
        _pool.setAuctionPeriod(365 days);

        vm.warp(block.timestamp + 1);

        // start the auction
        _pool.startAuction();

        address auction = _pool.auctions(0);

        cToken.mint(user, Auction(auction).totalBuyCouponAmount());

        vm.startPrank(user);

        cToken.approve(auction, type(uint256).max);

        // complete auction
        Auction(auction).bid(1e18,Auction(auction).totalBuyCouponAmount());

        vm.warp(Auction(auction).endTime());

        // this will fail with error CallerIsNotAuction()
        Auction(auction).endAuction();
    }

    Output:

    [FAIL. Reason: CallerIsNotAuction()] testTransferReserveToAuction() (gas: 4272556)

### Mitigation

In L579 check currentPeriod - 1 rather than currentPeriod