Sneaky Eggplant Pike

Medium

# The `NotInAuction` modifier allows modifying relevant pool params even while in auction

### Summary

The `distributionPeriod`, `auctionPeriod`, and `sharesPerToken` parameters of the `Pool` contract are sensitive that should not be changed while the pool is running an auction.

Therefore, the `Pool` contract wraps the `setDistributionPeriod()`, `setAuctionPeriod()`, and `setSharesPerToken()` functions with the `NotInAuction` modifier to prevent modifying these params while in an auction.

However, due to incorrect validation in the `NotInAuction` modifier, these params can be changed during an auction period.

### Root Cause

The `NotInAuction` modifier validates the following condition to check if there's no running auction currently:

[Pool.sol#L750-L754](https://github.com/sherlock-audit/2024-12-plaza-finance/blob/main/plaza-evm/src/Pool.sol#L750-L754)
```solidity
  modifier NotInAuction() {
    (uint256 currentPeriod,) = bondToken.globalPool();
@>  require(auctions[currentPeriod] == address(0), AuctionIsOngoing());
    _;
  }
```

However, at this moment, the `currentPeriod` has already been increased when starting an auction, so even while in auction, `auctions[currentPeriod]` is `address(0)` by default, making the relevant setting functions bypass this modifier successfully.

### Internal pre-conditions

*None*

### External pre-conditions

*None*

### Attack Path

Just invokes the above 3 functions while the pool is running auction.

### Impact

This vulnerability could lead to unexpected issues, such as duplicate running auctions.

### PoC

Here is a test case to expose the vulnerability:

```solidity
// SPDX-License-Identifier: UNLICENSED
pragma solidity ^0.8.26;

import {console} from "forge-std/console.sol";
import "forge-std/Test.sol";

import {Pool} from "../src/Pool.sol";
import {Token} from "./mocks/Token.sol";
import {Auction} from "../src/Auction.sol";
import {Utils} from "../src/lib/Utils.sol";
import {MockPool} from "./mocks/MockPool.sol";
import {BondToken} from "../src/BondToken.sol";
import {TestCases} from "./data/TestCases.sol";
import {Decimals} from "../src/lib/Decimals.sol";
import {PoolFactory} from "../src/PoolFactory.sol";
import {Distributor} from "../src/Distributor.sol";
import {OracleFeeds} from "../src/OracleFeeds.sol";
import {Validator} from "../src/utils/Validator.sol";
import {OracleReader} from "../src/OracleReader.sol";
import {LeverageToken} from "../src/LeverageToken.sol";
import {MockPriceFeed} from "./mocks/MockPriceFeed.sol";
import {Deployer} from "../src/utils/Deployer.sol";
import {Strings} from "@openzeppelin/contracts/utils/Strings.sol";
import {UpgradeableBeacon} from "@openzeppelin/contracts/proxy/beacon/UpgradeableBeacon.sol";


contract PoolMarketRateTest is Test, TestCases {
  using Decimals for uint256;
  using Strings for uint256;

  PoolFactory public poolFactory;
  PoolFactory.PoolParams public params;
  Pool pool;

  MockPriceFeed public mockPriceFeed;
  MockPriceFeed public bondMockPriceFeed;
  address public oracleFeedsContract;

  address public deployer = address(0x1);
  address public minter = address(0x2);
  address public governance = address(0x3);
  address public securityCouncil = address(0x4);
  address public user = address(0x5);
  address public user2 = address(0x6);

  address public constant bondPriceFeed = address(0x5a1b1E88A4E0a7635c965DF8e312264e2d8C59a2);
  address public constant ethPriceFeed = address(0x71041dddad3595F9CEd3DcCFBe3D1F4b0a16Bb70);
  uint256 public constant CHAINLINK_DECIMAL_PRECISION = 10**8;
  uint8 public constant CHAINLINK_DECIMAL = 8;

  function setUp() public {
    vm.startPrank(deployer);

    address contractDeployer = address(new Deployer());
    oracleFeedsContract = address(new OracleFeeds());

    address poolBeacon = address(new UpgradeableBeacon(address(new Pool()), governance));
    address bondBeacon = address(new UpgradeableBeacon(address(new BondToken()), governance));
    address levBeacon = address(new UpgradeableBeacon(address(new LeverageToken()), governance));
    address distributorBeacon = address(new UpgradeableBeacon(address(new Distributor()), governance));

    poolFactory = PoolFactory(Utils.deploy(address(new PoolFactory()), abi.encodeCall(
      PoolFactory.initialize, 
      (governance, contractDeployer, oracleFeedsContract, poolBeacon, bondBeacon, levBeacon, distributorBeacon)
    )));

    params.fee = 0;
    params.feeBeneficiary = governance;
    params.reserveToken = address(new Token("Wrapped ETH", "WETH", false));
    params.sharesPerToken = 20e6;
    params.distributionPeriod = 10 days;
    params.couponToken = address(new Token("USDC", "USDC", false));
    
    OracleFeeds(oracleFeedsContract).setPriceFeed(params.reserveToken, address(0), ethPriceFeed, 1 days);

    // Deploy the mock price feed
    mockPriceFeed = new MockPriceFeed();

    // Use vm.etch to deploy the mock contract at the specific address
    bytes memory bytecode = address(mockPriceFeed).code;
    vm.etch(ethPriceFeed, bytecode);

    // Set oracle price
    mockPriceFeed = MockPriceFeed(ethPriceFeed);
    mockPriceFeed.setMockPrice(3000 * int256(CHAINLINK_DECIMAL_PRECISION), uint8(CHAINLINK_DECIMAL));
    
    vm.stopPrank();

    vm.startPrank(governance);
    poolFactory.grantRole(poolFactory.POOL_ROLE(), governance);
    poolFactory.grantRole(poolFactory.SECURITY_COUNCIL_ROLE(), securityCouncil);

    vm.stopPrank();
  }

  // @audit-poc
  function testNotInAuctionModifier() public {
    vm.startPrank(governance);

    Token rToken = Token(params.reserveToken);
    rToken.mint(governance, 1000e18);
    rToken.approve(address(poolFactory), 500e18);

    // Create pool and approve deposit amount
    pool = Pool(poolFactory.createPool(params, 450e18, 10000e18, 9000e18, "", "", "", "", false));
    pool.setAuctionPeriod(5 days);
    rToken.approve(address(pool), 1000e18);
    vm.stopPrank();

    Pool.PoolInfo memory poolInfo = pool.getPoolInfo();
    assertEq(poolInfo.distributionPeriod, 10 days);
    assertEq(poolInfo.auctionPeriod, 5 days);
    assertEq(poolInfo.sharesPerToken, 20e6);

    vm.warp(block.timestamp + 11 days);
    pool.startAuction();

    vm.startPrank(governance);

    // The governance changes pool parameters even while in auction
    vm.expectEmit(true, true, true, true);
    emit Pool.DistributionPeriodChanged(10 days, 15 days);
    pool.setDistributionPeriod(15 days);

    vm.expectEmit(true, true, true, true);
    emit Pool.AuctionPeriodChanged(5 days, 10 days);
    pool.setAuctionPeriod(10 days);

    vm.expectEmit(true, true, true, true);
    emit Pool.SharesPerTokenChanged(30e6);
    pool.setSharesPerToken(30e6);

    vm.stopPrank();
  }
}
```

### Mitigation

Consider increasing `currentPeriod` after the auction has ended like in `transferReserveToAuction()` function.

