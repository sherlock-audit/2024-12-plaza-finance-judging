Crazy Carmine Corgi

Medium

# Pool#NotInAuctionModifier is completely broken

### Summary

The NotInAuction modifier is intended to prevent sensitive settings from being changed while an auction is ongoing. Due to how auctions are created and map this modifier will never work, which disables a key safeguard. Although governance may manually wait for auctions to finish before calling, according to Sherlock rules if the issue can be triggered due to admin ignorance then the issue is valid.

[Pool.sol#L750-L754](https://github.com/sherlock-audit/2024-12-plaza-finance/blob/main/plaza-evm/src/Pool.sol#L750-L754)

        modifier NotInAuction() {
            (uint256 currentPeriod,) = bondToken.globalPool();
    @>      require(auctions[currentPeriod] == address(0), AuctionIsOngoing());
            _;
        }

Above we see that it attempts to pull the auction from the current period to check if it is ongoing. However during the creation of the auction the currentPeriod is incremented meaning that this check will always pass even if there is an active auction.

### Root Cause

[Pool.sol#L752](https://github.com/sherlock-audit/2024-12-plaza-finance/blob/main/plaza-evm/src/Pool.sol#L752) is incorrect and pulls the wrong period and fails to check if the auction is running

### Internal preconditions

None

### External preconditions

None

### Attack Path

N/A. Function is simply broken

### Impact

Sensitive settings may be changed at the wrong time leading to contract disfunction

### POC

Tests for all vulnerabilities can be found [here](https://gist.github.com/IAm0x52/05589415ce45af83aa4f7a5f63afbf45).

Insert the following test into Pool.t.sol

    function testNotInAuction() public {
        //test setup
        Token rToken = Token(params.reserveToken);
        Token cToken = Token(params.couponToken);

        rToken.mint(governance, 1e20);
        vm.prank(governance);
        rToken.approve(address(poolFactory), 1e20);

        string memory salt = "test";
        PoolFactory.PoolParams memory _params = params;
        
        // create pool
        vm.prank(governance);
        Pool _pool = Pool(poolFactory.createPool(_params, 1e20, 1e20, 1e20, "", salt, "", "", false));

        vm.prank(governance);
        _pool.setAuctionPeriod(365 days);

        vm.warp(block.timestamp + 1);

        _pool.startAuction();

        address auction = _pool.auctions(0);

        vm.prank(governance);

        // this call should fail because the auction is ongoing but it doesn't
        _pool.setAuctionPeriod(1 days);

        assert(Auction(auction).state() == Auction.State.BIDDING);
    }

    Output:
    [PASS] testNotInAuction() (gas: 3814937)

The call to setAuctionPeriod should fail as we have an active auction that is in the bidding state but it still succeeds due to the modifier being broken

### Mitigation

Pool#NotInAuction() should pull the auction for the previous period and then check if `block.timestamp < endTime`