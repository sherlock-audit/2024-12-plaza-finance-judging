Creamy Misty Rooster

High

# Coupon Shares are allocated even if auction fails, resulting in users unable to claim rewards

### Summary

If an auction fails (due to `FAILED_POOL_SALE_LIMIT` or `FAILED_UNDERSOLD`), it cannot be restarted, and the rewards distribution for that period is skipped. 
However, the `startAuction()` function already records the rewards share for that period in `globalPool.previousPoolAmounts` by invoking `BondToken::increaseIndexedAssetPeriod`. This creates a mismatch between the users' calculated rewards, as determined by `BondToken::getIndexedUserAmount`, and the actual coupon funds available in the Distributor contract. 
Consequently, many bondholders are unable to claim their rewards due to insufficient funds in the contract.

### Root Cause

- The `startAuction()` function records the rewards share for a period in the `globalPool.previousPoolAmounts` state, regardless of whether the auction succeeds or fails.

- It is not possible to restart an auction for the same period if the initial auction fails.

- Rewards shares are still recorded for bondholders, even though the rewards are not distributed during a failed auction.

These issues result in incorrect internal share accounting. When `BondToken::getIndexedUserAmount` is invoked, the calculated rewards do not match the actual funds available in the Distributor contract, preventing some bondholders from claiming their rewards.

### Internal Pre-conditions

None 

### External Pre-conditions

None 

### Attack Path

None

### Impact

* Bond holders will be unable to claim their rewards from the Distributor, not only for the periods were auctions failed but for all others periods as well.

### PoC


For each distribution period, a new auction is initiated to collect the required coupon tokens for rewarding bondholders. This process is handled by the [`startAuction`](https://github.com/sherlock-audit/2024-12-plaza-finance/blob/main/plaza-evm/src/Pool.sol#L530-L571) function:

```solidity
function startAuction() external whenNotPaused() {
    ...

    // Ensure that an auction for the current period hasn't already started
    (uint256 currentPeriod,) = bondToken.globalPool();
    require(auctions[currentPeriod] == address(0), AuctionAlreadyStarted());

    // Determine the maximum precision for calculations
    uint8 bondDecimals = bondToken.decimals();
    uint8 sharesDecimals = bondToken.SHARES_DECIMALS();
    uint8 maxDecimals = bondDecimals > sharesDecimals ? bondDecimals : sharesDecimals;

    // Normalize the bond token supply and shares-per-token for consistent calculations
    uint256 normalizedTotalSupply = bondToken.totalSupply().normalizeAmount(bondDecimals, maxDecimals);
    uint256 normalizedShares = sharesPerToken.normalizeAmount(sharesDecimals, maxDecimals);

    // Calculate the amount of coupons needed for distribution
    uint256 couponAmountToDistribute = (normalizedTotalSupply * normalizedShares)
        .toBaseUnit(maxDecimals * 2 - IERC20(couponToken).safeDecimals());

    // Deploy a new auction contract for the current period
    auctions[currentPeriod] = Utils.deploy(
        address(new Auction()),
        abi.encodeWithSelector(
            Auction.initialize.selector,
            address(couponToken),
            address(reserveToken),
            couponAmountToDistribute,
            block.timestamp + auctionPeriod,
            1000,
            address(this),
            poolSaleLimit
        )
    );

    // Update the bond token period with the current shares per token
    bondToken.increaseIndexedAssetPeriod(sharesPerToken);

    // Record the timestamp of the last distribution
    lastDistribution = block.timestamp;
}
```

This function calculates the coupon amount required, deploys a new auction contract, and records the current shares per token (`globalPool.sharesPerToken`) in `globalPool.previousPoolAmounts` via the [`BondToken::increaseIndexedAssetPeriod`](https://github.com/sherlock-audit/2024-12-plaza-finance/blob/main/plaza-evm/src/BondToken.sol#L217-L225) function.  

The auction outcome is determined by the [`endAuction`](https://github.com/sherlock-audit/2024-12-plaza-finance/blob/main/plaza-evm/src/Auction.sol#L158-L190) function, which can result in one of three states:

```solidity
function endAuction() external auctionExpired whenNotPaused {
    if (state != State.BIDDING) revert AuctionAlreadyEnded();

    // Determine the auction's success or failure
    if (currentCouponAmount < totalBuyCouponAmount) {
        state = State.FAILED_UNDERSOLD;
    } else if (totalSellReserveAmount >= (IERC20(sellReserveToken).balanceOf(pool) * poolSaleLimit) / 100) {
        state = State.FAILED_POOL_SALE_LIMIT;
    } else {
        state = State.SUCCEEDED;

        // Handle successful auction
        Pool(pool).transferReserveToAuction(totalSellReserveAmount);
        IERC20(buyCouponToken).safeTransfer(beneficiary, IERC20(buyCouponToken).balanceOf(address(this)));
    }

    emit AuctionEnded(state, totalSellReserveAmount, totalBuyCouponAmount);
}
```

If the auction **succeeds**, coupons are transferred to the Distributor contract, and everything functions as expected.  
However, if the auction **fails** due to insufficient funds (`FAILED_UNDERSOLD`) or exceeding the pool sale limit (`FAILED_POOL_SALE_LIMIT`), no coupons are distributed to the Distributor contract for that period.

### Problem: Failed Auction and Rewards Distribution

When users claim their rewards, the system calculates their shares based on `globalPool.previousPoolAmounts`, which already includes the failed auction's shares per token. This creates a mismatch because the coupons for the failed auction were never allocated.  

The issue arises in the `Distributor::claim` function:  

```solidity
function claim() external whenNotPaused nonReentrant {
    BondToken bondToken = Pool(pool).bondToken();
    address couponToken = Pool(pool).couponToken();

    if (address(bondToken) == address(0) || couponToken == address(0)){
        revert UnsupportedPool();
    }

    (uint256 currentPeriod,) = bondToken.globalPool();
    uint256 balance = bondToken.balanceOf(msg.sender);

    // Calculate user's accrued shares
    uint256 shares = bondToken.getIndexedUserAmount(msg.sender, balance, currentPeriod)
                              .normalizeAmount(bondToken.decimals(), IERC20(couponToken).safeDecimals());

    ...

    couponAmountToDistribute -= shares;
    bondToken.resetIndexedUserAssets(msg.sender);
    IERC20(couponToken).safeTransfer(msg.sender, shares);

    emit ClaimedShares(msg.sender, currentPeriod, shares);
}
```

The user's accrued shares are computed using `globalPool.previousPoolAmounts.sharesPerToken` as shown in `getIndexedUserAmount` below :

```solidity
function getIndexedUserAmount(address user, uint256 balance, uint256 period) public view returns (uint256) {
    IndexedUserAssets memory userPool = userAssets[user];
    uint256 shares = userPool.indexedAmountShares;

    // Accumulate shares for all periods since the last update
    for (uint256 i = userPool.lastUpdatedPeriod; i < period; i++) {
        shares += (balance * globalPool.previousPoolAmounts[i].sharesPerToken).toBaseUnit(SHARES_DECIMALS);
    }

    return shares;
}
```

- **Failed auctions** still record shares in `globalPool.previousPoolAmounts`, inflating the users' expected rewards.
- When users attempt to claim, the Distributor contract lacks sufficient funds due to the failed auction, causing claims to fail.
- Late claimants may be entirely unable to claim their rewards, even for successful auctions, because of incorrect internal share calculations.

This problem persists because failed auctions cannot be restarted in the same distribution period due to the restriction in `startAuction`:  

```solidity
require(lastDistribution + distributionPeriod < block.timestamp, DistributionPeriodNotPassed());
```

### Mitigation

The simplest way to solve this issue is to allow an auction to be restarted during the same period if it has previously failed.

Another option is to delay the call to `BondToken::increaseIndexedAssetPeriod` until the auction succeeds, and invoke it in the `transferReserveToAuction` function. However, this would require additional changes in the Pool code, as the `currentPeriod` would not be increased at the auction's start.