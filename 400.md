Fun Ceramic Mustang

High

# Invalid index used to fetch auction's address will prevent any auction from successfully ending

### Summary

Off-by-one error in Pool's `transferReserveToAuction` function will cause every call to `endAuction()` to revert. This freezes the bidders' funds and severly impacts the protocol functionality.

### Root Cause

Protocol allows anyone to start new auction by calling the [startAuction](https://github.com/sherlock-audit/2024-12-plaza-finance/blob/14a962c52a8f4731bbe4655a2f6d0d85e144c7c2/plaza-evm/src/Pool.sol#L530) function:
```solidity
  function startAuction() external whenNotPaused() {
     ...
    // Check if auction for current period has already started
    (uint256 currentPeriod,) = bondToken.globalPool();
    require(auctions[currentPeriod] == address(0), AuctionAlreadyStarted());
    ...
    auctions[currentPeriod] = Utils.deploy(
      address(new Auction()),
      abi.encodeWithSelector(
        Auction.initialize.selector,
        address(couponToken),
        address(reserveToken),
        couponAmountToDistribute,
        block.timestamp + auctionPeriod,
        1000,
        address(this),
        poolSaleLimit
      )
    );

    // Increase the bond token period
    bondToken.increaseIndexedAssetPeriod(sharesPerToken);
  }
```

Function will access the `currentPeriod` index from the bondToken's config. Then new auction contract is deployed and its address is stored in `auctions` array. First index will be 0. Immediately after, [increaseIndexedAssetPeriod](https://github.com/sherlock-audit/2024-12-plaza-finance/blob/14a962c52a8f4731bbe4655a2f6d0d85e144c7c2/plaza-evm/src/BondToken.sol#L217C1-L229C4) is called which bumps the `currentPeriod` counter:
```solidity
  function increaseIndexedAssetPeriod(uint256 sharesPerToken) public onlyRole(DISTRIBUTOR_ROLE) whenNotPaused() {
    ...
    globalPool.currentPeriod++;
    ...
  }
```

So, if the auction created is the first one ever, it will be stored at index 0 and `currentPeriod ` will be subsequently increased to 1.

Now that auction is active bidders can submit their bids which move the coupon tokens (USDC)  from bidders to the auction contract. After auction time has finished, anyone can call [endAuction](https://github.com/sherlock-audit/2024-12-plaza-finance/blob/14a962c52a8f4731bbe4655a2f6d0d85e144c7c2/plaza-evm/src/Auction.sol#L336) function:
```solidity
  function endAuction() external auctionExpired whenNotPaused {
    ...
    } else {
      state = State.SUCCEEDED;
      Pool(pool).transferReserveToAuction(totalSellReserveAmount);
      IERC20(buyCouponToken).safeTransfer(beneficiary, IERC20(buyCouponToken).balanceOf(address(this)));
    }
  }
```

If enough coupon tokens have been collected contract calls pool's [transferReserveToAuction](https://github.com/sherlock-audit/2024-12-plaza-finance/blob/14a962c52a8f4731bbe4655a2f6d0d85e144c7c2/plaza-evm/src/Pool.sol#L577) function to transfer the tokens from the pool to the auction. In this function's implementation lies the root cause:
```solidity
  function transferReserveToAuction(uint256 amount) external virtual {
    (uint256 currentPeriod, ) = bondToken.globalPool();
    // @audit accessing the array index which contains nothing
    address auctionAddress = auctions[currentPeriod];
    // @audit msg.sender is compared to address(0) and thus always fails 
    require(msg.sender == auctionAddress, CallerIsNotAuction());
    
    IERC20(reserveToken).safeTransfer(msg.sender, amount);
```

Function will fetch the auction address from index `currentPeriod` instead of `currentPeriod  - 1`.  That index will always contain empty values, so the msg.sender check in the next line will always fail because it will be compared to zero address. So TX will revert and auction cannot be finalized.

Now all the USDC funds are stuck in auction contract and cannot be recovered by the bidders.




### Internal Pre-conditions

1. Auction is started by anyone calling `startAuction`
2. Auction end time has passed
3. Enough bids are collected to successfully end the auction
4. Anyone calls `endAuction()` - this will always fail

### External Pre-conditions

None

### Attack Path

This is an issue in the happy path logic caused by the bug in the code. It will be triggered as soon as first auction collects enough bids.

### Impact

All the coupon tokens collected from bidders are stuck in auction contract. Also, since auction cannot be finalized, bond token holders cannot get their coupons. That breaks one of the main protocol features.

### PoC

Add this to `Pool.t.sol`

```solidity
    function test_Audit_EndAuction() public {
        // fund the deployer
        vm.startPrank(governance);
        uint256 reserveAmount = 1 ether;
        deal(params.reserveToken, governance, reserveAmount);
        Token(params.reserveToken).approve(address(poolFactory), reserveAmount);

        // deploy the pool
        Pool _pool = Pool(
            poolFactory.createPool({
                params: PoolFactory.PoolParams({
                    fee: 40000,
                    feeBeneficiary: address(this),
                    reserveToken: params.reserveToken,
                    sharesPerToken: 2_500_000,
                    distributionPeriod: 90 days,
                    couponToken: params.couponToken
                }),
                reserveAmount: reserveAmount,
                bondAmount: 10 ether,
                leverageAmount: 20 ether,
                bondName: "Bond ETH",
                bondSymbol: "bondETH",
                leverageName: "Levered ETH",
                leverageSymbol: "levETH",
                pauseOnCreation: false
            })
        );
        vm.stopPrank();

        // create some bond tokens
        address alice = makeAddr("alice");
        vm.startPrank(alice);
        deal(params.reserveToken, alice, 5e18);
        Token(params.reserveToken).approve(address(_pool), 5e18);
        _pool.create({
            tokenType: Pool.TokenType.BOND,
            depositAmount: 5e18,
            minAmount: 0,
            deadline: block.timestamp,
            onBehalfOf: alice
        });
        vm.stopPrank();

        // set 10 days auction period
        vm.startPrank(governance);
        _pool.setAuctionPeriod(10 days);

        // start auction after distribution period passed
        vm.warp(block.timestamp + _pool.getPoolInfo().distributionPeriod + 1);
        _pool.startAuction();
        vm.stopPrank();

        // bid - single bid that bids full amount of coupon amount
        address bob = makeAddr("bob");
        Auction auction = Auction(_pool.auctions(0));
        uint256 couponAmount = auction.totalBuyCouponAmount();
        deal(params.couponToken, bob, couponAmount);
        vm.startPrank(bob);
        Token(params.couponToken).approve(address(auction), couponAmount);
        auction.bid({buyReserveAmount: 1 ether, sellCouponAmount: couponAmount});
        vm.stopPrank();

        // try to end the auction
        vm.warp(block.timestamp + 10 days);
        // @audit this will fail every time.
        auction.endAuction();

        // check auction successfully ended
        console.log("Auction ended!");
        assertEq(uint256(auction.state()), uint256(Auction.State.SUCCEEDED));
    }
```

Run it:
```solidity
❯ forge test --mt test_Audit_EndAuction

Ran 1 test for test/Pool.t.sol:PoolTest
[FAIL: CallerIsNotAuction()] test_Audit_EndAuction() (gas: 8011853)
Suite result: FAILED. 0 passed; 1 failed; 0 skipped; finished in 18.51ms (7.84ms CPU time)
```

If mitigation is applied:
```solidity
❯ forge test --mt test_Audit_EndAuction -vvv

Ran 1 test for test/Pool.t.sol:PoolTest
[PASS] test_Audit_EndAuction() (gas: 7629998)
Logs:
  Auction ended!
```

### Mitigation

```diff
  function transferReserveToAuction(uint256 amount) external virtual {
    (uint256 currentPeriod, ) = bondToken.globalPool();
-   address auctionAddress = auctions[currentPeriod];
+   address auctionAddress = auctions[currentPeriod - 1];
    require(msg.sender == auctionAddress, CallerIsNotAuction());
    
    IERC20(reserveToken).safeTransfer(msg.sender, amount);
  }
```