Fit Alabaster Goblin

High

# Not considering fees when ending an auction could incorrectly set an auction as succeeded

### Summary

When ending an auction,  fees are not considered to check if the `poolSaleLimit` was actually surpassed. This can lead to situations where an auction is set as succeeded, when in reality it should have been set as `FAILED_POOL_SALE_LIMIT`.

### Root Cause

In `Auction`'s `endAuction`, there’s a check to verify if the maximum allowed `poolSaleLimit` was reached when performing the auction. The `poolSaleLimit` acts as a safeguard that essentially limits the maximum amount of pool reserves that can be sold in an auction:

```solidity
// File: Auction.sol

function endAuction() external auctionExpired whenNotPaused {
    if (state != State.BIDDING) revert AuctionAlreadyEnded();

    if (currentCouponAmount < totalBuyCouponAmount) {
      state = State.FAILED_UNDERSOLD;
    } else if (totalSellReserveAmount >= (IERC20(sellReserveToken).balanceOf(pool) * poolSaleLimit) / 100) { 
        state = State.FAILED_POOL_SALE_LIMIT;
    } else {
      state = State.SUCCEEDED;
      Pool(pool).transferReserveToAuction(totalSellReserveAmount);
      IERC20(buyCouponToken).safeTransfer(beneficiary, IERC20(buyCouponToken).balanceOf(address(this)));
    }

    emit AuctionEnded(state, totalSellReserveAmount, totalBuyCouponAmount);
  }
```

The problem lies in the `if(totalSellReserveAmount >= (IERC20(sellReserveToken).balanceOf(pool) * poolSaleLimit) / 100)` check. This logic directly queries the reserve token’s pool balance, without considering fees. In reality, a portion of the whole balance in the pool does not actually correspond to the pool, but is taken out as a protocol fee. This can lead to a situation where an auction is set to `SUCCEEDED` state, when in reality it should have failed and set to `FAILED_POOL_SALE_LIMIT`.

### Internal Pre-conditions

_No response_

### External Pre-conditions

_No response_

### Attack Path

Consider the following scenario. An auction has expired and the `endAuction` function is called. Let’s consider that the `currentCouponAmount` bought was enough to cover the `totalBuyCouponAmount`, so the first requirement in `endAuction` passes. In addition, let’s consider that the `totalSellReserveAmount` is 85,  the pool’s reserve balance is 100, and that the `poolSaleLimit` is set to a 90%.

1. In the second requirement to see if the pool limit was surpassed, the current calculations show that the auction has succeeded. This is because `totalSellReserveAmount`, which is 85, is smaller than `IERC20(sellReserveToken).balanceOf(pool) * poolSaleLimit) / 100` , which is 100 * 90 / 100 = 90. Because of this, the code logic directly enters the `else` statement and sets the state of the auction to `SUCCEEDED`.
2. However, let’s consider that the current protocol fee, [given by the `getFeeAmount` function](https://github.com/sherlock-audit/2024-12-plaza-finance/blob/ab5bbd7d54042e4444b711a32def5b639fbd63b4/plaza-evm/src/Pool.sol#L698), is 15. This actually reduces the pool balance from the initial 100 to an actual 85 tokens. Because of this, the check should have been 85 ≥ (85 * 90 / 100), which is false as (85 * 90 / 100) = 76,5. In this case, `totalSellReserveAmount` **is actually bigger than the pool balance, considering fees and applying the pool sale limit percentage.** This is the actual check that should have been performed, as it properly considers the fees in the pool. In this scenario, the auction would have correctly been set to `FAILED_POOL_SALE_LIMIT` state.

### Impact

As demonstrated, this issue can lead to an auction being set as succeeded, when in reality the pool sale limit was surpassed and should instead have been set to `FAILED_POOL_SALE_LIMIT` state.

The impact of this vulnerability is high, given that overselling more reserve assets than the ones protected by the pool sale limit can lead to a situation where there is not enough assets in the pool to allow redemptions for protocol depositors. Following the previous example, given that `poolSaleLimit` was configured to 90, this means that 90% of the pool can be sold in auctions. However, considering the additional fees, the pool can be left with zero assets:

- From the initial 100 tokens, 85 are substracted from the auction, leaving 15 tokens remaining.
- Then, the `feeBeneficiary` calls `claimFees` and claims their entitled 15 tokens in fees. This leaves a total of 0 assets in the pool.
- Because of this, there is no liquidity in the pool, and users can’t redeem, as the amount of ETH was oversold, breaking a core mechanic in the protocol and causing a loss of funds for users.

### PoC

_No response_

### Mitigation

Substract the corresponding fees when checking if a pool sale limit was reached:

```diff
// File: Auction.sol

function endAuction() external auctionExpired whenNotPaused {
    if (state != State.BIDDING) revert AuctionAlreadyEnded();

    if (currentCouponAmount < totalBuyCouponAmount) {
      state = State.FAILED_UNDERSOLD;
-    } else if (totalSellReserveAmount >= (IERC20(sellReserveToken).balanceOf(pool) * poolSaleLimit) / 100) { 
+    } else if (totalSellReserveAmount >= ((IERC20(sellReserveToken).balanceOf(pool) - Pool(pool).getFeeAmount()) * poolSaleLimit) / 100) { 
        state = State.FAILED_POOL_SALE_LIMIT;
    } else {
      state = State.SUCCEEDED;
      Pool(pool).transferReserveToAuction(totalSellReserveAmount);
      IERC20(buyCouponToken).safeTransfer(beneficiary, IERC20(buyCouponToken).balanceOf(address(this)));
    }

    emit AuctionEnded(state, totalSellReserveAmount, totalBuyCouponAmount);
  }
```