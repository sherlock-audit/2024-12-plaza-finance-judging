Immense Orchid Gecko

High

# Pool uses incorrect `currentPeriod` in `transferReserveToAuction(uint256 amount)`

### Summary

Incorrect `currentPeriod` value to access `auctions` mapping will always cause `endAuction()` in the `Auction` contract to revert, breaking `Auction` functionality.

### Root Cause

The root cause is the wrong `currentPeriod` value used in `transferReserveToAuction()` in `Pool`

```solidity
function transferReserveToAuction(uint256 amount) external virtual { 
   (uint256 currentPeriod, ) = bondToken.globalPool(); 
   address auctionAddress = auctions[currentPeriod]; 
   require(msg.sender == auctionAddress, CallerIsNotAuction()); 
    
   IERC20(reserveToken).safeTransfer(msg.sender, amount); 
 } 
```

### Internal Pre-conditions

_No response_

### External Pre-conditions

_No response_

### Attack Path

Whenever an auction is started via `pool.startAuction()`, the pool will 

1. First call `bondToken.globalPool()` to get the `currentPeriod`. At the very start, that `currentPeriod` will be `0`.
2. Set `auctions[0]` to be the `Auction` address.
3. call  `bondToken.increaseIndexedAssetPeriod(sharesPerToken)` to increment `currentPeriod`. `currentPeriod` is now `1`.

Code is as such:
https://github.com/sherlock-audit/2024-12-plaza-finance/blob/main/plaza-evm/src/Pool.sol#L537-L565

When the `Auction` ends, and `endAuction()`is called and if the `Auction` succeeds, this code will be executed:
https://github.com/sherlock-audit/2024-12-plaza-finance/blob/main/plaza-evm/src/Auction.sol#L344-L346

In `Pool`, the `transferReserveToAuction()` function is shown as such:

https://github.com/sherlock-audit/2024-12-plaza-finance/blob/main/plaza-evm/src/Pool.sol#L577-L583

`currentPeriod` is now `1`, and `auctions[currentPeriod]` will return `address(0)` as it is not set yet, causing the `CallerIsNotAuction()` revert to always occur.

### Impact

Auction cannot be ended, breaking protocol functionality.

### PoC

```solidity
// SPDX-License-Identifier: UNLICENSED
pragma solidity ^0.8.26;

import "forge-std/Test.sol";

import {Pool} from "../src/Pool.sol";
import {Auction} from "../src/Auction.sol";
import {Utils} from "../src/lib/Utils.sol";
import {MockPool} from "./mocks/MockPool.sol";
import {BondToken} from "../src/BondToken.sol";
import {TestCases} from "./data/TestCases.sol";
import {Decimals} from "../src/lib/Decimals.sol";
import {PoolFactory} from "../src/PoolFactory.sol";
import {Distributor} from "../src/Distributor.sol";
import {OracleFeeds} from "../src/OracleFeeds.sol";
import {Validator} from "../src/utils/Validator.sol";
import {OracleReader} from "../src/OracleReader.sol";
import {LeverageToken} from "../src/LeverageToken.sol";
import {MockPriceFeed} from "./mocks/MockPriceFeed.sol";
import {Deployer} from "../src/utils/Deployer.sol";
import {BalancerRouter} from "../src/BalancerRouter.sol";
import {Strings} from "@openzeppelin/contracts/utils/Strings.sol";
import {UpgradeableBeacon} from "@openzeppelin/contracts/proxy/beacon/UpgradeableBeacon.sol";
import {IERC20} from "@openzeppelin/contracts/token/ERC20/IERC20.sol";
import {PreDeposit} from "../src/PreDeposit.sol";
import {IVault} from "@balancer/contracts/interfaces/contracts/vault/IVault.sol";
import {IAsset} from "@balancer/contracts/interfaces/contracts/vault/IAsset.sol";
import {SafeERC20} from "@openzeppelin/contracts/token/ERC20/utils/SafeERC20.sol";
import {WeightedPoolUserData} from "@balancer/contracts/interfaces/contracts/pool-weighted/WeightedPoolUserData.sol";
import {StablePoolUserData} from "@balancer/contracts/interfaces/contracts/pool-stable/StablePoolUserData.sol";

contract EndAuctionPOC is Test, TestCases {
    enum TokenType {
        BOND, // bond
        LEVERAGE
    }

    using SafeERC20 for IERC20;

    PreDeposit public preDeposit;

    using Decimals for uint256;
    using Strings for uint256;

    MockPriceFeed public mockPriceFeed;

    PoolFactory private poolFactory;
    PoolFactory.PoolParams private params;
    Pool public pool;
    Distributor public distributor;
    BondToken public bondToken;
    LeverageToken public leverageToken;

    OracleFeeds private oracleFeedsContract;
    BalancerRouter public balancerRouter;

    address private deployer = makeAddr("deployer");
    address private minter = makeAddr("minter");
    address private governance = makeAddr("governance");
    address private securityCouncil = makeAddr("security council");
    address private user1 = makeAddr("user1");
    address private user2 = makeAddr("user2");
    address private user3 = makeAddr("user3");
    address private user4 = makeAddr("user4");
    address private user5 = makeAddr("user5");
    address private user6 = makeAddr("user6");
    uint256 constant RESERVE_CAP = 100 ether;

    IERC20 baseReth = IERC20(address(0xB6fe221Fe9EeF5aBa221c348bA20A1Bf5e73624c));
    IERC20 baseWeth = IERC20(address(0x4200000000000000000000000000000000000006));
    IERC20 baseUSDC = IERC20(address(0x833589fCD6eDb6E08f4c7C32D4f71b54bdA02913));
    address cbBtc = address(0xcbB7C0000aB88B473b1f5aFd9ef808440eed33Bf);

    address public constant ethPriceFeed = address(0x71041dddad3595F9CEd3DcCFBe3D1F4b0a16Bb70);

    address baseUSDCWhale = address(0x20FE51A9229EEf2cF8Ad9E89d91CAb9312cF3b7A);
    address baseWethWhale = address(0x3a8A1F045cd4F7246c6B3A78861269CC6065433a);
    address anotherBaseWethWhale = address(0xD4a0e0b9149BCee3C920d2E00b5dE09138fd8bb7);
    address cbBtcWhale = address(0x15abb66bA754F05cBC0165A64A11cDed1543dE48);
    address baseRethWhale = address(0xe5EBcDE141e98c23c150bc350B72C23fDCb747c1);

    uint256 private constant CHAINLINK_DECIMAL_PRECISION = 10 ** 8;
    uint8 private constant CHAINLINK_DECIMAL = 8;
    uint256 private constant reserveAmount = 0.001 ether;
    uint256 private constant bondAmount = 0.025 ether;
    uint256 private constant leverageAmount = 0.001 ether;
    IVault public baseBalancerVault = IVault(address(0xBA12222222228d8Ba445958a75a0704d566BF2C8));
    // weth-reth balancerPoolId
    bytes32 balancerPoolId = 0xc771c1a5905420daec317b154eb13e4198ba97d0000000000000000000000023;
    // weth-reth balancer pool =
    address balancerPool = address(0xC771c1a5905420DAEc317b154EB13e4198BA97D0);
    uint256 constant FORK_BLOCK_NUMBER = 5_500_000;

    function giveMoneyToUsers() public {

   
        vm.startPrank(anotherBaseWethWhale);
        baseWeth.transfer(user1, 110e18);
        baseWeth.transfer(user2, 110e18);
        baseWeth.transfer(user3, 110e18);
        baseWeth.transfer(user4, 110e18);
        baseWeth.transfer(user5, 110e18);
        vm.stopPrank();
        // give users usdc
        vm.startPrank(baseUSDCWhale);
        // let's say 10k usdc each

        baseUSDC.transfer(user1, 10_000_000_000);
        baseUSDC.transfer(user2, 10_000_000_000);
        baseUSDC.transfer(user3, 10_000_000_000);
        baseUSDC.transfer(user4, 10_000_000_000);
        baseUSDC.transfer(user5, 10_000_000_000);
        vm.stopPrank();

        vm.startPrank(baseRethWhale);
        baseReth.transfer(user1, 10e18);
        baseReth.transfer(user2, 10e18);
        baseReth.transfer(user3, 10e18);
        baseReth.transfer(user4, 10e18);
        baseReth.transfer(user5, 10e18);
        vm.stopPrank();
        
        
    }


    function setUp() public {
        string memory BASE_RPL_URL = vm.envString("BASE_RPC_URL");
        uint256 baseFork = vm.createFork(BASE_RPL_URL, 20265816);
        vm.selectFork(baseFork);
        vm.startPrank(baseWethWhale);
        baseWeth.transfer(deployer, 10e18);
        vm.stopPrank();
        // setup pool factory
        vm.startPrank(deployer);
        // give deployer some WETH

        address contractDeployer = address(new Deployer());
        oracleFeedsContract = new OracleFeeds();
        address poolBeacon = address(new UpgradeableBeacon(address(new Pool()), governance));

        address bondBeacon = address(new UpgradeableBeacon(address(new BondToken()), governance));
        
        address levBeacon = address(new UpgradeableBeacon(address(new LeverageToken()), governance));
       
        address distributorBeacon = address(new UpgradeableBeacon(address(new Distributor()), governance));

 
        poolFactory = PoolFactory(
            Utils.deploy(
                address(new PoolFactory()),
                abi.encodeCall(
                    PoolFactory.initialize,
                    (
                        governance,
                        contractDeployer,
                        address(oracleFeedsContract),
                        poolBeacon,
                        bondBeacon,
                        levBeacon,
                        distributorBeacon
                    )
                )
            )
        );

        params.fee = 20_000;
        params.feeBeneficiary = governance;
        params.reserveToken = address(baseWeth);
        params.sharesPerToken = 2_500_000;
        params.distributionPeriod = 7776000;
        params.couponToken = address(baseUSDC);

        // this one reverts
        oracleFeedsContract.setPriceFeed(params.reserveToken, address(0), ethPriceFeed, 1 days);

        IERC20(baseWeth).approve(address(poolFactory), reserveAmount);
        // this one is failing
        // need governance to grant POOL ROLE to deployer or predeposit
        vm.stopPrank();
        vm.startPrank(governance); //TODO: are you sure
        poolFactory.grantRole(poolFactory.POOL_ROLE(), deployer);
        poolFactory.grantRole(poolFactory.POOL_ROLE(), address(preDeposit));
        vm.stopPrank();
        vm.startPrank(deployer);
        pool = Pool(
            poolFactory.createPool(
                params, reserveAmount, bondAmount, leverageAmount, "Bond ETH", "bondETH", "Levered ETH", "levETH", false
            )
        );

        // make PreDeposit

        params.reserveToken = balancerPool;
        // This should be BPT

        preDeposit = PreDeposit(
            Utils.deploy(
                address(new PreDeposit()),
                abi.encodeCall(
                    PreDeposit.initialize,
                    (
                        params,
                        address(poolFactory),
                        block.timestamp,
                        block.timestamp + 7 days,
                        RESERVE_CAP,
                        "Bond ETH",
                        "bondETH",
                        "Levered ETH",
                        "levETH"
                    )
                )
            )
        );
        vm.stopPrank();

        // Give monies to all users
        giveMoneyToUsers();

        vm.startPrank(governance);
        poolFactory.grantRole(poolFactory.POOL_ROLE(), governance);
        poolFactory.grantRole(poolFactory.SECURITY_COUNCIL_ROLE(), securityCouncil);
        vm.stopPrank();
        bondToken = pool.bondToken();
        leverageToken = pool.lToken();
        distributor = Distributor(poolFactory.distributors(address(pool)));
    }

    function testAuctionStuff() public {
   
        vm.startPrank(governance);
        pool.setAuctionPeriod(10 days);
        vm.stopPrank();

        // do some bond create
        vm.startPrank(user1);
        baseWeth.approve(address(pool), type(uint256).max);

        assertTrue(address(pool) != address(0));

        uint256 depositAmount = 100e18;
        uint256 minAmount = uint256(10000);
      
        uint256 amountOfBondTokenMinted = pool.create(Pool.TokenType.BOND, depositAmount, minAmount);
        vm.warp(block.timestamp + 90 days + 1 seconds);
      
        pool.startAuction();
        address auctionAddr = pool.auctions(0);
      
        Auction auction = Auction(auctionAddr);
        uint256 max = auction.totalBuyCouponAmount();
        assertEq(auction.buyCouponToken(), address(baseUSDC));
        assertEq(auction.sellReserveToken(), address(baseWeth));
        uint256 slotSize = (auction.totalBuyCouponAmount() / auction.maxBids());
        uint256 maxUSDCAmountBySlots = slotSize * auction.maxBids();
        vm.stopPrank();
        
   
        vm.startPrank(user2);
        baseUSDC.approve(address(auction), type(uint256).max);
        auction.bid(10e18, maxUSDCAmountBySlots);
        vm.stopPrank();

        vm.startPrank(user3);
        baseUSDC.approve(address(auction), type(uint256).max); 
        auction.bid(0.5e18, (auction.totalBuyCouponAmount() / auction.maxBids()));

        vm.warp(block.timestamp+10 days + 1 seconds);
        vm.expectRevert(Pool.CallerIsNotAuction.selector);
        auction.endAuction();
        vm.stopPrank();
     
    }
}


```

### Mitigation

Change code as such
```diff
  function transferReserveToAuction(uint256 amount) external virtual {
    (uint256 currentPeriod, ) = bondToken.globalPool();
+ address auctionAddress = auctions[currentPeriod - 1];
- address auctionAddress = auctions[currentPeriod];
    require(msg.sender == auctionAddress, CallerIsNotAuction());
    
    IERC20(reserveToken).safeTransfer(msg.sender, amount);
  }
```