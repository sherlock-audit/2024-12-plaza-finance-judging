Nice Citron Tardigrade

High

# ​​getIndexedUserAmount​ calculates user rewards based on the balance at a specific period, ignoring the duration the user held BondTokens

### Summary
​​getIndexedUserAmount​ calculates user rewards based on the balance at a specific period, ignoring the duration the user held BondTokens. This may lead to users gaining coupons with minimal risk.

```solidity
function claim() external whenNotPaused nonReentrant {  
    BondToken bondToken = Pool(pool).bondToken();
    address couponToken = Pool(pool).couponToken();

    if (address(bondToken) == address(0) || couponToken == address(0)){
      revert UnsupportedPool();
    }

    (uint256 currentPeriod,) = bondToken.globalPool();
    uint256 balance = bondToken.balanceOf(msg.sender);
    uint256 shares = bondToken.getIndexedUserAmount(msg.sender, balance, currentPeriod)  // @audit Potential step-jump attack
                              .normalizeAmount(bondToken.decimals(), IERC20(couponToken).safeDecimals());
    ...
  }
```

When users attempt to call `Distributor.claim` to calculate rewards, the function calls `BondToken.getIndexedUserAmount`. Note that there is no time restriction for calling `Distributor.claim`.

```solidity
  function getIndexedUserAmount(address user, uint256 balance, uint256 period) public view returns(uint256) {
    IndexedUserAssets memory userPool = userAssets[user];
    uint256 shares = userPool.indexedAmountShares;

    for (uint256 i = userPool.lastUpdatedPeriod; i < period; i++) {
      shares += (balance * globalPool.previousPoolAmounts[i].sharesPerToken).toBaseUnit(SHARES_DECIMALS);
    }

    return shares;
  }
```

When calculating rewards using `BondToken.getIndexedUserAmount`, the function only considers the BondToken balance at a specific period. For accumulated rewards, the function iterates from `i = userPool.lastUpdatedPeriod` to `i = currentPeriod - 1`, summing up the rewards for each period. Note that the current period's rewards are not included, as they are only calculated when the period advances.

Regarding the start and end times of an auction, note the following:

```solidity
contract Auction is Initializable, UUPSUpgradeable, PausableUpgradeable {
  ...
  uint256 public endTime;

  ...
}
```

In the `Auction` contract, `endTime` is publicly accessible, allowing anyone to know the `endTime` of a specific auction. This means the end time of any period is predictable.

```solidity
  function startAuction() external whenNotPaused() { 
    // Check if distribution period has passed
    require(lastDistribution + distributionPeriod < block.timestamp, DistributionPeriodNotPassed());
    // Check if auction period hasn't passed
    require(lastDistribution + distributionPeriod + auctionPeriod >= block.timestamp, AuctionPeriodPassed());

    ...

    // Increase the bond token period
    bondToken.increaseIndexedAssetPeriod(sharesPerToken);
    // Update last distribution time
    lastDistribution = block.timestamp;  // @audit lastDistribution is updated when a new auction starts
  }
```

Each call to `Pool.startAuction` increases the bond token period and updates `lastDistribution`. Regarding the constraints:

```solidity
    require(lastDistribution + distributionPeriod < block.timestamp, DistributionPeriodNotPassed());
    require(lastDistribution + distributionPeriod + auctionPeriod >= block.timestamp, AuctionPeriodPassed());
```

Although these are private variables, they can still be directly read through storage slots.

```solidity
  uint256 private sharesPerToken;
  uint256 private distributionPeriod; // in seconds
  uint256 private auctionPeriod; // in seconds
  uint256 private lastDistribution; // timestamp in seconds
```

This allows an attacker to calculate the earliest time to call `Pool.startAuction` and execute it immediately, thereby controlling the start time of the next period. Here's the attack flow:

1. **End of Current Auction Period**: The attacker queries the `endTime` of the current auction period (let's say `n`) and deposits a large amount of reserve tokens just before it ends to receive a significant amount of BondTokens. At this point, the auction for period `n` ends, and the attacker holds a large number of BondTokens during period `n`.
2. **Manipulate Period Transition**: The attacker calculates the exact time to call `Pool.startAuction` and does so immediately, initiating the next period (`n+1`). The attacker then immediately redeems their BondTokens to withdraw their reserve tokens.
3. **Claim Excessive Rewards**: When calling Distributor.claim, only considering attacker's holdings during period = n, which is calculated as:

    ```solidity
    (balance * globalPool.previousPoolAmounts[n].sharesPerToken).toBaseUnit(SHARES_DECIMALS)
    ```

    The calculation ignores how long the BondTokens were actually held within the period. Since the attacker held a large number of BondTokens at the end of period = n, they can claim a disproportionate amount of rewards for that period, regardless of their actual holding duration.
4. **Exit Without Risk**: After starting the auction for period `n+1`, the attacker redeems their reserve tokens. This strategy allows the attacker to hold BondTokens for a minimal duration while earning substantial rewards, effectively a risk-free trade.



### Root Cause

Rewards are calculated based on the BondToken balance at a specific moment in the period, and attackers can manipulate the start and end times of periods.


### Affected Code

[https://github.com/sherlock-audit/2024-12-plaza-finance/blob/main/plaza-evm/src/BondToken.sol#L194-L196](https://github.com/sherlock-audit/2024-12-plaza-finance/blob/main/plaza-evm/src/BondToken.sol#L194-L196)


### Impact

Allows attackers to exploit the reward system for risk-free profits.


### Mitigation

Calculate rewards based on the actual duration for which BondTokens were held, rather than the balance at a specific moment in the period. This would prevent short-term exploitation and align rewards with genuine token holdings.