Wonderful Candy Manatee

High

# The funds of the user can be lost while accessing with `joinBalancerAndPredeposit()`

## Summary

The user can join the balancer pool with his/her assets and deposit to `preDeposit` using `BalancerRouter.joinBalancerAndPredeposit()`.

In the `PreDeposit.deposit()` function, it checks if `reserveAmount` is reached to `reserveCap` and only allows the user to deposit the rest up to the cap. This means that the amount user wants to deposit is not fully deposited.

When user invokes `BalancerRouter.joinBalancerAndPredeposit()`, the assets of user are swapped to reserve tokens, and `BalancerRouter` holds these reserve tokens. When reserve tokens are not fully used for deposit, the remaining should be sent to user, user can't receive remaining reserve tokens due to missing handling of this operation.

## Root Cause

In the [BalancerRouter.sol](https://github.com/sherlock-audit/2024-12-plaza-finance/blob/14a962c52a8f4731bbe4655a2f6d0d85e144c7c2/plaza-evm/src/BalancerRouter.sol#L23-L40) `joinBalancerAndPredeposit()` function, the user can pre deposit with his/her assets.

```solidity
    function joinBalancerAndPredeposit(
        bytes32 balancerPoolId,
        address _predeposit,
        IAsset[] memory assets,
        uint256[] memory maxAmountsIn,
        bytes memory userData
    ) external nonReentrant returns (uint256) {
        // Step 1: Join Balancer Pool
        uint256 balancerPoolTokenReceived = joinBalancerPool(balancerPoolId, assets, maxAmountsIn, userData);

        // Step 2: Approve balancerPoolToken for PreDeposit
        balancerPoolToken.safeIncreaseAllowance(_predeposit, balancerPoolTokenReceived);

        // Step 3: Deposit to PreDeposit
        PreDeposit(_predeposit).deposit(balancerPoolTokenReceived, msg.sender);

        return balancerPoolTokenReceived;
    }
```

The assets of user are swapped to `balancerPoolToken` (reserve token)invoking `joinBalancerPool()`, and contract holds this token. Then, it deposits this token to `PreDeposit`.

```solidity
    function joinBalancerPool(
        bytes32 poolId,
        IAsset[] memory assets,
        uint256[] memory maxAmountsIn,
        bytes memory userData
    ) internal returns (uint256) {
        // Transfer assets from user to this contract
        for (uint256 i = 0; i < assets.length; i++) {
            IERC20(address(assets[i])).safeTransferFrom(msg.sender, address(this), maxAmountsIn[i]);
            IERC20(address(assets[i])).safeIncreaseAllowance(address(balancerVault), maxAmountsIn[i]);
        }

        IVault.JoinPoolRequest memory request = IVault.JoinPoolRequest({
            assets: assets,
            maxAmountsIn: maxAmountsIn,
            userData: userData,
            fromInternalBalance: false
        });

        // Join Balancer pool
        uint256 balancerPoolTokenBalanceBefore = balancerPoolToken.balanceOf(address(this));
        balancerVault.joinPool(poolId, address(this), address(this), request);
        uint256 balancerPoolTokenBalanceAfter = balancerPoolToken.balanceOf(address(this));

        return balancerPoolTokenBalanceAfter - balancerPoolTokenBalanceBefore;
    }
```

In the [PreDeposit.sol](https://github.com/sherlock-audit/2024-12-plaza-finance/blob/14a962c52a8f4731bbe4655a2f6d0d85e144c7c2/plaza-evm/src/PreDeposit.sol#L125) `_deposit()` function, it checks if `reserveAmount` is reached to `reserveCap` and only allows the user to deposit the rest up to the cap.

Therefore, it could be possible the full amount of user's reserve token is not deposited when user would like to put more than available in cap.

When full amount of reserve token is not deposited, the rest reserve tokens are owed by `BalancerRouter` and user will lost the remaining reserve tokens.

```solidity
  function _deposit(uint256 amount, address onBehalfOf) private checkDepositStarted checkDepositNotEnded {
@>  if (reserveAmount >= reserveCap) revert DepositCapReached();

    address recipient = onBehalfOf == address(0) ? msg.sender : onBehalfOf;

    // if user would like to put more than available in cap, fill the rest up to cap and add that to reserves
    if (reserveAmount + amount >= reserveCap) {
@>    amount = reserveCap - reserveAmount;
    }

    balances[recipient] += amount;
    reserveAmount += amount;

    IERC20(params.reserveToken).safeTransferFrom(msg.sender, address(this), amount);

    emit Deposited(recipient, amount);
  }
```

## Internal pre-conditions

When reserve tokens which are swapped with user's assets are more than available in cap.

## External pre-conditions


## Attack Path


## Impact

The user won't receive remaining reserve tokens while depositing and lose funds.

## Mitigation

Implement sending back the remaining reserve token to user or swapping assets to the only available reserve token to be deposited.