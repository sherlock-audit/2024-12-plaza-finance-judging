Big Blush Boar

High

# transferReserveToAuction function will always reverts

### Summary

If globalPool.currentPeriod=x before calling pool::startAuction() function.
startAuction function will deploy a new Auction contract and update it's address in auctions mapping.like auctions[x]=new auction contract address.
https://github.com/sherlock-audit/2024-12-plaza-finance/blob/main/plaza-evm/src/Pool.sol#L530-L538
```solidity
  function startAuction() external whenNotPaused() {
    // Check if distribution period has passed
    require(lastDistribution + distributionPeriod < block.timestamp, DistributionPeriodNotPassed());


    // Check if auction period hasn't passed
    require(lastDistribution + distributionPeriod + auctionPeriod >= block.timestamp, AuctionPeriodPassed());


    // Check if auction for current period has already started
    (uint256 currentPeriod,) = bondToken.globalPool();
```
https://github.com/sherlock-audit/2024-12-plaza-finance/blob/main/plaza-evm/src/Pool.sol#L552-L564
```solidity
    auctions[currentPeriod] = Utils.deploy(
      address(new Auction()),
      abi.encodeWithSelector(
        Auction.initialize.selector,
        address(couponToken),
        address(reserveToken),
        couponAmountToDistribute,
        block.timestamp + auctionPeriod,
        1000,
        address(this),
        poolSaleLimit
      )
    );
```
after deploying new auction contract and updating auctions mapping, this function will call bondtoken::increaseIndexedAssetPeriod function(which will increase the globalpool.currentperiod.
https://github.com/sherlock-audit/2024-12-plaza-finance/blob/main/plaza-evm/src/Pool.sol#L567
```solidity
    bondToken.increaseIndexedAssetPeriod(sharesPerToken);
```
https://github.com/sherlock-audit/2024-12-plaza-finance/blob/main/plaza-evm/src/BondToken.sol#L217-L227
```solidity
  function increaseIndexedAssetPeriod(uint256 sharesPerToken) public onlyRole(DISTRIBUTOR_ROLE) whenNotPaused() {
    globalPool.previousPoolAmounts.push(
      PoolAmount({
        period: globalPool.currentPeriod,
        amount: totalSupply(),
        sharesPerToken: globalPool.sharesPerToken
      })
    );
    globalPool.currentPeriod++;
    globalPool.sharesPerToken = sharesPerToken;
```
now globalPool.currentPeriod = x+1.
after auction period ends we will call Auction::endAuction function which will end the auction.
But if the auction was SUCCEEDED, it will call Pool::transferReserveToAuction 
https://github.com/sherlock-audit/2024-12-plaza-finance/blob/main/plaza-evm/src/Auction.sol#L336-L348
```solidity
  function endAuction() external auctionExpired whenNotPaused {
    if (state != State.BIDDING) revert AuctionAlreadyEnded();


    if (currentCouponAmount < totalBuyCouponAmount) {
      state = State.FAILED_UNDERSOLD;
    } else if (totalSellReserveAmount >= (IERC20(sellReserveToken).balanceOf(pool) * poolSaleLimit) / 100) {
        state = State.FAILED_POOL_SALE_LIMIT;
    } else {
      state = State.SUCCEEDED;
      Pool(pool).transferReserveToAuction(totalSellReserveAmount);
      IERC20(buyCouponToken).safeTransfer(beneficiary, IERC20(buyCouponToken).balanceOf(address(this)));
    }
```
let's see how pool::transferReserveToAuction function was implemented,
https://github.com/sherlock-audit/2024-12-plaza-finance/blob/main/plaza-evm/src/Pool.sol#L577-L583
```solidity
  function transferReserveToAuction(uint256 amount) external virtual {
    (uint256 currentPeriod, ) = bondToken.globalPool();
    address auctionAddress = auctions[currentPeriod];
    require(msg.sender == auctionAddress, CallerIsNotAuction());
    
    IERC20(reserveToken).safeTransfer(msg.sender, amount);
  }
```
here
```solidity
bondToken.globalPool();
```
will return x+1.
auctions[x+1]=address(0).
so this require statement
```solidity
require(msg.sender == auctionAddress, CallerIsNotAuction());
```
will always revert.



### Root Cause

wrong checking of auction contract address in transferReserveToAuction function.
transferReserveToAuction function should check
```solidity
(uint256 currentPeriod, ) = bondToken.globalPool();
    address auctionAddress = auctions[currentPeriod-1];
    require(msg.sender == auctionAddress, CallerIsNotAuction());
```
instead of 
```solidity
(uint256 currentPeriod, ) = bondToken.globalPool();
    address auctionAddress = auctions[currentPeriod];
    require(msg.sender == auctionAddress, CallerIsNotAuction());
```


### Internal Pre-conditions

_No response_

### External Pre-conditions

_No response_

### Attack Path

_No response_

### Impact

users bid amounts of buyCouponToken tokens in Auction contract.
If endAuction will always reverts then state of the auction will be State.BIDDING.
users can't call claimBid and claimRefund functions as they can only be called after auctionSucceeded, auctionFailed respectively.(due to auctionSucceeded and auctionFailed modifiers respectively.
so user bid amounts will permanantly locked in auction contract forever.

### PoC

_No response_

### Mitigation

transferReserveToAuction function should check
```solidity
(uint256 currentPeriod, ) = bondToken.globalPool();
    address auctionAddress = auctions[currentPeriod-1];
    require(msg.sender == auctionAddress, CallerIsNotAuction());
```
instead of 
```solidity
(uint256 currentPeriod, ) = bondToken.globalPool();
    address auctionAddress = auctions[currentPeriod];
    require(msg.sender == auctionAddress, CallerIsNotAuction());
```