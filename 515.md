Furry Walnut Mandrill

High

# BalancerRouter.sol locks excess user funds when PreDeposit.sol hits reserveCap

### Summary

In contract `PreDeposit.sol` method `deposit()` pulls reserve token and stakes it in the contract. If the input `reserveToken > reserveCap` then it calculated `amount = reserveCap - reserveAmount` and pulls **amount**. 

But for `BalancerRouter.sol` the above flow is problematic. As, The total amount of reserve tokens received by calling `joinBalancerPool()` is sent to the `PreDeposit.deposit()` as input. It'll only pull part of the balance and rest is left in the `BalancerRouter.sol` contract. There is no method to pull this fund out of `BalancerRouter.sol` contract. Hence, will be stuck in the contract.

```solidity
    function joinBalancerAndPredeposit(
        bytes32 balancerPoolId,
        address _predeposit,
        IAsset[] memory assets,
        uint256[] memory maxAmountsIn,
        bytes memory userData
    ) external nonReentrant returns (uint256) {
        // Step 1: Join Balancer Pool
        uint256 balancerPoolTokenReceived = joinBalancerPool(balancerPoolId, assets, maxAmountsIn, userData);

        // Step 2: Approve balancerPoolToken for PreDeposit
        balancerPoolToken.safeIncreaseAllowance(_predeposit, balancerPoolTokenReceived);

        // Step 3: Deposit to PreDeposit
@=>     PreDeposit(_predeposit).deposit(balancerPoolTokenReceived, msg.sender);

        return balancerPoolTokenReceived;
    }
```

### Root Cause

The `BalancerRouter.sol` doesn't refund excess **balancerPoolTokenReceived** to the user when current deposit surpasses **reserveCap**.

- https://github.com/sherlock-audit/2024-12-plaza-finance/blob/main/plaza-evm/src/BalancerRouter.sol#L37
- https://github.com/sherlock-audit/2024-12-plaza-finance/blob/main/plaza-evm/src/PreDeposit.sol#L124C4-L132C1

### Internal Pre-conditions

_No response_

### External Pre-conditions

_No response_

### Attack Path

_No response_

### Impact

As shown in the POC below the excess reserve Tokens will be stuck in the `BalancerRouter.sol` forever. Leading to user loss. Every time Vault's balance overflows this will happen. 

### PoC

Add this test in `plaza-evm/test/BalancerRouter.t.sol`

```solidity
  function testJoinBalancerAndPredepositLocksUserFunds() public {
    
    vm.startPrank(deployer);
    
    // Deploy mock contracts
    vault = new MockBalancerVault(balancerPoolToken);
    predeposit = PreDeposit(Utils.deploy(address(new PreDeposit()), abi.encodeCall(
      PreDeposit.initialize, 
      (params, address(poolFactory), block.timestamp, block.timestamp + 1 hours, 0.5 ether, "Bond ETH", "bondETH", "Leveraged ETH", "levETH")
    )));
    router = new BalancerRouter(address(vault), address(balancerPoolToken));

    vm.stopPrank();
    
    vm.startPrank(user);

    IAsset[] memory assets = new IAsset[](2);
    assets[0] = IAsset(address(asset1));
    assets[1] = IAsset(address(asset2));

    uint256[] memory maxAmountsIn = new uint256[](2);
    maxAmountsIn[0] = 1 ether;
    maxAmountsIn[1] = 1 ether;

    asset1.approve(address(router), 1 ether);
    asset2.approve(address(router), 1 ether);

    console.log("Max cap of PreVault is 0.5 Ether");
    console.log("User Deposits 1 Ether worth of each tokens to Balancer Router");
    console.log("User Pre Deposit Balance Before: ", predeposit.balances(user));

    uint256 balancerPoolTokenReceived = router.joinBalancerAndPredeposit(
      BALANCER_POOL_ID,
      address(predeposit),
      assets,
      maxAmountsIn,
      ""
    );

    console.log("Balancer pool token received from Balancer Vault: ", balancerPoolTokenReceived);
    console.log("Balancer pool token staked to PreDeposit on behalf of user: ", predeposit.balances(user));
    console.log("Balancer pool token stuck in BalancerRouter Contract :", balancerPoolToken.balanceOf(address(router)));

    vm.stopPrank();
  }
```
## Output
The max cap of `PreVault.sol` was **0.5 Ether**. Users deposited **1 Ether** worth of tokens. **0.5 Ether** was staked to `PreDeposit.sol` and **0.5 Ether** got stuck in the `BalancerRouter.sol` contract.
```log
  Max cap of PreVault is 0.5 Ether
  User Deposits 1 Ether worth of each tokens to Balancer Router
  User Pre Deposit Balance Before:  0
  Balancer pool token received from Balancer Vault:  1000000000000000000
  Balancer pool token staked to PreDeposit on behalf of user:  500000000000000000
  Balancer pool token stuck in BalancerRouter Contract : 500000000000000000
```

### Mitigation

Do the below changes in `BalancerRouter.sol`. It should fix the above vulnerability.

```diff
function joinBalancerAndPredeposit(
        bytes32 balancerPoolId,
        address _predeposit,
        IAsset[] memory assets,
        uint256[] memory maxAmountsIn,
        bytes memory userData
    ) external nonReentrant returns (uint256) {
        // Step 1: Join Balancer Pool
        uint256 balancerPoolTokenReceived = joinBalancerPool(balancerPoolId, assets, maxAmountsIn, userData);

        // Step 2: Approve balancerPoolToken for PreDeposit
        balancerPoolToken.safeIncreaseAllowance(_predeposit, balancerPoolTokenReceived);

        // Step 3: Deposit to PreDeposit
+++        uint256 userPredepositBalance = PreDeposit(_predeposit).balances(msg.sender);
        PreDeposit(_predeposit).deposit(balancerPoolTokenReceived, msg.sender);
+++        uint256 userPostdepositBalance = PreDeposit(_predeposit).balances(msg.sender);
+++        uint256 diffAmount = userPostdepositBalance - userPredepositBalance;
       
+++       if(diffAmount < balancerPoolTokenReceived) {
+++           balancerPoolToken.transfer(msg.sender, balancerPoolTokenReceived - diffAmount);
+++       }

        return balancerPoolTokenReceived;
    }
```