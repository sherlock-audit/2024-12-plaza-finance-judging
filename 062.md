Shaggy Clay Cobra

Medium

# The last bond holder can not redeem

### Summary

The last bond holder can not redeem because unable to calculate collateral level in logics calculating redeem amount

### Root Cause

The function [getRedeemAmount()](https://github.com/sherlock-audit/2024-12-plaza-finance/blob/main/plaza-evm/src/Pool.sol#L477-L525) computes the amount of reserve token to be redeemed by the users. 
```solidity
  function getRedeemAmount(
    TokenType tokenType,
    uint256 depositAmount,
    uint256 bondSupply,
    uint256 levSupply,
    uint256 poolReserves,
    uint256 ethPrice,
    uint8 oracleDecimals,
    uint256 marketRate
  ) public pure returns(uint256) {
    if (bondSupply == 0) {
      revert ZeroDebtSupply();
    }

    uint256 tvl = (ethPrice * poolReserves).toBaseUnit(oracleDecimals);
    uint256 assetSupply = bondSupply;
    uint256 multiplier = POINT_EIGHT;

    // Calculate the collateral level based on the token type
    uint256 collateralLevel;
    if (tokenType == TokenType.BOND) {
@>      collateralLevel = ((tvl - (depositAmount * BOND_TARGET_PRICE)) * PRECISION) / ((bondSupply - depositAmount) * BOND_TARGET_PRICE);
    } else {
      multiplier = POINT_TWO;
      assetSupply = levSupply;
      collateralLevel = (tvl * PRECISION) / (bondSupply * BOND_TARGET_PRICE);

      if (assetSupply == 0) {
        revert ZeroLeverageSupply();
      }
    }
    
    // Calculate the redeem rate based on the collateral level and token type
    uint256 redeemRate;
    if (collateralLevel <= COLLATERAL_THRESHOLD) {
      redeemRate = ((tvl * multiplier) / assetSupply);
    } else if (tokenType == TokenType.LEVERAGE) {
      redeemRate = ((tvl - (bondSupply * BOND_TARGET_PRICE)) / assetSupply) * PRECISION;
    } else {
      redeemRate = BOND_TARGET_PRICE * PRECISION;
    }

    if (marketRate != 0 && marketRate < redeemRate) {
      redeemRate = marketRate;
    }
    
    // Calculate and return the final redeem amount
    return ((depositAmount * redeemRate).fromBaseUnit(oracleDecimals) / ethPrice) / PRECISION;
  }
```
In case last the user to redeem all his Bond token, the condition `bondSupply == depositAmount` holds. In this scenario, the function will revert because of `division by 0` when computing `collateralLevel`.

### Internal Pre-conditions

_No response_

### External Pre-conditions

_No response_

### Attack Path

_No response_

### Impact

- Can not redeem all Bond from pool
- There will be reserve token left in the pool because unable to redeem all Bond token

### PoC

Add the test to `Pool.t.sol`:

```solidity
  function testRedeemAllBondSupply() public {
    // @audit poc redeem the remaining supply in pool
    vm.startPrank(governance);
    Token rToken = Token(params.reserveToken);

    // Mint reserve tokens
    rToken.mint(governance, 1e18);
    rToken.approve(address(poolFactory), 1e18);

    uint bondSupply = 1e18;

    // Create pool and approve deposit amount
    Pool _pool = Pool(poolFactory.createPool(params, 1e18, bondSupply, 1e18, "", "", "", "", false));
    rToken.approve(address(_pool), bondSupply);

    // Call create and assert minted tokens
    uint256 amount = _pool.redeem(Pool.TokenType.BOND, bondSupply, 33);
  }
```

Test failed
```bash
Failing tests:
Encountered 1 failing test in test/Pool.t.sol:PoolTest
[FAIL: panic: division or modulo by zero (0x12)] testRedeemAllBondSupply() (gas: 1938515)
```

### Mitigation

_No response_