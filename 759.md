Suave Vermilion Worm

High

# Auction will never succeed due to wrong check

### Summary

Auction will never be able to successfully end, because of a revert in the transfer of reserve tokens from the pool to the auction.

### Root Cause

Right after an auction is started, the Bond Token's Index for asset period is incremented. ([ref](https://github.com/sherlock-audit/2024-12-plaza-finance/blob/main/plaza-evm/src/Pool.sol#L567)).

When the auction ends and the required amount of coupon tokens has been reached, the function [endAuction()](https://github.com/sherlock-audit/2024-12-plaza-finance/blob/main/plaza-evm/src/Auction.sol#L336) is called, which on the other hand calls [transferReserveToAuction()](https://github.com/sherlock-audit/2024-12-plaza-finance/blob/main/plaza-evm/src/Pool.sol#L577) to retrieve the required reserve tokens from the pool.  In order to avoid being called from unauthorized addresses, the function checks the `msg.sender` against the auction address of the current period. However, as shown above the current period is incremented after the auction has been created, therefore the address of the current period will always be `address(0)` and the function will always revert.

### Internal Pre-conditions

_No response_

### External Pre-conditions

_No response_

### Attack Path

1. Auction is created.
2. Auction is fulfilled.
3. Auction expires
4. A user tries to end the Auction 

### Impact

The auction will never be finalized, which will lead to two major problems:
1. The Bond token holders will not receive their rewards.
2. The Auction bidders will not be able to retrieve neither their coupon tokens, nor reserve amount purchased, because the auction will be expired and still in a `BIDDING` state. 

### PoC

```solidity
// SPDX-License-Identifier: UNLICENSED
pragma solidity ^0.8.26;

import {Test, console} from "forge-std/Test.sol";

import {Pool} from "src/Pool.sol";
import {Token} from "test/mocks/Token.sol";
import {Utils} from "src/lib/Utils.sol";
import {Auction} from "src/Auction.sol";
import {BondToken} from "src/BondToken.sol";
import {PoolFactory} from "src/PoolFactory.sol";
import {Distributor} from "src/Distributor.sol";
import {OracleFeeds} from "src/OracleFeeds.sol";
import {LeverageToken} from "src/LeverageToken.sol";
import {Deployer} from "src/utils/Deployer.sol";
import {IERC20} from "@openzeppelin/contracts/token/ERC20/IERC20.sol";
import {UpgradeableBeacon} from "@openzeppelin/contracts/proxy/beacon/UpgradeableBeacon.sol";
import {PausableUpgradeable} from "@openzeppelin/contracts-upgradeable/utils/PausableUpgradeable.sol";

contract AuctionPoC is Test {
    Auction auction;
    Token usdc;
    Token weth;

    address bidder = address(0x1);
    address governance = address(0x4);

    address pool;

    uint256 amount;
    uint256 constant RESERVE_AMOUNT = 100 ether;
    uint256 constant POOL_SALE_LIMIT = 90; // 90%

    function createPool(address reserve, address coupon) public returns (address) {
        vm.startPrank(governance);
        address deployer = address(new Deployer());
        address oracleFeeds = address(new OracleFeeds());

        address poolBeacon = address(new UpgradeableBeacon(address(new Pool()), governance));
        address bondBeacon = address(new UpgradeableBeacon(address(new BondToken()), governance));
        address levBeacon = address(new UpgradeableBeacon(address(new LeverageToken()), governance));
        address distributorBeacon = address(new UpgradeableBeacon(address(new Distributor()), governance));

        PoolFactory poolFactory = PoolFactory(
            Utils.deploy(
                address(new PoolFactory()),
                abi.encodeCall(
                    PoolFactory.initialize,
                    (governance, deployer, oracleFeeds, poolBeacon, bondBeacon, levBeacon, distributorBeacon)
                )
            )
        );

        PoolFactory.PoolParams memory params;
        params.fee = 0;
        params.reserveToken = reserve;
        params.sharesPerToken = 2500000;
        params.distributionPeriod = 90 days;
        params.couponToken = coupon;

        poolFactory.grantRole(poolFactory.GOV_ROLE(), governance);
        poolFactory.grantRole(poolFactory.POOL_ROLE(), governance);

        Token(reserve).mint(governance, RESERVE_AMOUNT);
        Token(reserve).approve(address(poolFactory), RESERVE_AMOUNT);

        return poolFactory.createPool(
            params,
            RESERVE_AMOUNT,
            10000 * 10 ** 18,
            10000 * 10 ** 18,
            "Bond ETH",
            "bondETH",
            "Leverage ETH",
            "levETH",
            false
        );
    }

    function setUp() public {
        usdc = new Token("USDC", "USDC", false);
        weth = new Token("WETH", "WETH", false);

        pool = createPool(address(weth), address(usdc));

        Pool(pool).setAuctionPeriod(10 days);
        skip(95 days);
        Pool(pool).startAuction();
        auction = Auction(Pool(pool).auctions(0));
        amount = auction.totalBuyCouponAmount();
    }

    function test_endAuctionAlwaysReverts() public {
        // Fill the auction
        vm.startPrank(bidder);
        usdc.mint(bidder, amount);
        usdc.approve(address(auction), amount);
        uint256 bidIndex = auction.bid(1 ether, auction.totalBuyCouponAmount());
        vm.stopPrank();

        // Go to the end of the auction
        vm.warp(block.timestamp + 15 days);
        vm.prank(pool);

        // Try to end the auction
        vm.expectRevert(Pool.CallerIsNotAuction.selector);
        auction.endAuction();

        // Validate that the auction is not ended
        assertEq(uint256(auction.state()), uint256(Auction.State.BIDDING));

        assert(block.timestamp >= auction.endTime());

        vm.startPrank(bidder);
        vm.expectRevert(Auction.AuctionFailed.selector);
        auction.claimBid(bidIndex);

        vm.expectRevert(Auction.AuctionFailed.selector);
        auction.claimRefund(bidIndex);

        vm.stopPrank();
    }
}
```

### Mitigation

```diff
    function transferReserveToAuction(uint256 amount) external virtual {
        (uint256 currentPeriod,) = bondToken.globalPool();
-       address auctionAddress = auctions[currentPeriod];
+       address auctionAddress = auctions[currentPeriod - 1];
        require(msg.sender == auctionAddress, CallerIsNotAuction());

        IERC20(reserveToken).safeTransfer(msg.sender, amount);
    }
```