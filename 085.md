Quiet Seafoam Carp

High

# endAuction() will always revert in successful Auction because current period auctionAddress always equals address(0)

### Summary

[endAuction()](https://github.com/sherlock-audit/2024-12-plaza-finance/blob/main/plaza-evm/src/Auction.sol#L336) in successful `Auction` will always revert during [Pool.transferReserveToAuction()](https://github.com/sherlock-audit/2024-12-plaza-finance/blob/main/plaza-evm/src/Pool.sol#L577) call because `Pool.auctions[currentPeriod]` always equals `address(0)`.

### Root Cause

Calling `Auction.endAuction()` will always revert if Auction was successful because in `Pool.transferReserveToAuction()` there is a requirement - caller address should be equal current auction address. And if current auction always equals `address(0)` it will revert:
```Solidity
/**
  * @dev Ends the auction and transfers the reserve to the auction.
  */
function endAuction() external auctionExpired whenNotPaused {
  if (state != State.BIDDING) revert AuctionAlreadyEnded();

  if (currentCouponAmount < totalBuyCouponAmount) {
    state = State.FAILED_UNDERSOLD;
  } else if (totalSellReserveAmount >= (IERC20(sellReserveToken).balanceOf(pool) * poolSaleLimit) / 100) {
      state = State.FAILED_POOL_SALE_LIMIT;
  } else {
    state = State.SUCCEEDED;
    //call pool to transfer reserve revert
    Pool(pool).transferReserveToAuction(totalSellReserveAmount);
    IERC20(buyCouponToken).safeTransfer(beneficiary, IERC20(buyCouponToken).balanceOf(address(this)));
  }

  emit AuctionEnded(state, totalSellReserveAmount, totalBuyCouponAmount);
}
```
`Pool.transferReserveToAuction()`:
```Solidity
/**
  * @dev Transfers reserve tokens to the current auction.
  * @param amount The amount of reserve tokens to transfer.
  */
function transferReserveToAuction(uint256 amount) external virtual {
  (uint256 currentPeriod, ) = bondToken.globalPool();
  address auctionAddress = auctions[currentPeriod];
  //@audit revert because auctionAddress == address(0)
  require(msg.sender == auctionAddress, CallerIsNotAuction());
  
  IERC20(reserveToken).safeTransfer(msg.sender, amount);
}
```
`Pool.auctions[currentPeriod]` always equals `address(0)` because `BondToken.currentPeriod` being updated during [Pool.startAuction()](https://github.com/sherlock-audit/2024-12-plaza-finance/blob/main/plaza-evm/src/Pool.sol#L530) through [bondToken.increaseIndexedAssetPeriod()](https://github.com/sherlock-audit/2024-12-plaza-finance/blob/main/plaza-evm/src/BondToken.sol#L217):
```Solidity
/**
  * @dev Starts an auction for the current period.
  */
function startAuction() external whenNotPaused() {
  // Check if distribution period has passed
  require(lastDistribution + distributionPeriod < block.timestamp, DistributionPeriodNotPassed());

  // Check if auction period hasn't passed
  require(lastDistribution + distributionPeriod + auctionPeriod >= block.timestamp, AuctionPeriodPassed());

  // Check if auction for current period has already started
  (uint256 currentPeriod,) = bondToken.globalPool();
  //@audit requirement true auctions[currentPeriod] == address(0)
  require(auctions[currentPeriod] == address(0), AuctionAlreadyStarted());

  uint8 bondDecimals = bondToken.decimals();
  uint8 sharesDecimals = bondToken.SHARES_DECIMALS();
  uint8 maxDecimals = bondDecimals > sharesDecimals ? bondDecimals : sharesDecimals;

  uint256 normalizedTotalSupply = bondToken.totalSupply().normalizeAmount(bondDecimals, maxDecimals);
  uint256 normalizedShares = sharesPerToken.normalizeAmount(sharesDecimals, maxDecimals);

  // Calculate the coupon amount to distribute
  uint256 couponAmountToDistribute = (normalizedTotalSupply * normalizedShares)
      .toBaseUnit(maxDecimals * 2 - IERC20(couponToken).safeDecimals());
 
  //@audit setting auctions[currentPeriod] to the address of new Auction
  auctions[currentPeriod] = Utils.deploy(
    address(new Auction()),
    abi.encodeWithSelector(
      Auction.initialize.selector,
      address(couponToken),
      address(reserveToken),
      couponAmountToDistribute,
      block.timestamp + auctionPeriod,
      1000,
      address(this),
      poolSaleLimit
    )
  );

  //Increase the bond token period
  //@audit auctions[currentPeriod] again equals address(0) because now currentPeriod++
  bondToken.increaseIndexedAssetPeriod(sharesPerToken);

  // Update last distribution time
  lastDistribution = block.timestamp;
}
```
`BondToken.increaseIndexedAssetPeriod()`:
```Solidity
/*
  * @dev Increases the current period and updates the shares per token.
  * @param sharesPerToken The new number of shares per token
  * @notice Can only be called by addresses with the GOV_ROLE and when the contract is not paused.
  */
function increaseIndexedAssetPeriod(uint256 sharesPerToken) public onlyRole(DISTRIBUTOR_ROLE) whenNotPaused() {
  globalPool.previousPoolAmounts.push(
    PoolAmount({
      period: globalPool.currentPeriod,
      amount: totalSupply(),
      sharesPerToken: globalPool.sharesPerToken
    })
  );
  globalPool.currentPeriod++;
  globalPool.sharesPerToken = sharesPerToken;

  emit IncreasedAssetPeriod(globalPool.currentPeriod, sharesPerToken);
}
```

### Internal Pre-conditions

_No response_

### External Pre-conditions

_No response_

### Attack Path

_No response_

### Impact

`Auction.endAuction()` function is broken and users lost their funds in the contract.

### PoC

Create test contract `AuctionEndRevert.t.sol` in the `/test` folder:
```Solidity
// SPDX-License-Identifier: UNLICENSED
pragma solidity ^0.8.26;

import "forge-std/Test.sol";

import {Pool} from "../src/Pool.sol";
import {Token} from "./mocks/Token.sol";
import {Auction} from "../src/Auction.sol";
import {Utils} from "../src/lib/Utils.sol";
import {MockPool} from "./mocks/MockPool.sol";
import {BondToken} from "../src/BondToken.sol";
import {Decimals} from "../src/lib/Decimals.sol";
import {PoolFactory} from "../src/PoolFactory.sol";
import {Distributor} from "../src/Distributor.sol";
import {OracleFeeds} from "../src/OracleFeeds.sol";
import {LeverageToken} from "../src/LeverageToken.sol";
import {Deployer} from "../src/utils/Deployer.sol";
import {Strings} from "@openzeppelin/contracts/utils/Strings.sol";
import {UpgradeableBeacon} from "@openzeppelin/contracts/proxy/beacon/UpgradeableBeacon.sol";

contract AuctionEndRevert is Test {
    using Decimals for uint256;
    using Strings for uint256;

    PoolFactory private poolFactory;
    PoolFactory.PoolParams private params;

    address oracleFeedsContract;

    address private deployer = address(0x1);
    address private minter = address(0x2);
    address private governance = address(0x3);
    address private securityCouncil = address(0x4);
    address private user = address(0x5);

    address deployedPoolFactory;
    Token usdc;

    /**
   * @dev Sets up the testing environment.
   * Deploys the BondToken contract and a proxy, then initializes them.
   * Grants the minter and governance roles and mints initial tokens.
   */
    function setUp() public {
        vm.startPrank(deployer);

        address contractDeployer = address(new Deployer());
        oracleFeedsContract = address(new OracleFeeds());

        address poolBeacon = address(new UpgradeableBeacon(address(new Pool()), governance));
        address bondBeacon = address(new UpgradeableBeacon(address(new BondToken()), governance));
        address levBeacon = address(new UpgradeableBeacon(address(new LeverageToken()), governance));
        address distributorBeacon = address(new UpgradeableBeacon(address(new Distributor()), governance));

        deployedPoolFactory = address(new PoolFactory());

        poolFactory = PoolFactory(Utils.deploy(deployedPoolFactory, abi.encodeCall(
        PoolFactory.initialize, 
        (governance, contractDeployer, oracleFeedsContract, poolBeacon, bondBeacon, levBeacon, distributorBeacon)
        )));

        params.fee = 0;
        params.feeBeneficiary = governance;
        params.reserveToken = address(new Token("Wrapped ETH", "WETH", false));
        params.sharesPerToken = 50 * 10 ** 18;
        params.distributionPeriod = 0;
        usdc = new Token("USDC", "USDC", false);
        params.couponToken = address(usdc);
        
        vm.stopPrank();

        vm.startPrank(governance);
        poolFactory.grantRole(poolFactory.POOL_ROLE(), governance);
        poolFactory.grantRole(poolFactory.SECURITY_COUNCIL_ROLE(), securityCouncil);
        vm.stopPrank();
    }

    function testEndAuctionRevert() public {
        vm.startPrank(governance);
        //create pool
        uint256 totalUnderlyingAssets = uint256(1000000000);
        Token rToken = Token(params.reserveToken);
        rToken.mint(governance, totalUnderlyingAssets);
        rToken.approve(address(poolFactory), totalUnderlyingAssets);
        Pool _pool = Pool(poolFactory.createPool(
            params, 
            totalUnderlyingAssets,   //TotalUnderlyingAssets
            25000000000,             //DebtAssets
            1000000000,              //LeverageAssets
            "", "salt", "", "", false));
        //updating auction period to pass AuctionPeriodPassed()
        _pool.setAuctionPeriod(1000);
        //add a bit of time to pass distribution time
        vm.warp(10);
        vm.stopPrank();

        //start auction
        _pool.startAuction();
        Auction _auction = Auction(_pool.auctions(0));
        /*mocking currentCouponAmount to pass and proceed to state.SUCCEED
        if (currentCouponAmount < totalBuyCouponAmount) {
            state = State.FAILED_UNDERSOLD;
        }
        */
        vm.startPrank(user);
        usdc.mint(user, 1250000 ether);
        usdc.approve(address(_auction), 1250000 ether);
        _auction.bid(1, 1250000 ether);
        vm.stopPrank();
        //add time to end auction
        vm.warp(2000);

        vm.expectRevert(Pool.CallerIsNotAuction.selector);
        _auction.endAuction();
    }

}
```
In cmd run this command:
```Shell
forge test --mt testEndAuctionRevert
```
Output:
```Shell
Ran 1 test for test/AuctionEndRevert.t.sol:AuctionEndRevert
[PASS] testEndAuctionRevert() (gas: 4111479)
Suite result: ok. 1 passed; 0 failed; 0 skipped; finished in 2.84ms (1.08ms CPU time)
```

### Mitigation

Update `Pool.transferReserveToAuction()` function like this:
```Solidity
/**
  * @dev Transfers reserve tokens to the current auction.
  * @param amount The amount of reserve tokens to transfer.
  */
function transferReserveToAuction(uint256 amount) external virtual {
  (uint256 currentPeriod, ) = bondToken.globalPool();
  //@audit add currentPeriod actualy equals currentPeriod - 1
  address auctionAddress = auctions[currentPeriod - 1];
  require(msg.sender == auctionAddress, CallerIsNotAuction());
  
  IERC20(reserveToken).safeTransfer(msg.sender, amount);
}
```