Icy Chocolate Kangaroo

Medium

# User's funds will stuck in `BalancerRouter` at a condition

### Summary

User funds in the form of `BalancerPoolToken` can become stuck in the `BalancerRouter` when the `reserveCap` is reached, as the `Predeposit` only accepts a partial amount. This leaves the remaining tokens in the router, making them inaccessible to the user.

### Vulnerability Details

At `BalancerRouter`, it facilitates users to join `Predeposit` via function `joinBalancerAndPredeposit`. This function mechanism is transfering user's funds into `BalancerRouter`. Afterthat, `BalancerRouter` join `BalancerVault` via `joinPool`. Afterthat, `BalancerVault` sends `BalancerPoolToken` into `BalancerRouter`. Then, `BalancerRouter` deposits `BalancerTokenPool` into `Predeposit`. This flow act correctly. Let's see details on `Predeposit`.

```solidity
// Predeposit.sol
function _deposit(uint256 amount, address onBehalfOf) private checkDepositStarted checkDepositNotEnded {
    if (reserveAmount >= reserveCap) revert DepositCapReached();

    address recipient = onBehalfOf == address(0) ? msg.sender : onBehalfOf;

    // if user would like to put more than available in cap, fill the rest up to cap and add that to reserves
    // if value exceed, predeposit just put the difference of reserveCap - reserveAmount 
@>  if (reserveAmount + amount >= reserveCap) {
      amount = reserveCap - reserveAmount;
    }

    balances[recipient] += amount;
    reserveAmount += amount;

    // predeposit just take amount of rest, not all token
@>  IERC20(params.reserveToken).safeTransferFrom(msg.sender, address(this), amount);

    emit Deposited(recipient, amount);
  }
```

when `reserveAmount + amount >= reserveCap`, `Predeposi`t only put difference of `amount = reserveCap - reserveAmount` (`Predeposit` not fully taking amount) and this logic supposedly correct. Let's see `BalancerRouter`. 

https://github.com/sherlock-audit/2024-12-plaza-finance/blob/main/plaza-evm/src/BalancerRouter.sol#L42-L63

Otherhand, `BalancerRouter` still want to deposit all of amount `BalancerTokenPool` even `Predeposit` only put rest of amount from `amount = reserveCap - reserveAmount`. it causes `BalancerRouter` still have a `BalancerTokenPool` and user's fund that is `BalancerTokenPool` stuck in `BalancerRouter`. 

Let's build scenario :

1. Assume that `reserveCap = 5 ether`
2. Assume that `MockBalancerVault.joinPool()` whatever it params, always mint `3 ether`
3. Bob deposits some tokens to `BalancerRouter` via `joinBalancerAndPredeposit()` and `BalancerRouter` receives `3 ether` of `BalancerPoolToken` from `BalancerVault`. `BalancerRouter` deposit all of `3 ether BalancerPoolToken` into `Predeposit` 
4. Current condition in `Predeposit` is `reserveAmount = 3 ether`
5. Alice deposits some tokens to `BalancerRouter` via `joinBalancerAndPredeposit()` and `BalancerRouter` receives `3 ether BalancerPoolToken` from `BalancerVault`. `BalancerRouter` still deposit all of `3 ether BalancerPoolToken` into `Predeposit` 
6. Current conditions in `Predeposit` :
        `amount + reserveAmount >= reserveCap`  -> 3 ether + 3 ether >= 5 ether
        `amount = reserveCap - reserveAmount`   -> 5 ether - 3 ether = 2 ether
                                     `amount`   -> 2 ether                             
7. `BalancerRouter` has `3 ether` but `Predeposit` only put `2 ether`
8. So, `BalancerRouter` has `1 ether` of rest of `BalancerPoolToken`

### Impact

User's fund (`BalancerTokenPool`) will stuck at `BalancerRouter` and making them inaccessible to the user

### Proof Of Concepts

```solidity
function test_JoinBalancerAndPredeposit_couldStucInBalancerRouter() public {

        // 1. Bob deposit
        
        vm.startPrank(bob);
        IAsset[] memory assets = new IAsset[](2);
        assets[0] = IAsset(address(asset1));
        assets[1] = IAsset(address(asset2));

        uint256[] memory maxAmountsIn = new uint256[](2);
        maxAmountsIn[0] = 1 ether;
        maxAmountsIn[1] = 1 ether;

        asset1.approve(address(router), 1 ether);
        asset2.approve(address(router), 1 ether);

        uint256 balancerPoolTokenReceived = router.joinBalancerAndPredeposit(
            BALANCER_POOL_ID,
            address(predeposit),
            assets,
            maxAmountsIn,
            ""
        );

        console.log("====================Bob Action===============================");
        console.log(
            "Token received (BalancerPoolToken) : ",
            balancerPoolTokenReceived
        );
        console.log(
            "Balance of BalanceRouter (BalancerPoolToken)",
            balancerPoolToken.balanceOf(address(router))
        );
        console.log(
            "Balance of Bob from Predeposit: ",
            predeposit.balances(bob)
        );
        assertEq(
            balancerPoolTokenReceived,
            3 ether,
            "Incorrect balancerPoolToken amount received"
        );
        assertEq(
            predeposit.balances(bob),
            3 ether,
            "incorrect predeposit balance"
        );
        assertEq(
            balancerPoolToken.balanceOf(address(router)),
            0,
            "Balance must be zero after transfer into predeposit"
        );
        vm.stopPrank();


        // 2. Alice deposit

        vm.startPrank(alice);
        asset1.approve(address(router), 1 ether);
        asset2.approve(address(router), 1 ether);

        uint256 balancerPoolTokenReceived2 = router.joinBalancerAndPredeposit(
            BALANCER_POOL_ID,
            address(predeposit),
            assets,
            maxAmountsIn,
            ""
        );

        console.log("====================Alice Action===============================");
        console.log(
            "Token received (BalancerPoolToken) : ",
            balancerPoolTokenReceived2
        );
        console.log(
            "Balance of BalanceRouter (BalancerPoolToken)",
            balancerPoolToken.balanceOf(address(router))
        );
        console.log(
            "Balance of alice from Predeposit: ",
            predeposit.balances(alice)
        );
        assertEq(
            balancerPoolTokenReceived2,
            3 ether,
            "Incorrect balancerPoolToken amount received"
        );
        assertEq(
            predeposit.balances(alice),
            2 ether,
            "incorrect predeposit balance"
        );
        assertNotEq(balancerPoolToken.balanceOf(address(router)), 0);
        vm.stopPrank();
    }

```

```bash
Ran 1 test for test/BalancerRouter.t.sol:BalancerRouterTest
[PASS] test_JoinBalancerAndPredeposit_couldStucInBalancerRouter() (gas: 599391)
Logs:
  ====================Bob Action===============================
  Token received (BalancerPoolToken) :  3000000000000000000
  Balance of BalanceRouter (BalancerPoolToken) 0
  Balance of Bob from Predeposit:  3000000000000000000
  ====================Alice Action===============================
  Token received (BalancerPoolToken) :  3000000000000000000
  Balance of BalanceRouter (BalancerPoolToken) 1000000000000000000
  Balance of alice from Predeposit:  2000000000000000000

Suite result: ok. 1 passed; 0 failed; 0 skipped; finished in 27.31ms (4.87ms CPU time)
```
