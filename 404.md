Handsome Mandarin Frog

High

# Pool:transferReserveToAuction reverts due to incorrect auction address, preventing `Auction:endAuction` execution

### Summary

The `currentPeriod` in `bondToken:globalPool` is incremented when a new auction starts, as seen in the `Pool:startAuction` function. However, the auctions mapping only associates an address with the previous period. Since the `currentPeriod` has already been increased, querying the auctions mapping with the `currentPeriod` returns `address(0)` instead of the correct auction address.

```solidity
    function startAuction() external whenNotPaused {
        ...
@>      auctions[currentPeriod] = Utils.deploy(
            address(new Auction()),
            abi.encodeWithSelector(
                Auction.initialize.selector,
                address(couponToken),
                address(reserveToken),
                couponAmountToDistribute,
                block.timestamp + auctionPeriod,
                1000,
                address(this),
                poolSaleLimit
            )
        );

        // Increase the bond token period
@>      bondToken.increaseIndexedAssetPeriod(sharesPerToken);
        ...
    }
```

The increment of `currentPeriod` happens in `bondToken:increaseIndexedAssetPeriod`, as shown below:

```solidity
    /**
     * @dev Increases the current period and updates the shares per token.
     * @param sharesPerToken The new number of shares per token
     * @notice Can only be called by addresses with the GOV_ROLE and when the contract is not paused.
     */
    function increaseIndexedAssetPeriod(uint256 sharesPerToken) public onlyRole(DISTRIBUTOR_ROLE) whenNotPaused {
        globalPool.previousPoolAmounts.push(
            PoolAmount({
                period: globalPool.currentPeriod,
                amount: totalSupply(),
                sharesPerToken: globalPool.sharesPerToken
            })
        );
@>      globalPool.currentPeriod++;
        globalPool.sharesPerToken = sharesPerToken;

        emit IncreasedAssetPeriod(globalPool.currentPeriod, sharesPerToken);
    }
```

Since the auction address is stored in the auctions mapping for the previous period, `Pool:transferReserveToAuction` will always revert with the `CallerIsNotAuction()` error because the retrieved `auctionAddress` is `address(0)`.

```solidity
    /**
     * @dev Transfers reserve tokens to the current auction.
     * @param amount The amount of reserve tokens to transfer.
     */
    function transferReserveToAuction(uint256 amount) external virtual {
        (uint256 currentPeriod,) = bondToken.globalPool();

@>      address auctionAddress = auctions[currentPeriod];
@>      require(msg.sender == auctionAddress, CallerIsNotAuction());

        IERC20(reserveToken).safeTransfer(msg.sender, amount);
    }
```

### Root Cause

In [Pool.sol:577](https://github.com/sherlock-audit/2024-12-plaza-finance/blob/main/plaza-evm/src/Pool.sol#L577), the `transferReserveToAuction` function attempts to retrieve the `auctionAddress` using the `currentPeriod` from `bondToken.globalPool`. However, since the `currentPeriod` is incremented after assigning the auction address in [Pool:530](https://github.com/sherlock-audit/2024-12-plaza-finance/blob/main/plaza-evm/src/Pool.sol#L530) within `startAuction`, subsequent calls referencing the `currentPeriod` will fail to retrieve the correct `auctionAddress`.

### Internal Pre-conditions

_No response_

### External Pre-conditions

_No response_

### Attack Path

_No response_

### Impact

Successful auctions cannot be finalized, causing bidders to suffer a 100% loss of their contributed funds.

### PoC

To demonstrate the issue, a new test file was created since the provided test files utilize the `MockPool` contract, which bypasses the address verification in the `Pool::transferReserveToAuction` function. The [`MockPool`](https://github.com/sherlock-audit/2024-12-plaza-finance/blob/main/plaza-evm/test/mocks/MockPool.sol#L18) implementation does not enforce the `CallerIsNotAuction` check, leading to inaccurate testing of the problem. Below are the steps taken to construct and validate the proof of code:

1. Create a New Test File
   A new test file named Shiazinho.t.sol was added to the plaza-evm/test directory. The full implementation is provided below:

<details>
<summary>Code</summary>

```solidity
// SPDX-License-Identifier: UNLICENSED
pragma solidity ^0.8.13;

import {Test, console, console2} from "forge-std/Test.sol";

import {Pool} from "../src/Pool.sol";
import {Token} from "./mocks/Token.sol";
import {Utils} from "../src/lib/Utils.sol";
import {Auction} from "../src/Auction.sol";
import {MockPool} from "./mocks/MockPool.sol";
import {BondToken} from "../src/BondToken.sol";
import {PoolFactory} from "../src/PoolFactory.sol";
import {Distributor} from "../src/Distributor.sol";
import {OracleFeeds} from "../src/OracleFeeds.sol";
import {LeverageToken} from "../src/LeverageToken.sol";
import {Deployer} from "../src/utils/Deployer.sol";
import {IERC20} from "@openzeppelin/contracts/token/ERC20/IERC20.sol";
import {UpgradeableBeacon} from "@openzeppelin/contracts/proxy/beacon/UpgradeableBeacon.sol";
import {PausableUpgradeable} from "@openzeppelin/contracts-upgradeable/utils/PausableUpgradeable.sol";

contract ShiazinhoTest is Test {
PoolFactory poolFactory;
Token usdc;
Token weth;

    address governance = address(0x4);
    address securityCouncil = address(0x5);

    address user = makeAddr("user");

    address pool;

    function setUp() public {
        usdc = new Token("USDC", "USDC", false);
        weth = new Token("WETH", "WETH", false);

        pool = createPool(address(weth), address(usdc));
    }

    function createPool(address reserve, address coupon) public returns (address) {
        vm.startPrank(governance);
        address deployer = address(new Deployer());
        address oracleFeeds = address(new OracleFeeds());

        address poolBeacon = address(new UpgradeableBeacon(address(new Pool()), governance));
        address bondBeacon = address(new UpgradeableBeacon(address(new BondToken()), governance));
        address levBeacon = address(new UpgradeableBeacon(address(new LeverageToken()), governance));
        address distributorBeacon = address(new UpgradeableBeacon(address(new Distributor()), governance));

        poolFactory = PoolFactory(
            Utils.deploy(
                address(new PoolFactory()),
                abi.encodeCall(
                    PoolFactory.initialize,
                    (governance, deployer, oracleFeeds, poolBeacon, bondBeacon, levBeacon, distributorBeacon)
                )
            )
        );

        PoolFactory.PoolParams memory params;
        params.fee = 5e4;
        params.reserveToken = reserve;
        params.sharesPerToken = 2500000;
        params.distributionPeriod = 90 days;
        params.couponToken = coupon;

        poolFactory.grantRole(poolFactory.GOV_ROLE(), governance);
        poolFactory.grantRole(poolFactory.POOL_ROLE(), governance);
        poolFactory.grantRole(poolFactory.SECURITY_COUNCIL_ROLE(), securityCouncil);

        Token(reserve).mint(governance, 500e18);
        Token(reserve).approve(address(poolFactory), 500e18);

        return
            poolFactory.createPool(params, 500e18, 1e13, 1e13, "Bond ETH", "bondETH", "Leverage ETH", "levETH", false);
    }

    function createAuction(address _pool) public returns (address) {
        Pool(_pool).startAuction();

        (uint256 currentPeriod,) = Pool(_pool).bondToken().globalPool();
        address _auction = Pool(_pool).auctions(currentPeriod - 1);

        return _auction;
    }

    function test_shiazinho_endAuctionRevertsWithCallerIsNotAuction() public {
        address[] memory bidders = new address[](10);

        for (uint256 i = 0; i < 10; i++) {
            bidders[i] = address(uint160(i + 11));
        }

        vm.startPrank(governance);
        Pool(pool).setAuctionPeriod(10 days);
        vm.stopPrank();

        vm.warp(90 days + 2);

        Pool(pool).startAuction();
        (uint256 currentPeriod,) = Pool(pool).bondToken().globalPool();
        address auction = Pool(pool).auctions(currentPeriod - 1);

        uint256 totalBuyCouponAmount = Auction(auction).totalBuyCouponAmount();
        uint256 buyAmount = totalBuyCouponAmount / 10;

        uint256 poolReservers = weth.balanceOf(pool);
        uint256 sellAmount = ((poolReservers * 89) / 100) / 10;

        for (uint256 i = 0; i < 10; i++) {
            vm.startPrank(bidders[i]);
            usdc.mint(bidders[i], buyAmount);
            usdc.approve(address(auction), buyAmount);
            Auction(auction).bid(sellAmount, buyAmount);
            vm.stopPrank();
        }

        vm.warp(block.timestamp + 10 days);
        vm.expectRevert(Pool.CallerIsNotAuction.selector);
        Auction(auction).endAuction();

        address auctionForCurrentPeriod = Pool(pool).auctions(currentPeriod);
        assertEq(auctionForCurrentPeriod, address(0));

        uint256 previousPeriod = currentPeriod - 1;
        address auctionForPreviousPeriod = Pool(pool).auctions(previousPeriod);
        assertEq(auctionForPreviousPeriod, auction);
    }

}
```

</details>

2. Run the test
   Run the following command to execute the test and verify the issue:

```bash
	forge test --mt test_shiazinho_endAuctionRevertsWithCallerIsNotAuction -vvvv
```

This test highlights the issue, confirming that `Pool:transferReserveToAuction` reverts due to the incorrect auction address being derived for the `currentPeriod`. The assertion checks confirm that the correct auction address corresponds to the previous period.

### Mitigation

To address the issue, modify the `transferReserveToAuction` function to retrieve the auction address using `auctions[currentPeriod - 1]` instead of `auctions[currentPeriod]`. This ensures that the correct auction address for the previous period is used.

```diff
function transferReserveToAuction(uint256 amount) external virtual {
        (uint256 currentPeriod,) = bondToken.globalPool();

-       address auctionAddress = auctions[currentPeriod];
+       address auctionAddress = auctions[currentPeriod - 1];
        require(msg.sender == auctionAddress, CallerIsNotAuction());

        IERC20(reserveToken).safeTransfer(msg.sender, amount);
}
```