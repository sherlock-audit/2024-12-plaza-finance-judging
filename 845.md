Future Linen Pelican

High

# A Logic Flaw Causes Reverts After Auction Success In Pool::transferReserveToAuction function

### Summary

The transferReserveToAuction function in the Pool contract references an incorrect auction period due to the increment of `bondToken.globalPool()` in the startAuction function. This results in a revert during the SUCCEEDED state of an auction, preventing the transfer of reserve tokens to the auction contract.



### Root Cause

When a new auction is initiated via the startAuction function, the current period (currentPeriod) is retrieved from the bondToken.globalPool() and used to associate the auction contract address with that period. After creating the auction, the increaseIndexedAssetPeriod function is called on the bondToken, which increments the currentPeriod by 1.
When the auction transitions to the SUCCEEDED state, the endAuction function calls the transferReserveToAuction function of the Pool contract. However, the transferReserveToAuction function retrieves the incremented currentPeriod from bondToken.globalPool(). Because this period has no associated auction in the auctions mapping, the function reverts.

This mismatch in periods causes the transferReserveToAuction function to always fail during a successful auction settlement.

```solidity
 function startAuction() external whenNotPaused() {
    // Check if distribution period has passed
    require(lastDistribution + distributionPeriod < block.timestamp, DistributionPeriodNotPassed());

    // Check if auction period hasn't passed
    require(lastDistribution + distributionPeriod + auctionPeriod >= block.timestamp, AuctionPeriodPassed());

    // Check if auction for current period has already started
    (uint256 currentPeriod,) = bondToken.globalPool();
    require(auctions[currentPeriod] == address(0), AuctionAlreadyStarted());

    uint8 bondDecimals = bondToken.decimals();
    uint8 sharesDecimals = bondToken.SHARES_DECIMALS();
    uint8 maxDecimals = bondDecimals > sharesDecimals ? bondDecimals : sharesDecimals;

    uint256 normalizedTotalSupply = bondToken.totalSupply().normalizeAmount(bondDecimals, maxDecimals);
    uint256 normalizedShares = sharesPerToken.normalizeAmount(sharesDecimals, maxDecimals);

    // Calculate the coupon amount to distribute
    uint256 couponAmountToDistribute = (normalizedTotalSupply * normalizedShares)
        .toBaseUnit(maxDecimals * 2 - IERC20(couponToken).safeDecimals());

    auctions[currentPeriod] = Utils.deploy(
      address(new Auction()),
      abi.encodeWithSelector(
        Auction.initialize.selector,
        address(couponToken),
        address(reserveToken),
        couponAmountToDistribute,
        block.timestamp + auctionPeriod,
        1000,
        address(this),
        poolSaleLimit
      )
    );

    // Increase the bond token period
@>>    bondToken.increaseIndexedAssetPeriod(sharesPerToken);

    // Update last distribution time
    lastDistribution = block.timestamp;
  }

  /**
   * @dev Transfers reserve tokens to the current auction.
   * @param amount The amount of reserve tokens to transfer.
   */
  function transferReserveToAuction(uint256 amount) external virtual {
@>>    (uint256 currentPeriod, ) = bondToken.globalPool();
    address auctionAddress = auctions[currentPeriod];
@>>    require(msg.sender == auctionAddress, CallerIsNotAuction());
    
    IERC20(reserveToken).safeTransfer(msg.sender, amount);
  }

```
https://github.com/sherlock-audit/2024-12-plaza-finance/blob/main/plaza-evm/src/Pool.sol#L530-L584

In the require this will revert CallerIsNotAuction()

### Internal Pre-conditions

_No response_

### External Pre-conditions

_No response_

### Attack Path

_No response_

### Impact

The totalSellReserveAmount of reserveToken cannot be transferred from the pool to the auction contract.

### PoC

_No response_

### Mitigation

Modify the transferReserveToAuction function to reference the correct period by using the previousPeriod, similar to the logic in the distribute function.

