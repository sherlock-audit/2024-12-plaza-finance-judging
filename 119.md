Acidic Zinc Shrimp

High

# The `transferReserveToAuction` Function uses the wrong `currentPeriod`, Leading to DoS

## Finding description
The `increaseIndexedAssetPeriod` function increments the `currentPeriod` upon the initiation of a new auction (`startAuction`) in `Pool.sol`. However, the `transferReserveToAuction` function relies on the `currentPeriod` value when transferring reserve tokens at the end of the auction.

The `transferReserveToAuction` function uses the `currentPeriod` to fetch the associated `auctionAddress`. However, since `currentPeriod` is incremented in `increaseIndexedAssetPeriod` at the start of a new auction, the `currentPeriod` no longer reflects the period of the auction that just concluded.

## Proof of Concept
As you can see below that the `startAuction` at the end calls the function `increaseIndexedAssetPeriod` (which increases the `currentPeriod`)
- https://github.com/sherlock-audit/2024-12-plaza-finance/blob/main/plaza-evm/src/Pool.sol#L567
```solidity
  function startAuction() external whenNotPaused() {
    // --snip -- ( sets up the auction)

    // Increase the bond token period
    bondToken.increaseIndexedAssetPeriod(sharesPerToken);

    // Update last distribution time
    lastDistribution = block.timestamp;
}
```
However when the Auction ends, the function `transferReserveToAuction` gets called below
- https://github.com/sherlock-audit/2024-12-plaza-finance/blob/main/plaza-evm/src/Auction.sol#L345
```solidity
  function endAuction() external auctionExpired whenNotPaused {
    if (state != State.BIDDING) revert AuctionAlreadyEnded();

    if (currentCouponAmount < totalBuyCouponAmount) {
      state = State.FAILED_UNDERSOLD;
    } else if (totalSellReserveAmount >= (IERC20(sellReserveToken).balanceOf(pool) * poolSaleLimit) / 100) {
        state = State.FAILED_POOL_SALE_LIMIT;
    } else {
      state = State.SUCCEEDED;
@>    Pool(pool).transferReserveToAuction(totalSellReserveAmount);
      IERC20(buyCouponToken).safeTransfer(beneficiary, IERC20(buyCouponToken).balanceOf(address(this)));
    }

    emit AuctionEnded(state, totalSellReserveAmount, totalBuyCouponAmount);
  }
```

This is what `transferReserveToAuction` does below.
- https://github.com/sherlock-audit/2024-12-plaza-finance/blob/main/plaza-evm/src/Pool.sol#L577C1-L583C4
```solidity
  function transferReserveToAuction(uint256 amount) external virtual {
    (uint256 currentPeriod, ) = bondToken.globalPool();
    address auctionAddress = auctions[currentPeriod];
    require(msg.sender == auctionAddress, CallerIsNotAuction());
    
    IERC20(reserveToken).safeTransfer(msg.sender, amount);
  }
```

The `auctionAddress` will be `address(0)` and that's an invalid Address, causing DoS

### Steps Summary:
- A new auction starts, and `increaseIndexedAssetPeriod` is called, incrementing the `currentPeriod`
- Before the `transferReserveToAuction` function is executed for the previous auction, the `currentPeriod` now reflects new clean values.
- The function fetches the auction address using the updated `currentPeriod`, leading to invalid `auctionAddress `

## Impact
- If the `auctionAddress` resolves to `address(0)`, it will cause any auctions initiated via the Pool contract's `startAuction` function to revert during the `transferReserveToAuction` process. This effectively makes it impossible to finalize the auction or transfer the reserves, rendering the auction mechanism unusable and preventing the distribution of assets to participants.

## Recommended mitigation steps
- Modify `transferReserveToAuction` to reference the `previousPoolAmounts` or the last `currentPeriod` stored before the increment.
```diff
  function transferReserveToAuction(uint256 amount) external virtual {
    (uint256 currentPeriod, ) = bondToken.globalPool();
-   address auctionAddress = auctions[currentPeriod];
+   address auctionAddress = auctions[currentPeriod - 1];
    require(msg.sender == auctionAddress, CallerIsNotAuction());
    
    IERC20(reserveToken).safeTransfer(msg.sender, amount);
  }
```