Fit Alabaster Goblin

High

# BondToken indexed shares logic wrongly assumes auctions will always be successful, DoSing future distributions

### Summary

The Bond Token indexed share logic is flawed, as it does not consider failed auctions, effectively preventing users from claiming their corresponding shares for periods after a failed auction.

### Root Cause

In `BondToken`'s [`getIndexedUserAmount` function](https://github.com/sherlock-audit/2024-12-plaza-finance/blob/ab5bbd7d54042e4444b711a32def5b639fbd63b4/plaza-evm/src/BondToken.sol#L190), the logic always assumes that auctions for every period were successful:

```solidity
// File: BondToken.sol

function getIndexedUserAmount(address user, uint256 balance, uint256 period) public view returns(uint256) {
    IndexedUserAssets memory userPool = userAssets[user];
    uint256 shares = userPool.indexedAmountShares;

    for (uint256 i = userPool.lastUpdatedPeriod; i < period; i++) {
      shares += (balance * globalPool.previousPoolAmounts[i].sharesPerToken).toBaseUnit(SHARES_DECIMALS);
    }

    return shares;
  }
```

As shown in the code snippet, the total indexed shares corresponding for a certain user is computed by iterating all the previous periods until the current one (excluded), and multiplying the bond token balance of the user by the corresponding `sharesPerToken`.

However, this assumes that auctions for all previous periods were successful, which is an incorrect assumption. In case an auction fails, the actual `sharesPerToken` for the corresponding failed auction period should be 0, given that the USDC couldn’t be obtained from the auction in order to be distributed to the users.

An additional problem to this is that, when claiming distributions, users **can’t select the amount they want to claim, and are instead forced to claim their whole entitled balance:**

```solidity
// File: Distributor.sol

function claim() external whenNotPaused nonReentrant {
        ...
        uint256 shares = bondToken
            .getIndexedUserAmount(msg.sender, balance, currentPeriod)
            .normalizeAmount(
                bondToken.decimals(),
                IERC20(couponToken).safeDecimals()
            );

        ...
        IERC20(couponToken).safeTransfer(msg.sender, shares);

        emit ClaimedShares(msg.sender, currentPeriod, shares);
    }
```

The problem with this approach is that there won’t be enough balance in the `Distributor` contract to cover all claims, given that the auction didn’t succeed, and USDC was actually never transferred from the auction to the distributor.

### Internal Pre-conditions

_No response_

### External Pre-conditions

_No response_

### Attack Path

Let’s consider the simplest possible scenario, with only one depositor, and two periods (period 0 and period 1). The user has deposited some reserve tokens into Plaza in period 0, and has obtained 30 bond tokens. The `sharesPerToken` in both periods is set to 2,5 USDC per bond token. 

1. The auction for period 0 starts by calling `startAuction`, an auction contract is deployed and the `increaseIndexedAssetPeriod` function from Bond Token is called. This function will push the total supply and shares per token for period 0 into the `globalPool.previousPoolAmounts` array, and the current period will incremented from 0 to 1:
    
    ```solidity
    // File: BondToken.sol
    
    function increaseIndexedAssetPeriod(uint256 sharesPerToken) public onlyRole(DISTRIBUTOR_ROLE) whenNotPaused() {
        globalPool.previousPoolAmounts.push(
          PoolAmount({
            period: globalPool.currentPeriod,
            amount: totalSupply(),
            sharesPerToken: globalPool.sharesPerToken
          })
        );
        globalPool.currentPeriod++;
        globalPool.sharesPerToken = sharesPerToken;
    
        emit IncreasedAssetPeriod(globalPool.currentPeriod, sharesPerToken);
      }
    ```
    
2. The total amount required for bids is bond’s `totalSupply` * `sharesPerToken`, so 75 USDC are needed to be covered in the auction. Let’s say that in distribution period 0, the auction fails (maybe because the `poolSaleLimit` configured in the Auction contract was surpassed). In this case, the `getIndexedUserAmount` for the user will still be 75 USDC, even if the auction failed. This is because `getIndexedUserAmount` fetches the `sharesPerToken` previously stored in the `globalPool.previousPoolAmounts` array:
    
    ```solidity
    // File: BondToken.sol
    function getIndexedUserAmount(address user, uint256 balance, uint256 period) public view returns(uint256) {
        IndexedUserAssets memory userPool = userAssets[user];
        uint256 shares = userPool.indexedAmountShares;
    
        for (uint256 i = userPool.lastUpdatedPeriod; i < period; i++) {
          shares += (balance * globalPool.previousPoolAmounts[i].sharesPerToken).toBaseUnit(SHARES_DECIMALS);
        }
    
        return shares;
      }
    ```
    
3. After some time, the auction for period 1 begins, and the current period is incremented from 1 to 2 by calling `increaseIndexedAssetPeriod` inside the pool’s `startAuction` function. The user’s balance is still 30 bond tokens, and the `sharesPerToken` for period 1 is still 2,5 USDC, so a new auction contract is deployed requiring 75 USDC to be covered. However, this time the auction succeeds, and the 75 USDC corresponding to the auction are sent to the distributor contract. 

At this point, the amount of shares returned by `getIndexedUserAmount` for the user is 150, given that the information for period 0 and period 1 was already stored in the `globalPool.previousPoolAmounts` array in the bond token contract. Essentially, the loop in `getIndexedUserAmount` will perform the following computations:

- In the first iteration (period 0), 75 USDC in shares correspond to the user (30 bond tokens * 2,5 USDC per bond token).
- In the second iteration (period 1), another 75 USDC shares correspond to the user. This gets added to the previous 75 USDC in shares, totaling 150 USDC for the user.

If the user wanted to claim their assets in the Distributor, the amount of `shares` to claim would be 150 USDC. However, the Distributor contract only holds 75 USDC (given that the auction for period 0 failed), so the call to `Distributor.claim()` will revert, effectively DoS’in future rewards distribution, and leading to a loss of funds for the user.

### Impact

As demonstrated, the faulty logic of not considering failed auctions when computing the user’s corresponding shares will completely DoS future rewards. Even if only one auction fails, it is enough to prevent future claims due to a lack of funds in the distributor contract. Because of this, the impact is high as it effectively causes a big loss for users and breaks the core idea of the protocol, which is issuing rewards quarterly for bond token holders.

### PoC

_No response_

### Mitigation

Consider updating the bond token’s `sharesPerToken` for periods where the auction has failed. A way to do this is by including an `updateSharesOnFailure` function in the BondToken:

```diff
// BondToken.sol

+ function updateSharesOnFailure(uint256 period) external onlyRole(DISTRIBUTOR_ROLE) whenNotPaused() {
+	if(period > globalPool.previousPoolAmounts.length - 1) revert OutOfBounds();
+ 	globalPool.previousPoolAmounts[period].sharesPerToken = 0;
+ }
```

Then, call this function in the pool in case the auction fails:

```diff
// Pool.sol

function distribute() external whenNotPaused { 
        (uint256 currentPeriod, ) = bondToken.globalPool();
        require(currentPeriod > 0, AccessDenied());

        // Period is increased when auction starts, we want to distribute for the previous period
        uint256 previousPeriod = currentPeriod - 1;
        ...

        if (
            Auction(auctions[previousPeriod]).state() ==
            Auction.State.FAILED_POOL_SALE_LIMIT ||
            Auction(auctions[previousPeriod]).state() ==
            Auction.State.FAILED_UNDERSOLD
        ) {
+	    bondToken.updateSharesOnFailure(previousPeriod);
            emit DistributionRollOver(previousPeriod, couponAmountToDistribute);
            return;
        }
```