Happy Linen Osprey

Medium

# Bidding will not be possible if the bidder with the lowest bid is blacklisted by USDC

### Summary

During the process of bidding, the lowest bid or at least a proportion of it might have to be removed. In both cases, the refunded amount of `buyCouponToken`, which is USDC, will be refunded to the bidder. However, if that bidder is blacklisted, the transaction will revert and bidding will not be possible.

### Root Cause

A bid is removed by transferring the `sellCouponAmount` to the bidder.

https://github.com/sherlock-audit/2024-12-plaza-finance/blob/14a962c52a8f4731bbe4655a2f6d0d85e144c7c2/plaza-evm/src/Auction.sol#L325

```solidity
  function _removeBid(uint256 bidIndex) internal {
    Bid storage bidToRemove = bids[bidIndex];
    uint256 nextIndex = bidToRemove.nextBidIndex;
    uint256 prevIndex = bidToRemove.prevBidIndex;

    // Update linked list pointers
    if (prevIndex == 0) {
      // Removing the highest bid
      highestBidIndex = nextIndex;
    } else {
      bids[prevIndex].nextBidIndex = nextIndex;
    }

    if (nextIndex == 0) {
      // Removing the lowest bid
      lowestBidIndex = prevIndex;
    } else {
      bids[nextIndex].prevBidIndex = prevIndex;
    }

    address bidder = bidToRemove.bidder;
    uint256 buyReserveAmount = bidToRemove.buyReserveAmount;
    uint256 sellCouponAmount = bidToRemove.sellCouponAmount;
    currentCouponAmount -= sellCouponAmount;
    totalSellReserveAmount -= buyReserveAmount;

    // Refund the buy tokens for the removed bid
 >> IERC20(buyCouponToken).safeTransfer(bidder, sellCouponAmount);

    emit BidRemoved(bidIndex, bidder, buyReserveAmount, sellCouponAmount);

    delete bids[bidIndex];
    bidCount--;
  }
```

The same applies for when a proportion of the lowest bid is refunded.

https://github.com/sherlock-audit/2024-12-plaza-finance/blob/14a962c52a8f4731bbe4655a2f6d0d85e144c7c2/plaza-evm/src/Auction.sol#L286

```solidity
  function removeExcessBids() internal {
    if (currentCouponAmount <= totalBuyCouponAmount) {
      return;
    }

    uint256 amountToRemove = currentCouponAmount - totalBuyCouponAmount;
    uint256 currentIndex = lowestBidIndex;

    while (currentIndex != 0 && amountToRemove != 0) {
      // Cache the current bid's data into local variables
      Bid storage currentBid = bids[currentIndex];
      uint256 sellCouponAmount = currentBid.sellCouponAmount;
      uint256 prevIndex = currentBid.prevBidIndex;

      if (amountToRemove >= sellCouponAmount) {
        ... ...
      } else {
        // Calculate the proportion of sellAmount being removed
        uint256 proportion = (amountToRemove * 1e18) / sellCouponAmount;
        
        // Reduce the current bid's amounts
        currentBid.sellCouponAmount = sellCouponAmount - amountToRemove;
        currentCouponAmount -= amountToRemove;

        uint256 reserveReduction = ((currentBid.buyReserveAmount * proportion) / 1e18);
        currentBid.buyReserveAmount = currentBid.buyReserveAmount - reserveReduction;
        totalSellReserveAmount -= reserveReduction;
        
        // Refund the proportional sellAmount
     >> IERC20(buyCouponToken).safeTransfer(currentBid.bidder, amountToRemove);
        
        amountToRemove = 0;
        emit BidReduced(currentIndex, currentBid.bidder, currentBid.buyReserveAmount, currentBid.sellCouponAmount);
      }
    }
  }
```

### Internal Pre-conditions

1. The bidder with the current lowest bid placed their bid before being blacklisted by USDC.

### External Pre-conditions

_No response_

### Attack Path

1. Alice bids for the auction.
2. Alice is blacklisted by USDC.
3. Bob bids and Alice's bid needs to be removed due to her bid being the lowest. The transaction reverts.

### Impact

The lowest bid can neither be removed nor partially refunded. This means that no one can bid anymore, and the bidder with the lowest bid will be able to claim a portion of the pool after the auction ends, even if other users wanted to outbid them.

### PoC

_No response_

### Mitigation

Instead of pushing the `buyCouponToken` amount to bidders, let them claim it.