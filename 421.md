Sweet Fiery Aardvark

Medium

# `BondOracleAdapter::latestRoundData` will return incorrect price for negative ticks

### Summary

The function [BondOracleAdapter::latestRoundData](https://github.com/sherlock-audit/2024-12-plaza-finance/tree/main/plaza-evm/src/BondOracleAdapter.sol#L99-L114) is used to get twap price of a BOND/ASSET pool.

```solidity
    function latestRoundData() external view returns (uint80, int256, uint256, uint256, uint80) {
        uint32[] memory secondsAgos = new uint32[](2);
        secondsAgos[0] = twapInterval; // from (before)
        secondsAgos[1] = 0; // to (now)

        (int56[] memory tickCumulatives,) = ICLPool(dexPool).observe(secondsAgos);

        uint160 getSqrtTwapX96 =
            TickMath.getSqrtRatioAtTick(int24((tickCumulatives[1] - tickCumulatives[0]) / int56(uint56(twapInterval))));

   
        return (
            uint80(0), int256(getPriceX96FromSqrtPriceX96(getSqrtTwapX96)), block.timestamp, block.timestamp, uint80(0)
        );
    }
```

It gets the `getSqrtTwapX96` by getting the ratio at tick `tickCumulatives[1] - tickCumulatives[0] / twapInterval`, and then returns the price.

### Root Cause

When getting the `getSqrtTwapX96`, if `(tickCumulatives[1] - tickCumulatives[0])` is negative the tick should be rounded down as it's done in the `OracleLibrary` from uniswap.

### Internal Pre-conditions

None

### External Pre-conditions

1. `(tickCumulatives[1] - tickCumulatives[0])` is negative
2. `(tickCumulatives[1] - tickCumulatives[0]) % twapInterval != 0`

### Attack Path

_No response_

### Impact

in case if `(tickCumulatives[1] - tickCumulatives[0])` is negative and `(tickCumulatives[1] - tickCumulatives[0]) % secondsAgo != 0`, then returned tick will be bigger then it should be, hence incorrect prices would be used.

### PoC

_No response_

### Mitigation

Consider rounding down the arithmetic mean tick before fetching the price

```solidity
        int56 secondsAgoInt56 = int56(uint56(twapInterval));
        int56 tickCumulativesDelta = tickCumulatives[1] - tickCumulatives[0];
        int24 arithmeticMeanTick = int24((tickCumulatives[1] - tickCumulatives[0]) / secondsAgoInt56);

        if (tickCumulativesDelta < 0 && (tickCumulativesDelta % secondsAgoInt56 != 0)) arithmeticMeanTick--;

        uint160 getSqrtTwapX96 = TickMath.getSqrtRatioAtTick(arithmeticMeanTick);
```