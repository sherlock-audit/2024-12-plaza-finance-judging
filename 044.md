Quiet Seafoam Carp

High

# Most shareholdes of BondToken are DeFi Contracts, Not Users

### Summary

In current implementation of the `BondToken` shares most of the shareholders will be DeFi smart contracts from other protocols like [Morpho](https://morpho.org/) or [AAVE](https://aave.com/), not regular users.

### Root Cause

Most users of the protocol will use `bondETH` as a token for their DeFi interaction because their `reserveTokens` will be locked in the [Pool](https://github.com/sherlock-audit/2024-12-plaza-finance/blob/main/plaza-evm/src/Pool.sol) contract.
By the [docs](https://docs.plaza.finance/#:~:text=Users%20can%20deposit%20an%20underlying%20pool%20asset%20like%20wstETH%20and%20receive%20levETH%20or%20bondETH%20in%20return%2C%20which%20are%20represented%20as%20ERC20%20tokens.%20These%20tokens%20are%20composable%20with%20protocols%20such%20as%20DEXes%2C%20lending%20markets%2C%20restaking%20platforms%2C%20etc.):
*Users can deposit an underlying pool asset like [wstETH](https://basescan.org/token/0xc1cba3fcea344f92d9239c08c0568f6f2f0ee452#balances) and receive levETH or bondETH in return, which are represented as ERC20 tokens. These tokens are composable with protocols such as DEXes, lending markets, restaking platforms, etc.*
[And](https://docs.plaza.finance/plaza-assets/bondeth-an-ethereum-backed-bond#:~:text=As%20a%20bondETH%20holder%2C%20you%27re%20entitled%20to%20a%20fixed%20quarterly%20coupon%20of%202.50%20USDC%20per%20bondETH%20(10.00%20USDC%20per%20bondETH%20per%20year)%2C%20paid%20out%20in%20perpetuity.):
*As a bondETH holder, you're entitled to a fixed quarterly coupon of 2.50 USDC per bondETH (10.00 USDC per bondETH per year), paid out in perpetuity.*

But actually most of the `bondETH` holders will be DeFi smart contracts as it happening with [wstETH](https://basescan.org/token/0xc1cba3fcea344f92d9239c08c0568f6f2f0ee452#balances) and [cbBTC](https://basescan.org/token/0xcbb7c0000ab88b473b1f5afd9ef808440eed33bf#balances) where most holders are DeFi protocols like Morpho and AAVE.

This happens because current `BondToken.sol` contract [_update](https://github.com/sherlock-audit/2024-12-plaza-finance/blob/main/plaza-evm/src/BondToken.sol#L154) implementation changes `shares` of both sides, meaning that interacting with DeFi smart contracts users will give them `shares`:

```solidity
/**
  * @dev Internal function to update user assets after a transfer.
  * @param from The address tokens are transferred from
  * @param to The address tokens are transferred to
  * @param amount The amount of tokens transferred
  * @notice This function is called during token transfer and is paused when the contract is paused.
  */
function _update(address from, address to, uint256 amount) internal virtual override whenNotPaused() {
  if (from != address(0)) {
    updateIndexedUserAssets(from, balanceOf(from));
  }

  if (to != address(0)) {
    updateIndexedUserAssets(to, balanceOf(to));
  }

super._update(from, to, amount);
}

/**
  * @dev Updates the indexed user assets for a specific user.
  * @param user The address of the user
  * @param balance The current balance of the user
  * @notice This function updates the number of shares held by the user based on the current period.
  */
function updateIndexedUserAssets(address user, uint256 balance) internal {
  uint256 period = globalPool.currentPeriod;
  uint256 shares = getIndexedUserAmount(user, balance, period);

  userAssets[user].indexedAmountShares = shares;
  userAssets[user].lastUpdatedPeriod = period;

  emit UpdatedUserAssets(user, period, shares);
}

/**
  * @dev Returns the indexed amount of shares for a specific user.
  * @param user The address of the user
  * @param balance The current balance of the user
  * @param period The period to calculate the shares for
  * @return The indexed amount of shares for the user
  * @notice This function calculates the number of shares based on the current period and the previous pool amounts.
  */
function getIndexedUserAmount(address user, uint256 balance, uint256 period) public view returns(uint256) {
  IndexedUserAssets memory userPool = userAssets[user];
  uint256 shares = userPool.indexedAmountShares;

  for (uint256 i = userPool.lastUpdatedPeriod; i < period; i++) {
    shares += (balance * globalPool.previousPoolAmounts[i].sharesPerToken).toBaseUnit(SHARES_DECIMALS);
  }

  return shares;
}
```

### Internal Pre-conditions

*No response*

### External Pre-conditions

*No response*

### Attack Path

*No response*

### Impact

Real users who deposited their reserve tokens in the `Pool` will not be able to get their coupons or will get less because most real `bondETH` holders will be DeFi smart contracts and they will get shares.

### PoC

PoC is written using [PreDeposit.sol](https://github.com/sherlock-audit/2024-12-plaza-finance/blob/main/plaza-evm/src/PreDeposit.sol#L163) contract which also can receive shares under certain circumstances like - During claim of tokens created pool increased indexed asset period (that can happen easily because not everyone claiming their reward fast).

Add this test in the `/test/PreDeposit.t.sol`:

```solidity
function testPreDepositSharesOnBond() public {
  (, address bondToken, address lToken) = deployFakePool();
  //add DISTRIBUTOR_ROLE to the governance
  vm.startPrank(governance);
  BondToken(bondToken).grantRole(BondToken(bondToken).DISTRIBUTOR_ROLE(), governance);
  vm.stopPrank();

  // Setup initial deposit
  vm.startPrank(user1);
  reserveToken.approve(address(preDeposit), DEPOSIT_AMOUNT);
  preDeposit.deposit(DEPOSIT_AMOUNT);
  vm.stopPrank();
  vm.startPrank(user2);
  reserveToken.approve(address(preDeposit), DEPOSIT_AMOUNT);
  preDeposit.deposit(DEPOSIT_AMOUNT);
  vm.stopPrank();

  // Create pool
  vm.startPrank(governance);
  preDeposit.setBondAndLeverageAmount(BOND_AMOUNT, LEVERAGE_AMOUNT);
  vm.warp(block.timestamp + 8 days); // After deposit period

  // fake bond/lev to predeposit contract, simulating a pool created
  BondToken(bondToken).mint(address(preDeposit), 10000 ether);
  LeverageToken(lToken).mint(address(preDeposit), 10000 ether);

  vm.stopPrank();

  //simulating that during claim of tokens, existed pools increased indexed asset period
  //that easily can happen because not everyone claiming their reward fast 
  vm.startPrank(governance);
  BondToken(bondToken).increaseIndexedAssetPeriod(50*10**6); //number of shares from Distributor.t.sol
  vm.stopPrank();

  // Claim tokens user1
  vm.startPrank(user1);
  preDeposit.claim();
  vm.stopPrank();

  //simulating that during claim of tokens, existed pools increased indexed asset period
  //that easily can happen because not everyone claiming their reward fast 
  vm.startPrank(governance);
  BondToken(bondToken).increaseIndexedAssetPeriod(50*10**6); //number of shares from Distributor.t.sol
  vm.stopPrank();

  // Claim tokens user2
  vm.startPrank(user2);
  preDeposit.claim();
  vm.stopPrank();

  (, uint256 indexedAmountShares) = BondToken(bondToken).userAssets(address(preDeposit));
  console.log("Indexed amount of shares for preDeposit contract: ", indexedAmountShares);
  assert(indexedAmountShares > 0);
}
```

In cmd run this command:

```bash
forge test -vv --mt testPreDepositSharesOnBond
```

Output:

```bash
Ran 1 test for test/PreDeposit.t.sol:PreDepositTest
[PASS] testPreDepositSharesOnBond() (gas: 11119178)
Logs:
  Indexed amount of shares for preDeposit contract:  498750000000000000000000

Suite result: ok. 1 passed; 0 failed; 0 skipped; finished in 3.32ms (1.03ms CPU time)
```

### Mitigation

I will suggest to calculate `shares` based on reserve tokens that users locked in `Pool` contract.