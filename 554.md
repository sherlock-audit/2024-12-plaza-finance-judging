Swift Wintergreen Viper

High

# Funds are permanently locked in the PreDeposit contract if bond/leverage amounts are not set before deposit period ends

### Summary

Missing validation of bond/leverage amounts during initialization will cause a loss of funds for users as their funds will be locked in the `PreDeposit` contract when the {Owner} fails to set these amounts before the deposit period ends.

### Root Cause

In  [PreDeposit.sol:151](https://github.com/sherlock-audit/2024-12-plaza-finance/blob/main/plaza-evm/src/PreDeposit.sol#L151) the `createPool()` function requires `bondAmount` and `leverageAmount` to be non-zero, but these values can only be set during the deposit period. If the deposit period ends without these being set, funds become permanently locked.

```solidity
function createPool() external nonReentrant whenNotPaused checkDepositEnded {
    if (reserveAmount == 0) revert NoReserveAmount();
    if (bondAmount == 0 || leverageAmount == 0) revert InvalidBondOrLeverageAmount(); // <-- This check
    if (poolCreated) revert PoolAlreadyCreated();
    // ... rest of function
}
```

### Internal Pre-conditions

1. {Users} need to deposit any amount of reserve tokens into the `PreDeposit` contract
2. {Owner} needs to not call `setBondAndLeverageAmount()` before deposit period ends
3. Deposit period needs to end (time needs to pass `depositEndTime`)

### External Pre-conditions

_No response_

### Attack Path

1. {Users} deposit reserve tokens during the deposit period
2. {Owner} forgets to call `setBondAndLeverageAmount()`
3. Deposit period ends
4. `createPool()` will revert due to zero bond/leverage amounts
5. {Users} cannot claim tokens (requires pool to be created)
6. {Users} cannot withdraw (deposit period ended)
7. **Funds are permanently locked in the contract**

### Impact

{Users} suffer a complete loss of their deposited funds as they become permanently locked in the `PreDeposit` contract with no recovery mechanism available.

### PoC

Paste this into the `PreDeposit.t.sol` file and run the test command: `forge test --mt testCreatePoolWithUnsetBondOrLeverageAmountLeadsToStuckFunds`

```solidity
    function testCreatePoolWithUnsetBondOrLeverageAmountLeadsToStuckFunds() public {
        vm.startPrank(user1);
        reserveToken.approve(address(preDeposit), DEPOSIT_AMOUNT);
        preDeposit.deposit(DEPOSIT_AMOUNT);
        vm.stopPrank();

        vm.startPrank(governance);
        vm.warp(block.timestamp + 8 days); // After deposit period

        vm.expectRevert(PreDeposit.InvalidBondOrLeverageAmount.selector);
        // governance can't create pool
        preDeposit.createPool();

        vm.expectRevert(PreDeposit.DepositEnded.selector);
        // governance can't set bond and leverage amount
        preDeposit.setBondAndLeverageAmount(BOND_AMOUNT, LEVERAGE_AMOUNT);
        vm.stopPrank();

        vm.startPrank(user1);
        // user can't claim
        vm.expectRevert(PreDeposit.ClaimPeriodNotStarted.selector);
        preDeposit.claim();

        // user can't withdraw
        vm.expectRevert(PreDeposit.DepositEnded.selector);
        preDeposit.withdraw(DEPOSIT_AMOUNT);

        // check that the funds are still in the preDeposit contract
        assertEq(reserveToken.balanceOf(address(preDeposit)), DEPOSIT_AMOUNT);
    }
```

### Mitigation

Require bond and leverage amounts to be set during initialization:

```solidity
function initialize(
    PoolFactory.PoolParams memory _params,
    address _factory,
    uint256 _depositStartTime,
    uint256 _depositEndTime,
    uint256 _reserveCap,
    string memory _bondName,
    string memory _bondSymbol,
    string memory _leverageName,
    string memory _leverageSymbol,
    uint256 _bondAmount,    // Add these parameters
    uint256 _leverageAmount
) public initializer {
    if (_bondAmount == 0 || _leverageAmount == 0) revert InvalidBondOrLeverageAmount();
    bondAmount = _bondAmount;
    leverageAmount = _leverageAmount;
    // ... rest of initialization
}
```