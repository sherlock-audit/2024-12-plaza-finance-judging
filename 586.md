Creamy Misty Rooster

High

# Incorrect `currentPeriod` in `transferReserveToAuction` Leads to Auction Funds Being Stuck

### Summary

The `transferReserveToAuction` function uses the `currentPeriod` value from `bondToken.globalPool()` to identify the current auction. However, during `startAuction`, the `globalPool.currentPeriod` is incremented by `increaseIndexedAssetPeriod` **AFTER** assigning the auction address to  the previous period. 
This mismatch causes `transferReserveToAuction` to retrieve the wrong address, leading to a revert when attempting to transfer reserve tokens. 
Thus, `endAuction` also fails since it invokes on `transferReserveToAuction`. This leaves the auction stuck in the `BIDDING` state, and funds collected during the auction cannot be finalized or refunded to bidders.  

### Root Cause

In `startAuction`, the call to `bondToken.increaseIndexedAssetPeriod` increments the `currentPeriod` after assigning the auction address for the past period. As a result:  
1. `currentPeriod` references the next period rather than the ongoing auction period.  
2. The `transferReserveToAuction` function, which uses `currentPeriod` to identify the current auction, retrieves the wrong address.  
3. This mismatch between the retrieved address and the auction contract address causes a revert in `transferReserveToAuction`, preventing `endAuction` from finalizing the auction.  

### Internal Pre-conditions

1. The `startAuction` function is called, and a new auction is initialized.  

### External Pre-conditions

No external conditions are required for this issue to occur. 

### Attack Path

This is a logic flaw and not an exploitable attack vector. The issue arises during normal protocol operation when transitioning between auction periods.  

### Impact

If `transferReserveToAuction` reverts:  
- The `endAuction` function fails, leaving the auction in the `BIDDING` state.  
- Funds collected from bidders during the auction remain locked in the `Auction` contract as Bidders cannot claim their tokens (neither `claimBid` nor `claimRefund` is accessible due to the `BIDDING` state).  
- Protocol functionality is disrupted, leading to direct loss of funds for the bidders and no rewards ditribution for the Bond holders (as auction never concluded) 

### PoC

The [`startAuction`](https://github.com/sherlock-audit/2024-12-plaza-finance/blob/main/plaza-evm/src/Pool.sol#L530-L571) function increments the `currentPeriod` **AFTER** assigning the auction address [`BondToken::increaseIndexedAssetPeriod`](https://github.com/sherlock-audit/2024-12-plaza-finance/blob/main/plaza-evm/src/BondToken.sol#L217-L225):  

```solidity
  function startAuction() external whenNotPaused() {
    // Check if distribution period has passed
    require(lastDistribution + distributionPeriod < block.timestamp, DistributionPeriodNotPassed());

    // Check if auction period hasn't passed
    require(lastDistribution + distributionPeriod + auctionPeriod >= block.timestamp, AuctionPeriodPassed());

    // Check if auction for current period has already started
    (uint256 currentPeriod,) = bondToken.globalPool();
    require(auctions[currentPeriod] == address(0), AuctionAlreadyStarted());

    ...

    auctions[currentPeriod] = Utils.deploy(
      address(new Auction()),
      abi.encodeWithSelector(
        Auction.initialize.selector,
        address(couponToken),
        address(reserveToken),
        couponAmountToDistribute,
        block.timestamp + auctionPeriod,
        1000,
        address(this),
        poolSaleLimit
      )
    );

    // Increase the bond token period
    bondToken.increaseIndexedAssetPeriod(sharesPerToken);

    // Update last distribution time
    lastDistribution = block.timestamp;
  }
```  

So from that point on the period given by `bondToken.globalPool()` will be the next period and not the current one. 

The [`transferReserveToAuction`](https://github.com/sherlock-audit/2024-12-plaza-finance/blob/main/plaza-evm/src/Pool.sol#L577-L580) function will try to check that the correct auction contract is making the call, it retrieves the  current period using `bondToken.globalPool()` and try to get the auction address using `auctions[currentPeriod]` but this will return `address(0)` as the `currentPeriod` is referencing a period which has no auction yet:  

```solidity
function transferReserveToAuction(uint256 amount) external virtual {
    (uint256 currentPeriod, ) = bondToken.globalPool();
    address auctionAddress = auctions[currentPeriod]; //@audit will be address(0)

    require(msg.sender == auctionAddress, CallerIsNotAuction()); // Reverts

    IERC20(reserveToken).safeTransfer(msg.sender, amount);
}
```  

So `transferReserveToAuction` will revert when checking the sender. Since [`endAuction`](https://github.com/sherlock-audit/2024-12-plaza-finance/blob/main/plaza-evm/src/Auction.sol#L336-L350) invokes `transferReserveToAuction`, it also reverts, leaving the auction in the `BIDDING` state:  

```solidity
function endAuction() external auctionExpired whenNotPaused {
if (state != State.BIDDING) revert AuctionAlreadyEnded();

if (currentCouponAmount < totalBuyCouponAmount) {
    state = State.FAILED_UNDERSOLD;
} else if (totalSellReserveAmount >= (IERC20(sellReserveToken).balanceOf(pool) * poolSaleLimit) / 100) {
    state = State.FAILED_POOL_SALE_LIMIT;
} else {
    state = State.SUCCEEDED;
    //@audit will revert
    Pool(pool).transferReserveToAuction(totalSellReserveAmount);
    IERC20(buyCouponToken).safeTransfer(beneficiary, IERC20(buyCouponToken).balanceOf(address(this)));
}

emit AuctionEnded(state, totalSellReserveAmount, totalBuyCouponAmount);
}
``` 

In the `BIDDING` state, bidders can not get neither get their Coupon token back (when auction fails) nor collect the sold reserve tokens (when auction successed), because the modifiers [`auctionFailed`](https://github.com/sherlock-audit/2024-12-plaza-finance/blob/main/plaza-evm/src/Auction.sol#L410-L413) and [`auctionSucceeded`](https://github.com/sherlock-audit/2024-12-plaza-finance/blob/main/plaza-evm/src/Auction.sol#L405-L408) respectively will prevent that (see [code](https://github.com/sherlock-audit/2024-12-plaza-finance/blob/main/plaza-evm/src/Auction.sol#L356-L376)):

```solidity
function claimBid(uint256 bidIndex) auctionExpired auctionSucceeded whenNotPaused external {
    ...
}

function claimRefund(uint256 bidIndex) auctionExpired auctionFailed whenNotPaused external {
    ...
}

modifier auctionSucceeded() {
    if (state != State.SUCCEEDED) revert AuctionFailed();
    _;
}

modifier auctionFailed() {
    if (state == State.SUCCEEDED || state == State.BIDDING) revert AuctionFailed();
    _;
}
``` 

### Example scenario:

We simulate a sequence of events that leads to the auction funds being stuck due to the `currentPeriod` mismatch.  

---

#### **Initial State:**  
- `globalPool.currentPeriod = 0`.  
- No auction has been created yet (`auctions[0] = address(0)`).  
- `lastDistribution = 0`.  

---

#### **Step 1: Start Auction**  
- The `startAuction` function is called.  
- The following actions occur in sequence:  
  1. The current period is retrieved as `currentPeriod = 0`.  
  2. A new auction contract is deployed for period `0` and assigned to `auctions[0]`.  
  3. The `increaseIndexedAssetPeriod` function is called, incrementing `globalPool.currentPeriod` to `1`.  
  4. `lastDistribution` is updated to the current timestamp.  

**Result:**  
- `globalPool.currentPeriod = 1`.  
- `auctions[0]` is assigned the new auction contract address.  

---

#### **Step 2: Auction is in Progress**  
- Bidders participate in the auction, placing bids and locking their funds in the auction contract (`auctions[0]`).  
- The auction proceeds normally until the `auctionPeriod` expires.  

---

#### **Step 3: End Auction**  
- The `endAuction` function is invoked to finalize the auction.  
- Inside `endAuction`, the following occurs:  
  1. The `transferReserveToAuction` function is called to transfer reserve tokens to the auction contract.  
  2. `transferReserveToAuction` retrieves the current period from `globalPool.currentPeriod`, which is now `1`.  
  3. It attempts to retrieve the auction address for `currentPeriod = 1`, but `auctions[1] = address(0)` since no auction has been assigned for period `1`.  
  4. The `require` check for `msg.sender == auctionAddress` fails because `auctionAddress` is `address(0)`, causing the function to revert.  

**Result:**  
- `endAuction` fails, leaving the auction in the `BIDDING` state.  

---

#### **Step 4: Auction Funds are Stuck**  
- Because the auction remains in the `BIDDING` state:  
  - Bidders cannot call `claimBid` to claim their tokens (success case).  
  - Bidders cannot call `claimRefund` to retrieve their funds (failure case).  
  - Funds locked in the auction contract cannot be redistributed.  

### Mitigation

To resolve this issue:  
1. Assign the auction address **after** incrementing the `currentPeriod` in `startAuction`:  

   ```solidity
   bondToken.increaseIndexedAssetPeriod(sharesPerToken);
   auctions[currentPeriod] = Utils.deploy(...);
   ```

2. Alternatively, maintain a separate `activePeriod` variable for auction-specific logic to avoid relying on `currentPeriod` for ongoing auctions.  

By ensuring that `transferReserveToAuction` correctly identifies the active auction, the protocol can finalize auctions without errors and avoid locking user funds.  