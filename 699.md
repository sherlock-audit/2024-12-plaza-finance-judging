Ambitious Rusty Turkey

Medium

# Subsequent auctions after the first one cannot be created

### Summary

Subsequent auctions after the first one cannot be created because of an incorrect auction and distribution completion period for the last auction created in the Pool contract's `startAuction` function.

### Root Cause

The function below has a logic for trying to ensure that the previous auction has ended and its reserve asset has been distributed. However, it does this with an incorrect way:

```solidity
function startAuction() external whenNotPaused() {
    // Check if distribution period has passed
    require(lastDistribution + distributionPeriod < block.timestamp, DistributionPeriodNotPassed());

    // Check if auction period hasn't passed
    // @audit New auctions cannot be created once the old one's distro and auction period has passed

@>    require(lastDistribution + distributionPeriod + auctionPeriod >= block.timestamp, AuctionPeriodPassed());

    // Check if auction for current period has already started
    (uint256 currentPeriod,) = bondToken.globalPool();
    require(auctions[currentPeriod] == address(0), AuctionAlreadyStarted());

    uint8 bondDecimals = bondToken.decimals();
    uint8 sharesDecimals = bondToken.SHARES_DECIMALS();
    uint8 maxDecimals = bondDecimals > sharesDecimals ? bondDecimals : sharesDecimals;

    uint256 normalizedTotalSupply = bondToken.totalSupply().normalizeAmount(bondDecimals, maxDecimals);
    uint256 normalizedShares = sharesPerToken.normalizeAmount(sharesDecimals, maxDecimals); //

    // Calculate the coupon amount to distribute
    uint256 couponAmountToDistribute = (normalizedTotalSupply * normalizedShares)
        .toBaseUnit(maxDecimals * 2 - IERC20(couponToken).safeDecimals());

    auctions[currentPeriod] = Utils.deploy(
      address(new Auction()),
      abi.encodeWithSelector(
        Auction.initialize.selector,
        address(couponToken),
        address(reserveToken),
        couponAmountToDistribute,
        block.timestamp + auctionPeriod,
        1000,
        address(this),
        poolSaleLimit
      )
    );

    // Increase the bond token period
    bondToken.increaseIndexedAssetPeriod(sharesPerToken);

    // Update last distribution time
    lastDistribution = block.timestamp;
  }
```

LINES OF CODE
https://github.com/sherlock-audit/2024-12-plaza-finance/blob/main/plaza-evm/src/Pool.sol#L535

### Internal Pre-conditions

1. First auction kicks off
2. First auction is concluded
3. First auction distribution period is complete and distribution has been finalized
4. Call to start auction number 2 fails because of the wrong check here `require(lastDistribution + distributionPeriod + auctionPeriod >= block.timestamp, AuctionPeriodPassed());`

### External Pre-conditions

There are no internal preconditions for this issue to become realized.

### Attack Path

1. First auction kicks off
2. The first auction is concluded
3. The first auction distribution period is complete and distribution has been finalized with the auction contract now holding the reserve token auctioned off.
4. Call to start auction number 2 fails because of the wrong check here `require(lastDistribution + distributionPeriod + auctionPeriod >= block.timestamp, AuctionPeriodPassed());`

### Impact

Creation of subsequent auctions after the first auction has ended will not be possible for the Pool contract. Hence, the Pool's `startAuction` function will be DoSed.

### PoC

Do this diff in the Pool.t.sol test contract:

```diff
using Decimals for uint256;
  using Strings for uint256;

  Pool.TokenType initassetType;
+ Pool.TokenType initassetType1;
  PoolFactory private poolFactory;
  PoolFactory.PoolParams private params;

  MockPriceFeed private mockPriceFeed;
+ MockPriceFeed private mockPriceFeed1;
  address private oracleFeedsContract;

+ Token public auctionCouponToken;

  address private deployer = address(0x1);
  address private minter = address(0x2);
  address private governance = address(0x3);
  address private securityCouncil = address(0x4);
  address private user = address(0x5);
  address private user2 = address(0x6);

  address public constant ethPriceFeed = address(0x71041dddad3595F9CEd3DcCFBe3D1F4b0a16Bb70);
+ address public constant bondEthPriceFeed = address(0x71041dDdAD3595f9Ced3DcCfbE3d1f4B0A16bB71);
  uint256 private constant CHAINLINK_DECIMAL_PRECISION = 10**8;
  uint8 private constant CHAINLINK_DECIMAL = 8;

function setUp() public {
    vm.startPrank(deployer);

    address contractDeployer = address(new Deployer());
    oracleFeedsContract = address(new OracleFeeds());

    address poolBeacon = address(new UpgradeableBeacon(address(new Pool()), governance));
    address bondBeacon = address(new UpgradeableBeacon(address(new BondToken()), governance));
    address levBeacon = address(new UpgradeableBeacon(address(new LeverageToken()), governance));
    address distributorBeacon = address(new UpgradeableBeacon(address(new Distributor()), governance));

    poolFactory = PoolFactory(Utils.deploy(address(new PoolFactory()), abi.encodeCall(
      PoolFactory.initialize, 
      (governance, contractDeployer, oracleFeedsContract, poolBeacon, bondBeacon, levBeacon, distributorBeacon)
    )));

    params.fee = 0;
    params.feeBeneficiary = governance;
    params.reserveToken = address(new Token("Wrapped ETH", "WETH", false));
    params.sharesPerToken = 50 * 10 ** 18;
    params.distributionPeriod = 0;
+    auctionCouponToken = new Token("USDC", "USDC", false);
-    params.couponToken = address(new Token("USDC", "USDC", false));
+    params.couponToken = address(auctionCouponToken);
    
    OracleFeeds(oracleFeedsContract).setPriceFeed(params.reserveToken, address(0), ethPriceFeed, 1 days);

    // Deploy the mock price feed
    mockPriceFeed = new MockPriceFeed();

    // Use vm.etch to deploy the mock contract at the specific address
    bytes memory bytecode = address(mockPriceFeed).code;
    vm.etch(ethPriceFeed, bytecode);

    // Set oracle price
    mockPriceFeed = MockPriceFeed(ethPriceFeed);
    mockPriceFeed.setMockPrice(3000 * int256(CHAINLINK_DECIMAL_PRECISION), uint8(CHAINLINK_DECIMAL));
    
    vm.stopPrank();

    vm.startPrank(governance);
    poolFactory.grantRole(poolFactory.POOL_ROLE(), governance);
    poolFactory.grantRole(poolFactory.SECURITY_COUNCIL_ROLE(), securityCouncil);
    vm.stopPrank();
  }
```

Add the IAuction interface in the Pool.t.sol file:

```solidity
interface IAuction {
  function bid(uint256 buyReserveAmount, uint256 sellCouponAmount) external returns(uint256);
  function endAuction() external;
  function totalBuyCouponAmount() external returns(uint256);
}
```

Then paste this test case and run the test with `forge test --mt testNewAuctionCannotStart -vvv`

```solidity
function testNewAuctionCannotStart() public {
    initializeTestCasesFixedEth();

    vm.startPrank(governance);
    Token rToken = Token(params.reserveToken); // WETH

    // Mint reserve tokens for PreDeposit actions
    rToken.mint(governance, 10 ether);
    rToken.approve(address(poolFactory), 10 ether);

    // Mint reserve tokens to Alice from Dallas
    address aliceFromDallas = makeAddr("aliceFromDallas");
    rToken.mint(aliceFromDallas, 1 ether);

    // Create salt to create the pool at a different address
    string memory salt = "NEW POOL";
    
    Pool _pool = Pool(poolFactory.createPool(params, 10 ether, 300 ether, 40 ether, "", salt, "", "", false));

    console.log("Pool: ", address(_pool));
    console.log("Pool ETH balance after creation: ", rToken.balanceOf(address(_pool)));

    uint256 startBondBalance = BondToken(_pool.bondToken()).balanceOf(governance);
    uint256 startLevBalance = LeverageToken(_pool.lToken()).balanceOf(governance);

    console.log("startBondBalance: ", startBondBalance);
    console.log("startLevBalance: ", startLevBalance);

    initassetType = Pool.TokenType.BOND;
    initassetType1 = Pool.TokenType.LEVERAGE;
    vm.stopPrank();

    // ORACLE FEEDS ARE SET AND PRICES TOO FOR THE BOND FEED
    vm.startPrank(deployer);
    OracleFeeds(oracleFeedsContract).setPriceFeed(address(_pool.bondToken()), address(0), bondEthPriceFeed, 1 days);

    // Deploy the mock price feed
    mockPriceFeed1 = new MockPriceFeed();

    // Use vm.etch to deploy the mock contract at the specific address
    bytes memory bytecode = address(mockPriceFeed1).code;
    vm.etch(bondEthPriceFeed, bytecode);

    // Set oracle price
    mockPriceFeed1 = MockPriceFeed(bondEthPriceFeed);
    mockPriceFeed1.setMockPrice(100 * int256(CHAINLINK_DECIMAL_PRECISION), uint8(CHAINLINK_DECIMAL)); 
    vm.stopPrank();

    vm.prank(aliceFromDallas);
    rToken.approve(address(_pool), 1 ether);
    vm.prank(aliceFromDallas);
    uint256 amountForAliceInBond = _pool.create(initassetType, 1 ether, 0);

    console.log("Alice minted bondETH: ", amountForAliceInBond);
    console.log("Alice WETH balance after create: ", rToken.balanceOf(aliceFromDallas));

    vm.startPrank(governance);
    _pool.setAuctionPeriod(10 days);
    vm.stopPrank();

    vm.warp(block.timestamp + 1 days);
    vm.roll(block.number + 200);

    vm.prank(aliceFromDallas);
    _pool.startAuction();

    Pool.PoolInfo memory info = _pool.getPoolInfo();

    console.log("Current Period: ", info.currentPeriod);
    console.log("Deployed auction in auctions[0]: ", _pool.auctions(0));
    console.log("Deployed auction in current period auctions[1]: ", _pool.auctions(info.currentPeriod));

    IAuction deployedAuction = IAuction(_pool.auctions(0));
    uint256 totalBuyCouponAmount = deployedAuction.totalBuyCouponAmount();

    console.log("totalBuyCouponAmount: ", totalBuyCouponAmount);

    auctionCouponToken.mint(aliceFromDallas, totalBuyCouponAmount);

    vm.prank(aliceFromDallas);
    auctionCouponToken.approve(address(deployedAuction), totalBuyCouponAmount);

    vm.prank(aliceFromDallas);
    deployedAuction.bid(1e18, totalBuyCouponAmount);

    vm.warp(block.timestamp + 10.5 days);

    vm.prank(aliceFromDallas);
    deployedAuction.endAuction();

    vm.warp(block.timestamp + 30 days);

    vm.prank(aliceFromDallas);
    _pool.startAuction();
  }
```

Test result:
```javascript
 ├─ [4811] BeaconProxy::fallback()
    │   ├─ [722] UpgradeableBeacon::implementation() [staticcall]
    │   │   └─ ← [Return] Pool: [0xc051134F56d56160E8c8ed9bB3c439c78AB27cCc]
    │   ├─ [2662] Pool::startAuction() [delegatecall]
    │   │   └─ ← [Revert] AuctionPeriodPassed()
    │   └─ ← [Revert] AuctionPeriodPassed()
    └─ ← [Revert] AuctionPeriodPassed()
```

As can be seen from the POC log, new auction creations will fail for the pool.

NOTE: another issue exists within the `transferReserveToAuction` function of the Pool contract which I have fixed that allows us to run the POC. You can fix that issue first and then run this POC or just comment out this `require(msg.sender == auctionAddress, CallerIsNotAuction());` for now in the Pool.sol contract

```diff
function transferReserveToAuction(uint256 amount) external virtual {
    (uint256 currentPeriod,) = bondToken.globalPool();
    address auctionAddress = auctions[currentPeriod];
-     require(msg.sender == auctionAddress, CallerIsNotAuction());
+    // require(msg.sender == auctionAddress, CallerIsNotAuction());
    
    IERC20(reserveToken).safeTransfer(msg.sender, amount);
  }
```

### Mitigation

```diff
-  error AuctionPeriodPassed();
+  error AuctionPeriodNotPassed();
```

```diff
-   require(lastDistribution + distributionPeriod + auctionPeriod >= block.timestamp, AuctionPeriodPassed());
+   require(block.timestamp > lastDistribution + distributionPeriod + auctionPeriod, AuctionPeriodNotPassed());
```