Quiet Seafoam Carp

High

# CouponToken distribution is incorrect because transfer amount does not used proportionally to all shares

### Summary

[Distributor.claim()](https://github.com/sherlock-audit/2024-12-plaza-finance/blob/main/plaza-evm/src/Distributor.sol#L78) function directly uses user's `shares` amount to distribute `couponToken`, instead of scaling `couponAmountToDistribute` proportionally to the user's `share` relative to the total `shares` of the `BondToken`.

### Root Cause

In the `Distributor.claim()` function `couponToken` amount to claim equals user's `shares` amount of `BondToken`:
```Solidity
/**
  * @dev Allows a user to claim their shares from a specific pool.
  * Calculates the number of shares based on the user's bond token balance and the shares per token.
  * Transfers the calculated shares to the user's address.
  */
function claim() external whenNotPaused nonReentrant {
  BondToken bondToken = Pool(pool).bondToken();
  address couponToken = Pool(pool).couponToken();

  if (address(bondToken) == address(0) || couponToken == address(0)){
    revert UnsupportedPool();
  }

  (uint256 currentPeriod,) = bondToken.globalPool();
  uint256 balance = bondToken.balanceOf(msg.sender);
  //@audit get user shares from bondToken
  uint256 shares = bondToken.getIndexedUserAmount(msg.sender, balance, currentPeriod)
                            .normalizeAmount(bondToken.decimals(), IERC20(couponToken).safeDecimals());

  if (IERC20(couponToken).balanceOf(address(this)) < shares) {
    revert NotEnoughSharesBalance();
  }
  
  // check if pool has enough *allocated* shares to distribute
  if (couponAmountToDistribute < shares) {
    revert NotEnoughSharesToDistribute();
  }

  // check if the distributor has enough shares tokens as the amount to distribute
  if (IERC20(couponToken).balanceOf(address(this)) < couponAmountToDistribute) {
    revert NotEnoughSharesToDistribute();
  }

  couponAmountToDistribute -= shares;    
  bondToken.resetIndexedUserAssets(msg.sender);
  //@audit use shares amount directly not proportionally to all shares of BondToken
  IERC20(couponToken).safeTransfer(msg.sender, shares);
  
  emit ClaimedShares(msg.sender, currentPeriod, shares);
}
```

### Internal Pre-conditions

_No response_

### External Pre-conditions

_No response_

### Attack Path

_No response_

### Impact

Users receive incorrect amount of couponTokens, and, moreover, in certain circumstances, may not receive any at all.

### PoC

**Note that in this test we use low amounts of coupon tokens just to show that contract does not use proportion of shares.**
Create test contract `DistributorFailClaim.t.sol` in `/test` folder and paste this code:
```Solidity
// SPDX-License-Identifier: UNLICENSED
pragma solidity ^0.8.26;

import "forge-std/Test.sol";

import {Pool} from "../src/Pool.sol";
import {Token} from "./mocks/Token.sol";
import {Auction} from "../src/Auction.sol";
import {Utils} from "../src/lib/Utils.sol";
import {BondToken} from "../src/BondToken.sol";
import {Distributor} from "../src/Distributor.sol";
import {PoolFactory} from "../src/PoolFactory.sol";
import {LeverageToken} from "../src/LeverageToken.sol";
import {Deployer} from "../src/utils/Deployer.sol";
import {Decimals} from "../src/lib/Decimals.sol";
import {ERC20Extensions} from "../src/lib/ERC20Extensions.sol";
import {IERC20} from "@openzeppelin/contracts/token/ERC20/IERC20.sol";
import {SafeERC20} from "@openzeppelin/contracts/token/ERC20/utils/SafeERC20.sol";
import {UpgradeableBeacon} from "@openzeppelin/contracts/proxy/beacon/UpgradeableBeacon.sol";

contract DistributorFailClaimTest is Test {
    using SafeERC20 for IERC20;
    using ERC20Extensions for IERC20;
    using Decimals for uint256;

    Distributor public distributor;
    Pool public _pool;
    PoolFactory.PoolParams private params;
    PoolFactory public poolFactory;

    address public user0 = address(0x1);
    address public user1 = address(0x100);
    address private deployer = address(0x3);
    address private governance = address(0x4);
    address private securityCouncil = address(0x5);
    address public constant ethPriceFeed = address(0x71041dddad3595F9CEd3DcCFBe3D1F4b0a16Bb70);

    function setUp() public {
        vm.startPrank(deployer);

        // Deployer deploy
        address contractDeployer = address(new Deployer());

        // Pool, Bond & Leverage Beacon deploy
        address poolBeacon = address(new UpgradeableBeacon(address(new Pool()), governance));
        address bondBeacon = address(new UpgradeableBeacon(address(new BondToken()), governance));
        address levBeacon = address(new UpgradeableBeacon(address(new LeverageToken()), governance));
        address distributorBeacon = address(new UpgradeableBeacon(address(new Distributor()), governance));

        // PoolFactory deploy
        poolFactory = PoolFactory(Utils.deploy(address(new PoolFactory()), abi.encodeCall(
        PoolFactory.initialize, 
        (governance, contractDeployer, ethPriceFeed, poolBeacon, bondBeacon, levBeacon, distributorBeacon)
        )));

        vm.stopPrank();

        vm.startPrank(governance);

        params.fee = 0;
        params.sharesPerToken = 50*10**6;
        params.reserveToken = address(new Token("Wrapped ETH", "WETH", false));
        params.distributionPeriod = 0;
        params.couponToken = address(new Token("Circle USD", "USDC", false));
        
        vm.stopPrank(); 
        vm.startPrank(governance);
        poolFactory.grantRole(poolFactory.POOL_ROLE(), governance);
        Token rToken = Token(params.reserveToken);

        // Mint reserve tokens
        rToken.mint(governance, 10000000000);
        rToken.approve(address(poolFactory), 10000000000);

        // Create pool and approve deposit amount
        _pool = Pool(poolFactory.createPool(params, 10000000000, 10000*10**18, 10000*10**18, "", "", "", "", false));
        distributor = Distributor(poolFactory.distributors(address(_pool)));
        poolFactory.grantRole(poolFactory.SECURITY_COUNCIL_ROLE(), securityCouncil);

        _pool.bondToken().grantRole(_pool.bondToken().DISTRIBUTOR_ROLE(), governance);
        _pool.bondToken().grantRole(_pool.bondToken().DISTRIBUTOR_ROLE(), address(distributor));
    }

    function fakeSucceededAuction(address poolAddress, uint256 period) public {
        address auction = Utils.deploy(address(new Auction()), abi.encodeWithSelector(Auction.initialize.selector, params.couponToken, params.reserveToken, 1000000000000, block.timestamp + 10 days, 1000, address(0), 95));

        uint256 auctionSlot = 11;
        bytes32 auctionPeriodSlot = keccak256(abi.encode(period, auctionSlot));
        vm.store(address(poolAddress), auctionPeriodSlot, bytes32(uint256(uint160(auction))));

        uint256 stateSlot = 6;
        vm.store(auction, bytes32(stateSlot), bytes32(uint256(1)));
    }

    //note that in this test we use low amounts of coupon tokens just to show that contract does not use proportion of shares
    function testClaimSharesAbleToDoOnlyOneUser() public {
        Token sharesToken = Token(_pool.couponToken());
        uint256 totalBuyCouponAmount = 10*10**19;
        //mock that user0 has bondTokens
        vm.startPrank(address(_pool));
        _pool.bondToken().mint(user0, 1*10**18);
        vm.stopPrank();

        //start two auctions, between them mock that user1 got the same amount of bondTokens as user0
        vm.startPrank(governance);
        //start auction 1
        fakeSucceededAuction(address(_pool), 0);
        vm.mockCall(
        address(0),
        abi.encodeWithSignature("state()"),
        abi.encode(uint256(1))
        );
        vm.mockCall(
        address(0),
        abi.encodeWithSignature("totalBuyCouponAmount()"),
        abi.encode(uint256(totalBuyCouponAmount)) //amount here does not matter, it just to show that coupon tokens distributed not fair
        );
        //increase indexed asset period - this is done by Pool when Auction starts but its mocked on this test
        _pool.bondToken().increaseIndexedAssetPeriod(params.sharesPerToken); //params.sharesPerToken = 50*10**6
        vm.stopPrank();
        //mock that user1 has bondTokens
        vm.startPrank(address(_pool));
        _pool.bondToken().mint(user1, 1*10**18);
        vm.stopPrank();
        //start auction 2
        vm.startPrank(governance);
        fakeSucceededAuction(address(_pool), 1);
        vm.mockCall(
        address(1),
        abi.encodeWithSignature("state()"),
        abi.encode(uint256(1))
        );
        vm.mockCall(
        address(1),
        abi.encodeWithSignature("totalBuyCouponAmount()"),
        abi.encode(uint256(totalBuyCouponAmount)) //amount here does not matter, it just to show that coupon tokens distributed not fair
        );
        //we reduce sharesPerToken because now we have two users with simalar deposits
        params.sharesPerToken = params.sharesPerToken / 2;
        //increase indexed asset period - this is done by Pool when Auction starts but its mocked on this test
        _pool.bondToken().increaseIndexedAssetPeriod(params.sharesPerToken); //params.sharesPerToken = 25*10**6
        //mint amount to distribute (equals only totalBuyCouponAmount from auction number 2 because distribution will be number 2)
        sharesToken.mint(address(_pool), totalBuyCouponAmount);
        //we distibute it only now, not between auctions, because user0 does not want to claim after first distribution
        _pool.distribute();
        vm.stopPrank();

        //get users shares to show that user1 should be able to claim something
        (uint256 currentPeriod,) = _pool.bondToken().globalPool();
        uint256 sharesUser0 = _pool.bondToken().getIndexedUserAmount(user0, _pool.bondToken().balanceOf(user0), currentPeriod)
                                .normalizeAmount(_pool.bondToken().decimals(), IERC20(address(sharesToken)).safeDecimals());
        uint256 sharesUser1 = _pool.bondToken().getIndexedUserAmount(user1, _pool.bondToken().balanceOf(user1), currentPeriod)
                                .normalizeAmount(_pool.bondToken().decimals(), IERC20(address(sharesToken)).safeDecimals());
        assert(sharesUser1 > 0);

        //user0 claims all coupon tokens from distribution
        vm.startPrank(user0);
        vm.expectEmit(true, true, true, true);
        emit Distributor.ClaimedShares(user0, 2, totalBuyCouponAmount);
        distributor.claim();
        uint256 couponTokenBalanceUser0 = sharesToken.balanceOf(user0);
        assertEq(couponTokenBalanceUser0, totalBuyCouponAmount);
        vm.stopPrank();

        //user1 have nothing from distribution 
        //note that if amount of coupon token will be higher, he will not get revert, but still will not get fair coupon amount
        vm.startPrank(user1);
        vm.expectRevert(Distributor.NotEnoughSharesBalance.selector);
        distributor.claim();
        uint256 couponTokenBalanceUser1 = sharesToken.balanceOf(user1);
        assertEq(couponTokenBalanceUser1, 0);
        vm.stopPrank();

        console.log("user0 shares balance before claim", sharesUser0);
        console.log("user1 shares balance before claim", sharesUser1);
        console.log("-----");
        console.log("user0 coupon balance after claim", couponTokenBalanceUser0);
        console.log("user1 coupon balance after claim", couponTokenBalanceUser1);
    }
}
```
In cmd run command:
```Shell
forge test -vv --mt testClaimSharesAbleToDoOnlyOneUser
```
Output:
```Shell
Ran 1 test for test/DistributorFailClaim.t.sol:DistributorTest
[PASS] testClaimSharesAbleToDoOnlyOneUser() (gas: 4245906)
Logs:
  user0 shares balance before claim 100000000000000000000
  user1 shares balance before claim 50000000000000000000
  -----
  user0 coupon balance after claim 100000000000000000000
  user1 coupon balance after claim 0

Suite result: ok. 1 passed; 0 failed; 0 skipped; finished in 5.59ms (1.89ms CPU time)
```

### Mitigation
**Note that in this mitigation you also have to add function to `Distributor.sol` contract to retrieve not claimed coupon tokens after some time.**
Add `totalShares` variable in the `BondToken.sol` contract:
```Solidity
/// @dev total amount of shares
uint256 public totalShares;
```
Update [BondToken.sol:updateIndexedUserAssets()](https://github.com/sherlock-audit/2024-12-plaza-finance/blob/main/plaza-evm/src/BondToken.sol#L172):
```Solidity
/**
  * @dev Updates the indexed user assets for a specific user.
  * @param user The address of the user
  * @param balance The current balance of the user
  * @notice This function updates the number of shares held by the user based on the current period.
  */
function updateIndexedUserAssets(address user, uint256 balance) internal {
  uint256 period = globalPool.currentPeriod;
  uint256 shares = getIndexedUserAmount(user, balance, period);
  
  userAssets[user].indexedAmountShares = shares;
  userAssets[user].lastUpdatedPeriod = period;
  //@audit add total shares
  totalShares += shares;

  emit UpdatedUserAssets(user, period, shares);
}
```
Update [BondToken.sol:resetIndexedUserAssets()](https://github.com/sherlock-audit/2024-12-plaza-finance/blob/main/plaza-evm/src/BondToken.sol#L207):
```Solidity
/**
  * @dev Resets the indexed user assets for a specific user.
  * @param user The address of the user
  * @notice This function resets the last updated period and indexed amount of shares to zero.
  * Can only be called by addresses with the DISTRIBUTOR_ROLE and when the contract is not paused.
  */
function resetIndexedUserAssets(address user) external onlyRole(DISTRIBUTOR_ROLE) whenNotPaused(){
  userAssets[user].lastUpdatedPeriod = globalPool.currentPeriod;
  //@audit decrease totalShares
  totalShares -= userAssets[user].indexedAmountShares;
  userAssets[user].indexedAmountShares = 0;
}
```
Update `Distributor.sol:claim()`:
```Solidity
/**
 * @dev Allows a user to claim their shares from a specific pool.
 * Calculates the user's proportional couponToken amount based on their shares relative to the total shares in the pool.
 * Transfers the proportional couponToken amount to the user's address.
 */
function claim() external whenNotPaused nonReentrant {
  BondToken bondToken = Pool(pool).bondToken();
  address couponToken = Pool(pool).couponToken();

  if (address(bondToken) == address(0) || couponToken == address(0)) {
      revert UnsupportedPool();
  }

  (uint256 currentPeriod,) = bondToken.globalPool();
  uint256 balance = bondToken.balanceOf(msg.sender);

  //@audit Get total shares and user's proportional shares
  uint256 totalShares = bondToken.totalShares(); // Total shares in the pool
  if (totalShares == 0) {
      revert("totalShares == 0"); // Prevent division by zero
  }

  totalShares = totalShares.normalizeAmount(bondToken.decimals(), IERC20(couponToken).safeDecimals());
  uint256 userShares = bondToken.getIndexedUserAmount(msg.sender, balance, currentPeriod)
                                .normalizeAmount(bondToken.decimals(), IERC20(couponToken).safeDecimals());

  //@audit Calculate the proportional couponToken amount for the user
  //@note ADD PRECISION LOSS CHECK
  uint256 proportionalCouponAmount = (couponAmountToDistribute * userShares) / totalShares;

  // Check if there are enough couponTokens to distribute
  if (IERC20(couponToken).balanceOf(address(this)) < proportionalCouponAmount) {
      revert NotEnoughSharesBalance();
  }

  // Update the distributable coupon amount and reset the user's shares
  couponAmountToDistribute -= proportionalCouponAmount;
  bondToken.resetIndexedUserAssets(msg.sender);

  // Transfer the proportional couponTokens to the user
  IERC20(couponToken).safeTransfer(msg.sender, proportionalCouponAmount);

  emit ClaimedShares(msg.sender, currentPeriod, proportionalCouponAmount);
}
```