Dry Cherry Goose

Medium

# Fees discrepency in `Pool::claimFees` and pool reserves calculation while creating and redeeming will potentially lock funds in the contract for reserveTokens with not 18 decimals

### Summary

In [`Pool::getFeeAmount`](https://github.com/sherlock-audit/2024-12-plaza-finance/blob/main/plaza-evm/src/Pool.sol#L719), the calculation of the fees are handled differently than in [`simulateCreate`](https://github.com/sherlock-audit/2024-12-plaza-finance/blob/main/plaza-evm/src/Pool.sol#L269-L273) and [`simulateRedeem`](https://github.com/sherlock-audit/2024-12-plaza-finance/blob/main/plaza-evm/src/Pool.sol#L429-L433), creating a discrepency of the amount of fees in case the reserveToken is not 18 decimals.

If the token used has less than 18 decimals, then the simulate functions will calculate a higher amount of fee than what the fee beneficiary will be able to receive, locking the difference in the contract until the fee beneficiary claims the fees (at which point the calculation of fee goes back to 0 as it depends on time since last claim). Since the fee amount is calculated by using the amount of seconds per year, I suppose the beneficiary will not intend to claim regularly, which could lock the funds for longer than a week.

If the token used has more than 18 decimals, then the fee calculations in simulations will be less than what the beneficiary can claim, and users will be able to withdraw more than they should. It would result in a loss of profit for the beneficiary if they expect to claim a certain amount but users redeem before.

Note that while the contest details specified that only standard ERC20 tokens should be used, the protocol will initially use [USDC which has 6 decimals](https://basescan.org/token/0x833589fcd6edb6e08f4c7c32d4f71b54bda02913#readProxyContract#F11) and [cbBTC which has 8](https://basescan.org/token/0xcbb7c0000ab88b473b1f5afd9ef808440eed33bf#readProxyContract#F11).

### Root Cause

Discrepency in fee calculation between `Pool::getFeeAmount` and the two simulate function `Pool::simulateCreate` & `Pool::simulateRedeem`

### Internal Pre-conditions

_No response_

### External Pre-conditions

_No response_

### Attack Path

Interacting with `simulateCreate` and `simulateRedeem` while the reserve token does not have 18 decimals and the beneficiary has more than 0 fees to claim.

### Impact

Loss of profit for the protocol, temporary lock of funds (more than a week), loss of profit for the beneficiary.

### PoC

> You will need to change the visibility to public of the variables `fee` and `lastFeeClaimTime`, and the function `getFeeAmount`

Copy the PoC at the end of `Pool.t.sol` and run it.

```solidity
// forge test --mt test_fees_discrepency -vvv
function test_fees_discrepency() public {
    vm.startPrank(governance);
    Token rToken = Token(params.reserveToken);
    // Change the amount of tokens to see the difference in the logs
    rToken.setDecimals(8);
    // rToken.setDecimals(20);

    // Mint reserve tokens
    rToken.mint(governance, 10000001000);
    rToken.approve(address(poolFactory), 10000000000);

    // Create pool
    Pool _pool = Pool(poolFactory.createPool(params, 10000000000, 10000, 10000, "", "", "", "", false));
    _pool.setAuctionPeriod(10 days);
    _pool.setFee(10_000);

    vm.warp(1 days);

    // You need to set the visibility of `getFeeAmount` to `public` to see its value
    console.log("Fee amount calculated in `claimFees`", _pool.getFeeAmount());
    console.log("Fee amount calculated in simulate functions", getFeeAmountCalculatedInSimulate(_pool));
    console.log();
}

function getFeeAmountCalculatedInSimulate(Pool _pool) private view returns (uint256) {
    uint8 COMMON_DECIMALS = 18;
    uint256 PRECISION = 1000000;
    uint256 SECONDS_PER_YEAR = 365 days;

    uint256 poolReserves = Token(_pool.reserveToken()).balanceOf(address(_pool)).normalizeTokenAmount(
        _pool.reserveToken(), COMMON_DECIMALS
    );

    // You will need to set the variables `fee` and `lastFeeClaimTime` to public in the pool
    return
        (poolReserves * _pool.fee() * (block.timestamp - _pool.lastFeeClaimTime())) / (PRECISION * SECONDS_PER_YEAR);
}
```

With decimals = 8, we get the following output :

```console
$ forge test --mt test_fees_discrepency -vvv
[⠒] Compiling...
[⠃] Compiling 1 files with Solc 0.8.27
[⠒] Solc 0.8.27 finished in 33.74s
Compiler run successful!

Ran 1 test for test/Pool.t.sol:PoolTest
[PASS] test_fees_discrepency() (gas: 1861446)
Logs:
  Fee amount calculated in `claimFees` 273969
  Fee amount calculated in simulate functions 2739694317605276
  

Suite result: ok. 1 passed; 0 failed; 0 skipped; finished in 17.60ms (4.80ms CPU time)

Ran 1 test suite in 40.73ms (17.60ms CPU time): 1 tests passed, 0 failed, 0 skipped (1 total tests)
```

With decimals = 20, we get the following output :

```console
$ forge test --mt test_fees_discrepency -vvv
[⠆] Compiling...
[⠆] Compiling 1 files with Solc 0.8.27
[⠔] Solc 0.8.27 finished in 33.98s
Compiler run successful!

Ran 1 test for test/Pool.t.sol:PoolTest
[PASS] test_fees_discrepency() (gas: 1862538)
Logs:
  Fee amount calculated in `claimFees` 273969
  Fee amount calculated in simulate functions 2739
  

Suite result: ok. 1 passed; 0 failed; 0 skipped; finished in 17.43ms (4.86ms CPU time)

Ran 1 test suite in 40.32ms (17.43ms CPU time): 1 tests passed, 0 failed, 0 skipped (1 total tests)
```

Setting the decimals to `18` gives the same fee amount. This PoC shows that if the decimals of the reserve token are **under 18**, the fee beneficiary will not be able to claim as much fees as calculated in the simulate functions. If the decimals are over 18, then the opposite happens.

### Mitigation

Edit the fee calculation in `Pool::getFeeAmount` to handle the fees with a normalized amount of decimals like in the simulate functions :

```diff
  function getFeeAmount() public view returns (uint256) {
    return (IERC20(reserveToken).balanceOf(address(this))
+       .normalizeTokenAmount(reserveToken, COMMON_DECIMALS) 
        * fee * (block.timestamp - lastFeeClaimTime)) / (PRECISION * SECONDS_PER_YEAR);
  }
```