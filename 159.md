Joyful Yellow Raccoon

Medium

# The Imprecise Calculation of the Redeem Rate Results in the Final Amount Being Lower than the Actual Value

### Summary

The `getRedeemAmount()` function in the Pool contract performs an imprecise calculation for the redeem amount. It divides the `(tvl - (bondSupply * BOND_TARGET_PRICE))` by `assetSupply`, and then multiplies the result by `PRECISION`, causing a loss of precision. The correct approach should involve multiplying by `PRECISION` first and then dividing by `assetSupply` to ensure accurate calculation.

### Root Cause

Inside the function `getRedeemAmount()` of the Pool contract calculates the `redeemRate` variable for the case where the token is of the leverage type:

```Solidity
  function getRedeemAmount(
    TokenType tokenType,
    uint256 depositAmount,
    uint256 bondSupply,
    uint256 levSupply,
    uint256 poolReserves,
    uint256 ethPrice,
    uint8 oracleDecimals,
    uint256 marketRate
  ) public pure returns(uint256) {
    if (bondSupply == 0) {
      revert ZeroDebtSupply();
    }

    ...
    
    // Calculate the redeem rate based on the collateral level and token type
    uint256 redeemRate;
    if (collateralLevel <= COLLATERAL_THRESHOLD) {
      redeemRate = ((tvl * multiplier) / assetSupply);
    } else if (tokenType == TokenType.LEVERAGE) {
      redeemRate = ((tvl - (bondSupply * BOND_TARGET_PRICE)) / assetSupply) * PRECISION;  // Precision loss occurs here
    } else {
      redeemRate = BOND_TARGET_PRICE * PRECISION;
    }
    ...
  }
```
https://github.com/sherlock-audit/2024-12-plaza-finance/blob/main/plaza-evm/src/Pool.sol#L514

As can be seen from the mentioned snippet, the `redeemRate` is calculated by this formula:

$$ rate_{redeem} = \frac{TVL - (100 * supply_{bond})}{supply_{asset}} \times 1000000 $$

However, the current implementation lacks a precision loss, as it prioritizes division over multiplication. This significantly truncates the accuracy of the final result in some cases as the Solidity does not support floating numbers and rounds down the division.

### Internal Pre-conditions

_No response_

### External Pre-conditions

_No response_

### Attack Path

_No response_

### Impact

This bug results in the miscalculation of the redeem amount, leading to users receiving less than the correct amount when redeeming their tokens. 

### PoC

This test shows the discrepancy between the actual and accurate numbers: (these variables are for testing purposes)
```Solidity
    function test_precissionLoss() public {

        uint tvl = 4.8152e22;
        uint bondSupply = 6.5412e18;
        uint assetSupply = 8.9252e21;

        uint actual = ((tvl - (bondSupply * BOND_TARGET_PRICE)) / assetSupply) * PRECISION;
        uint accurate= ((tvl - (bondSupply * BOND_TARGET_PRICE)) * PRECISION) / assetSupply;
        
        console.log("Current Implementation: ", actual );
        console.log("Accurate Implementation: ", accurate);
    }
```

The result would be:

```Markdown

     Current Implementation:  5000000
     Accurate Implementation:  5321772
```

### Mitigation

Consider modifying the `redeemRate` calculation and prioritizing the multiplication:

```diff
    if (collateralLevel <= COLLATERAL_THRESHOLD) {
      redeemRate = ((tvl * multiplier) / assetSupply);
    } else if (tokenType == TokenType.LEVERAGE) {
-      redeemRate = ((tvl - (bondSupply * BOND_TARGET_PRICE)) / assetSupply) * PRECISION;
+      redeemRate = (tvl - (bondSupply * BOND_TARGET_PRICE)) * PRECISION / assetSupply;
    } else {
```
https://github.com/sherlock-audit/2024-12-plaza-finance/blob/main/plaza-evm/src/Pool.sol#L514