Fit Alabaster Goblin

High

# Incorrect sender check in `transferReserveToAuction` will DoS succeeded auctions, leading to loss of funds

### Summary

An incorrect period is used to check the sender in the transferReserveToAuction, which prevents transferReserveToAuction from ever being called, leading to a complete DoS of the auction mechanism.

### Root Cause

In [`Pool.transferReserveToAuction`](https://github.com/sherlock-audit/2024-12-plaza-finance/blob/ab5bbd7d54042e4444b711a32def5b639fbd63b4/plaza-evm/src/Pool.sol#L557), the `auctionAddress` is checked considering the `currentPeriod`. However, this is wrong as the currently active auction corresponds to `currentPeriod - 1`.

```solidity
// File: Pool.sol
function transferReserveToAuction(uint256 amount) external virtual {
    (uint256 currentPeriod, ) = bondToken.globalPool();
    address auctionAddress = auctions[currentPeriod];
    require(msg.sender == auctionAddress, CallerIsNotAuction());
    
    IERC20(reserveToken).safeTransfer(msg.sender, amount);
  }
```

### Internal Pre-conditions

_No response_

### External Pre-conditions

_No response_

### Attack Path

1. An auction is started via the Pool’s `startAuction` function. This will start an auction for the `currentPeriod`. As an example, if `currentPeriod` is 3, an auction will be started for period 3, and the Auction contract will be deployed and stored in the `auctions` mapping for period 3:
    
    ```solidity
    // File: Pool.sol
    
    function startAuction() external whenNotPaused {
            ...
    
            // Check if auction for current period has already started
            (uint256 currentPeriod, ) = bondToken.globalPool();
            require(auctions[currentPeriod] == address(0), AuctionAlreadyStarted());
    
            ...
    
            auctions[currentPeriod] = Utils.deploy(
                address(new Auction()),
                abi.encodeWithSelector(
                    Auction.initialize.selector,
                    address(couponToken),
                    address(reserveToken),
                    couponAmountToDistribute,
                    block.timestamp + auctionPeriod,
                    1000, // maxBids
                    address(this),
                    poolSaleLimit
                )
            );
    
            // Increase the bond token period
            bondToken.increaseIndexedAssetPeriod(sharesPerToken);
    
            // Update last distribution time
            lastDistribution = block.timestamp;
        }
    ```
    
2. After deploying the auction, the bondToken’s `increaseIndexedAssetPeriod` function is called, which increases the current period by 1 (so the current period will increase from 3 to 4):
    
    ```solidity
    // File: BondToken.sol
    
    function increaseIndexedAssetPeriod(uint256 sharesPerToken) public onlyRole(DISTRIBUTOR_ROLE) whenNotPaused() {
        globalPool.previousPoolAmounts.push(
          PoolAmount({
            period: globalPool.currentPeriod,
            amount: totalSupply(),
            sharesPerToken: globalPool.sharesPerToken
          })
        );
        globalPool.currentPeriod++;
        globalPool.sharesPerToken = sharesPerToken;
    
        emit IncreasedAssetPeriod(globalPool.currentPeriod, sharesPerToken);
      }
    ```
    
3. When the auction expires, the `endAuction` function can be called in the `Auction` contract. Then, if the auction was successful, the `transferReserveToAuction` function in the pool will be called, in order to send the corresponding reserve to the auction contract. The problem is that `transferReserveToAuction` will fetch the `currentPeriod`, which was previously incremented from 3 to 4, in order to check if the caller is the correct auction contract. However, because the actual ongoing auction corresponds to period 3, the `auctionAddress` returned in the mapping will be `address(0)`, and the contract will revert with `CallerIsNotAuction()`.

### Impact

This vulnerability will lead to a complete DoS of auctions. This makes the bond distribution functionality to never succeed, effectively leading to a loss of funds for bond holders (given that not being able to finalize auctions prevents the corresponding coupons from being distributed). Additionally, bidders will also get their funds stuck forever in the Auction contract, as they can only claim their bid funds back if the auction did **not** succeed:

```solidity
// File: Auction.sol

modifier auctionFailed() {
    if (state == State.SUCCEEDED || state == State.BIDDING) revert AuctionFailed();
    _;
  }

function claimRefund(uint256 bidIndex) auctionExpired auctionFailed whenNotPaused external {
    ...
    
  }
```

### PoC

_No response_

### Mitigation

Update `transferReserveToAuction` to check the caller corresponding to the previous period:

```diff
function transferReserveToAuction(uint256 amount) external virtual {
        (uint256 currentPeriod, ) = bondToken.globalPool();
-       address auctionAddress = auctions[currentPeriod];
+       address auctionAddress = auctions[currentPeriod - 1];
        require(msg.sender == auctionAddress, CallerIsNotAuction()); 

        IERC20(reserveToken).safeTransfer(msg.sender, amount); 
    }
```