Great Snowy Goblin

Medium

# A user can loss all his shares and get no coupon tokens

### Summary

A round-down to zero can cause the user to lose all their funds and receive no coupon tokens.

### Root Cause

In the Depositor contract, when a user wants to claim his coupon tokens, his shares will be adjusted to match the decimals of the coupon token.

As we can see:

https://github.com/sherlock-audit/2024-12-plaza-finance/blob/main/plaza-evm/src/Distributor.sol#L88-L89

According to the documentation, the coupon token should be USDC, which has 6 decimals, while the balance of shares has 18 decimals. This means that if the user has less than 1e12 shares, the adjustment will round down to zero.

Normally, this would not be an issue, as the user could simply reclaim the coupons once they have accumulated more shares. However, the contract completely resets the shares, as we can see here:

https://github.com/sherlock-audit/2024-12-plaza-finance/blob/main/plaza-evm/src/Distributor.sol#L106

The function will set the shares at zero  as we can see in the BondToken(L-207)
```solidity
function resetIndexedUserAssets(address user) external onlyRole(DISTRIBUTOR_ROLE) whenNotPaused(){
    userAssets[user].lastUpdatedPeriod = globalPool.currentPeriod;
    userAssets[user].indexedAmountShares = 0;
  }
```
So  if the user has less than 1e12 shares he will receive no tokens and will loss all his previous accumulated shares.

### Internal Pre-conditions

1. The coupon token should be a token with less than 18 decimal.
2. The user should not have enough shares to compensate the round-down.

### External Pre-conditions

none.

### Attack Path

None

### Impact

The user will not receive shares and will lose all his accumulated shares. 

### PoC

You can run the POC by copy past this code in the Distributor.t.sol file in the test folder and run `forge test --mt testClaimSharesPOC -vv`

```solidity
 function testClaimSharesPOC() public {
      
        Token sharesToken = Token(_pool.couponToken());
        // we set the decimals of the shares token at 6
        sharesToken.setDecimals(6);
        // The shares per token is eqal to 50e6 to have less than 1e12 we give 1e10 to the user 
        //Because 1e10*50e6/1e6 = 5e11 which is less than 1e12
        vm.startPrank(address(_pool));
        _pool.bondToken().mint(user,1e10);
        sharesToken.mint(address(_pool), 50 * (1 + 10000) * 10 ** 6);
        vm.stopPrank();

        vm.startPrank(governance);
        fakeSucceededAuction(address(_pool), 0);

        vm.mockCall(address(0), abi.encodeWithSignature("state()"), abi.encode(uint256(1)));

        vm.mockCall(
            address(0),
            abi.encodeWithSignature("totalBuyCouponAmount()"),
            abi.encode(uint256(50 * (1 + 10000) * 10 ** 6))
        );

        // increase indexed asset period - this is done by Pool when Auction starts but its mocked on this test
        _pool.bondToken().increaseIndexedAssetPeriod(params.sharesPerToken);
       
        _pool.distribute();
        vm.stopPrank();
       uint256 sharesBeforeClaiming = _pool.bondToken().getIndexedUserAmount(user,_pool.bondToken().balanceOf(user),1);
        vm.startPrank(user);

        distributor.claim();
        //the user received no token 
        assertEq(sharesToken.balanceOf(user), 0);
        vm.stopPrank();
        uint256 sharesAfterClaiming = _pool.bondToken().getIndexedUserAmount(user,_pool.bondToken().balanceOf(user),1);
        //The user had shares before the call
        assertGt(sharesBeforeClaiming,0);
        //The user has no shares after the call 
        assertEq(sharesAfterClaiming,0);
        console2.log("Shares before the call : %d",sharesBeforeClaiming);
        console2.log("Shares after the call : %d",sharesAfterClaiming);
    }

```

You should have this output : 

```solidity 
Ran 1 test for test/Distributor.t.sol:DistributorTest
[PASS] testClaimSharesPOC() (gas: 2342699)
Logs:
  Shares before the call : 500000000000
  Shares after the call : 0

Suite result: ok. 1 passed; 0 failed; 0 skipped; finished in 2.61ms (499.42Âµs CPU time)
```
We can see that the user lost 500000000000 shares by calling the Distributor.

### Mitigation

Add a check to revert if the operation round-down to zero : 

```solidity
 function claim() external whenNotPaused nonReentrant {
    BondToken bondToken = Pool(pool).bondToken();
    address couponToken = Pool(pool).couponToken();

    if (address(bondToken) == address(0) || couponToken == address(0)){
      revert UnsupportedPool();
    }

    (uint256 currentPeriod,) = bondToken.globalPool();
    uint256 balance = bondToken.balanceOf(msg.sender);
    uint256 shares = bondToken.getIndexedUserAmount(msg.sender, balance, currentPeriod)
                              .normalizeAmount(bondToken.decimals(), IERC20(couponToken).safeDecimals());

   if(shares == 0) {
      revert NotEnoughSharesToDistribute();
   }
    if (IERC20(couponToken).balanceOf(address(this)) < shares) {
      revert NotEnoughSharesBalance();
    }
    
    // check if pool has enough *allocated* shares to distribute
    if (couponAmountToDistribute < shares) {
      revert NotEnoughSharesToDistribute();
    }

    // check if the distributor has enough shares tokens as the amount to distribute
    if (IERC20(couponToken).balanceOf(address(this)) < couponAmountToDistribute) {
      revert NotEnoughSharesToDistribute();
    }

    couponAmountToDistribute -= shares;    
    bondToken.resetIndexedUserAssets(msg.sender);
    IERC20(couponToken).safeTransfer(msg.sender, shares);
    
    emit ClaimedShares(msg.sender, currentPeriod, shares);
  }
```
