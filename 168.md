Itchy Wintergreen Newt

High

# Users will receive incorrect redemption amounts due to decimal normalization error in market rate calculation

### Summary

The code in pool's redeem functionality normalizes the market rate to oracle decimals without considering the full decimal context, leading to not utilizing the market rate or even incorrect redemption price calculation in an edge case (when oracle decimals for reserve token is less than 6).

### Root Cause

In [`Pool.sol:L446`](https://github.com/sherlock-audit/2024-12-plaza-finance/blob/main/plaza-evm/src/Pool.sol#L446) the market rate decimal normalization is incorrect:

```solidity
uint8 oracleDecimals = getOracleDecimals(reserveToken, USD);

uint256 marketRate;
address feed = OracleFeeds(oracleFeeds).priceFeeds(address(bondToken), USD);
if (feed != address(0)) {
    marketRate = getOraclePrice(address(bondToken), USD).normalizeAmount(
            getOracleDecimals(address(bondToken), USD),
@>        oracleDecimals // this is the decimals of the reserve token chainlink feed
    );
}
```

The issue is that `marketRate` is normalized to match the reserve token oracle decimals, but in `getRedeemAmount` the marketRate is treated as a redeemRate [`Pool.sol:L520`](https://github.com/sherlock-audit/2024-12-plaza-finance/blob/main/plaza-evm/src/Pool.sol#L520) even though redeemRate has `PRECISION` decimals. 

```solidity
    // Calculate the redeem rate based on the collateral level and token type
    uint256 redeemRate;
    if (collateralLevel <= COLLATERAL_THRESHOLD) {
      // @audit-info the redeem rate is scaled up to PRECISION since multiplier has same base as PRECISION
      redeemRate = ((tvl * multiplier) / assetSupply);
    } else if (tokenType == TokenType.LEVERAGE) {
      // @audit-info the redeem rate is scaled up to PRECISION
      redeemRate = ((tvl - (bondSupply * BOND_TARGET_PRICE)) / assetSupply) * PRECISION;
    } else {
      // @audit-info the redeem rate is scaled up to PRECISION
      redeemRate = BOND_TARGET_PRICE * PRECISION; 
    }

    if (marketRate != 0 && marketRate < redeemRate) {
      // @audit we are replacing with marketRate without considering its decimals.
      redeemRate = marketRate;
    }
    
    // Calculate and return the final redeem amount
    // @audit-info the numerator was divided with PRECISION because redeemRate has PRECISION decimals
    return ((depositAmount * redeemRate).fromBaseUnit(oracleDecimals) / ethPrice) / PRECISION;
```

as seen from the code above in the return statement the numerator was divided with PRECISION because redeemRate is EXPECTED to have PRECISION decimals. But in the case when redeemRate is set to marketRate the decimals are not equal to PRECISION but rather oracleDecimals of the reserveToken price feed. 

### Internal Pre-conditions

1. Pool needs to be initialized
2. Price feed for bondToken/USD needs to be configured

### External Pre-conditions

_No response_

### Attack Path
*When oracle decimals of reserve token are higher than 6*
1. User calls create() on the pool to deposit reserve token to get BondETH.
2. User calls redeem() with tokenType BOND
3. simulateRedeem() is called internally
4. marketRate is fetched from oracle and incorrectly normalized 
5. getRedeemAmount() will NEVER use the marketRate even though it is set.
6. The marketRate from the bond oracle will NEVER be utilized.

*Alternatively when oracle decimals of reserve token is less than 6*
1. User calls create() on the pool to deposit reserve token to get BondETH.
2. User calls redeem() with tokenType BOND
3. simulateRedeem() is called internally
4. marketRate is fetched from oracle and incorrectly normalized 
5. getRedeemAmount() uses this incorrect marketRate for calculations
6. User receives much fewer reserve tokens by huge magnitude.

### Impact
1. In the most probable case the oracle decimals set will be higher than 6 that means the market rate will never be utilized in the redemption process as the condition [`Pool.sol:L519`](https://github.com/sherlock-audit/2024-12-plaza-finance/blob/main/plaza-evm/src/Pool.sol#L519) `marketRate < redeemRate` will never be true.
2. In case the oracle decimals of reserve price feed is less than 6 (not much probable but still an edge case), users will receive MUCH less reserve token when they redeem it using their bondToken.

### PoC
place in test folder and run `forge test` on it
```solidity
// SPDX-License-Identifier: UNLICENSED
pragma solidity ^0.8.26;

import "forge-std/Test.sol";
import {Create3} from "@create3/contracts/Create3.sol";
import {PoolFactory} from "src/PoolFactory.sol";
import {Utils} from "src/lib/Utils.sol";
import {Deployer} from "src/utils/Deployer.sol";
import {BondToken} from "src/BondToken.sol";
import {PoolFactory} from "src/PoolFactory.sol";
import {Distributor} from "src/Distributor.sol";
import {LeverageToken} from "src/LeverageToken.sol";
import {Pool} from "src/Pool.sol";
import {UpgradeableBeacon} from "@openzeppelin/contracts/proxy/beacon/UpgradeableBeacon.sol";
import {IERC20} from "@openzeppelin/contracts/token/ERC20/ERC20.sol";
import {MockERC20} from "./MockERC20.sol";
import {OracleFeeds} from "src/OracleFeeds.sol";
import {MockPriceFeed} from "./mocks/MockPriceFeed.sol";
import {OracleFeeds} from "src/OracleFeeds.sol";

contract MyTest is Test {
    PoolFactory private poolFactory;
    PoolFactory.PoolParams private params;
    Pool private pool;
    MockPriceFeed private mockPriceFeed;
    OracleFeeds private oracleFeeds;

    address private minter = address(0x2);
    address private governance = address(0x3);
    address private securityCouncil = address(0x4);
    address private alice = address(0x5);
    address private bob = address(0x6);
    address private feeBeneficiary = address(0x7);
    address public constant ethPriceFeed = address(0x71041dddad3595F9CEd3DcCFBe3D1F4b0a16Bb70);
    address public bondPriceFeed = makeAddr("bond_price_feed");
    MockPriceFeed private bondMockPriceFeed;

    function setUp() public {
        params.fee = 0;
        params.reserveToken = address(new MockERC20("Wrapped ETH", "WETH"));
        params.distributionPeriod = 0;
        params.couponToken = address(new MockERC20("USDC", "USDC"));
        // params.sharesPerToken = 50 * 10 ** 18;
        params.feeBeneficiary = feeBeneficiary;

        oracleFeeds = new OracleFeeds();
        address contractDeployer = address(new Deployer());

        address poolBeacon = address(new UpgradeableBeacon(address(new Pool()), governance));
        address bondBeacon = address(new UpgradeableBeacon(address(new BondToken()), governance));
        address levBeacon = address(new UpgradeableBeacon(address(new LeverageToken()), governance));
        address distributorBeacon = address(new UpgradeableBeacon(address(new Distributor()), governance));

        poolFactory = PoolFactory(
            Utils.deploy(
                address(new PoolFactory()),
                abi.encodeWithSelector(
                    PoolFactory.initialize.selector,
                    governance,
                    contractDeployer,
                    address(oracleFeeds),
                    poolBeacon,
                    bondBeacon,
                    levBeacon,
                    distributorBeacon
                )
            )
        );

        oracleFeeds.setPriceFeed(params.reserveToken, address(0), ethPriceFeed, 1 days);
        mockPriceFeed = new MockPriceFeed();
        bytes memory bytecode = address(mockPriceFeed).code;
        vm.etch(ethPriceFeed, bytecode);
        mockPriceFeed = MockPriceFeed(ethPriceFeed);

        deal(params.reserveToken, governance, 100e18, true);
        deal(params.reserveToken, alice, 10e18, true);
        deal(params.reserveToken, bob, 10e18, true);

        vm.startPrank(governance);
        // granting roles
        poolFactory.grantRole(poolFactory.POOL_ROLE(), governance);
        poolFactory.grantRole(poolFactory.SECURITY_COUNCIL_ROLE(), securityCouncil);

        // creating the pool with 100 WETH, and will be minted 10 of bondEth and LevEth each
        IERC20(params.reserveToken).approve(address(poolFactory), 100e18);
        pool = Pool(poolFactory.createPool(params, 100e18, 10e18, 10e18, "Bond ETH", "BE", "Lev ETH", "LE", false));
        vm.stopPrank();

        // we are using a mock bondPriceFeed
        oracleFeeds.setPriceFeed(address(pool.bondToken()), address(0), bondPriceFeed, 1 days);
        bondMockPriceFeed = new MockPriceFeed();
        bytes memory bondBytecode = address(bondMockPriceFeed).code;
        vm.etch(bondPriceFeed, bondBytecode);
        bondMockPriceFeed = MockPriceFeed(bondPriceFeed);
    }

    function testRedemption() public {
        // oracle decimals for reserve token is set to 5 which is less than 6
        mockPriceFeed.setMockPrice(2000e5, 5);

        // sets bond Oracle price to 100 USDC/BondEth which is very reasonable
        bondMockPriceFeed.setMockPrice(100e6, 6);

        vm.startPrank(alice);
        IERC20(params.reserveToken).approve(address(pool), 10e18);
        pool.create(Pool.TokenType.BOND, 10e18, 0);
        // alice deposited 10 WETH (10e18) and she got back bondTokenBal worth of bondToken
        uint256 bondTokenBal = IERC20(pool.bondToken()).balanceOf(alice);

        IERC20(pool.bondToken()).approve(address(pool), bondTokenBal);
        // alice attempts to put back the bondTokenBal so as to get back the ~ 10 WETH (10e18)
        pool.redeem(Pool.TokenType.BOND, bondTokenBal, 0);
        // alice gets back only 1 WETH (1e18) instead of 10 WETH (10e18). it is HUGE LOSS!
        uint256 aliceReserveBalance = IERC20(params.reserveToken).balanceOf(alice);
        assertEq(aliceReserveBalance, 1e18);
        console.log(aliceReserveBalance); // 1e18
        vm.stopPrank();
    }
}
```

### Mitigation
Fix the lines in [`Pool.sol:L443-451`](https://github.com/sherlock-audit/2024-12-plaza-finance/blob/main/plaza-evm/src/Pool.sol#L443-L451) as follows;
```solidity
uint256 marketRate;
address feed = OracleFeeds(oracleFeeds).priceFeeds(address(bondToken), USD);
if (feed != address(0)) {
    uint256 rawPrice = getOraclePrice(address(bondToken), USD);
    uint8 bondOracleDecimals = getOracleDecimals(address(bondToken), USD);
    
    // Convert to rate with PRECISION (1e6) scaling
    marketRate = (rawPrice * PRECISION) / (10 ** bondOracleDecimals);
}
```