Joyous Paisley Goblin

Medium

# `Pool.transferReserveToAuction()` reverts due to incorrect usage of index for current auction

## Summary

After an auction concludes, the required reserve tokens are transferred from the pool to the auction contract using the `Pool.transferReserveToAuction()` function. However, this function utilizes an incorrect index for the current auction, leading to a revert.

## Root Cause

Once an auction expires, the [endAuction()](https://github.com/sherlock-audit/2024-12-plaza-finance/blob/14a962c52a8f4731bbe4655a2f6d0d85e144c7c2/plaza-evm/src/Auction.sol#L336-L350) function in `Auction.sol` is called.

This function invokes `Pool.transferReserveToAuction()` to transfer the `totalSellReserveAmount` of reserve tokens from the pool.

```solidity
  function endAuction() external auctionExpired whenNotPaused {
    if (state != State.BIDDING) revert AuctionAlreadyEnded();

    if (currentCouponAmount < totalBuyCouponAmount) {
      state = State.FAILED_UNDERSOLD;
    } else if (totalSellReserveAmount >= (IERC20(sellReserveToken).balanceOf(pool) * poolSaleLimit) / 100) {
        state = State.FAILED_POOL_SALE_LIMIT;
    } else {
      state = State.SUCCEEDED;
      Pool(pool).transferReserveToAuction(totalSellReserveAmount);
      IERC20(buyCouponToken).safeTransfer(beneficiary, IERC20(buyCouponToken).balanceOf(address(this)));
    }

    emit AuctionEnded(state, totalSellReserveAmount, totalBuyCouponAmount);
  }
```

In the [transferReserveToAuction()](https://github.com/sherlock-audit/2024-12-plaza-finance/blob/14a962c52a8f4731bbe4655a2f6d0d85e144c7c2/plaza-evm/src/Pool.sol#L577-L583) function of `Pool.sol`, it checks whether `msg.sender` is the current auction address.

It retrieves `currentPeriod` from `bondToken.globalPool()`, but this value does not accurately indicate the current auction period. Since the period increments when the auction starts, the correct index for the current auction should be `currentPeriod - 1`. As a result, the function will revert due to the incorrect index, causing the auction to fail.

```solidity
  function transferReserveToAuction(uint256 amount) external virtual {
    (uint256 currentPeriod, ) = bondToken.globalPool();
    address auctionAddress = auctions[currentPeriod];
    require(msg.sender == auctionAddress, CallerIsNotAuction());
    
    IERC20(reserveToken).safeTransfer(msg.sender, amount);
  }
```

## Internal Pre-Conditions


## External Pre-Conditions


## Attack Path


## Impact

Auctions cannot succeed and will always fail, preventing bond holders from receiving coupon tokens.

## Mitigation

To resolve this issue, modify the code to use the previous period instead of the current period when accessing the auction address.

```diff
  function transferReserveToAuction(uint256 amount) external virtual {
    (uint256 currentPeriod, ) = bondToken.globalPool();
+   uint256 previousPeriod = currentPeriod - 1;
-   address auctionAddress = auctions[currentPeriod];
+   address auctionAddress = auctions[previousPeriod];
    require(msg.sender == auctionAddress, CallerIsNotAuction());
    
    IERC20(reserveToken).safeTransfer(msg.sender, amount);
  }
```
