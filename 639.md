Sneaky Eggplant Pike

Medium

# The coupon tokens can permanently be locked in the pool if the bought coupon tokens have not been distributed in the current period

### Summary

The `Pool::distribution()` function distributes the coupon tokens collected through an auction to the `Distributor` contract.

However, since this function only transfers the tokens corresponding to the `currentPeriod`, if one distribution period has elpased without distribution and a new auction starts, the coupon tokens will never be able to be distributed and be locked in the pool.

### Root Cause

[Pool.sol#L589-L614](https://github.com/sherlock-audit/2024-12-plaza-finance/blob/main/plaza-evm/src/Pool.sol#L589-L614)
```solidity
  function distribute() external whenNotPaused {
    (uint256 currentPeriod,) = bondToken.globalPool();
    require(currentPeriod > 0, AccessDenied());

    // Period is increased when auction starts, we want to distribute for the previous period
    uint256 previousPeriod = currentPeriod - 1;
    uint256 couponAmountToDistribute = Auction(auctions[previousPeriod]).totalBuyCouponAmount();

    ... ...
  }
```

As can be seen from the above code snippet, the function only addresses the coupon tokens of the last auction. Therefore, if there exist more tokens of previous auctions, those tokens can be locked.

### Internal pre-conditions

One distribution period should have elapsed without distribution

### External pre-conditions

*None*

### Attack Path

*None*

### Impact

Actually, the likelihood that one distribution period has elapsed without distribution is LOW, however its impact is HIGH because it causes locked coupon tokens.

So I think this finding deserves Medium severity.

### PoC

I already mentioned that the `Pool::transferReserveToAuction()` function has an incorrect validation in my report `Auctions will never be successfully finished due to period mismatch in post-auction validation`, so I fixed that vulnerability like the below:

```diff
  function transferReserveToAuction(uint256 amount) external virtual {
    (uint256 currentPeriod, ) = bondToken.globalPool();
-   address auctionAddress = auctions[currentPeriod];
+   address auctionAddress = auctions[currentPeriod - 1];
    require(msg.sender == auctionAddress, CallerIsNotAuction());
    
    IERC20(reserveToken).safeTransfer(msg.sender, amount);
  }
```

After fixing, I implemented an end-to-end test from creating a pool to distribution after an auction:

```solidity
pragma solidity ^0.8.26;

import {console} from "forge-std/console.sol";
import "forge-std/Test.sol";

import {Pool} from "../src/Pool.sol";
import {Token} from "./mocks/Token.sol";
import {Auction} from "../src/Auction.sol";
import {Utils} from "../src/lib/Utils.sol";
import {MockPool} from "./mocks/MockPool.sol";
import {BondToken} from "../src/BondToken.sol";
import {TestCases} from "./data/TestCases.sol";
import {Decimals} from "../src/lib/Decimals.sol";
import {PoolFactory} from "../src/PoolFactory.sol";
import {Distributor} from "../src/Distributor.sol";
import {OracleFeeds} from "../src/OracleFeeds.sol";
import {Validator} from "../src/utils/Validator.sol";
import {OracleReader} from "../src/OracleReader.sol";
import {LeverageToken} from "../src/LeverageToken.sol";
import {MockPriceFeed} from "./mocks/MockPriceFeed.sol";
import {Deployer} from "../src/utils/Deployer.sol";
import {Strings} from "@openzeppelin/contracts/utils/Strings.sol";
import {UpgradeableBeacon} from "@openzeppelin/contracts/proxy/beacon/UpgradeableBeacon.sol";


contract PoolMarketRateTest is Test, TestCases {
  using Decimals for uint256;
  using Strings for uint256;

  PoolFactory public poolFactory;
  PoolFactory.PoolParams public params;
  Pool pool;

  MockPriceFeed public mockPriceFeed;
  MockPriceFeed public bondMockPriceFeed;
  address public oracleFeedsContract;

  address public deployer = address(0x1);
  address public minter = address(0x2);
  address public governance = address(0x3);
  address public securityCouncil = address(0x4);
  address public user = address(0x5);
  address public user2 = address(0x6);

  address public constant bondPriceFeed = address(0x5a1b1E88A4E0a7635c965DF8e312264e2d8C59a2);
  address public constant ethPriceFeed = address(0x71041dddad3595F9CEd3DcCFBe3D1F4b0a16Bb70);
  uint256 public constant CHAINLINK_DECIMAL_PRECISION = 10**8;
  uint8 public constant CHAINLINK_DECIMAL = 8;

  function setUp() public {
    vm.startPrank(deployer);

    address contractDeployer = address(new Deployer());
    oracleFeedsContract = address(new OracleFeeds());

    address poolBeacon = address(new UpgradeableBeacon(address(new Pool()), governance));
    address bondBeacon = address(new UpgradeableBeacon(address(new BondToken()), governance));
    address levBeacon = address(new UpgradeableBeacon(address(new LeverageToken()), governance));
    address distributorBeacon = address(new UpgradeableBeacon(address(new Distributor()), governance));

    poolFactory = PoolFactory(Utils.deploy(address(new PoolFactory()), abi.encodeCall(
      PoolFactory.initialize, 
      (governance, contractDeployer, oracleFeedsContract, poolBeacon, bondBeacon, levBeacon, distributorBeacon)
    )));

    params.fee = 0;
    params.feeBeneficiary = governance;
    params.reserveToken = address(new Token("Wrapped ETH", "WETH", false));
    params.sharesPerToken = 20e6;
    params.distributionPeriod = 10 days;
    params.couponToken = address(new Token("USDC", "USDC", false));
    
    OracleFeeds(oracleFeedsContract).setPriceFeed(params.reserveToken, address(0), ethPriceFeed, 1 days);

    // Deploy the mock price feed
    mockPriceFeed = new MockPriceFeed();

    // Use vm.etch to deploy the mock contract at the specific address
    bytes memory bytecode = address(mockPriceFeed).code;
    vm.etch(ethPriceFeed, bytecode);

    // Set oracle price
    mockPriceFeed = MockPriceFeed(ethPriceFeed);
    mockPriceFeed.setMockPrice(3000 * int256(CHAINLINK_DECIMAL_PRECISION), uint8(CHAINLINK_DECIMAL));
    
    vm.stopPrank();

    vm.startPrank(governance);
    poolFactory.grantRole(poolFactory.POOL_ROLE(), governance);
    poolFactory.grantRole(poolFactory.SECURITY_COUNCIL_ROLE(), securityCouncil);

    vm.stopPrank();
  }

  function testSkipDistributionPeriod() public {
    vm.startPrank(governance);

    Token rToken = Token(params.reserveToken);
    rToken.mint(governance, 1000e18);
    rToken.approve(address(poolFactory), 500e18);

    // Create pool and approve deposit amount
    pool = Pool(poolFactory.createPool(params, 450e18, 10000e18, 9000e18, "", "", "", "", false));
    pool.setAuctionPeriod(5 days);
    rToken.approve(address(pool), 1000e18);
    vm.stopPrank();

    // 1. Run First Auction
    vm.warp(block.timestamp + 11 days);
    pool.startAuction();

    (uint256 currentPeriod,) = pool.bondToken().globalPool();
    Auction auction = Auction(pool.auctions(currentPeriod - 1));
    uint256 totalBuyCouponAmount1 = auction.totalBuyCouponAmount();

    vm.deal(user, 1 ether); // Provide gas
    deal(auction.buyCouponToken(), user, totalBuyCouponAmount1);

    vm.startPrank(user);
    Token(auction.buyCouponToken()).approve(address(auction), totalBuyCouponAmount1);
    auction.bid(100e18, totalBuyCouponAmount1);
    vm.warp(block.timestamp + 5 days);
    auction.endAuction(); // Ends first auction
    vm.stopPrank();

    assertEq(Token(pool.couponToken()).balanceOf(address(pool)), totalBuyCouponAmount1);

    // 2. Run Second Auction without distributing
    Pool.PoolInfo memory poolInfo = pool.getPoolInfo();
    
    vm.warp(poolInfo.lastDistribution + poolInfo.distributionPeriod + 1);
    pool.startAuction();

    (currentPeriod,) = pool.bondToken().globalPool();
    auction = Auction(pool.auctions(currentPeriod - 1));
    uint256 totalBuyCouponAmount2 = auction.totalBuyCouponAmount();

    deal(auction.buyCouponToken(), user, totalBuyCouponAmount2);

    vm.startPrank(user);
    Token(auction.buyCouponToken()).approve(address(auction), totalBuyCouponAmount2);
    auction.bid(100e18, totalBuyCouponAmount2);
    vm.warp(block.timestamp + 5 days + 1);
    auction.endAuction(); // Ends second auction
    vm.stopPrank();

    // Check if all bought tokens are transferred to the Distributor
    assertEq(Token(pool.couponToken()).balanceOf(address(pool)), totalBuyCouponAmount1 + totalBuyCouponAmount2);
    pool.distribute();
    assertEq(Token(pool.couponToken()).balanceOf(address(pool)), totalBuyCouponAmount1);
  }
}
```

Logs: 
```bash
Ran 1 test for test/Pool.poc.t.sol:PoolMarketRateTest
[PASS] testSkipDistributionPeriod() (gas: 6654841)
Suite result: ok. 1 passed; 0 failed; 0 skipped; finished in 2.89ms (1.49ms CPU time)
```

As can be seen from the test case, the coupon tokens only for second auction has been transferred to the Distributor, and no way to withdraw tokens of the first auction.

### Mitigation

Two alternative options are available:

1. Consider adopting a state like `lastDistributionPeriod` which stores the period number of last distrubution, and transfering all tokens from `lastDistributionPeriod` to `currentPeriod` in the `distribute()` function.

2. I suggest calling `distribute()` function in the `Pool::transferReserveToAuction()` function so that distribution can never be missed for a certain distribution period.