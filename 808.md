Fit Alabaster Goblin

Medium

# The system can start in an extremely undercollateralized state due to restrictions in PreDeposit

### Summary

The `PreDeposit` contract is designed to set the expected `bondAmount` and `leverageAmount` prior to the depositing period finishes. This can be leveraged by a malicious depositor (or affected by normal withdrawals) to force incorrect `bondAmount` and `leverageAmount` to be minted when creating the pool after predepositing.

### Root Cause

In `PreDeposit`, the [`setBondAndLeverageAmount` includes a check](https://github.com/sherlock-audit/2024-12-plaza-finance/blob/ab5bbd7d54042e4444b711a32def5b639fbd63b4/plaza-evm/src/PreDeposit.sol#L204)  to ensure that `bondAmount` and `leverageAmount` can’t be changed after the deposit period has finalized:

```solidity
// PreDeposit.sol

modifier checkDepositNotEnded() {
    if (block.timestamp >= depositEndTime) revert DepositEnded();
    _;
  }

function setBondAndLeverageAmount(uint256 _bondAmount, uint256 _leverageAmount) external onlyOwner checkDepositNotEnded { 
    if (poolCreated) revert PoolAlreadyCreated();

    bondAmount = _bondAmount;
    leverageAmount = _leverageAmount;
  }
```

This means that `bondAmount` and `leverageAmount` **must be set before the deposit period finalizes.**

This can be leveraged by a whale that has deposited into the `PreDeposit` contract to force incorrect `bondAmount` and `leverageAmount` to be configured, compared to the expected creation/redemption computations that take into account the collateral level in the protocol. Besides the whale, users could also withdraw prior to `bondAmount` and `leverageAmount` being set, also breaking the CL.

### Internal Pre-conditions

_No response_

### External Pre-conditions

_No response_

### Attack Path

1. A whale deposits a big `reserveAmount` into the `PreDeposit` contract.
2. Off-chain, the Plaza team monitors the `PreDeposit` contract, and makes some computations to determine the corresponding `bondAmount` and `leverageAmount` that should be minted considering the PreDeposited amounts. The addition of hardcoded `bondAmount` and `leverageAmount` stems from a finding in [Zellic’s audit report](https://file.notion.so/f/f/082d2312-4715-4914-8f85-448446db8bd8/c09fe83d-e2c5-4208-b5c9-f7cd288ea06b/Programmable_Derivatives_-_Zellic_Audit_Report_Draft.pdf?table=block&id=17a3b674-4d76-80de-84ef-c9a3bddfcc3a&spaceId=082d2312-4715-4914-8f85-448446db8bd8&expirationTimestamp=1737648000000&signature=7bFRjls-Xt2QgxirYuPWLfUHqg0WyfcHTz8uI4zbKD4&downloadName=Programmable+Derivatives+-+Zellic+Audit+Report+Draft.pdf) (3.3 - *“Incorrect PreDeposit reward”*), where it shows that previously the total amount of bond and leverage tokens was obtained by directly querying the Pool’s balance, which could lead to incorrect results. This made the Plaza team change the approach, and now the `bondAmount` and `leverageAmount` must be set by the Plaza team to values that make the system start with a healthy collateral level.
3. Near the end of the deposit period, the Plaza team calls `setBondAndLeverageAmount` to set the proper amounts. The only way to set the amounts so that the system starts in a healthy state is by considering the current state of the deposits in the `PreDeposit` pool.
4. At the last block prior to the depositing period finalizing, the malicious whale **withdraws all of their deposit,** greatly reducing the amount of TVL in the protocol, compared to the amount of `bondAmount` and `leverageAmount` to be minted. Because this is performed in the last block before the deposit period ends, and due to the fact that `setBondAndLeverageAmount` can’t be called after the deposit period has finalized, the `bondAmount` and `leverageAmount` that will be minted will be incorrect, and heavily different to what would be expected considering the [calculations in the protocol.](https://docs.plaza.finance/protocol-mechanics/create-buy#creation-of-bondeth)

Checking the PreDeposit script, we can see that an expected reasonable value to be set as a max `RESERVE_CAP` would be around 100 ether:

```solidity
// PreDeposit.s.sol
contract PreDepositScript is Script {
    PreDeposit public preDeposit;
    PoolFactory.PoolParams private params;

    uint256 constant RESERVE_CAP = 100 ether;

    function run(
        address _reserveToken,
        address _couponToken,
        address _poolFactory,
        address _feeBeneficiary,
        uint256 _distributionPeriod,
        uint256 _sharesPerToken
    ) public {
        vm.startBroadcast(vm.envUint("PRIVATE_KEY"));

        params = PoolFactory.PoolParams({
            fee: 0,
            reserveToken: _reserveToken,
            couponToken: _couponToken,
            distributionPeriod: _distributionPeriod,
            sharesPerToken: _sharesPerToken,
            feeBeneficiary: _feeBeneficiary
        });

        preDeposit = PreDeposit(Utils.deploy(address(new PreDeposit()), abi.encodeCall(PreDeposit.initialize, (
            params,
            _poolFactory,
            block.timestamp,
            block.timestamp + 7 days,
            RESERVE_CAP,
            "Bond ETH",
            "bondETH",
            "Levered ETH",
            "levETH"
        ))));
    }
}
```

This allows us to consider that 100 ETH is more than a reasonable value to be set as the reserve cap.

The malicious whale can slowly deposit ETH and accumulate a huge part of the pool. Even if the reserve cap is increased to 200 ETH, the malicious whale would dominate half of the pool with only $300000 worth of ETH (considering a price of $3000 per ETH). 

It is also relevant to note that **there is no punishment for withdrawals, so the attacker does not really gain but also does not incur any loss.** The `PreDeposit` contract **does not include mechanisms to avoid withdrawing or paying fees as a sort of mechanism to avoid the mentioned scenario. In addition,** there’s no way for the Plaza team to know if assets will actually be withdrawn or not until the PreDeposit period has passed.

Considering the previous example, with a pool whose reserve cap has increased to 200 ETH, and where 100 ETH belong to a malicious depositor, a fair amount of bond tokens to be minted in order to keep the CL at 1.2 would be around 5000 bond tokens in supply (200 ETH * $3000 / 5000 * 100 gives a CL of 1.2). 

However, if the malicious depositor withdraws at the last time, the pool is left with only 100 ETH, but 5000 bond tokens are still minted.

### Impact

Considering the previous example, the pool is initially created with a CL of 100 ETH * $3000 / 5000 * 100 = 0.6, making the whole system start in an extremely undercollateralized state. This  forces users who interacted with the `PreDeposit` contract to lose money on redemptions until the CL gets back to a healthy state, and discourages protocol interactions given that it is never profitable for users in the first place to interact with the protocol.

### PoC

_No response_

### Mitigation

Consider allowing setting `bondAmount` and `leverageAmount` **after** the deposit period has finished, which allows to know exactly the total amount of tokens predeposited.