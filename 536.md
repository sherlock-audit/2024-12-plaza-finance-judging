Jovial Pear Otter

High

# Auction cannot end due to incorrect period handling

### Summary

The incorrect usage of `currentPeriod` in `transferReserveToAuction()` will cause an auction to always fail as the `transferReserveToAuction()` will always revert due to incorrect `auctionAddress`.

### Root Cause

*   In `Pool.sol`, the `startAuction()` function ([Pool.sol#L546](https://github.com/sherlock-audit/2024-12-plaza-finance/blob/main/plaza-evm/src/Pool.sol#L546)) increments the `currentPeriod` through `bondToken.increaseIndexedAssetPeriod()` in `BondToken.sol`.
* When an auction succeeds, `Auction.sol` will call `transferReserveToAuction()` ([Auction.sol#345](https://github.com/sherlock-audit/2024-12-plaza-finance/blob/main/plaza-evm/src/Auction.sol#L345)).
*   However, the `transferReserveToAuction()` function ([Pool.sol#L579](https://github.com/sherlock-audit/2024-12-plaza-finance/blob/main/plaza-evm/src/Pool.sol#L579)) uses the updated `currentPeriod` to access the auction address. Meaning that the `currentPeriod` used in `transferReserveToAuction()` is always one period ahead and will return a zero address. 
* The call will revert due to the `require` statement ([Pool.sol#L580](https://github.com/sherlock-audit/2024-12-plaza-finance/blob/main/plaza-evm/src/Pool.sol#L580)).

### Internal Pre-conditions

1.  `Pool` needs to call `startAuction()` to set the auction address.
2.  A successful auction must be completed.
3.  Someone needs to call `endAuction()` to trigger the vulnerability.

### External Pre-conditions

N/A

### Attack Path

1.  Someone calls `startAuction()` in `Pool.sol`.
2.  An auction is created and bids are placed, resulting in a successful auction.
3.  Someone calls `endAuction()` in `Auction.sol`.
4.  `endAuction()` calls `Pool(pool).transferReserveToAuction(totalSellReserveAmount)`.
5.  `transferReserveToAuction()` in `Pool.sol` retrieves the auction address using the incorrect `currentPeriod`, resulting in a zero address.
6.  The `require(msg.sender == auctionAddress)` statement in `transferReserveToAuction()` reverts, preventing the auction from ending and blocking coupon distribution.

### Impact

The `transferReserveToAuction()` will always revert, preventing the auction from succeeding and the coupon tokens from being distributed.

### PoC

[AuctionNeverEnd.t.sol.txt](https://github.com/user-attachments/files/18506678/AuctionNeverEnd.t.sol.txt)

```solidity
function testAuctionNeverEnd() public {
    Token rToken = Token(params.reserveToken);
    address bidder = address(0x1);

    vm.startPrank(governance);
    uint256 reserveAmount = 10000000000 * 10**18;
    rToken.mint(governance, reserveAmount);
    rToken.approve(address(poolFactory), reserveAmount); 

    params.sharesPerToken = 2500000; // 2.5e6 in Token.sol it's 18dp but it changes nothing 
    uint256 bondAmount = 1000 * 10**18;
    uint256 leverageAmount = 1000 * 10**18;
    Pool _pool = Pool(poolFactory.createPool(params, reserveAmount, bondAmount, leverageAmount, "", "", "", "", false)); // 10000 - 10000 - 10000000000
    
    BondToken bondToken = _pool.bondToken();

    
    // Check currentPeriod and auctions[currentPeriod]
    (uint256 currentPeriod,) = bondToken.globalPool();
    address auctionAddress = _pool.auctions(currentPeriod);
    console.log("currentPeriod = %s and  auctions[currentPeriod] = %s", currentPeriod, auctionAddress); //  0 - 0x0000000000000000000000000000000000000000

    // Enter in auction period
    _pool.setAuctionPeriod(2 days);
    // Fast forward one day
    vm.warp(block.timestamp + 1 days);
    _pool.startAuction();


    // mint USDC for bidding
    Auction auction =Auction(_pool.auctions(0)); 
    uint256 slotSize = auction.totalBuyCouponAmount() / (auction.maxBids());
    uint expectedCouponAmountToDistribute = slotSize * auction.maxBids(); // * (auction.maxBids()+1); // Be sure that we will fill the whole couponAmount
    Token usdc = Token(params.couponToken);
    usdc.mint(bidder, expectedCouponAmountToDistribute); 
    vm.stopPrank();

    // Bid
    vm.startPrank(bidder);

    usdc.approve(address(auction), type(uint256).max);
    auction.bid(1, expectedCouponAmountToDistribute);

    // Fast forward two day
    vm.warp(block.timestamp + 2 days);

    // will revert
    vm.expectRevert(Pool.CallerIsNotAuction.selector);
    auction.endAuction();
    vm.stopPrank();

    // The reasons
    // Check currentPeriod and auctions[currentPeriod]
    vm.startPrank(governance);
    (currentPeriod,) = bondToken.globalPool();
    auctionAddress = _pool.auctions(currentPeriod);
    console.log("currentPeriod = %s and  auctions[currentPeriod] = %s", currentPeriod, auctionAddress); // 1 -  0x0000000000000000000000000000000000000000
    vm.stopPrank();
  }
```

### Mitigation

Use the same logic as [Pool.sol#L593-L595](https://github.com/sherlock-audit/2024-12-plaza-finance/blob/main/plaza-evm/src/Pool.sol#L593C1-L595C7)

```solidity
(uint256 currentPeriod,) = bondToken.globalPool();
uint256 previousPeriod = currentPeriod - 1;
address auctionAddress = auctions[previousPeriod];
```
This ensures that the correct auction address is fetched for the current distribution period.