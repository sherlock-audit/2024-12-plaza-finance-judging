Fun Ceramic Mustang

Medium

# Underflow in collateral calculation can cause inability to redeem

### Summary

When redeeming, logic for calculation of collateral level will underflow in cases where pool TVL is lower than the value of bond tokens being burned. As a consequence, in such situation attempts to redeem will end up with failure. This is a code logic error and can be easily avoided.

### Root Cause

Function [getRedeemAmount](https://github.com/sherlock-audit/2024-12-plaza-finance/blob/14a962c52a8f4731bbe4655a2f6d0d85e144c7c2/plaza-evm/src/Pool.sol#L477) in Pool.sol is used to calculate the amount of reserve tokens that can be redeemed for the amount of burned bond tokens. It involves collateral level calculation:
```solidity
    uint256 collateralLevel;
    if (tokenType == TokenType.BOND) {
      collateralLevel = ((tvl - (depositAmount * BOND_TARGET_PRICE)) * PRECISION) / ((bondSupply - depositAmount) * BOND_TARGET_PRICE);
    }
```

Issue with this part of code is that it assumes that `tvl >= (depositAmount * BOND_TARGET_PRICE)`, because otherwise calculation will fail due to underflow. For example, if ETH price falls sharply that will significantly decrease TVL. On the other hand target bond price is hard-coded to $100, and if the amount of bond tokens being burned is big enough underflow will be triggered. This is demonstrated in PoC.

### Internal Pre-conditions

1. TVL becomes lower than (amount of bond tokens redeemed * $100)
2. That triggers the undeflow

### External Pre-conditions

Factors that contribute to the TVL lowering:
1. Reserve token price decline
2. Fees not being claimed for a long time (fee part of reserves is not calculated towards TVL)
3. Fee percentage being high

Factors that contribute to the relatively high redemption value compared to the TVL:
1. Protocol "whale" (user who holds big chunk of supply) trying to redeem

### Attack Path

This bug will get triggered in certain conditions, no need for any attack to be involved

### Impact

Attempts to redeem will be failing. It's not a permanent loss, as user could redeem smaller chunks (but that involves extra transaction fees for the user) or user could wait for TVL to increase enough (but there is the opportunity cost as funds are stuck).

### PoC

Here is the complete test case showing the problem including these steps:
- pool is created
- Alice deposits 7 ether
- ETH price decreases by 50%
- Alice tries to redeem her funds, but can't as redemption underflows

```solidity
// SPDX-License-Identifier: UNLICENSED
pragma solidity ^0.8.26;

import "forge-std/Test.sol";

import {Pool} from "../src/Pool.sol";
import {Token} from "./mocks/Token.sol";
import {Utils} from "../src/lib/Utils.sol";
import {BondToken} from "../src/BondToken.sol";
import {PoolFactory} from "../src/PoolFactory.sol";
import {Distributor} from "../src/Distributor.sol";
import {OracleFeeds} from "../src/OracleFeeds.sol";
import {LeverageToken} from "../src/LeverageToken.sol";
import {MockPriceFeed} from "./mocks/MockPriceFeed.sol";
import {Deployer} from "../src/utils/Deployer.sol";
import {UpgradeableBeacon} from "@openzeppelin/contracts/proxy/beacon/UpgradeableBeacon.sol";
import "@openzeppelin/contracts/token/ERC20/ERC20.sol";

import "forge-std/console.sol";

contract PoolTest_FeeCollection is Test {
    address deployer = makeAddr("deployer");
    address feeBeneficiary = makeAddr("feeBeneficiary");
    MockPriceFeed mockPriceFeed;

    // 5% fee
    uint256 fee = 50000;

    function test_G_RedeemUnderflow() public {
        // create factory
        PoolFactory factory = _createFactory();
        console.log("Factory created");

        // create tokens
        address reserveToken = address(new Token("Wrapped  ETH", " WETH", false));
        address couponToken = address(new Token("USDC", "USDC", false));
        Token(couponToken).setDecimals(6);

        // create pool
        Pool pool = _createPool(factory, reserveToken, couponToken);
        console.log("Pool created");

        // initial price is $3000
        mockPriceFeed.setMockPrice(3000 * 10 ** 8, 8);

        // alice makes a deposit
        address alice = makeAddr("alice");
        uint256 deposit = 7 ether;
        deal(reserveToken, alice, deposit);
        console.log("Alice deposits WETH:", deposit);
        vm.startPrank(alice);
        IERC20(reserveToken).approve(address(pool), deposit);
        uint256 received = pool.create({tokenType: Pool.TokenType.BOND, depositAmount: deposit, minAmount: 0});
        console.log("Alice receives bond tokens:", received);

        // price drops 50% causing lowering the TVL
        mockPriceFeed.setMockPrice(1500 * 10 ** 8, 8);

        // redeem
        console.log("Alice burns bond tokens:", received);
        IERC20(address(pool.bondToken())).approve(address(pool), received);
        uint256 wethReceived = pool.redeem({tokenType: Pool.TokenType.BOND, depositAmount: received, minAmount: 0});
        console.log("Redeem successful!");
        console.log("Alice redeemed WETH:", wethReceived);
        vm.stopPrank();
    }

    function _createFactory() internal returns (PoolFactory) {
        vm.startPrank(deployer);

        // create factory
        address oracleFeedsContract = address(new OracleFeeds());
        address governance = makeAddr("governance");
        PoolFactory factory = PoolFactory(
            Utils.deploy(
                address(new PoolFactory()),
                abi.encodeCall(
                    PoolFactory.initialize,
                    (
                        governance,
                        address(new Deployer()),
                        oracleFeedsContract,
                        address(new UpgradeableBeacon(address(new Pool()), deployer)),
                        address(new UpgradeableBeacon(address(new BondToken()), deployer)),
                        address(new UpgradeableBeacon(address(new LeverageToken()), deployer)),
                        address(new UpgradeableBeacon(address(new Distributor()), deployer))
                    )
                )
            )
        );
        vm.stopPrank();

        vm.startPrank(governance);
        factory.grantRole(factory.POOL_ROLE(), deployer);
        vm.stopPrank();

        return factory;
    }

    function _createPool(PoolFactory factory, address reserveToken, address couponToken) internal returns (Pool) {
        vm.startPrank(deployer);
        uint256 reserveAmount = 1e18;
        deal(reserveToken, deployer, reserveAmount);
        IERC20(reserveToken).approve(address(factory), reserveAmount);

        // create pool
        Pool pool = Pool(
            factory.createPool({
                params: PoolFactory.PoolParams({
                    fee: fee,
                    feeBeneficiary: feeBeneficiary,
                    reserveToken: reserveToken,
                    sharesPerToken: 2_500_000,
                    distributionPeriod: 90 days,
                    couponToken: couponToken
                }),
                reserveAmount: reserveAmount,
                bondAmount: 10 ether,
                leverageAmount: 20 ether,
                bondName: "Bond  WETH",
                bondSymbol: "bond WETH",
                leverageName: "Levered  WETH",
                leverageSymbol: "lev WETH",
                pauseOnCreation: false
            })
        );

        // Deploy the mock price feed
        mockPriceFeed = new MockPriceFeed();
        mockPriceFeed.setMockPrice(3000 * int256(10 ** 8), uint8(8));
        OracleFeeds(factory.oracleFeeds()).setPriceFeed(
            address(pool.reserveToken()), address(0), address(mockPriceFeed), 1 days
        );
        vm.stopPrank();

        return pool;
    }
}
```

Run it:
```solidity
❯ forge test --mt test_G_RedeemUnderflow -vv

[FAIL: panic: arithmetic underflow or overflow (0x11)] test_G_RedeemUnderflow() (gas: 31334885)
Logs:
  Factory created
  Pool created
  Alice deposits WETH: 7000000000000000000
  Alice receives bond tokens: 210000000000000000000
  Alice burns bond tokens: 210000000000000000000

Suite result: FAILED. 0 passed; 1 failed; 0 skipped; finished in 14.24ms (7.54ms CPU time)
```

If mitigation is applied, Alice successfully redeems 6.1 WETH:
```solidity
❯ forge test --mt test_G_RedeemUnderflow -vv

[PASS] test_G_RedeemUnderflow() (gas: 31071100)
Logs:
  Factory created
  Pool created
  Alice deposits WETH: 7000000000000000000
  Alice receives bond tokens: 210000000000000000000
  Alice burns bond tokens: 210000000000000000000
  Redeem successful!
  Alice redeemed WETH: 6109090820000000000

Suite result: ok. 1 passed; 0 failed; 0 skipped; finished in 7.57ms (5.11ms CPU time)
```

### Mitigation

Avoid underflow, for example:
```diff
        if (tokenType == TokenType.BOND) {
+           if (tvl < (depositAmount * BOND_TARGET_PRICE)) {
+              collateralLevel = 0;
+           } else {
                collateralLevel = ((tvl - (depositAmount * BOND_TARGET_PRICE)) * PRECISION)
                    / ((bondSupply - depositAmount) * BOND_TARGET_PRICE);
            }
```