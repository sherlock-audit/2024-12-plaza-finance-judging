Sneaky Eggplant Pike

Medium

# The market rate will never be applied during redemption due to decimals descrepancies

### Summary

The [`getRedeemAmount()`](https://github.com/sherlock-audit/2024-12-plaza-finance/blob/main/plaza-evm/src/Pool.sol#L477-L525) function calculates the redemption rate to determine the reserve token amount to redeem. If this redemption rate is greater than the market rate(i.e bond token price from Oracle), then the market rate is applied as redemption rate.

```solidity
  function getRedeemAmount(
    TokenType tokenType,
    uint256 depositAmount,
    uint256 bondSupply,
    uint256 levSupply,
    uint256 poolReserves,
    uint256 ethPrice,
    uint8 oracleDecimals,
    uint256 marketRate
  ) public pure returns(uint256) {
    ... ...

    if (marketRate != 0 && marketRate < redeemRate) {
      redeemRate = marketRate;
    }
    
    // Calculate and return the final redeem amount
    return ((depositAmount * redeemRate).fromBaseUnit(oracleDecimals) / ethPrice) / PRECISION;
  }
```

However, due to discrepancies between the decimals of the market rate and the redemption rate, the market rate will not be applied when calculating the reserve token amount.

### Root Cause

A market rate is a bond price and is calculated in the [`Pool::simulateRedeem()`](https://github.com/sherlock-audit/2024-12-plaza-finance/blob/main/plaza-evm/src/Pool.sol#L422-L463) function.

```solidity
  function simulateRedeem(TokenType tokenType, uint256 depositAmount) public view returns(uint256) {
    ... ...

    uint8 oracleDecimals = getOracleDecimals(reserveToken, USD);

    uint256 marketRate;
    address feed = OracleFeeds(oracleFeeds).priceFeeds(address(bondToken), USD);
    if (feed != address(0)) {
      marketRate = getOraclePrice(address(bondToken), USD)
        .normalizeAmount(
          getOracleDecimals(address(bondToken), USD), 
          oracleDecimals // this is the decimals of the reserve token chainlink feed
        );
    }
    ... ...
  }
```

In Chainlink price feeds, the decimals of fed price is usually `8` when the base currency is `USD`. As can be seen from the above code snippet, the market rate is derived by converting the bond token price to oracle decimals(`8`).

On the other hand, the precision of market rate is 1e6, meaning that its decimals is `6`.

Because oracle price unit is `100`x greater than the redemption rate unit, the market rate will almost always be greater than the redemtion rate.

Therefore, the market rate will never be applied while redemption.

### Internal pre-conditions

The oracle price feed for bond token should be available.

### External pre-conditions

*None*

### Attack Path

*None*

### Impact

Market rate(i.e bond token price) will never be applied for redemption even though the bondETH price feed is available.

### PoC

First, adds the following line to the `Pool::getRedeemAmount()` function to demonstrate the comparison between market rate and redemption rate.

```diff
  function getRedeemAmount(...) public pure returns(uint256) {
    ... ...

+   console.log("Market Rate", marketRate);
+   console.log("Reedem Rate", redeemRate);

    if (marketRate != 0 && marketRate < redeemRate) { // @audit-issue ? How can marketRate(Oracle Decimals) and redeemRate(1e6) be comparable?
      redeemRate = marketRate;
    }
    ... ...
  }
```

After then, add the following test code snippet to the `Pool.t.sol`. The purpose of this test case is to compare market rate and redemption rate by enabling bond token price feed.

```solidity
contract PoolMarketRateTest is Test, TestCases {
  using Decimals for uint256;
  using Strings for uint256;

  PoolFactory public poolFactory;
  PoolFactory.PoolParams public params;
  Pool pool;

  MockPriceFeed public mockPriceFeed;
  MockPriceFeed public bondMockPriceFeed;
  address public oracleFeedsContract;

  address public deployer = address(0x1);
  address public minter = address(0x2);
  address public governance = address(0x3);
  address public securityCouncil = address(0x4);
  address public user = address(0x5);
  address public user2 = address(0x6);

  address public constant bondPriceFeed = address(0x5a1b1E88A4E0a7635c965DF8e312264e2d8C59a2);
  address public constant ethPriceFeed = address(0x71041dddad3595F9CEd3DcCFBe3D1F4b0a16Bb70);
  uint256 public constant CHAINLINK_DECIMAL_PRECISION = 10**8;
  uint8 public constant CHAINLINK_DECIMAL = 8;

  function setUp() public {
    vm.startPrank(deployer);

    address contractDeployer = address(new Deployer());
    oracleFeedsContract = address(new OracleFeeds());

    address poolBeacon = address(new UpgradeableBeacon(address(new Pool()), governance));
    address bondBeacon = address(new UpgradeableBeacon(address(new BondToken()), governance));
    address levBeacon = address(new UpgradeableBeacon(address(new LeverageToken()), governance));
    address distributorBeacon = address(new UpgradeableBeacon(address(new Distributor()), governance));

    poolFactory = PoolFactory(Utils.deploy(address(new PoolFactory()), abi.encodeCall(
      PoolFactory.initialize, 
      (governance, contractDeployer, oracleFeedsContract, poolBeacon, bondBeacon, levBeacon, distributorBeacon)
    )));

    params.fee = 0;
    params.feeBeneficiary = governance;
    params.reserveToken = address(new Token("Wrapped ETH", "WETH", false));
    params.sharesPerToken = 50 * 10 ** 18;
    params.distributionPeriod = 0;
    params.couponToken = address(new Token("USDC", "USDC", false));
    
    OracleFeeds(oracleFeedsContract).setPriceFeed(params.reserveToken, address(0), ethPriceFeed, 1 days);

    // Deploy the mock price feed
    mockPriceFeed = new MockPriceFeed();

    // Use vm.etch to deploy the mock contract at the specific address
    bytes memory bytecode = address(mockPriceFeed).code;
    vm.etch(ethPriceFeed, bytecode);

    // Set oracle price
    mockPriceFeed = MockPriceFeed(ethPriceFeed);
    mockPriceFeed.setMockPrice(3000 * int256(CHAINLINK_DECIMAL_PRECISION), uint8(CHAINLINK_DECIMAL));
    
    vm.stopPrank();

    vm.startPrank(governance);
    poolFactory.grantRole(poolFactory.POOL_ROLE(), governance);
    poolFactory.grantRole(poolFactory.SECURITY_COUNCIL_ROLE(), securityCouncil);

    vm.stopPrank();
  }

  // @audit-poc
  function testRedeemWithMarketRate() public {
    vm.startPrank(governance);

    Token rToken = Token(params.reserveToken);
    rToken.mint(governance, 1000e18);
    rToken.approve(address(poolFactory), 500e18);

    // Create pool and approve deposit amount
    pool = Pool(poolFactory.createPool(params, 450e18, 10000e18, 9000e18, "", "", "", "", false));
    rToken.approve(address(pool), 1000e18);
    vm.stopPrank();

    // Make bondETH price feed available
    vm.startPrank(deployer);
    OracleFeeds(oracleFeedsContract).setPriceFeed(address(pool.bondToken()), address(0), bondPriceFeed, 1 days);

    bondMockPriceFeed = new MockPriceFeed();
    vm.etch(bondPriceFeed, address(bondMockPriceFeed).code);
    bondMockPriceFeed = MockPriceFeed(bondPriceFeed);
    
    // Set bond token price as 100 USD/bondETH for test
    bondMockPriceFeed.setMockPrice(100 * int256(CHAINLINK_DECIMAL_PRECISION), uint8(CHAINLINK_DECIMAL));
    vm.stopPrank();

    vm.prank(governance);
    pool.redeem(Pool.TokenType.LEVERAGE, 1000e18, 0);
  }
}
```

Then, runs the test case:

Logs:
```bash
stuart@DESKTOP-R9POSPL:~/audit/2024-12-plaza-finance-royalsalute/plaza-evm$ forge test --match-test testRedeemWithMarketRate -vvv
[⠒] Compiling...
[⠃] Compiling 15 files with Solc 0.8.27
[⠊] Solc 0.8.27 finished in 16.69s
Compiler run successful!

Ran 1 test for test/Pool.poc.t.sol:PoolMarketRateTest
[PASS] testRedeemWithMarketRate() (gas: 2225898)
Logs:
  Market Rate 10000000000
  Reedem Rate 38000000
```

As we can see from the logs, the decimals of market rate is 8 and for redemption rate, it's 6, meaning the two values are incomparable.

### Mitigation

The market rate should be converted to the `PRECISION` decimals.

```diff
    address feed = OracleFeeds(oracleFeeds).priceFeeds(address(bondToken), USD);
    if (feed != address(0)) {
      marketRate = getOraclePrice(address(bondToken), USD)
        .normalizeAmount(
          getOracleDecimals(address(bondToken), USD), 
-         oracleDecimals // this is the decimals of the reserve token chainlink feed
+         6 // 
        );
    }
```
