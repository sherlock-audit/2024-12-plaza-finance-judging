Swift Wintergreen Viper

High

# Funds Locked: Period Mismatch Prevents Auction Contract from Receiving Reserve Tokens from Pool

### Summary

A period tracking misalignment in the `Pool:transferReserveToAuction` function will cause all successful Auctions to fail as the `Pool` contract will reject reserve token transfers to the `Auction` contract, effectively breaking the core auction functionality and resulting in loss/locked funds for the bidder of the auction.

### Root Cause

In [Pool.sol:579](https://github.com/sherlock-audit/2024-12-plaza-finance/blob/main/plaza-evm/src/Pool.sol#L579) the `transferReserveToAuction` function uses the current period from `bondToken.globalPool()` to validate the auction address, but this period is incremented when the auction starts and calls [bondToken.increaseIndexedAssetPeriod()](https://github.com/sherlock-audit/2024-12-plaza-finance/blob/main/plaza-evm/src/Pool.sol#L567). This means the auction contract address is stored at period N, but when trying to access it, the function looks at period N+1. The address stored at `auctions[period N+1]` has not been set yet so it'll return address(0). 

```solidity
  function transferReserveToAuction(uint256 amount) external virtual {
    (uint256 currentPeriod, ) = bondToken.globalPool();
    address auctionAddress = auctions[currentPeriod]; // <= address will be address(0)
    require(msg.sender == auctionAddress, CallerIsNotAuction());
    
    IERC20(reserveToken).safeTransfer(msg.sender, amount);
  }
```

### Internal Pre-conditions

1. A pool must be created and initialized
2. The distribution period must have passed
3. An auction must have been started via `Pool:startAuction()`
4. A successful bid must have been placed on the auction

### External Pre-conditions

_No response_

### Attack Path

1. Pool is created and initialized with reserve tokens
2. Distribution period passes
3. startAuction() is called, which:
    - Creates new auction contract
    - Stores auction address at `auctions[currentPeriod]`
    - Increases the bond token period
4. Bidder successfully bids on auction
5. When auction ends and tries to claim reserve tokens via `transferReserveToAuction`:
    - Function gets current period (N+1)
    - Looks for auction at auctions[N+1] which is empty
    - Transaction reverts with `CallerIsNotAuction`

### Impact

1. Successful bidders cannot receive their reserve tokens because the state of the `Auction` wont be updated 
2. The protocol cannot distribute coupon tokens to `bondToken` holders
3. The Auction contract is locked in a `BIDDING` state resulting in:
    - Locked `couponTokens` inside the `Auction` contract    
    - Total loss of `couponTokens` for the Bidder

### PoC

Paste the code below in the `Pool.t.sol` file and then run it with `forge test -vvv --mt testBrokenTransferReserveToAuction`

```solidity
    function testBrokenTransferReserveToAuction() public {
        address bidder = makeAddr("bidder");
        uint256 reserveAmount = 10000000000;
        uint256 bondAmount = 10000;
        uint256 leverageAmount = 10000;

        Token rToken = Token(params.reserveToken);
        Token couponToken = Token(params.couponToken);

        // set distribution period to 1 day for this test
        params.distributionPeriod = 1 days;

        // Mint and approve reserve tokens
        rToken.mint(governance, reserveAmount);
        rToken.approve(address(poolFactory), reserveAmount);

        // Create pool and approve deposit amount
        vm.startPrank(governance);
        rToken.approve(address(poolFactory), reserveAmount);
        Pool _pool = Pool(
            poolFactory.createPool(
                params, reserveAmount, bondAmount, leverageAmount, "bondETH", "bETH", "leverageETH", "lETH", false
            )
        );
        _pool.setAuctionPeriod(10 days);
        vm.stopPrank();

        Pool.PoolInfo memory info = _pool.getPoolInfo();

        uint256 lastDistribution = info.lastDistribution;
        uint256 distributionPeriod = info.distributionPeriod;

        // wait for distribution period to pass so we can start auction
        vm.warp(block.timestamp + lastDistribution + distributionPeriod + 1 hours);
        // get pool reserve before auction
        uint256 poolReserveBeforeAuction = rToken.balanceOf(address(_pool));
        // start new auction
        _pool.startAuction();

        // get auction for current period
        address currentAuction = _pool.auctions(info.currentPeriod);

        uint256 couponAmountToDistribute = Auction(currentAuction).totalBuyCouponAmount();

        // mint & approve coupon tokens then bid
        vm.startPrank(bidder);
        couponToken.mint(bidder, couponAmountToDistribute);
        couponToken.approve(currentAuction, couponAmountToDistribute);
        // bid on 85% of pool reserve
        Auction(currentAuction).bid((poolReserveBeforeAuction * 85) / 100, couponAmountToDistribute);
        vm.stopPrank();

        // wait for auction to end
        vm.warp(block.timestamp + 10 days);

        // now auction calls Pool(pool).transferReserveToAuction(totalSellReserveAmount);
        // but this call fails because Pool:transferReserveToAuction() gets the updated currentPeriod from bondToken.globalPool()
        // which is incremented by 1 after an auction is started.
        vm.expectRevert(Pool.CallerIsNotAuction.selector);
        Auction(currentAuction).endAuction();

        // get state of auction
        Auction.State state = Auction(currentAuction).state();
        assertEq(uint256(state), 0); // = BIDDING

        // reserve token balance of auction is still 0
        assertEq(rToken.balanceOf(currentAuction), 0);
        // couponToken amount is locked in auction
        assertEq(couponToken.balanceOf(currentAuction), couponAmountToDistribute);

        // claim refund fails because auction is still in BIDDING state
        vm.startPrank(bidder);
        vm.expectRevert(Auction.AuctionFailed.selector);
        Auction(currentAuction).claimRefund(0);
        vm.stopPrank();
    }
```

### Mitigation

Modify the `transferReserveToAuction` function to use the previous period:

```solidity
  function transferReserveToAuction(uint256 amount) external virtual {
    (uint256 currentPeriod, ) = bondToken.globalPool();
    address auctionAddress = auctions[currentPeriod - 1];
    require(msg.sender == auctionAddress, CallerIsNotAuction());
    
    IERC20(reserveToken).safeTransfer(msg.sender, amount);
  }
```