Swift Wintergreen Viper

Medium

# Governance Can Set 100% Pool Sale Limit Causing Total Auction Sale of Reserve Tokens

### Summary

The wrong maximum threshold check in `Pool.sol:setPoolSaleLimit` will cause a complete loss of pool reserves for users if governance set the pool sale limit to 100% despite documentation specifying 95% maximum. The `poolSaleLimit` defines the amount the `Auction` contract is allowed to sell during a "Coupon Auction".

[Read documentation coupon-auction (1.)](https://docs.plaza.finance/protocol-mechanics/coupon-auction)

"A new auction is created, and the system sets a 10-day period for participants to bid for ETH related assets using USDC. A portion of the underlying assets (like wstETH) in the pool are made available for bidding, **not to exceed 95% percent of the value of the pool**."


### Root Cause

https://github.com/sherlock-audit/2024-12-plaza-finance/blob/main/plaza-evm/src/Pool.sol#L177

The choice to implement `setPoolSaleLimit()` without a maximum threshold violates the protocol's documented safety guarantee of maintaining at least 5% of the pools reserve token. The function checks if the new `_poolSaleLimit` is below 90 and reverts in this case. This will allow the {governance} role to increase it to 100 or even above.

```solidity
  uint256 private constant MIN_POOL_SALE_LIMIT = 90;

  function setPoolSaleLimit(uint256 _poolSaleLimit) external onlyRole(poolFactory.GOV_ROLE()) {
    if (_poolSaleLimit < MIN_POOL_SALE_LIMIT) {
      revert PoolSaleLimitTooLow();
    }
    uint256 oldThreshold = poolSaleLimit;
    poolSaleLimit = _poolSaleLimit;
    emit PoolSaleLimitChanged(oldThreshold, _poolSaleLimit);
  }
```

### Internal Pre-conditions

1. The {governance} role needs to call {setPoolSaleLimit} to set the {poolSaleLimit} to 100. 
2. Pool needs to have reserves of the underlying reserve token (in this test case WETH).
3. The {bidder} for the Coupon Auction needs to have enough coupon tokens (in this test case USDC).

### External Pre-conditions

_No response_

### Attack Path

1. {governance} calls `setPoolSaleLimit(100)` setting the limit to 100%
2. {Bidder} approves USDC for auction contract
3. {Bidder} calls bid() with parameters to purchase 100% of pool reserves
4. After auction period ends, {bidder} calls `endAuction()`
5. 100% of pool reserves are transferred to the {bidder}
6. There are no reserve tokens in the `Pool` left for redemption

### Impact

The pool suffers to hold enough of it's reserve tokens for redemption of bond/lev ETH, violating the documented maximum of 95% that should remain untouchable. This breaks protocol invariants and makes it impossible to redeem any reserve tokens.

### PoC

Paste below code into the `Auction.t.sol` contract:


```solidity

    function testAuctionCanSell100Percent() public {
        // get pool reserve
        uint256 poolReserveBefore = IERC20(address(weth)).balanceOf(address(pool));

        console.log("weth.balanceOf(bidder) before", weth.balanceOf(bidder));
        console.log("poolReserveBefore", poolReserveBefore);

        //setPoolSaleLimit
        vm.startPrank(governance);
        vm.expectEmit(true, true, true, true);
        emit Pool.PoolSaleLimitChanged(90, 100);
        Pool(pool).setPoolSaleLimit(100);
        vm.stopPrank();

        vm.startPrank(bidder);
        usdc.mint(bidder, 1000000000000 ether);
        usdc.approve(address(auction), 1000000000000 ether);
        auction.bid(poolReserveBefore, 1000000000000); // 100% of pool's reserve
        vm.stopPrank();

        // End the auction
        vm.warp(block.timestamp + 15 days);

        auction.endAuction();

        uint256 poolReserveAfter = IERC20(address(weth)).balanceOf(address(pool));
        console.log("poolReserveAfter", poolReserveAfter);
        assertEq(poolReserveAfter, 0);

        vm.startPrank(bidder);
        auction.claimBid(1);
        vm.stopPrank();

        console.log("weth.balanceOf(bidder) after", weth.balanceOf(bidder));
        assertEq(weth.balanceOf(bidder), poolReserveBefore);
    }
```

**Logs:
  weth.balanceOf(bidder) before 0
  poolReserveBefore 500000000000000000000000000000
  poolReserveAfter 0
  weth.balanceOf(bidder) after 500000000000000000000000000000**

### Mitigation

Invert the check in the setPoolSaleLimit function to prevent setting the poolSaleLimit above the MIN_POOL_SALE_LIMIT.
Additional rename the MIN_POOL_SALE_LIMIT to MAX_POOL_SALE_LIMIT to avoid confusion.

```solidity

    uint256 private constant MAX_POOL_SALE_LIMIT = 90;

    function setPoolSaleLimit(uint256 _poolSaleLimit) external onlyRole(poolFactory.GOV_ROLE()) {
        if (_poolSaleLimit > MAX_POOL_SALE_LIMIT) {
            revert PoolSaleLimitTooLow();
        }
        uint256 oldThreshold = poolSaleLimit;
        poolSaleLimit = _poolSaleLimit;
        emit PoolSaleLimitChanged(oldThreshold, _poolSaleLimit);
    }
```