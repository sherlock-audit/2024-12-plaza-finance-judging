Big Mossy Crane

High

# Anyone can mint bondTokens before the auction starts, and redeem them after the auction starts to get free couponTokens.

### Summary

The holders of `bondTokens` get a set amount of `couponToken`, for every `bondTokens` they hold during a `period`. The `couponToken` amount necessary to distribute to `bondTokens` holders is earned by holding an auction of the Pool's reserves. The auction is successful only when this necessary amount is reached from users bidding the reserves. There can only be one auction at a `period`, and the `period` is increased when the auction starts. This allows anyone to mint `bondTokens`, start the auction, and redeem the tokens back. If the auction is successful, they can claim the `couponTokens`, and even if the auction fails, they are still eligible for an amount, and can claim it anytime the Distirbutor contract has enough `couponTokens` for them, since it is indexed in the `bondToken` contract.

### Root Cause

Each `bondToken` a user holds is entitled to receive an amount of `couponTokens` (i.e USDC) at every period. An auction is held at every period to get the necessary amount `couponTokens` to distribute to the holders of `bondTokens`. The amount of couponToken a user can receive is indexed in the bondToken and handled by the [`updateIndexedUserAssets()`](https://github.com/sherlock-audit/2024-12-plaza-finance/blob/main/plaza-evm/src/BondToken.sol#L172-L179).

```solidity
    function updateIndexedUserAssets(address user, uint256 balance) internal {
        uint256 period = globalPool.currentPeriod;
        uint256 shares = getIndexedUserAmount(user, balance, period);

        userAssets[user].indexedAmountShares = shares;
        userAssets[user].lastUpdatedPeriod = period;

        emit UpdatedUserAssets(user, period, shares);
    }
```


 This function calls [`getIndexedUserAmount()`](https://github.com/sherlock-audit/2024-12-plaza-finance/blob/main/plaza-evm/src/BondToken.sol#L190C1-L199C4) to update the user's `couponToken` amount, which calculates how much `couponToken` a user should receive based on their balance from `lastUpdatePeriod` up to the `currentPeriod`.

```solidity
  function getIndexedUserAmount(address user, uint256 balance, uint256 period) public view returns(uint256) {
    IndexedUserAssets memory userPool = userAssets[user];
    uint256 shares = userPool.indexedAmountShares;

    for (uint256 i = userPool.lastUpdatedPeriod; i < period; i++) {
      shares += (balance * globalPool.previousPoolAmounts[i].sharesPerToken).toBaseUnit(SHARES_DECIMALS);
    }

    return shares;
  }
```

This process is done before the token updates the balances of the user, as seen in [`_update()`](https://github.com/sherlock-audit/2024-12-plaza-finance/blob/main/plaza-evm/src/BondToken.sol#L154-L164).

```solidity 
  function _update(address from, address to, uint256 amount) internal virtual override whenNotPaused() {
    if (from != address(0)) {
      updateIndexedUserAssets(from, balanceOf(from));
    }

    if (to != address(0)) {
      updateIndexedUserAssets(to, balanceOf(to));
    }
    //this line updates the balances 
    super._update(from, to, amount);
  }
```

When a user mints bondTokens, this function will be called with the user's balance as `0` and the indexedAmountShares will be 0, until a user transfer the tokens with at least `1` period difference.

The period is incrementation of a period is handled by the [`increaseIndexedAssetPeriod()`](https://github.com/sherlock-audit/2024-12-plaza-finance/blob/main/plaza-evm/src/BondToken.sol#L217-L228), which is called by the Pool in [`startAuction()`](https://github.com/sherlock-audit/2024-12-plaza-finance/blob/main/plaza-evm/src/Pool.sol#L530-L570)

```solidity
  function increaseIndexedAssetPeriod(uint256 sharesPerToken) public onlyRole(DISTRIBUTOR_ROLE) whenNotPaused() {
    globalPool.previousPoolAmounts.push(
      PoolAmount({
        period: globalPool.currentPeriod,
        amount: totalSupply(),
        sharesPerToken: globalPool.sharesPerToken
      })
    );
    globalPool.currentPeriod++;
    globalPool.sharesPerToken = sharesPerToken;

    emit IncreasedAssetPeriod(globalPool.currentPeriod, sharesPerToken);
  }


  function startAuction() external whenNotPaused() {
  ...
    // Increase the bond token period
    bondToken.increaseIndexedAssetPeriod(sharesPerToken);

    // Update last distribution time
    lastDistribution = block.timestamp;
  }

```
By minting `bondTokens` before an auction starts, the user will have `0` amount of `indexedAmountShares` at an `x` period. 

When the user redeem after the auction starts, the user will have a `y` amount of `indexedAmountShares` based on their `bondToken` balance and `sharesPerToken` at an `x+1` period. Which can be claimed after the `couponTokens` are distributed to the Distributor. They can mint bondTokens with a `z` amount and redeem back `z` amount, if they execute this when the collateralLevel is > 1.2, when the price of bondToken is 100e6 for both minting and redeeming.

### Internal Pre-conditions

1. Collateral level needs to be > 1.2, this way the attacker can mint and redeem `bondTokens` at the same rate. 
2. The `startAuction()` must be available to call, so the attacker can increase the period only after they mint.

### External Pre-conditions

_No response_

### Attack Path

Assuming the conditions stated in the internal pre-conditions are true:

Lets say for every `bondTokens`, the users can `1 USDC` for simplification. The `currentPeriod` is `1`.

1. The attacker mints `1000 bondTokens` at a rate of `100 USDC`.
2. This sets the `lastUpdatedPeriod` of the attacker to `1`, the `indexedAmountShares` `0`.
3. The attacker calls the `startAuction()` function to increase the `period`.
4. This increase the `currentPeriod` to `2`.
5. The attacker redeem the `1000 bondTokens` back at a rate of `100 USDC`.
6. This sets the `lastUpdatedPeriod` of the attacker to `2` and the `indexedAmountShares` `1000`.
7. After the auction is successful or whenever the `Distributor` contract has `1000 USDC`, the attacker can claim it.

### Impact

Whether the auction is successful or not, just by timing the minting and redemption of `bondTokens`,  the attacker gets a balance in the Distributor contract, which they can withdraw as soon as the Distributor contract has enough tokens. 

### PoC

_No response_

### Mitigation

_No response_