Sneaky Eggplant Pike

Medium

# A Pool will unable to start a new auction if the auction period has elapsed without any auction

### Summary

The `Pool` contracts requires to start an auction during the auction period. 

However, due to incorrect validation, a new auction cannot be created if one auction period has elapsed without any auction.

### Root Cause

The `Pool::startAuction` function runs following validation prior to creating an auction:

[Pool.sol#L530-L571](https://github.com/sherlock-audit/2024-12-plaza-finance/blob/main/plaza-evm/src/Pool.sol#L530-L571)

```solidity
  function startAuction() external whenNotPaused() {
    ... ...
    // Check if auction period hasn't passed
@>  require(lastDistribution + distributionPeriod + auctionPeriod >= block.timestamp, AuctionPeriodPassed());
    ... ...
  }
```

The above validation means that if `distributionPeriod + auctionPeriod` has elapsed from `lastDistribution`, no auction will be able to begin.

### Internal pre-conditions

*None*

### External pre-conditions

*None*

### Attack Path

*None*

### Impact

Cannot run a new auction.

The only way to get rid of this is to manually increase `distributionPeriod` and `auctionPeriod`, but this will cause unexpectedly longer distribution and auction period.

### PoC

Here is a test case:
```solidity
// SPDX-License-Identifier: UNLICENSED
pragma solidity ^0.8.26;

import {console} from "forge-std/console.sol";
import "forge-std/Test.sol";

import {Pool} from "../src/Pool.sol";
import {Token} from "./mocks/Token.sol";
import {Auction} from "../src/Auction.sol";
import {Utils} from "../src/lib/Utils.sol";
import {MockPool} from "./mocks/MockPool.sol";
import {BondToken} from "../src/BondToken.sol";
import {TestCases} from "./data/TestCases.sol";
import {Decimals} from "../src/lib/Decimals.sol";
import {PoolFactory} from "../src/PoolFactory.sol";
import {Distributor} from "../src/Distributor.sol";
import {OracleFeeds} from "../src/OracleFeeds.sol";
import {Validator} from "../src/utils/Validator.sol";
import {OracleReader} from "../src/OracleReader.sol";
import {LeverageToken} from "../src/LeverageToken.sol";
import {MockPriceFeed} from "./mocks/MockPriceFeed.sol";
import {Deployer} from "../src/utils/Deployer.sol";
import {Strings} from "@openzeppelin/contracts/utils/Strings.sol";
import {UpgradeableBeacon} from "@openzeppelin/contracts/proxy/beacon/UpgradeableBeacon.sol";


contract PoolMarketRateTest is Test, TestCases {
  using Decimals for uint256;
  using Strings for uint256;

  PoolFactory public poolFactory;
  PoolFactory.PoolParams public params;
  Pool pool;

  MockPriceFeed public mockPriceFeed;
  MockPriceFeed public bondMockPriceFeed;
  address public oracleFeedsContract;

  address public deployer = address(0x1);
  address public minter = address(0x2);
  address public governance = address(0x3);
  address public securityCouncil = address(0x4);
  address public user = address(0x5);
  address public user2 = address(0x6);

  address public constant bondPriceFeed = address(0x5a1b1E88A4E0a7635c965DF8e312264e2d8C59a2);
  address public constant ethPriceFeed = address(0x71041dddad3595F9CEd3DcCFBe3D1F4b0a16Bb70);
  uint256 public constant CHAINLINK_DECIMAL_PRECISION = 10**8;
  uint8 public constant CHAINLINK_DECIMAL = 8;

  function setUp() public {
    vm.startPrank(deployer);

    address contractDeployer = address(new Deployer());
    oracleFeedsContract = address(new OracleFeeds());

    address poolBeacon = address(new UpgradeableBeacon(address(new Pool()), governance));
    address bondBeacon = address(new UpgradeableBeacon(address(new BondToken()), governance));
    address levBeacon = address(new UpgradeableBeacon(address(new LeverageToken()), governance));
    address distributorBeacon = address(new UpgradeableBeacon(address(new Distributor()), governance));

    poolFactory = PoolFactory(Utils.deploy(address(new PoolFactory()), abi.encodeCall(
      PoolFactory.initialize, 
      (governance, contractDeployer, oracleFeedsContract, poolBeacon, bondBeacon, levBeacon, distributorBeacon)
    )));

    params.fee = 0;
    params.feeBeneficiary = governance;
    params.reserveToken = address(new Token("Wrapped ETH", "WETH", false));
    params.sharesPerToken = 20e6;
    params.distributionPeriod = 10 days;
    params.couponToken = address(new Token("USDC", "USDC", false));
    
    OracleFeeds(oracleFeedsContract).setPriceFeed(params.reserveToken, address(0), ethPriceFeed, 1 days);

    // Deploy the mock price feed
    mockPriceFeed = new MockPriceFeed();

    // Use vm.etch to deploy the mock contract at the specific address
    bytes memory bytecode = address(mockPriceFeed).code;
    vm.etch(ethPriceFeed, bytecode);

    // Set oracle price
    mockPriceFeed = MockPriceFeed(ethPriceFeed);
    mockPriceFeed.setMockPrice(3000 * int256(CHAINLINK_DECIMAL_PRECISION), uint8(CHAINLINK_DECIMAL));
    
    vm.stopPrank();

    vm.startPrank(governance);
    poolFactory.grantRole(poolFactory.POOL_ROLE(), governance);
    poolFactory.grantRole(poolFactory.SECURITY_COUNCIL_ROLE(), securityCouncil);

    vm.stopPrank();
  }

  // @audit-poc
  function testCannotStartAuctionAfterAuctionPeriod() public {
    vm.startPrank(governance);

    Token rToken = Token(params.reserveToken);
    rToken.mint(governance, 1000e18);
    rToken.approve(address(poolFactory), 500e18);

    // Create pool and approve deposit amount
    pool = Pool(poolFactory.createPool(params, 450e18, 10000e18, 9000e18, "", "", "", "", false));
    pool.setAuctionPeriod(5 days);
    rToken.approve(address(pool), 1000e18);
    vm.stopPrank();

    vm.warp(block.timestamp + 16 days); // Distribution Period: 10 days, Auction Period: 5 days
    vm.expectRevert(Pool.AuctionPeriodPassed.selector);
    pool.startAuction();
  }
}
```

### Mitigation

In my opinion, the issued validation is not that necessary, so consider removing it.
