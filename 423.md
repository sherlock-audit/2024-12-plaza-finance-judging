Sneaky Eggplant Pike

High

# Users may receive a significantly reduced amount of reserve tokens upon redemption due to precision loss

### Summary

The [`Pool::getRedeemAmount()`](https://github.com/sherlock-audit/2024-12-plaza-finance/blob/main/plaza-evm/src/Pool.sol#L477-L525) function calculates the redemption rate to determine the amount of reserve tokens to be redeemed. 

However, this function is subject to precision loss during the calculation of the redemption rate for levETH, resulting in levETH token redeemers receiving fewer reserve tokens than anticipated.

### Root Cause

The precision loss occurs in the following line:

[Pool.sol#L514](https://github.com/sherlock-audit/2024-12-plaza-finance/blob/main/plaza-evm/src/Pool.sol#L514)

```solidity
  function getRedeemAmount(
    TokenType tokenType,
    uint256 depositAmount,
    uint256 bondSupply,
    uint256 levSupply,
    uint256 poolReserves,
    uint256 ethPrice,
    uint8 oracleDecimals,
    uint256 marketRate
  ) public pure returns(uint256) {
    ... ...
    // Calculate the redeem rate based on the collateral level and token type
    uint256 redeemRate;
    if (collateralLevel <= COLLATERAL_THRESHOLD) {
      redeemRate = ((tvl * multiplier) / assetSupply);
    } else if (tokenType == TokenType.LEVERAGE) {
@>    redeemRate = ((tvl - (bondSupply * BOND_TARGET_PRICE)) / assetSupply) * PRECISION;
    } else {
      redeemRate = BOND_TARGET_PRICE * PRECISION;
    }
    ... ...
  }

```

As observed, multiply of `PRECISION` is applied after the division operation, which results in precision loss.

As `redeemRate` gets lower due to precision loss, the reserve token amount to redeem becomes lower than anticipated from the following calculation.

```solidity
    return ((depositAmount * redeemRate).fromBaseUnit(oracleDecimals) / ethPrice) / PRECISION;
```

### Internal pre-conditions

*None*

### External pre-conditions

*None*

### Attack Path

*None*

### Impact

`levETH` holders may receive fewer reserve tokens than expected. The loss can easily be more than `1%`, which is classified as a High severity issue according to the [sherlock docs](https://docs.sherlock.xyz/audits/judging/guidelines#iv.-how-to-identify-a-high-issue):

> *Guidelines for Significant Loss:*
> - *Users lose more than 1% and more than $10 of their principal.*
> - *Users lose more than 1% and more than $10 of their yield.*
> - *The protocol loses more than 1% and more than $10 of the fees.*


### PoC

I added the following test case to `Pool.t.sol`:

```solidity
  function testRedeemPrecisionLossForLevETH() public {
    vm.startPrank(governance);
    Token rToken = Token(params.reserveToken);

    // Mint reserve tokens
    rToken.mint(governance, 1000e18);
    rToken.approve(address(poolFactory), 500e18);

    // Create pool and approve deposit amount
    Pool _pool = Pool(poolFactory.createPool(params, 450e18, 10000e18, 9000e18, "", "", "", "", false));
    rToken.approve(address(_pool), 1000e18);

    // Call create and assert minted tokens
    uint256 amount = _pool.redeem(Pool.TokenType.LEVERAGE, 1000e18, 0);
    console.log("Redeemed Amount", amount);

    // Reset reserve state
    rToken.burn(governance, rToken.balanceOf(governance));
    rToken.burn(address(_pool), rToken.balanceOf(address(_pool)));
  }
```

Logs:
```bash
Ran 1 test for test/Pool.t.sol:PoolTest
[PASS] testRedeemPrecisionLossForLevETH() (gas: 1842581)
Logs:
  Redeemed Amount 12666666666666666666
```

After running the test, the received amount is `12666666666666666666`.

After then, updates the issued line in the `getRedeemAmount()` function like below:
```diff
-    redeemRate = ((tvl - (bondSupply * BOND_TARGET_PRICE)) / assetSupply) * PRECISION;
+    redeemRate = ((tvl - (bondSupply * BOND_TARGET_PRICE)) * PRECISION) / assetSupply;
```

After running the test again, the received amount is `12962962666666666666`, meaning that the user loss is about `2.3%`

Logs:
```bash
Ran 1 test for test/Pool.t.sol:PoolTest
[PASS] testRedeemPrecisionLossForLevETH() (gas: 1842581)
Logs:
  Redeemed Amount 12962962666666666666
```
### Mitigation

Just update the issued line like the below:
```diff
-    redeemRate = ((tvl - (bondSupply * BOND_TARGET_PRICE)) / assetSupply) * PRECISION;
+    redeemRate = ((tvl - (bondSupply * BOND_TARGET_PRICE)) * PRECISION) / assetSupply;
```
