Joyful Cider Oyster

Medium

# Users will not be able to redeem bond tokens using Pool::redeem function under certain condition

### Summary

Users will not be able to redeem bond tokens using Pool::redeem function under certain condition

`Pool::redeem` function internally calls `_redeem` function which calls `simulateRedeem` function which further calls [`getRedeemAmount`](https://github.com/sherlock-audit/2024-12-plaza-finance/blob/main/plaza-evm/src/Pool.sol#L498) function that  will revert because of below line of code in certain condition

`collateralLevel = ((tvl - (depositAmount * BOND_TARGET_PRICE)) * PRECISION) / ((bondSupply - depositAmount) * BOND_TARGET_PRICE);`

### Root Cause

The actual root cause is this line in Pool.sol#L498. Because if `(depositAmount * BOND_TARGET_PRICE)` is greater than `tvl` then this call will revert hence reverting the overall redeem transaction.

`collateralLevel = ((tvl - (depositAmount * BOND_TARGET_PRICE)) * PRECISION) / ((bondSupply - depositAmount) * BOND_TARGET_PRICE);`

The tvl is [calculated](https://github.com/sherlock-audit/2024-12-plaza-finance/blob/main/plaza-evm/src/Pool.sol#L491)  as

`uint256 tvl = (ethPrice * poolReserves).toBaseUnit(oracleDecimals);`

`ethPrice` is uncontrollable  by the protocol team, which can cause the `tvl` to drop anytime at any extent. It depends on market conditions. The oracle price is chainlink feed dependent, so up to some extent incorrect data from oracle can also contribute to this because while calculating the [ethPrice](https://github.com/sherlock-audit/2024-12-plaza-finance/blob/main/plaza-evm/src/OracleReader.sol#L55C1-L76C4), we are only checking the heartbeat, we are not validating the answer in terms of number ranges like min and max.

```solidity
    (,int256 answer,,uint256 updatedTimestamp,) = AggregatorV3Interface(feed).latestRoundData();
    
    if (updatedTimestamp + OracleFeeds(oracleFeeds).feedHeartbeats(feed) < block.timestamp) {
      revert StalePrice();
    }
```

Even if the price is stable, protocol must also need to hold certain amount of reserveTokens otherwise the transaction will revert as mentioned.


### Internal Pre-conditions

1. `tvl` is less than `(depositAmount * BOND_TARGET_PRICE)`

### External Pre-conditions

1. User redeems bond tokens

### Attack Path

_No response_

### Impact

Under certain condition, users will not be able to redeem bond tokens. Which breaks the core functionality of the protocol.

### PoC

Create a new file as `Playground.t.sol` in `test/` and run `forge test --mt test_Play`

```solidity
// SPDX-License-Identifier: UNLICENSED
pragma solidity 0.8.28;

import {Test, console, stdError} from "forge-std/Test.sol";


contract TestPlayground is Test {
    Playground public playground;

    function setUp() external {
        playground = new Playground();
    }

    function test_Play() external {
        vm.expectRevert(stdError.arithmeticError);
        playground.play();
    }
}

contract Playground {
    error ZeroLeverageSupply();
    error ZeroDebtSupply();

    using MyLib for uint256;

    enum TokenType {
        BOND, // bond
        LEVERAGE
    }

    uint256 POINT_TWO = 0.2e6;
    uint256 POINT_EIGHT = 0.8e6;
    uint256 BOND_TARGET_PRICE = 100;
    uint256 PRECISION = 1e6;
    uint256 COLLATERAL_THRESHOLD = 1.2e6;

    function play() public {

        TokenType tokenType = TokenType.BOND;
        uint256 depositAmount = 5000e18;
        uint256 bondSupply = 5000e18;
        uint256 levSupply = 80e18;
        uint256 poolReserves = 100e18;
        uint256 ethPrice = 3000e6;
        uint8 oracleDecimals = 6;
        uint256 marketRate = 0;

        _getRedeemAmount(
            tokenType,
            depositAmount,
            bondSupply,
            levSupply,
            poolReserves,
            ethPrice,
            oracleDecimals,
            marketRate
        );
    }

    function _getRedeemAmount(
        TokenType tokenType,
        uint256 depositAmount,
        uint256 bondSupply,
        uint256 levSupply,
        uint256 poolReserves,
        uint256 ethPrice,
        uint8 oracleDecimals,
        uint256 marketRate
    ) internal returns (uint256) {
        if (bondSupply == 0) {
            revert ZeroDebtSupply();
        }

        uint256 tvl = (ethPrice * poolReserves).toBaseUnit(oracleDecimals);
        uint256 assetSupply = bondSupply;
        uint256 multiplier = POINT_EIGHT;

        // Calculate the collateral level based on the token type
        uint256 collateralLevel;
        if (tokenType == TokenType.BOND) {
            collateralLevel =
                ((tvl - (depositAmount * BOND_TARGET_PRICE)) * PRECISION) /
                ((bondSupply - depositAmount) * BOND_TARGET_PRICE);
        } else {
            multiplier = POINT_TWO;
            assetSupply = levSupply;
            collateralLevel =
                (tvl * PRECISION) /
                (bondSupply * BOND_TARGET_PRICE);

            if (assetSupply == 0) {
                revert ZeroLeverageSupply();
            }
        }

        // Calculate the redeem rate based on the collateral level and token type
        uint256 redeemRate;
        if (collateralLevel <= COLLATERAL_THRESHOLD) {
            redeemRate = ((tvl * multiplier) / assetSupply);
        } else if (tokenType == TokenType.LEVERAGE) {
            redeemRate =
                ((tvl - (bondSupply * BOND_TARGET_PRICE)) / assetSupply) *
                PRECISION;
        } else {
            redeemRate = BOND_TARGET_PRICE * PRECISION;
        }

        if (marketRate != 0 && marketRate < redeemRate) {
            redeemRate = marketRate;
        }

        // Calculate and return the final redeem amount
        return
            ((depositAmount * redeemRate).fromBaseUnit(oracleDecimals) /
                ethPrice) / PRECISION;
    }

}

library MyLib {
    function toBaseUnit(
        uint256 amount,
        uint8 decimals
    ) internal pure returns (uint256) {
        return amount / (10 ** decimals);
    }

    function fromBaseUnit(
        uint256 baseUnitAmount,
        uint8 decimals
    ) internal pure returns (uint256) {
        return baseUnitAmount * (10 ** decimals);
    }
}

```

### Mitigation

If `tvl` is less than `(depositAmount * BOND_TARGET_PRICE)` then set the `collateralLevel` to `zero`

Modify `getRedeemAmount` function in `Pool.sol` as below

```diff
        uint256 tvl = (ethPrice * poolReserves).toBaseUnit(oracleDecimals);
        uint256 assetSupply = bondSupply;
        uint256 multiplier = POINT_EIGHT;

        // Calculate the collateral level based on the token type
        uint256 collateralLevel;
        if (tokenType == TokenType.BOND) {
++        if(tvl < (depositAmount * BOND_TARGET_PRICE)) {
++            collateralLevel = 0;
++        } else {
++            collateralLevel =
++                ((tvl - (depositAmount * BOND_TARGET_PRICE)) * PRECISION) /
++                ((bondSupply - depositAmount) * BOND_TARGET_PRICE);
++        }
--        collateralLevel =
--            ((tvl - (depositAmount * BOND_TARGET_PRICE)) * PRECISION) /
--            ((bondSupply - depositAmount) * BOND_TARGET_PRICE);
        } else {
            multiplier = POINT_TWO;
            assetSupply = levSupply;
            collateralLevel =
                (tvl * PRECISION) /
                (bondSupply * BOND_TARGET_PRICE);

            if (assetSupply == 0) {
                revert ZeroLeverageSupply();
            }
        }
```

we should also implement a check in this flow to make sure that `depositAmount` is less than or equal to respective token's supply. 