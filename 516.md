Jovial Pear Otter

High

# Asymmetry between `sharesPerToken` in `Pool.sol` and `BondToken.sol` leads to incorrect coupon distribution

### Summary

A discrepancy in how `sharesPerToken` is handled between `Pool.sol` and `BondToken.sol` will cause incorrect coupon distribution for users as `Pool.sol` calculates the distribution amount using its local `sharesPerToken` while `BondToken.sol` uses its own, leading to an inconsistent coupon amount, potentially causing a shortfall or surplus of coupon tokens in the pool.


### Root Cause

*   In `Pool.sol`, the `startAuction()` function ( [Pool.sol#L546](https://github.com/sherlock-audit/2024-12-plaza-finance/blob/main/plaza-evm/src/Pool.sol#L546) ) calculates `couponAmountToDistribute` using the `sharesPerToken` stored within `Pool.sol`.
*   Immediately after, `bondToken.increaseIndexedAssetPeriod(sharesPerToken)` ( [Pool.sol#L567](https://github.com/sherlock-audit/2024-12-plaza-finance/blob/main/plaza-evm/src/Pool.sol#L567) ) updates the `sharesPerToken` in `BondToken.sol`.
    * However, when users claim coupons via `Distributor.sol`, `bondToken.getIndexedUserAmount()` ( [Distributor.sol#L88](https://github.com/sherlock-audit/2024-12-plaza-finance/blob/main/plaza-evm/src/Distributor.sol#L88) ) is called, which uses the *previous* `sharesPerToken` stored in `BondToken.sol` ( [BondToken.sol#L195](https://github.com/sherlock-audit/2024-12-plaza-finance/blob/main/plaza-evm/src/BondToken.sol#L195) ).
*   This discrepancy leads to a mismatch between the coupon amount calculated in `Pool.sol` and the amount distributed based on `BondToken.sol`'s previous state.


### Internal Pre-conditions

1.  The `Pool` contract's `setSharesPerToken()` function needs to be called to set a different `sharesPerToken` value.
2.  The distribution period must have passed, and the auction period must not have passed for the `startAuction()` function to be called.

### External Pre-conditions

N/A

### Attack Path

1.  The governance role calls `setSharesPerToken()` in `Pool.sol` to update the `sharesPerToken` to a new value.
2.  The distribution period passes, and the auction period has not passed, allowing `startAuction()` to be called.
3.  `startAuction()` in `Pool.sol` calculates `couponAmountToDistribute` using the updated `sharesPerToken` from `Pool.sol`.
4.  `startAuction()` calls `bondToken.increaseIndexedAssetPeriod(sharesPerToken)` which updates the `sharesPerToken` in `BondToken.sol`.
5. Auction end sucessfully and  `distribute()` is called.
6.  Users call `claim()` in `Distributor.sol`, which uses `bondToken.getIndexedUserAmount()` that calculates the user's coupon amount based on the *previous* `sharesPerToken` in `BondToken.sol`.
7.  The coupon amount calculated in `Pool.sol` and the amount distributed to users will be inconsistent.

### Impact

Due to the discrepancy, the pool might either lack sufficient coupon tokens for distribution or have leftover tokens after distribution.

### PoC

Provide 2 POCs : 
- not enough coupon tokens
- too much coupon tokens

[AssymetrySharesPerToken.t.sol.txt](https://github.com/user-attachments/files/18505543/AssymetrySharesPerToken.t.sol.txt)

```solidity
function testAssymetrySharesPerTokenNotEnough() public {
    Token rToken = Token(params.reserveToken);
    
    vm.startPrank(governance);
    uint256 reserveAmount = 10000000000 * 10**18;
    rToken.mint(governance, reserveAmount); 
    rToken.approve(address(poolFactory), reserveAmount); 

    params.sharesPerToken = 2500000; // 2.5e6 in Token.sol it's 18dp but it changes nothing 
    uint256 bondAmount = 1000 * 10**18;
    uint256 leverageAmount = 1000 * 10**18;
    Pool _pool = Pool(poolFactory.createPool(params, reserveAmount, bondAmount, leverageAmount, "", "", "", "", false));
    
    BondToken bondToken = _pool.bondToken();

    // Check current the variable sharesPerToken in Pool.sol
    // slot sharesPerToken = 59
    // OracleReader take 50 slots
    // sharesPerToken is at slot 9 in Pool 
    uint256 storedSharesPerToken = uint256(vm.load(address(_pool), bytes32(uint256(59))));
    console.log("sharesPerToken of Pool :  %s", storedSharesPerToken); // 2500000
    // Check current the variable sharesPerToken in BondToken.sol
    (uint256 currentPeriod, uint256 _sharesPerToken) = bondToken.globalPool();
    console.log("sharesPerToken of BondToken :  %s",_sharesPerToken); // 2500000

    // Set a lower SharesPerToken
    uint256 sharesPerToken = 2000000; // 2.0e6
    _pool.setSharesPerToken(sharesPerToken);

    // Check current the variable sharesPerToken in Pool.sol
    storedSharesPerToken = uint256(vm.load(address(_pool), bytes32(uint256(59))));
    console.log("sharesPerToken of Pool :  %s", storedSharesPerToken); // 2000000
    // Check current the variable sharesPerToken in BondToken.sol
    (currentPeriod, _sharesPerToken) = bondToken.globalPool();
    console.log("sharesPerToken of BondToken :  %s",_sharesPerToken); // 2500000

    // Start the auction 
    _pool.setAuctionPeriod(2 days);
    // Fast forward one day
    vm.warp(block.timestamp + 1 days);
    _pool.startAuction();

    // Expected couponAmountToDistribute
    uint8 bondDecimals = bondToken.decimals(); // 18
    uint8 sharesDecimals = bondToken.SHARES_DECIMALS(); // == 6
    uint8 maxDecimals = bondDecimals > sharesDecimals ? bondDecimals : sharesDecimals;

    uint256 normalizedTotalSupply = bondToken.totalSupply(); // 18dp
    uint256 normalizedShares =  storedSharesPerToken * (10 ** (maxDecimals - sharesDecimals));

    // Expectedcoupon amount to distribute
    uint256 expectedCouponAmountToDistribute = (normalizedTotalSupply * normalizedShares) / (10**(maxDecimals * 2 - sharesDecimals));

    console.log("Expected couponAmountToDistribute :  %s",expectedCouponAmountToDistribute); // 2000000000

    // Real couponAmountToDistribute
    (currentPeriod,) = bondToken.globalPool(); // 1 as increaseIndexedAssetPeriod() is called inside startAuction()
    uint256 balance = bondToken.balanceOf(governance);
    // not IERC20(couponToken).safeDecimals() because in Token.sol it's hard coded to 18 but should be 6 for USDC
    uint256 realCouponAmountToDistribute = bondToken.getIndexedUserAmount(governance, balance, currentPeriod) / (10**(bondToken.decimals() - sharesDecimals));
    
    console.log("Real couponAmountToDistribute :  %s",realCouponAmountToDistribute); // 2500000000

    vm.stopPrank();

    assertLt(expectedCouponAmountToDistribute, realCouponAmountToDistribute);
  }
```

```solidity
function testAssymetrySharesPerTokenTooMuch() public {
    Token rToken = Token(params.reserveToken);
    
    vm.startPrank(governance);
    uint256 reserveAmount = 10000000000 * 10**18;
    rToken.mint(governance, reserveAmount);
    rToken.approve(address(poolFactory), reserveAmount);

    params.sharesPerToken = 2500000; // 2.5e6 in Token.sol it's 18dp but it changes nothing 
    uint256 bondAmount = 1000 * 10**18;
    uint256 leverageAmount = 1000 * 10**18;
    Pool _pool = Pool(poolFactory.createPool(params, reserveAmount, bondAmount, leverageAmount, "", "", "", "", false));
    
    BondToken bondToken = _pool.bondToken();
    // address distributor = poolFactory.distributors(address(_pool));

    // Check current the variable sharesPerToken in Pool.sol
    // slot sharesPerToken = 59
    // OracleReader take 50 slots
    // sharesPerToken is at slot 9 in Pool 
    uint256 storedSharesPerToken = uint256(vm.load(address(_pool), bytes32(uint256(59))));
    console.log("sharesPerToken of Pool :  %s", storedSharesPerToken); // 2500000
    // Check current the variable sharesPerToken in BondToken.sol
    (uint256 currentPeriod, uint256 _sharesPerToken) = bondToken.globalPool();
    console.log("sharesPerToken of BondToken :  %s",_sharesPerToken); // 2500000

    // Set a bigger SharesPerToken
    uint256 sharesPerToken = 3000000; // 3.0e6
    _pool.setSharesPerToken(sharesPerToken);

    // Check current the variable sharesPerToken in Pool.sol
    storedSharesPerToken = uint256(vm.load(address(_pool), bytes32(uint256(59))));
    console.log("sharesPerToken of Pool :  %s", storedSharesPerToken); // 3000000
    // Check current the variable sharesPerToken in BondToken.sol
    (currentPeriod, _sharesPerToken) = bondToken.globalPool();
    console.log("sharesPerToken of BondToken :  %s",_sharesPerToken); // 2500000

    // Start the auction
    _pool.setAuctionPeriod(2 days);
    // Fast forward one day
    vm.warp(block.timestamp + 1 days);
    _pool.startAuction();

    // Expected couponAmountToDistribute
    uint8 bondDecimals = bondToken.decimals(); // 18
    uint8 sharesDecimals = bondToken.SHARES_DECIMALS(); // == 6
    uint8 maxDecimals = bondDecimals > sharesDecimals ? bondDecimals : sharesDecimals;
    
    uint256 normalizedTotalSupply = bondToken.totalSupply(); // 18dp
    uint256 normalizedShares =  storedSharesPerToken * (10 ** (maxDecimals - sharesDecimals));

    // Expectedcoupon amount to distribute
    uint256 expectedCouponAmountToDistribute = (normalizedTotalSupply * normalizedShares) / (10**(maxDecimals * 2 - sharesDecimals));

    console.log("Expected couponAmountToDistribute :  %s",expectedCouponAmountToDistribute); // 

    // Real couponAmountToDistribute
    (currentPeriod,) = bondToken.globalPool(); // 1 as increaseIndexedAssetPeriod() is called inside startAuction()
    uint256 balance = bondToken.balanceOf(governance); 
    // not IERC20(couponToken).safeDecimals() because in Token.sol it's hard coded to 18 but should be 6 for USDC
    uint256 realCouponAmountToDistribute = bondToken.getIndexedUserAmount(governance, balance, currentPeriod)  / (10**(bondToken.decimals() - sharesDecimals));
    
    console.log("Real couponAmountToDistribute :  %s",realCouponAmountToDistribute); // 2500000000

    vm.stopPrank();

    assertGt(expectedCouponAmountToDistribute, realCouponAmountToDistribute);
  }
```

### Mitigation

To ensure consistency between `Pool.sol` and `BondToken.sol`, the `startAuction()` function should retrieve the `sharesPerToken` value directly from the `globalPool` variable within the `bondToken` contract.