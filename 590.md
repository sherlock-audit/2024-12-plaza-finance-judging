Electric Mandarin Manatee

High

# `Pool::transferReserveToAuction` Will Always Revert Due to Incorrect Period

### Summary


The increment of `bondToken.globalPool().currentPeriod` in startAuction will cause a failed auction settlement as `Pool::transferReserveToAuction` references the incremented period, which has no associated auction, resulting in a revert.


### Root Cause


When the auction starts, it calls `bondToken.globalPool()` to retrieve the `currentPeriod` from the `bondToken` contract. Based on the `currentPeriod` value, it stores the new auction contract address in `auctions[currentPeriod]`. It then increases the bond token period by calling the `increaseIndexedAssetPeriod` function in the `bondToken`, which increments `globalPool.currentPeriod` by one.

```solidity
  function startAuction() external whenNotPaused() {
    
    // Check if auction for current period has already started
    (uint256 currentPeriod,) = bondToken.globalPool();
    require(auctions[currentPeriod] == address(0), AuctionAlreadyStarted());


    //... 
    
    auctions[currentPeriod] = Utils.deploy(
      address(new Auction()),
      abi.encodeWithSelector(
        Auction.initialize.selector,
        address(couponToken),
        address(reserveToken),
        couponAmountToDistribute,
        block.timestamp + auctionPeriod,
        1000,
        address(this),
        poolSaleLimit
      )
    );

    //...

    // Increase the bond token period
    bondToken.increaseIndexedAssetPeriod(sharesPerToken);

    //...
  }
```

Whenever the auction ends successfully (`State.SUCCEEDED`), it calls the `transferReserveToAuction` function of the pool contract. This function then transfers the `totalSellReserveAmount` of `reserveToken` *from the pool to the auction contract*.


```solidity
  function endAuction() external auctionExpired whenNotPaused {
    if (state != State.BIDDING) revert AuctionAlreadyEnded();

    if (currentCouponAmount < totalBuyCouponAmount) {
      state = State.FAILED_UNDERSOLD;
    } else if (totalSellReserveAmount >= (IERC20(sellReserveToken).balanceOf(pool) * poolSaleLimit) / 100) {
        state = State.FAILED_POOL_SALE_LIMIT;
    } else {
      state = State.SUCCEEDED;
      Pool(pool).transferReserveToAuction(totalSellReserveAmount);
      IERC20(buyCouponToken).safeTransfer(beneficiary, IERC20(buyCouponToken).balanceOf(address(this)));
    }

    emit AuctionEnded(state, totalSellReserveAmount, totalBuyCouponAmount);
  }
```

The problem is that in the `Pool::transferReserveToAuction` function, it retrieves the `bondToken.globalPool()` `currentPeriod`, which was incremented by one at the last line of the auction. As a result, `auctions[currentPeriod]` returns `address(0)` because no auction has been deployed for this `currentPeriod` value and this function will always revert it.


```solidity
  function transferReserveToAuction(uint256 amount) external virtual {
    (uint256 currentPeriod, ) = bondToken.globalPool();
    address auctionAddress = auctions[currentPeriod];
    require(msg.sender == auctionAddress, CallerIsNotAuction());
    
    IERC20(reserveToken).safeTransfer(msg.sender, amount);
  }
```  
https://github.com/sherlock-audit/2024-12-plaza-finance/blob/main/plaza-evm/src/Pool.sol#L577-L583


### Internal Pre-conditions

startAuction is called, initiating a new auction and incrementing currentPeriod.
The auction ends with state SUCCEEDED.

### External Pre-conditions

_No response_

### Attack Path


1. A valid auction is started, initializing `auctions[currentPeriod]`.

2. startAuction increments `currentPeriod`.

3. The auction ends successfully, calling `Pool::transferReserveToAuction`

4. `transferReserveToAuction` references `auctions[currentPeriod]`, which points to address(0) as no auction exists for the incremented period.

5. The transaction reverts.

### Impact



If the auction does not transition to the `FAILED_UNDERSOLD` or the `FAILED_POOL_SALE_LIMIT`, and the `SUCCEEDED` state revert due to this logic. As a result, the `currentCouponAmount` value of `buyCouponToken` belonging to the users will remain locked in the auction contract because the users have not called the `claimBid` or `claimRefund` functions because of the `auctionSucceeded` and `auctionFailed` modifiers.

```solidity
  function endAuction() external auctionExpired whenNotPaused {
    if (state != State.BIDDING) revert AuctionAlreadyEnded();

    if (currentCouponAmount < totalBuyCouponAmount) {
      state = State.FAILED_UNDERSOLD;
    } else if (totalSellReserveAmount >= (IERC20(sellReserveToken).balanceOf(pool) * poolSaleLimit) / 100) {
        state = State.FAILED_POOL_SALE_LIMIT;
    } else {
      state = State.SUCCEEDED;
      Pool(pool).transferReserveToAuction(totalSellReserveAmount);
      IERC20(buyCouponToken).safeTransfer(beneficiary, IERC20(buyCouponToken).balanceOf(address(this)));
    }

    emit AuctionEnded(state, totalSellReserveAmount, totalBuyCouponAmount);
  }
```

### PoC

_No response_

### Mitigation

Return the `previousPeriod` 

```diff
  function transferReserveToAuction(uint256 amount) external virtual {

    (uint256 currentPeriod, ) = bondToken.globalPool();

+    require(currentPeriod > 0, AccessDenied());
+    uint256 previousPeriod = currentPeriod - 1;

-    address auctionAddress = auctions[currentPeriod];
+    address auctionAddress = auctions[previousPeriod];
    require(msg.sender == auctionAddress, CallerIsNotAuction());
    
    IERC20(reserveToken).safeTransfer(msg.sender, amount);
  }
```