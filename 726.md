Spicy Alabaster Owl

High

# Cannot end auction due to failing validation - wrong distribution period used

### Summary

If the total amount of buy coupon tokens in the auction is raised, the auction cannot be ended. 
This results in stuck funds and inability to make bond coupon payouts.

### Root Cause

After an auction is created, the `globalPool.currentPeriod` in `BondToken` is advanced. 

When ending an auction that is fully funded, a call is made to `Pool::.transferReserveToAuction()`.
In [Pool.sol:579](https://github.com/sherlock-audit/2024-12-plaza-finance/blob/main/plaza-evm/src/Pool.sol#L579) the `auctionAddress` is taken from the `auctions` mapping, but the current period is used instead of the previous period.

Thus, the validation at [Pool.sol:580](https://github.com/sherlock-audit/2024-12-plaza-finance/blob/main/plaza-evm/src/Pool.sol#L580) fails, causing a revert.

### Internal Pre-conditions

1. Have an existing auction on a pool.
2. Auction should be in bidding state.
3. Total buy coupon amount the auction is trying to raise should be fulfilled.

### External Pre-conditions

_No response_

### Attack Path

_No response_

### Impact

Since auctions cannot be ended if they become fully funded, coupon payouts cannot be made.
Furthermore funds will be stuck in the contract, since users will not be able to claim refunds on their bids.

### PoC

Drop the following test and setup method in `Auction.t.sol`

```solidity
function setUpWithNonMockPool() public {
    usdc = new Token("USDC", "USDC", false);
    weth = new Token("WETH", "WETH", false);
    
    //create pool
    pool = createPool(address(weth), address(usdc));

    //set auction period on the pool
    Pool(pool).setAuctionPeriod(10 days);

    //forward time to distribution period
    vm.warp(block.timestamp + 90 days + 1 hours);
  }

  function testEndAuctionFails() public {
    setUpWithNonMockPool();

    //create auction
    Pool(pool).startAuction();
    auction = Auction(Pool(pool).auctions(0));

    //bid
    vm.startPrank(bidder);
    usdc.mint(bidder, 25000000000000000000000 wei);
    usdc.approve(address(auction), 25000000000000000000000 wei);
    uint256 bidIndex = auction.bid(1000000000000, 25000000000000000000000 wei);
    vm.stopPrank();

    vm.warp(block.timestamp + 10 days + 1 hours);
    vm.prank(bidder);
  
    //end auction reverts
    vm.expectRevert(Pool.CallerIsNotAuction.selector);
    auction.endAuction();

    //bidder cannot claim refund
    vm.expectRevert(Auction.AuctionFailed.selector);
    auction.claimRefund(bidIndex);

    vm.stopPrank();
  }
```

### Mitigation

```diff
function transferReserveToAuction(uint256 amount) external virtual {
    (uint256 currentPeriod, ) = bondToken.globalPool();
-   address auctionAddress = auctions[currentPeriod];
+   address auctionAddress = auctions[currentPeriod - 1];
    require(msg.sender == auctionAddress, CallerIsNotAuction());
    
    IERC20(reserveToken).safeTransfer(msg.sender, amount);
  }
```