Silly Violet Squid

High

# function endAuction() always revert in state = State.SUCCEEDED

### Summary

```javascript
 function endAuction() external auctionExpired whenNotPaused {
    if (state != State.BIDDING) revert AuctionAlreadyEnded();

    if (currentCouponAmount < totalBuyCouponAmount) {
      state = State.FAILED_UNDERSOLD;
    } else if (totalSellReserveAmount >= (IERC20(sellReserveToken).balanceOf(pool) * poolSaleLimit) / 100) {
        state = State.FAILED_POOL_SALE_LIMIT;
    } else {
      state = State.SUCCEEDED;
      Pool(pool).transferReserveToAuction(totalSellReserveAmount);
      IERC20(buyCouponToken).safeTransfer(beneficiary, IERC20(buyCouponToken).balanceOf(address(this)));
    }

    emit AuctionEnded(state, totalSellReserveAmount, totalBuyCouponAmount);
  }
```

Once `auctionExpired` is reached, the function [endAuction()](https://github.com/sherlock-audit/2024-12-plaza-finance/blob/main/plaza-evm/src/Auction.sol#L336) should be called. This function internally checks three cases and sets the `state` variable depending on each case. One of these cases allows setting the state variable to [SUCCEEDED,](https://github.com/sherlock-audit/2024-12-plaza-finance/blob/main/plaza-evm/src/Auction.sol#L344) then [transferReserveToAuction](https://github.com/sherlock-audit/2024-12-plaza-finance/blob/main/plaza-evm/src/Auction.sol#L345) and [transfer](https://github.com/sherlock-audit/2024-12-plaza-finance/blob/main/plaza-evm/src/Auction.sol#L346) are called. However, in this case, there is a vulnerability that causes the call to always revert.

### Root Cause

The root cause is that in the contract Pool.sol, when the contract Auction calls [transferReserveToAuction](https://github.com/sherlock-audit/2024-12-plaza-finance/blob/main/plaza-evm/src/Auction.sol#L345) to try to transfer all reserveToken from the pool, there is a [require](https://github.com/sherlock-audit/2024-12-plaza-finance/blob/main/plaza-evm/src/Pool.sol#L580) statement that checks if msg.sender is the address of the auction. 
The problem here is the way to obtain the [currentPeriod](https://github.com/sherlock-audit/2024-12-plaza-finance/blob/main/plaza-evm/src/Pool.sol#L578) in the function transferReserveToAuction and get the address in the mapping [auctions](https://github.com/sherlock-audit/2024-12-plaza-finance/blob/main/plaza-evm/src/Pool.sol#L579) with this currentPeriod

```javascript
function transferReserveToAuction(uint256 amount) external virtual {
    (uint256 currentPeriod, ) = bondToken.globalPool();
    address auctionAddress = auctions[currentPeriod];//@audit this not revert due to address(0)?
    require(msg.sender == auctionAddress, CallerIsNotAuction());
    
    IERC20(reserveToken).safeTransfer(msg.sender, amount);
  }
  
```

This is a problem because whenever the function [startAuction](https://github.com/sherlock-audit/2024-12-plaza-finance/blob/main/plaza-evm/src/Pool.sol#L530) is called to start a new auction, the address of the auction is stored in the mapping with the [currentPeriod.](https://github.com/sherlock-audit/2024-12-plaza-finance/blob/main/plaza-evm/src/Pool.sol#L552) However, then the currentPeriod is [incremented.](https://github.com/sherlock-audit/2024-12-plaza-finance/blob/main/plaza-evm/src/Pool.sol#L567) in the funcion [increaseIndexedAssetPeriod.](https://github.com/sherlock-audit/2024-12-plaza-finance/blob/main/plaza-evm/src/BondToken.sol#L225) Therefore, all calls to obtain the currentPeriod using [(uint256 currentPeriod, ) = bondToken.globalPool();](https://github.com/sherlock-audit/2024-12-plaza-finance/blob/main/plaza-evm/src/Pool.sol#L578) do not correspond with the auction created. As a result, when transferReserveToAuction checks if [msg.sender == auctionAddress](https://github.com/sherlock-audit/2024-12-plaza-finance/blob/main/plaza-evm/src/Pool.sol#L580), it reverts because auctionAddress is address(0)

```javascript
 function startAuction() external whenNotPaused() {
    // Check if distribution period has passed
    require(lastDistribution + distributionPeriod < block.timestamp, DistributionPeriodNotPassed());

    // Check if auction period hasn't passed
    require(lastDistribution + distributionPeriod + auctionPeriod >= block.timestamp, AuctionPeriodPassed());

    // Check if auction for current period has already started
    (uint256 currentPeriod,) = bondToken.globalPool();
    require(auctions[currentPeriod] == address(0), AuctionAlreadyStarted());

    uint8 bondDecimals = bondToken.decimals();
    uint8 sharesDecimals = bondToken.SHARES_DECIMALS();
    uint8 maxDecimals = bondDecimals > sharesDecimals ? bondDecimals : sharesDecimals;

    uint256 normalizedTotalSupply = bondToken.totalSupply().normalizeAmount(bondDecimals, maxDecimals);
    uint256 normalizedShares = sharesPerToken.normalizeAmount(sharesDecimals, maxDecimals);

    // Calculate the coupon amount to distribute
    uint256 couponAmountToDistribute = (normalizedTotalSupply * normalizedShares)
        .toBaseUnit(maxDecimals * 2 - IERC20(couponToken).safeDecimals());

    auctions[currentPeriod] = Utils.deploy(
      address(new Auction()),
      abi.encodeWithSelector(
        Auction.initialize.selector,
        address(couponToken),
        address(reserveToken),
        couponAmountToDistribute,
        block.timestamp + auctionPeriod,
        1000,
        address(this),
        poolSaleLimit
      )
    );

    // Increase the bond token period
    bondToken.increaseIndexedAssetPeriod(sharesPerToken);

    // Update last distribution time
    lastDistribution = block.timestamp;
  }
```




### Internal Pre-conditions

1- startAuction is call
2- currentPeriod is increment
3- auction auctionExpired
4- call endAuction 
5- revert

### External Pre-conditions

_No response_

### Attack Path

_No response_

### Impact

This breaks the core of the auction because it does not allow the auction to be completed, which is detrimental to the protocol

### PoC

run this test in auction.t.sol 

```javascript
function testStartAction() public {

    vm.startPrank(governance);
    Pool(pool).setAuctionPeriod(10 days);
    vm.stopPrank();
  
    vm.warp(block.timestamp + 95 days);
     Pool(pool).startAuction();
    (uint256 currentPeriod,) = Pool(pool).bondToken().globalPool();
     address auctionAddress = Pool(pool).auctions(currentPeriod-1);
     address auctionCurrentPeriod = Pool(pool).auctions(currentPeriod);

    console.log("auction CurrentPeriod:",auctionCurrentPeriod);
    console.log("auction previusPeriod:",auctionAddress);

    weth.mint(address(pool), 111111111111.1111 ether);
    vm.startPrank(bidder);
    usdc.mint(bidder, Auction(auctionAddress).totalBuyCouponAmount());
    usdc.approve(address(auctionAddress), type(uint256).max);
    Auction(auctionAddress).bid(100000000000 ether, Auction(auctionAddress).totalBuyCouponAmount());
    vm.stopPrank();
    vm.warp(block.timestamp + 15 days);
    //vm.prank(pool);
    Auction(auctionAddress).endAuction();

  }
```

the result 

```javascript
Ran 1 test for test/Auction.t.sol:AuctionTest
[FAIL: CallerIsNotAuction()] testStartAction() (gas: 2399639)
Logs:
  auction CurrentPeriod: 0x0000000000000000000000000000000000000000
  auction previusPeriod: 0x7576e4A0848577a952E1540AB79CF884C44b3a36
  
Suite result: FAILED. 0 passed; 1 failed; 0 skipped; finished in 2.00s (1.28s CPU time)


Suite result: FAILED. 0 passed; 1 failed; 0 skipped; finished in 2.00s (1.28s CPU time)

Ran 1 test suite in 3.17s (2.00s CPU time): 0 tests passed, 1 failed, 0 skipped (1 total tests)

Ran 1 test suite in 3.17s (2.00s CPU time): 0 tests passed, 1 failed, 0 skipped (1 total tests)


Failing tests:
Encountered 1 failing test in test/Auction.t.sol:AuctionTest
Encountered 1 failing test in test/Auction.t.sol:AuctionTest
[FAIL: CallerIsNotAuction()] testStartAction() (gas: 2399639)

Encountered a total of 1 failing tests, 0 tests succeeded
```

### Mitigation

shoud modifier this

```diff

function transferReserveToAuction(uint256 amount) external virtual {
      (uint256 currentPeriod, ) = bondToken.globalPool();
-      address auctionAddress = auctions[currentPeriod];
+      address auctionAddress = auctions[currentPeriod - 1];
       require(msg.sender == auctionAddress, CallerIsNotAuction());
       IERC20(reserveToken).safeTransfer(msg.sender, amount);
  }

```