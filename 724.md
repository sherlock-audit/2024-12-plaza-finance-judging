Creamy Misty Rooster

Medium

# `setFee` Fails When Previous Fee is Zero, Leading to unfair Fee collection

### Summary

The `setFee` function is designed to allow governance to update the pool fee. To prevent retroactive application of a higher fee on past deposits, it forces a call to `claimFees` before updating the fee. 
But if the fee was previously set to zero, the `getFeeAmount` function will always return zero, and `claimFees` will not be called. This causes the `lastFeeClaimTime` variable to remain outdated. 
As a result, when fees are later claimed, they will incorrectly include the period when the fee was zero, leading to improper fee collection from users' deposits.  

**NOTE:This issue can occur during normal protocol operation and is not relying on governance being trusted or not.**  

### Root Cause

The check in `setFee` to prevent retroactive fee application relies on `getFeeAmount > 0`. However, when the fee is zero, `getFeeAmount` always returns zero, bypassing the `claimFees` call. This leaves `lastFeeClaimTime` outdated, leading to incorrect fee calculations when fees are later claimed.  

### Internal Pre-conditions

1. The fee was previously set to `0`.  
2. A new fee is set using `setFee`.  

### External Pre-conditions

No external conditions are required for this issue to occur. 

### Attack Path

Let's illustrate a scenario in which this issue will occur:

#### **Initial State:**  
- Fee: `0%`.  
- Fee remains `0%` for **1 month**.  
- `lastFeeClaimTime = 1737000000`.  
- Reserve token balance: `1,000,000 tokens`.  

#### **Step 1: Update Fee to 5%**
- Timestamp: `1739592000` (30 days later).
- Governance calls `setFee(50000)` to set a new fee of `5%`.
- `getFeeAmount()` returns `0` because the fee is `0%`, so `claimFees` is **skipped**.
- The fee is updated, but `lastFeeClaimTime` is not updated.

**Result:**
- Fee = `5%`.
- `lastFeeClaimTime = 1737000000` (unchanged).  

#### **Step 2: 30 Days Later**
- Timestamp: `1742192000` (30 days after fee update).
- `claimFees` is called.
- `getFeeAmount()` calculates fees as:

```solidity
feeAmount = (1,000,000 * 0.05 * (1742192000 - 1737000000)) / (10^6 * 31536000);
```

- Fee amount: **823 tokens**, which incorrectly includes the 30 days when the fee was `0%`.  

### Impact

- Users are overcharged fees for the period when the fee was `0`.  
- Protocol beneficiaries receive fees that they should not have earned.  

### PoC

The [`setFee`](https://github.com/sherlock-audit/2024-12-plaza-finance/blob/main/plaza-evm/src/Pool.sol#L674-L687) function allow the governance to update the fee value reserved from the protocol, before setting a new fee the function will always try to claim the previous fees accrued and update [`lastFeeClaimTime`](https://github.com/sherlock-audit/2024-12-plaza-finance/blob/main/plaza-evm/src/Pool.sol#L708) which is used in fee calculation to prevent incorrect fee management, where wrong fee value is used for old deposits:  

```solidity
function setFee(uint256 _fee) external onlyRole(poolFactory.GOV_ROLE()) {
    // Fee cannot exceed 10%
    require(_fee <= 100000, FeeTooHigh());

    // Force a fee claim to prevent governance from setting a higher fee
    // and collecting increased fees on old deposits
    if (getFeeAmount() > 0) { //@audit Skipped because getFeeAmount() is 0 when fee == 0
        claimFees();
    }

    uint256 oldFee = fee;
    fee = _fee;
    emit FeeChanged(oldFee, _fee);
}
```  

When the function is called and the current fee is zero then `setFee` will return 0 and thus `claimFees` will not be called:

```solidity
function getFeeAmount() internal view returns (uint256) {
    return (IERC20(reserveToken).balanceOf(address(this)) * fee * (block.timestamp - lastFeeClaimTime)) / (PRECISION * SECONDS_PER_YEAR);
}
```  

Since `claimFees` is not called, `lastFeeClaimTime` is not updated and will keep its old value.

Now that the new fee was set, when `claimFees` is eventually called, it uses the outdated `lastFeeClaimTime`, incorrectly including the period when the fee was `0` in the fee calculation, which means that the edge the protocol was trying to prevent will still occurs where governance is collecting  fees on old deposits (even if it's not by malicious means).

### Mitigation

Update the `setFee` function to ensure `lastFeeClaimTime` is always updated, regardless of the fee amount:  

```solidity
function setFee(uint256 _fee) external onlyRole(poolFactory.GOV_ROLE()) {
    require(_fee <= 100000, FeeTooHigh());
    
    if (getFeeAmount() > 0) {
        claimFees();
    } else {
        lastFeeClaimTime = block.timestamp; //@audit Ensure lastFeeClaimTime is always updated
    }

    uint256 oldFee = fee;
    fee = _fee;
    emit FeeChanged(oldFee, _fee);
}
```  