Quiet Seafoam Carp

High

# Pool contract takes fees from its initial balance not from users balances

### Summary

Incorrect calculation of fees in `Pool.sol` contract leads to taking them from its initial `reserveTokens` balance instead of taking fees from users' balances, this results in users redeeming their `reserveTokens` without losses, while Pool contract loses its reserve funds.

### Root Cause

To be honest I'm not sure exactly where it come from (**but PoC clearly demonstrates this**), but with high probability because of **either** `poolReserve` calculation in [Pool.simulateCreate()](https://github.com/sherlock-audit/2024-12-plaza-finance/blob/main/plaza-evm/src/Pool.sol#L262) and [Pool.simulateRedeem()](https://github.com/sherlock-audit/2024-12-plaza-finance/blob/main/plaza-evm/src/Pool.sol#L422):
```Solidity
  uint256 poolReserves = IERC20(reserveToken).balanceOf(address(this))
                        .normalizeTokenAmount(reserveToken, COMMON_DECIMALS);

  // Calculate and subtract fees from poolReserves
  poolReserves = poolReserves - (poolReserves * fee * (block.timestamp - lastFeeClaimTime)) / (PRECISION * SECONDS_PER_YEAR);
```
or because of decimals mismath between `ethPrice` (has 8 decimals) and `poolReserves` (has 18 decimals)  in `Pool.getCreateAmount()` and `Pool.getRedeemAmount()` :
```Solidity
  /**
   * @dev Calculates the amount of new tokens to create based on the current pool state and oracle price.
   * @param tokenType The type of token to create (BOND or LEVERAGE).
   * @param depositAmount The amount of reserve tokens to deposit.
   * @param bondSupply The current supply of bond tokens.
   * @param levSupply The current supply of leverage tokens.
   * @param poolReserves The current amount of reserve tokens in the pool.
   * @param ethPrice The current ETH price from the oracle.
   * @param oracleDecimals The number of decimals used by the oracle.
   * @return amount of new tokens to create.
   */
  function getCreateAmount(
    TokenType tokenType,
    uint256 depositAmount,
    uint256 bondSupply, 
    uint256 levSupply, 
    uint256 poolReserves, 
    uint256 ethPrice,
    uint8 oracleDecimals) public pure returns(uint256) {
    if (bondSupply == 0) {
      revert ZeroDebtSupply();
    }

    uint256 assetSupply = bondSupply;
    uint256 multiplier = POINT_EIGHT;
    if (tokenType == TokenType.LEVERAGE) {
      multiplier = POINT_TWO;
      assetSupply = levSupply;
    }

    //@audit ethPrice has 8 decimals, poolReserves has 18 decimals
    uint256 tvl = (ethPrice * poolReserves).toBaseUnit(oracleDecimals);
    ...
}

  /**
   * @dev Calculates the amount of reserve tokens to be redeemed for a given amount of bond or leverage tokens.
   * @param tokenType The type of derivative token being redeemed (BOND or LEVERAGE).
   * @param depositAmount The amount of derivative tokens being redeemed.
   * @param bondSupply The total supply of bond tokens.
   * @param levSupply The total supply of leverage tokens.
   * @param poolReserves The total amount of reserve tokens in the pool.
   * @param ethPrice The current ETH price from the oracle.
   * @param oracleDecimals The number of decimals used by the oracle.
   * @param marketRate The current market rate of the bond token.
   * @return amount of reserve tokens to be redeemed.
   */
  function getRedeemAmount(
    TokenType tokenType,
    uint256 depositAmount,
    uint256 bondSupply,
    uint256 levSupply,
    uint256 poolReserves,
    uint256 ethPrice,
    uint8 oracleDecimals,
    uint256 marketRate
  ) public pure returns(uint256) {
    if (bondSupply == 0) {
      revert ZeroDebtSupply();
    }

    //@audit ethPrice has 8 decimals, poolReserves has 18 decimals
    uint256 tvl = (ethPrice * poolReserves).toBaseUnit(oracleDecimals);
    uint256 assetSupply = bondSupply;
    uint256 multiplier = POINT_EIGHT;
    ...
}
```
Later it affects `creationRate` and `redeemRate` in [Pool.getCreateAmount()](https://github.com/sherlock-audit/2024-12-plaza-finance/blob/main/plaza-evm/src/Pool.sol#L306C12-L306C27) and [Pool.getRedeemAmount()](https://github.com/sherlock-audit/2024-12-plaza-finance/blob/main/plaza-evm/src/Pool.sol#L477).
Because of this users' return balances remain the same.

### Internal Pre-conditions

_No response_

### External Pre-conditions

_No response_

### Attack Path

_No response_

### Impact

Instead of taking fees from users, `Pool` contract takes them from its initial balance, while users redeeming their `reserveTokens` without losses.

### PoC

Add this test to `/test/Pool.t.sol`:
```Solidity
function testClaimFeesNotFromUsers() public {
  //setting initial timestamp
  vm.warp(1737486440); //~21 Jan 2025
  setEthPrice(uint256(int256(3000) * int256(CHAINLINK_DECIMAL_PRECISION)));
  vm.startPrank(governance);
  Token rToken = Token(params.reserveToken);

  // Mint reserve tokens
  rToken.mint(governance, 10000001000);
  rToken.approve(address(poolFactory), 10000000000);
  //set max fee to show biggest impact
  params.fee = 100000;
  params.feeBeneficiary = address(0x111);
  // Create pool and approve deposit amount
  Pool _pool = Pool(poolFactory.createPool(params, 10000000000, 10000, 10000, "", "", "", "", false));

  vm.warp(1737547640); //~22 Jan 2025
  setEthPrice(uint256(int256(3000) * int256(CHAINLINK_DECIMAL_PRECISION)));
  //claim fees so lastFeeClaimTime is not zero. 
  //If its zero nothing changes, but this to show that even if fee claimed before create still test will work
  vm.expectEmit();
  //proof fees not zero
  emit Pool.FeeClaimed(params.feeBeneficiary, 1940639);
  _pool.claimFees();
  vm.stopPrank();

  vm.warp(1737634040); //~23 Jan 2025
  setEthPrice(uint256(int256(3000) * int256(CHAINLINK_DECIMAL_PRECISION)));

  address[] memory users = new address[](3);
  users[0] = address(0x100);
  users[1] = address(0x101);
  uint256 rTokenMintAmount = 10000000000;
  uint256 poolBalanceBefore = rToken.balanceOf(address(_pool));
  console.log("pool rToken balance before users deposited and before all fee claims: ", poolBalanceBefore);
  console.log("rTokenMintAmount for users: ", rTokenMintAmount);
  //user0 depositing his rTokens to the pool
  vm.startPrank(users[0]);
  rToken.mint(users[0], rTokenMintAmount);
  rToken.approve(address(_pool), rTokenMintAmount);
  uint256 amount = _pool.create(Pool.TokenType.BOND, rTokenMintAmount, 0);
  console.log("mint amount bond token for user0: ", amount);
  vm.stopPrank();

  //increase time to show that they like real users, not simultaneously adding
  vm.warp(1737720440); //~24 Jan 2025
  setEthPrice(uint256(int256(3000) * int256(CHAINLINK_DECIMAL_PRECISION)));

  //user1 depositing his rTokens to the pool
  vm.startPrank(users[1]);
  rToken.mint(users[1], rTokenMintAmount);
  rToken.approve(address(_pool), rTokenMintAmount);
  uint256 amount2 = _pool.create(Pool.TokenType.BOND, rTokenMintAmount, 0);
  console.log("mint amount bond token for user1: ", amount2);
  vm.stopPrank();

  //increase time to claim fees more
  vm.warp(1737760922); //~25 Jan 2025
  setEthPrice(uint256(int256(3000) * int256(CHAINLINK_DECIMAL_PRECISION)));

  vm.startPrank(governance);
  vm.expectEmit();
  //proof fees not zero
  emit Pool.FeeClaimed(params.feeBeneficiary, 20288071);
  _pool.claimFees();
  vm.stopPrank();

  //increase time to claim fees even more
  vm.warp(1740439322); //~25 Feb 2025
  setEthPrice(uint256(int256(3000) * int256(CHAINLINK_DECIMAL_PRECISION)));

  vm.startPrank(governance);
  vm.expectEmit();
  //proof fees not zero
  emit Pool.FeeClaimed(params.feeBeneficiary, 254605728);
  _pool.claimFees();
  vm.stopPrank();

  //users redeem their rTokens back without losing
  vm.startPrank(users[0]);
  uint256 redeemAmount = _pool.redeem(Pool.TokenType.BOND, amount, 0);
  console.log("user0 rToken amount redeemed after all claims: ", redeemAmount);
  vm.stopPrank();

  vm.startPrank(users[1]);
  uint256 redeemAmount2 = _pool.redeem(Pool.TokenType.BOND, amount2, 0);
  console.log("user1 rToken amount redeemed after all claims: ", redeemAmount2);
  vm.stopPrank();

  //check if rTokens as before depositing
  assertEq(rTokenMintAmount, redeemAmount);
  assertEq(rTokenMintAmount, redeemAmount2);
  //check that pool rToken balance is less than before
  uint256 poolBalanceAfter = rToken.balanceOf(address(_pool));
  assert(poolBalanceAfter < poolBalanceBefore);
  console.log("pool rToken balance after users redeemed and after all fee claims: ", poolBalanceAfter);
}
```
In cmd run this command:
```Shell
forge test -vv --mt testClaimFeesNotFromUsers
```
Output:
```Shell
Ran 1 test for test/Pool.t.sol:PoolTest
[PASS] testClaimFeesNotFromUsers() (gas: 2180932)
Logs:
  pool rToken balance before users deposited and before all fee claims:  9998059361
  rTokenMintAmount for users:  10000000000
  mint amount bond token for user0:  300000000000
  mint amount bond token for user1:  300000000000
  user0 rToken amount redeemed after all claims:  10000000000
  user1 rToken amount redeemed after all claims:  10000000000
  pool rToken balance after users redeemed and after all fee claims:  9723165562

Suite result: ok. 1 passed; 0 failed; 0 skipped; finished in 4.81ms (1.87ms CPU time)
```

### Mitigation

Consider taking fees directly from users `reserveTokens` deposit amount during `Pool.create()`.