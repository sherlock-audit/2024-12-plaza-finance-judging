Sweet Fiery Aardvark

High

# Auction will not be able to pull reserve tokens, due to updating the period after deploying the auction

### Summary

When `Pool::startAuction` is called it deploys an Auction and increases the period.


```solidity

     function startAuction() external whenNotPaused {
        require(lastDistribution + distributionPeriod < block.timestamp, DistributionPeriodNotPassed());
        require(lastDistribution + distributionPeriod + auctionPeriod >= block.timestamp, AuctionPeriodPassed()); //@note - I feel like this is wrong

        (uint256 currentPeriod,) = bondToken.globalPool();
        require(auctions[currentPeriod] == address(0), AuctionAlreadyStarted());

        uint8 bondDecimals = bondToken.decimals(); 
        uint8 sharesDecimals = bondToken.SHARES_DECIMALS(); 
        uint8 maxDecimals = bondDecimals > sharesDecimals ? bondDecimals : sharesDecimals; 

        uint256 normalizedTotalSupply = bondToken.totalSupply().normalizeAmount(bondDecimals, maxDecimals);
        uint256 normalizedShares = sharesPerToken.normalizeAmount(sharesDecimals, maxDecimals); 

        uint256 couponAmountToDistribute =
            (normalizedTotalSupply * normalizedShares).toBaseUnit(maxDecimals * 2 - IERC20(couponToken).safeDecimals());

        auctions[currentPeriod] = Utils.deploy(
            address(new Auction()),
            abi.encodeWithSelector(
                Auction.initialize.selector,
                address(couponToken),
                address(reserveToken),
                couponAmountToDistribute,
                block.timestamp + auctionPeriod,
                1000,
                address(this),
                poolSaleLimit
            )
        );

        // @review - Period is increased after deploying the auction
        bondToken.increaseIndexedAssetPeriod(sharesPerToken); //@audit - This should be increased
        lastDistribution = block.timestamp;
    }

```

So when deploying the first Auction it will be saved for period 0, and then the period will be increased to 1.


However, when the auction is over and [Auction::endAuciton](https://github.com/sherlock-audit/2024-12-plaza-finance/tree/main/plaza-evm/src/Auction.sol#L336-L350) is called it tries to call `Pool::transferReserveToAuction` to pull the reserve tokens for the bidders to claim them.

```solidity
    function transferReserveToAuction(uint256 amount) external virtual {
 
        (uint256 currentPeriod,) = bondToken.globalPool();
     
       //@audit - We are trying to get the auction for the current period, but period is increased after deploying it, which means the auction will not be saved yet
        address auctionAddress = auctions[currentPeriod];
        require(msg.sender == auctionAddress, CallerIsNotAuction());

  
        IERC20(reserveToken).safeTransfer(msg.sender, amount);
    }
```

The function gets the current period and tries to check, if the auction is the actual sender, however, due to increasing the period after the auction is created,  `auctions[currentPeriod]` will always return `address(0)` dissalowing the current auction to pull the reserve tokens.

### Root Cause

In `Pool::startAuction` the period is increased after deploying the auction dissalowing the auction to pull the tokens.

### Internal Pre-conditions

None

### External Pre-conditions

None

### Attack Path

_No response_

### Impact

1. All of the bond holders won't receive any coupon token rewards
2. Since `Auction::endAuction` is unable to be called, the bidders will be unable to refund their tokens, since `Auction::claimRefund` requires an Auction state to be set.

### PoC

_No response_

### Mitigation

Either increase the period before deploying the auction, or check for currentPeriod - 1 in `Pool::transferReserveToAuction`