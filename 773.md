Fit Alabaster Goblin

Medium

# An attacker can leverage pool selection logic in BondOracleAdapter's getPool to manipulate bond prices and DoS redemptions

### Summary

`BondOracleAdapter` will always choose the existing pool with the lowest tick spacing in order to fetch the Bond price. An attacker can leverage this logic to create a pool with the lowest tick spacing, set a malicious price and manipulate the price of Bond tokens to DoS valid redemptions.

### Root Cause

The `BondOracle` adapter will [select the pool to interact with by iterating a set of hardcoded tick spacings](https://github.com/sherlock-audit/2024-12-plaza-finance/blob/ab5bbd7d54042e4444b711a32def5b639fbd63b4/plaza-evm/src/BondOracleAdapter.sol#L122-L125). When a pool with such tick spacing is found in Aerodrome’s pool factory, such pool will be set as the pool to fetch the price from:

```solidity
// File: BondOracleAdapter.sol

function getPool(address tokenA, address tokenB) private view returns (address, uint24, int24) {
    // this only works for Aerodrome, they decided to break compatibility with getPool mapping
    int24[5] memory spacing = [int24(1), int24(50), int24(100), int24(200), int24(2000)];  

    for (uint24 i = 0; i < spacing.length; i++) {
      try ICLFactory(dexFactory).getPool(tokenA, tokenB, spacing[i]) returns (address _pool) {
        if (_pool == address(0)) continue;
        
        // Aerodrome CL specific
        (bool success, bytes memory data) = dexFactory.staticcall(abi.encodeWithSignature("tickSpacingToFee(int24)", spacing[i])); 
        if (!success) continue;
        
        return (_pool, abi.decode(data, (uint24)), spacing[i]);

      } catch {}
    }

    revert NoPoolFound();
  }
```

However, this logic can be exploited by an attacker that deploys a pool with the lowest tick spacing possible. This combined with the low observation cardinality in newly deployed pool allows the attacker to manipulate bond prices, preventing valid redemptions from actually being executed.

### Internal Pre-conditions

_No response_

### External Pre-conditions

1. The Plaza Finance team has deployed a `PreDeposit` contract for a given `reserveToken`, let’s say WETH. Users have deposited liquidity (including the attacker), that will later allow them to claim bondETH or levETH.
2. When the depositing period in `PreDeposit` ends, the pool is created by permissionlessly calling `PoolDeposit`’s `createPool` function. This will call the pool factory’s `createPool`.
3. When the `PoolFactory`'s `createPool` function is called, there are two crucial observations that need to be made:
    - The address of the Bond Token **can’t be known until the pool has been deployed**: This is because the token is actually deployed inside the `createPool` flow in the `PoolFactory`:
        
        ```solidity
        // File: PoolFactory.sol
        function createPool( 
            PoolParams calldata params,
            uint256 reserveAmount,
            uint256 bondAmount,
            uint256 leverageAmount,
            string memory bondName,
            string memory bondSymbol,
            string memory leverageName,
            string memory leverageSymbol,
            bool pauseOnCreation
          ) external whenNotPaused() onlyRole(POOL_ROLE) returns (address) {
        
            ...
            
            // Deploy Bond token
            BondToken bondToken = BondToken(deployer.deployBondToken( 
              bondBeacon,
              bondName,
              bondSymbol,
              address(this), // minter
              address(this), // governance
              address(this), // poolFactory
              params.sharesPerToken
            ));
            ...
            
         }
        ```
        
        One could argue that even if the Bond Token is deployed when calling `createPool` , it would still be possible to derive the bond token address considering [how contract addresses are derived when deploying a contract](https://docs.openzeppelin.com/cli/2.8/deploying-with-create2#create). However, in this case it is impossible to guarantee a bond token address prior to the bond token’s actual deployment, given that the Pool factory interacts with an external, **permissionless** deployer contract:
        
        ```solidity
        // File: Deployer.sol
        contract Deployer {
          ...
          function deployBondToken(
            address bondBeacon,
            string memory name,
            string memory symbol,
            address minter,
            address governance,
            address poolFactory,
            uint256 sharesPerToken
          ) external returns(address) {
            return address(new BeaconProxy(
              address(bondBeacon),
              abi.encodeCall(
                BondToken.initialize, (name, symbol, minter, governance, poolFactory, sharesPerToken)
              )
            ));
          }
        ```
        
        Because of this, anybody could call `deployBondToken` to increment the Deployer’s nonce and change the expected Bond Token address.
        
    - As a consequence of the previous fact, **the Aerodrome pool can’t be created until the Plaza pool has been deployed and liquidity has been deposited in it,** given that the Bond Token address is not known until the actual pool’s deployment.
        
        
        This two facts force the Plaza admins to create the Aerodrome pool **after the Plaza pool deployment process has ended,** and **** liquidity has actually been deposited into the Plaza pool.

### Attack Path

Considering the external pre-conditions, the following attack can be performed:

1. Immediately after the Plaza pool has been created, the attacker creates the Aerodrome pool for bondToken with fee tier 1. This ensures that this is the pool that will be selected when deploying the `BondOracleAdapter` and `getPool` is called.
2. The Plaza Finance team deploys the bond oracle adapter. They might have deployed and deposited liquidity into another Aerodrome pool prior to deploying the adapter, but as mentioned previously, `getPool` will be called on Bond oracle’s initialization and the pool with tier 1 will be selected, which will make the pool created by the attacker be set as the actual pool to fetch the Bond price from. As per the testnet and mainnet deployment scripts in the project, no post-checks are performed after deployment, which is a strong argument that the incorrect pool being set in the oracle will remain unnoticed to the plaza team).
3. The plaza team then finally sets the `BondOracle` in the `OracleFeeds` contract as feed for bond token.
4. At this point, the protocol has been configured with the pool created by the attacker. The attacker’s pool will have an [observation cardinality](https://uniswapv3book.com/milestone_5/price-oracle.html#observations-and-cardinality) of 1. Essentially, this makes the TWAP of the pool work as a spot oracle, given that only the latest recorded price will be stored in the pool. Observations in the pool will still work even if the `twapInterval` is passed when observing as long as there have been trades more recent than `twapInterval`. 
5. Immediately after the Plaza pool deployment and oracle configurations have been made, a depositor of `PreDeposit` decides to redeem their Bond Tokens in the Plaza Pool after having claimed them in `PreDeposit`. The attacker has previously performed some swaps to manipulate the price stored in the Aerodrome pool. When redeeming, the price of Bond is set as the minimum between the computed redeem rate and the market (aerodrome pool) rate, causing a DoS of the redemption due to the minimum expected redemption amount not being reached:
    
    ```solidity
    // File: Pool.sol
    
    function getRedeemAmount(
            TokenType tokenType,
            uint256 depositAmount,
            uint256 bondSupply,
            uint256 levSupply,
            uint256 poolReserves,
            uint256 ethPrice,
            uint8 oracleDecimals,
            uint256 marketRate
        ) public pure returns (uint256) {
            ...
    
            if (marketRate != 0 && marketRate < redeemRate) {
                redeemRate = marketRate;
            }
            
            // Calculate and return the final redeem amount
            return
                ((depositAmount * redeemRate).fromBaseUnit(oracleDecimals) /
                    ethPrice) / PRECISION;
    		}
    ```

### Impact

The impact of this attack is medium. An attacker can effectively manipulate Aerodrome’s Bond Token pool spot price by performing certain swaps, causing the `marketRate` of bond token to be extremely low (potentially near 0), and forcing users redeem transactions to always revert, even when they should still go through as per the real market price/computed redemption rate. Note that redeeming from the pool must be considered as time-sensitive, given that not being able to redeem at a certain moment might cause a loss of funds for the redeemer, or a loss of potential gains due to potential arbitrage actions performed.

Another impact is that the protocol will be forced to fetch prices from a highly illiquid pool during a certain period of time until more liquidity is added to the attacker’s deployed pool, which will cause a high price impact for swaps, reporting an incorrect market price to the Plaza Pool.

### PoC

_No response_

### Mitigation

Pass the desired fee tier as parameter when deploying the Bond Oracle adapter:

```diff
// File: BondOracleAdapter.sol

/**
   * @dev Initializes the BalancerOracleAdapter.
   * This function is called once during deployment or upgrading to initialize state variables.
   * @param _bondToken Address of the bond token used for the oracle.
   * @param _liquidityToken Address of the liquidity token used for the oracle.
   * @param _twapInterval The time interval for the TWAP calculation.
   * @param _dexFactory Address of the Concentrated Liquidity factory.
   * @param _owner Address of the owner of the contract.
   */
  function initialize(
    address _bondToken,
    address _liquidityToken,
    uint32 _twapInterval,
    address _dexFactory,
-    address _owner
+    address _owner,
+    int24 tickSpacing   
  ) initializer external {
    __Ownable_init(_owner);
    __Pausable_init();
    bondToken = _bondToken;
    liquidityToken = _liquidityToken;
    dexFactory = _dexFactory;
-    (dexPool,,) = getPool(bondToken, liquidityToken);
+    (dexPool,,) = getPool(bondToken, liquidityToken, tickSpacing);

    decimals = IERC20(_bondToken).safeDecimals();
    twapInterval = _twapInterval;
  }
  
  
-  function getPool(address tokenA, address tokenB) private view returns (address, uint24, int24) {
+ function getPool(address tokenA, address tokenB, int24 tickSpacing) private view returns (address, uint24, int24) {
-    // this only works for Aerodrome, they decided to break compatibility with getPool mapping
-    int24[5] memory spacing = [int24(1), int24(50), int24(100), int24(200), int24(2000)];  // @audit-issue [HIGH-02] - An attacker can leverage pool selection logic in `BondOracleAdapter`'s `getPool` to manipulate bond prices and DoS the protocol 

-    for (uint24 i = 0; i < spacing.length; i++) {
-      try ICLFactory(dexFactory).getPool(tokenA, tokenB, spacing[i]) returns (address _pool) {

-        if (_pool == address(0)) continue;
         
-        // Aerodrome CL specific
-        (bool success, bytes memory data) = dexFactory.staticcall(abi.encodeWithSignature("tickSpacingToFee(int24)", spacing[i])); 
-        if (!success) continue;
        
-        return (_pool, abi.decode(data, (uint24)), spacing[i]);

-      } catch {}
-    }
 
-    revert NoPoolFound();
+    return ICLFactory(dexFactory).getPool(tokenA, tokenB, tickSpacing);
  }
```