Rural Burgundy Hawk

Medium

# `BondEth` holders could end up claiming other users' `couponTokens`

### Summary

After a successful `Auction` the raised `couponTokens` are distributed for `bondEth` holder to claim, through the `Distributor` contract. If an auction is unsuccessful the distribution rolls over with no `couponToken` increase. Because of this and the fact that the [`indexPeriod`](https://github.com/sherlock-audit/2024-12-plaza-finance/blob/14a962c52a8f4731bbe4655a2f6d0d85e144c7c2/plaza-evm/src/Pool.sol#L567) is always increased when an auction starts, if a user does not claim his/her `couponTokens` after a successful auction, they might end up with no shares to claim, if the next auction fails and more people join and start claiming.

https://github.com/sherlock-audit/2024-12-plaza-finance/blob/main/plaza-evm/src/Pool.sol#L589-L614
https://github.com/sherlock-audit/2024-12-plaza-finance/blob/14a962c52a8f4731bbe4655a2f6d0d85e144c7c2/plaza-evm/src/Pool.sol#L567

```solidity
function startAuction() external whenNotPaused() {
    ...SNIP...
    auctions[currentPeriod] = Utils.deploy(
      address(new Auction()),
      abi.encodeWithSelector(
        Auction.initialize.selector,
        address(couponToken),
        address(reserveToken),
        couponAmountToDistribute,
        block.timestamp + auctionPeriod,
        1000,
        address(this),
        poolSaleLimit
      )
    );

    // Increase the bond token period
@>    bondToken.increaseIndexedAssetPeriod(sharesPerToken); // @audit - everyone that joins before the auction start will be accrued couponToken shares, even if the auction fails

    // Update last distribution time
    lastDistribution = block.timestamp;
  }
```

```solidity
function claim() external whenNotPaused nonReentrant {
    BondToken bondToken = Pool(pool).bondToken();
    address couponToken = Pool(pool).couponToken();

    if (address(bondToken) == address(0) || couponToken == address(0)){
      revert UnsupportedPool();
    }

    (uint256 currentPeriod,) = bondToken.globalPool();
    uint256 balance = bondToken.balanceOf(msg.sender);
@>    uint256 shares = bondToken.getIndexedUserAmount(msg.sender, balance, currentPeriod)
                              .normalizeAmount(bondToken.decimals(), IERC20(couponToken).safeDecimals()); // @audit - shares will be calculated regardless of whether a bondEth holder had a successful auction

    if (IERC20(couponToken).balanceOf(address(this)) < shares) {
      revert NotEnoughSharesBalance();
    }
    
    // check if pool has enough *allocated* shares to distribute
    if (couponAmountToDistribute < shares) {
      revert NotEnoughSharesToDistribute();
    }

    // check if the distributor has enough shares tokens as the amount to distribute
    if (IERC20(couponToken).balanceOf(address(this)) < couponAmountToDistribute) {
      revert NotEnoughSharesToDistribute();
    }

    couponAmountToDistribute -= shares;    
    bondToken.resetIndexedUserAssets(msg.sender);
    IERC20(couponToken).safeTransfer(msg.sender, shares);
    
    emit ClaimedShares(msg.sender, currentPeriod, shares);
  }
```

### Root Cause

Users who held `bondEth` without any successful auctions can claim coupon tokens that were accrued from past successful auctions because the `IndexedAssetPeriod` is always increased when an auction starts.

### Internal Pre-conditions

N/A

### External Pre-conditions

N/A

### Attack Path

1. Alice buys `bondEth` tokens and holds them through one successful auction.
2. She does not claim immediately (i.e. she does not need the tokens now or she just forgets to do so).
3. Bob joins and buys `bondEth`.
4. A new auction starts, but this time it fails.
5. Bob uses `Distributor::claim()` and successfully gets coupon tokens.
6. Alice tries to claim but is unable to do so due to `NotEnoughSharesBalance`

### Impact

`BondEth` holders are unable to claim their respectful `couponTokens` on demand, breaking one of the incentives of the [protocol](https://docs.plaza.finance/protocol-mechanics/coupon-distributions):

> Distributing simply allows the pool to send the total amount of USDC at distribution time, and users may claim their shares at any point thereafter.

### PoC

For this PoC, I am assuming that the issue `Invalid period used in Pool::transferReserveToAuction(...) function leads to DoS of the Auction contract` is fixed.


The following is the set-up:

<details>

<summary>Set-Up</summary>

```solidity
function setUp() public {
        vm.startPrank(deployer);

        // Deploy mock tokens
        balancerPoolToken = new Token("Balancer Pool Token", "balancerPoolToken", false);
        asset1 = new Token("Test Token 1", "TT1", true);
        asset2 = new Token("Test Token 2", "TT2", true);
        address contractDeployer = address(new Deployer());
        address oracleFeeds = address(new OracleFeeds());

        address poolBeacon = address(new UpgradeableBeacon(address(new Pool()), governance));
        address bondBeacon = address(new UpgradeableBeacon(address(new BondToken()), governance));
        address levBeacon = address(new UpgradeableBeacon(address(new LeverageToken()), governance));
        address distributorBeacon = address(new UpgradeableBeacon(address(new Distributor()), governance));

        poolFactory = PoolFactory(
            Utils.deploy(
                address(new PoolFactory()),
                abi.encodeCall(
                    PoolFactory.initialize,
                    (governance, contractDeployer, oracleFeeds, poolBeacon, bondBeacon, levBeacon, distributorBeacon)
                )
            )
        );

        params.fee = 2000;
        params.reserveToken = address(balancerPoolToken);
        params.sharesPerToken = 2_500_000;
        params.distributionPeriod = 90 days;
        params.couponToken = address(new Token("USDC", "USDC", false));

        Token(params.couponToken).setDecimals(6);

        OracleFeeds(oracleFeeds).setPriceFeed(params.reserveToken, address(0), ethPriceFeed, 1000 days);

        // Deploy the mock price feed
        mockPriceFeed = new MockPriceFeed();

        // Use vm.etch to deploy the mock contract at the specific address
        bytes memory bytecode = address(mockPriceFeed).code;
        vm.etch(ethPriceFeed, bytecode);

        // Set oracle price
        mockPriceFeed = MockPriceFeed(ethPriceFeed);
        mockPriceFeed.setMockPrice(3000 * int256(CHAINLINK_DECIMAL_PRECISION), uint8(CHAINLINK_DECIMAL));

        balancerPoolToken.mint(governance, 1000000 ether);
        vm.stopPrank();

        vm.startPrank(governance);
        poolFactory.grantRole(poolFactory.POOL_ROLE(), governance);

        balancerPoolToken.approve(address(poolFactory), 1000000 ether);

        _pool = Pool(poolFactory.createPool(params, 100 ether, 100 ether, 100 ether, "", "", "", "", false));
        distributor = Distributor(poolFactory.distributors(address(_pool)));
        vm.stopPrank();

        vm.startPrank(deployer);

        // Deploy mock contracts
        vault = new MockBalancerVault(balancerPoolToken);
        predeposit = PreDeposit(
            Utils.deploy(
                address(new PreDeposit()),
                abi.encodeCall(
                    PreDeposit.initialize,
                    (
                        params,
                        address(poolFactory),
                        block.timestamp,
                        block.timestamp + 1 hours,
                        30 ether,
                        "Bond ETH",
                        "bondETH",
                        "Leveraged ETH",
                        "levETH"
                    )
                )
            )
        );
        router = new BalancerRouter(address(vault), address(balancerPoolToken));

        // Setup initial token balances
        asset1.mint(user, 1000 ether);
        asset2.mint(user, 1000 ether);

        asset1.mint(user2, 1000 ether);
        asset2.mint(user2, 1000 ether);

        vm.stopPrank();
    }
```
</details>

For the sake of testing, I am using small amounts and only two users so that I can showcase the idea, the same could happen with more users and transactions:

```solidity
 function testUserCantClaim() public {
        deal(params.reserveToken, user, 5 ether);

        vm.startPrank(user);
        IERC20(params.reserveToken).approve(address(_pool), 5 ether);
        _pool.create(Pool.TokenType.BOND, 5 ether, 0);
        vm.stopPrank();

        vm.warp(block.timestamp + 91 days);

        vm.startPrank(governance);
        _pool.setAuctionPeriod(1 days);
        vm.stopPrank();

        _pool.startAuction();

        deal(params.couponToken, user, 625000000); // Adding exact amount to fill up the auction so that we can end it in success

        vm.startPrank(user);
        IERC20(params.couponToken).approve(address(_pool.auctions(0)), 625000000);
        vm.stopPrank();
        Auction auction = Auction(_pool.auctions(0));

        vm.startPrank(user);
        auction.bid(2e9, 625000000);
        vm.stopPrank();

        vm.warp(block.timestamp + 1 days);

        auction.endAuction(); // Successful auction

        _pool.distribute();

        // User does not claim

        deal(params.reserveToken, user2, 5 ether);

        vm.startPrank(user2);
        IERC20(params.reserveToken).approve(address(_pool), 5 ether);

        _pool.create(Pool.TokenType.BOND, 5 ether, 0);

        vm.stopPrank();

        vm.warp(block.timestamp + 92 days);

        vm.startPrank(governance);
        _pool.setAuctionPeriod(3 days);
        vm.stopPrank();

        _pool.startAuction();

        vm.warp(block.timestamp + 4 days);

        Auction auction1 = Auction(_pool.auctions(1));
        
        auction1.endAuction(); // Failed auction

        _pool.distribute(); // Distribution roll over

        vm.prank(user2);
        distributor.claim(); // User2 claims

        vm.prank(user);
        vm.expectRevert(Distributor.NotEnoughSharesBalance.selector);
        distributor.claim();
    }
```

### Mitigation

Think of a way to only allow users to claim `couponTokens` if they have held `bondEth` tokens for at least one successful auction.