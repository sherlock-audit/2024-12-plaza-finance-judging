Mammoth Green Bison

High

# Incorrect period in transferReserveToAuction() will make endAuction() always revert

### Summary

The `transferReserveToAuction()` function in `Pool` uses  `currentPeriod` to access `auctionAddress`, but `currentPeriod` is already incremented by `startAuction()`. This causes the function to look for `auctionAddress` at the wrong period index, leading to `endAuction()` always reverting. Resulting in all deposited auction user funds to be locked forever in the auction .

### Root Cause

To illustrate the issue we would provide the example. 

Lets just say that  `startAuction()` is called and `Auction` is created with `currentPeriod` of `10` .

```solidity
function startAuction() external whenNotPaused() {
	...
	
	(uint256 currentPeriod,) = bondToken.globalPool();
	require(auctions[currentPeriod] == address(0), AuctionAlreadyStarted());
	...
	
	auctions[currentPeriod] = Utils.deploy(
	...
	
	bondToken.increaseIndexedAssetPeriod(sharesPerToken);
	...
}	
```

Firstly `startAuction()` get `currentPeriod` of `10`, creates `Auction` with `currentPeriod` of `10` .
Then `currentPeriod` is increased in `increaseIndexedAssetPeriod()` to `11` .

When `Auction` succeeds and ends, `endAuction()` is called.

```solidity
function endAuction() external auctionExpired whenNotPaused {
	... 
	} else {
	  state = State.SUCCEEDED;
	  Pool(pool).transferReserveToAuction(totalSellReserveAmount);
	  IERC20(buyCouponToken).safeTransfer(beneficiary, IERC20(buyCouponToken).balanceOf(address(this)));
	}
	
	emit AuctionEnded(state, totalSellReserveAmount, totalBuyCouponAmount);

}
```

Now  transferReserveToAuction() is called from current Auction.

```solidity
function transferReserveToAuction(uint256 amount) external virtual {

	(uint256 currentPeriod, ) = bondToken.globalPool();
	address auctionAddress = auctions[currentPeriod];
	require(msg.sender == auctionAddress, CallerIsNotAuction());
	
	IERC20(reserveToken).safeTransfer(msg.sender, amount);
	
}
```
As we can see, `currentPeriod` will be `11`, and right now `auctions[11]` is an empty `Auction`.
Therefore  `transferReserveToAuction()` will fail do to `require` with checks if  `auctionAddress` is  `msg.sender` .

`endAuction()` fails, users can’t withdraw deposited money and every successful `Auction` will drain users deposited funds.

### Impact

If the `Auction` succeeds, all deposited user funds in  `Auction` will be locked forever.
The state of the protocol will be `BIDDING` forever, because  `endAuction()` will fail.
Therefore `claimBid()` and `claimRefund()` won’t work, because you can’t call functions with `auctionFailed()` and  `auctionSucceeded()` modifiers in the `BIDDING` state. 

### PoC

 `endAuction()` is being tested in `testEndAuctionSuccess()` in `Auction.t.sol`, however this issue is not detected because `transferReserveToAuction()` of `MockPool.sol` is used for testing, instead of the real `transferReserveToAuction()` in `Pool.sol`.

The difference in `transferReserveToAuction()` can be seen below:

[`src/Pool.sol`]
```solidity
function transferReserveToAuction(uint256 amount) external virtual {
        (uint256 currentPeriod,) = bondToken.globalPool();
        address auctionAddress = auctions[currentPeriod];
        require(msg.sender == auctionAddress, CallerIsNotAuction());

        IERC20(reserveToken).safeTransfer(msg.sender, amount);
    }
```

[`tests/mocks/MockPool.sol`]
```solidity
function transferReserveToAuction(uint256 amount) external override {
        IERC20(reserveToken).transfer(msg.sender, amount);
    }
```

The `MockPool.sol` contract doesn't have any checks for the matching of  `auctionAddress` and `msg.sender`. Therefore it will always pass instead of reverting because of the mentioned vulnerability. 

### Lines of Concern

[`Auction.sol:endAuction()`]

https://github.com/sherlock-audit/2024-12-plaza-finance/blob/14a962c52a8f4731bbe4655a2f6d0d85e144c7c2/plaza-evm/src/Auction.sol#L336-L350

[`Pool.sol:transferReserveToAuction()`]
https://github.com/sherlock-audit/2024-12-plaza-finance/blob/14a962c52a8f4731bbe4655a2f6d0d85e144c7c2/plaza-evm/src/Pool.sol#L577-L583

### Mitigation

Change the `currentPeriod`  in `transferReserveToAuction()` to `previousPeriod` :
```solidity
uint256 previousPeriod = currentPeriod - 1;
address auctionAddress = auctions[previousPeriod];
```
