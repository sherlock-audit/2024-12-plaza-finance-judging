Wide Pistachio Worm

High

# `Auction::endAuction` would always fail

### Summary

after the auction duration ended and all parameter for successful auction is passed, anyone can call `endAuction` so the reserveToken auctioned can be sent to auction contract.
but this cannot happen because when calling [transferReserveToAuction](https://github.com/sherlock-audit/2024-12-plaza-finance/blob/main/plaza-evm/src/Auction.sol#L345) would fail the checks.

[Pool::transferReserveToAuction](https://github.com/sherlock-audit/2024-12-plaza-finance/blob/main/plaza-evm/src/Pool.sol#L577-L583):
```solidity
  function transferReserveToAuction(uint256 amount) external virtual {
   (uint256 currentPeriod, ) = bondToken.globalPool();
    address auctionAddress = auctions[currentPeriod];
@>  require(msg.sender == auctionAddress, CallerIsNotAuction());
    
    IERC20(reserveToken).safeTransfer(msg.sender, amount);
  }
```

this function would always fails because the check `msg.sender == auctionAddress` would always false.
this is because when calling the `(uint256 currentPeriod, ) = bondToken.globalPool();` line, the currentPeriod is not the auction's (msg.sender's) period.

### Root Cause

the root cause is because in `Pool::startAuction` function when auction is started, the current period is also incremented

[`Pool.sol#L530-L571`](https://github.com/sherlock-audit/2024-12-plaza-finance/blob/main/plaza-evm/src/Pool.sol#L530-L571)
```solidity
  function startAuction() external whenNotPaused() {
    // Check if distribution period has passed
    require(lastDistribution + distributionPeriod < block.timestamp, DistributionPeriodNotPassed());

    // Check if auction period hasn't passed
    require(lastDistribution + distributionPeriod + auctionPeriod >= block.timestamp, AuctionPeriodPassed());

    // Check if auction for current period has already started
    (uint256 currentPeriod,) = bondToken.globalPool();
    require(auctions[currentPeriod] == address(0), AuctionAlreadyStarted());

.
.
.

    auctions[currentPeriod] = Utils.deploy(
      address(new Auction()),
      abi.encodeWithSelector(
        Auction.initialize.selector,
        address(couponToken),
        address(reserveToken),
        couponAmountToDistribute,
        block.timestamp + auctionPeriod,
        1000,
        address(this),
        poolSaleLimit
      )
    );

    // Increase the bond token period
@>  bondToken.increaseIndexedAssetPeriod(sharesPerToken);

    // Update last distribution time
    lastDistribution = block.timestamp;
  }
```
[BondToken.sol#L212-L229](https://github.com/sherlock-audit/2024-12-plaza-finance/blob/main/plaza-evm/src/BondToken.sol#L212-L229)
```solidity
  /**
   * @dev Increases the current period and updates the shares per token.
   * @param sharesPerToken The new number of shares per token
   * @notice Can only be called by addresses with the GOV_ROLE and when the contract is not paused.
   */
  function increaseIndexedAssetPeriod(uint256 sharesPerToken) public onlyRole(DISTRIBUTOR_ROLE) whenNotPaused() {
    globalPool.previousPoolAmounts.push(
      PoolAmount({
        period: globalPool.currentPeriod,
        amount: totalSupply(),
        sharesPerToken: globalPool.sharesPerToken
      })
    );
@>  globalPool.currentPeriod++;
    globalPool.sharesPerToken = sharesPerToken;


    emit IncreasedAssetPeriod(globalPool.currentPeriod, sharesPerToken);
  }
```

at period 0:
no active auction at address(0) for period 0
globalPool.currentPeriod = 0

when `startAuction` called at period 0, the globalPool.currentPeriod would be incremented by 1.
so the status now is:
active auction at address(x) for period 0
globalPool.currentPeriod = 1

then the `endAuction` is called when all parameter met, but the checks prevented it from succeeding because:
[Pool::transferReserveToAuction](https://github.com/sherlock-audit/2024-12-plaza-finance/blob/main/plaza-evm/src/Pool.sol#L577-L583):
```solidity
  function transferReserveToAuction(uint256 amount) external virtual {
   (uint256 currentPeriod, ) = bondToken.globalPool();
    address auctionAddress = auctions[currentPeriod];
@>  require(msg.sender == auctionAddress, CallerIsNotAuction());
    
    IERC20(reserveToken).safeTransfer(msg.sender, amount);
  }
```
the msg.sender is auction at address(x), and the auctions[currentPeriod] is address(0) because the currentPeriod now is 1.

### Internal Pre-conditions

_No response_

### External Pre-conditions

_No response_

### Attack Path

_No response_

### Impact

user funds are stuck in Auction contracts

### PoC

_No response_

### Mitigation

change the `transferReserveToAuction` to check if the msg.sender is auction address from previous period:
```diff
  function transferReserveToAuction(uint256 amount) external virtual {
   (uint256 currentPeriod, ) = bondToken.globalPool();
+   address auctionAddress = auctions[currentPeriod - 1];
-   address auctionAddress = auctions[currentPeriod];
    require(msg.sender == auctionAddress, CallerIsNotAuction());
    
    IERC20(reserveToken).safeTransfer(msg.sender, amount);
  }
```
