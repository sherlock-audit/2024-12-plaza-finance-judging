Shaggy Clay Cobra

Medium

# Market rate can not be used in token redemption flow

### Summary

The inconsistent decimals between market rate and redeem rate can cause the market rate unable to be applied in the redeem flow

### Root Cause

The function [simulateRedeem()](https://github.com/sherlock-audit/2024-12-plaza-finance/blob/main/plaza-evm/src/Pool.sol#L441-L451) fetches market rate from oracle and then normalize to **reserve token oracle price feed**'s decimal
```solidity
    ...
@>    uint8 oracleDecimals = getOracleDecimals(reserveToken, USD);

    uint256 marketRate;
    address feed = OracleFeeds(oracleFeeds).priceFeeds(address(bondToken), USD);
    if (feed != address(0)) {
      marketRate = getOraclePrice(address(bondToken), USD)
        .normalizeAmount(
          getOracleDecimals(address(bondToken), USD), 
@>          oracleDecimals // this is the decimals of the reserve token chainlink feed
        );
    }

    return getRedeemAmount(
      tokenType,
      depositAmount,
      bondSupply,
      levSupply,
      poolReserves,
      getOraclePrice(reserveToken, USD),
      oracleDecimals,
@>      marketRate
    ).normalizeAmount(COMMON_DECIMALS, IERC20(reserveToken).safeDecimals());
```

In the [function `getRedeemAmount()`](https://github.com/sherlock-audit/2024-12-plaza-finance/blob/main/plaza-evm/src/Pool.sol#L477-L525), `redeemRate` is expected to have 6 decimals. 

The problem is that with almost Chainlink price feeds, decimals is `8`. This makes the `marketRate` decimals will be `8` in almost cases. The logics to select the lowest rate between `marketRate` and `redeemRate` will be broken
```solidity
    if (marketRate != 0 && marketRate < redeemRate) {
      redeemRate = marketRate;
    }
```


### Internal Pre-conditions

_No response_

### External Pre-conditions

_No response_

### Attack Path

1. Assume that the collateral level is currently above the threshold -> a Bond token is priced `BOND_TARGET_PRICE = 100`
2. Market rate reported from Oracle is 95
3. An user redeem BondETH with rate = 100, instead of 95

### Impact

- Market rate can be unused within the system
- Redemption can not use the expected rate, such that when market rate is lower than calculated rate

### PoC

Add the test to `Pool.t.sol`:

```solidity
  function testGetRedeemAmount_MarketRate() public {
    // @audit poc: get reedem amount with market rate
    initializeRealisticTestCases();
    
    vm.startPrank(governance);
    Token rToken = Token(params.reserveToken);

    for (uint256 i = 1; i < 2; i++) {
      rToken.mint(governance, calcTestCases[i].TotalUnderlyingAssets);
      rToken.approve(address(poolFactory), calcTestCases[i].TotalUnderlyingAssets);

      // Create salt to create the pool at a different address
      string memory salt = i.toString();

      Pool _pool = Pool(poolFactory.createPool(params, calcTestCases[i].TotalUnderlyingAssets, calcTestCases[i].DebtAssets, calcTestCases[i].LeverageAssets, "", salt, "", "", false));

      uint256 amount = _pool.getRedeemAmount(
        calcTestCases[i].assetType, 
        calcTestCases[i].inAmount, 
        calcTestCases[i].DebtAssets, 
        calcTestCases[i].LeverageAssets, 
        calcTestCases[i].TotalUnderlyingAssets, 
        calcTestCases[i].ethPrice * CHAINLINK_DECIMAL_PRECISION,
        CHAINLINK_DECIMAL,
        95e8
      );
      uint precision = 1e6;
      assertEq(amount * precision * calcTestCases[i].ethPrice / calcTestCases[i].inAmount, 95e8, "market rate should be used");
    }
  }
```

Test failed
```bash
Failing tests:
Encountered 1 failing test in test/Pool.t.sol:PoolTest
[FAIL: market rate should be used: 100000000 != 9500000000] testGetRedeemAmount_MarketRate() (gas: 3063087)
```

### Mitigation

Market rate should be normalized to 6 decimals