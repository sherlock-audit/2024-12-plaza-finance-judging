Fun Ceramic Mustang

Medium

# Fee beneficiary can miss collecting the fees due to the broken calculation

### Summary

The protocol's fee calculation mechanism uses the current pool balance at fee claim time to calculate fees for the entire period since the last claim. This makes the collected fee amount dependent on the timing of  `claimFees()` call. In particular, withdrawn reserves escape fee accrual entirely and cause fee beneficiary to lose significant amount of fees that should have been collected.

### Root Cause

Fees are collected when fee beneficiary or the governance calls [claimFees](https://github.com/sherlock-audit/2024-12-plaza-finance/blob/14a962c52a8f4731bbe4655a2f6d0d85e144c7c2/plaza-evm/src/Pool.sol#L700) function:
```solidity
  function claimFees() public nonReentrant {
    require(msg.sender == feeBeneficiary || poolFactory.hasRole(poolFactory.GOV_ROLE(), msg.sender), NotBeneficiary());
    uint256 feeAmount = getFeeAmount();
    
    if (feeAmount == 0) {
      revert NoFeesToClaim();
    }
    
    lastFeeClaimTime = block.timestamp;
    IERC20(reserveToken).safeTransfer(feeBeneficiary, feeAmount);
    
    emit FeeClaimed(feeBeneficiary, feeAmount);
  }
```

The actual amount is calculated in following way:
```solidity
  function getFeeAmount() internal view returns (uint256) {
    return (IERC20(reserveToken).balanceOf(address(this)) * fee * (block.timestamp - lastFeeClaimTime)) / (PRECISION * SECONDS_PER_YEAR);
  }
```

As seen in the above function, amount to be claimed depends on the time since the last claim, but also on the **current reserves**. This is not good, because withdrawn amounts (reserves which are redeemed) are escaping the accrued fee entirely.

Consider following simplified example - pool has 5% fee defined and is initially empty. After few days Alice and Bob deposit 500 WETH each and receive back bond tokens. Exactly one year later fee beneficiary calls `claimFees()` and collects 5%*1000WETH = 50 WETH. 

However, imagine that Alice and Bob redeemed their bond tokens just before the fee claiming. In that case pool would be emptied out of the WETH and fee beneficiary would claim ~0 fees! This is not good as obviously protocol loses the fees that should have been accrued. Also it encourages fee beeneficiary to try to time their fee claim by guessing when the reserve balance is at the top.

### Internal Pre-conditions

1. Pool has non zero fee configured
2. Multiple deposits and withdrawals must occur between fee claims

### External Pre-conditions

None

### Attack Path

_No response_

### Impact

Fee beneficiary (ie. protocol treasury) can lose significant part of the fees due to the broken fee calculation mechanism which only looks at the current reserve balance. The amount of losses depends on the timing of the `claimFees` call and the amount of withdrawals performed since the last fee claim.

### PoC

Here is full test case with following scenario - pool is created with 1 WETH of reserves. Alice and Bob deposit 500 WETH each. One year later, fee beneficiary claims fees. Amounts claimed are compared for cases:
- no withdrawals before fee claim 
- 1 withdrawal before fee  claim
- both Alice and Bob withdraw before fee claim

```solidity
// SPDX-License-Identifier: UNLICENSED
pragma solidity ^0.8.26;

import "forge-std/Test.sol";

import {Pool} from "../src/Pool.sol";
import {Token} from "./mocks/Token.sol";
import {Utils} from "../src/lib/Utils.sol";
import {BondToken} from "../src/BondToken.sol";
import {PoolFactory} from "../src/PoolFactory.sol";
import {Distributor} from "../src/Distributor.sol";
import {OracleFeeds} from "../src/OracleFeeds.sol";
import {LeverageToken} from "../src/LeverageToken.sol";
import {MockPriceFeed} from "./mocks/MockPriceFeed.sol";
import {Deployer} from "../src/utils/Deployer.sol";
import {UpgradeableBeacon} from "@openzeppelin/contracts/proxy/beacon/UpgradeableBeacon.sol";
import "@openzeppelin/contracts/token/ERC20/ERC20.sol";

import "forge-std/console.sol";

contract PoolTest_FeeCollection is Test {
    address deployer = makeAddr("deployer");
    address feeBeneficiary = makeAddr("feeBeneficiary");
    MockPriceFeed mockPriceFeed;

    // 5% fee
    uint256 fee = 50000;

    function test_FeeCollection() public {
        // create factory
        PoolFactory factory = _createFactory();
        console.log("Factory created");

        // create tokens
        address reserveToken = address(new Token("Wrapped  ETH", " WETH", false));
        address couponToken = address(new Token("USDC", "USDC", false));
        Token(couponToken).setDecimals(6);

        // create pool
        Pool pool = _createPool(factory, reserveToken, couponToken);
        console.log("Pool created");

        // fund alice for deposit
        address alice = makeAddr("alice");
        uint256 deposit = 500e18;
        deal(reserveToken, alice, deposit);

        // Alice deposit
        console.log("Alice deposits");
        vm.startPrank(alice);
        IERC20(reserveToken).approve(address(pool), deposit);
        pool.create({tokenType: Pool.TokenType.BOND, depositAmount: deposit, minAmount: 0});
        vm.stopPrank();

        // Bob deposit
        address bob = makeAddr("bob");
        deal(reserveToken, bob, deposit);
        console.log("Bob deposits");
        vm.startPrank(bob);
        IERC20(reserveToken).approve(address(pool), deposit);
        pool.create({tokenType: Pool.TokenType.BOND, depositAmount: deposit, minAmount: 0});
        vm.stopPrank();

        // 1 year has passed, price unchanged
        vm.warp(block.timestamp + 365 days);
        mockPriceFeed.setMockPrice(3000 * int256(10 ** 8), uint8(8));

        // claim fees
        vm.prank(feeBeneficiary);
        pool.claimFees();

        console.log("Fee beneficiary claimed WETH:", IERC20(reserveToken).balanceOf(feeBeneficiary));
    }

    function _createFactory() internal returns (PoolFactory) {
        vm.startPrank(deployer);

        // create factory
        address oracleFeedsContract = address(new OracleFeeds());
        address governance = makeAddr("governance");
        PoolFactory factory = PoolFactory(
            Utils.deploy(
                address(new PoolFactory()),
                abi.encodeCall(
                    PoolFactory.initialize,
                    (
                        governance,
                        address(new Deployer()),
                        oracleFeedsContract,
                        address(new UpgradeableBeacon(address(new Pool()), deployer)),
                        address(new UpgradeableBeacon(address(new BondToken()), deployer)),
                        address(new UpgradeableBeacon(address(new LeverageToken()), deployer)),
                        address(new UpgradeableBeacon(address(new Distributor()), deployer))
                    )
                )
            )
        );
        vm.stopPrank();

        vm.startPrank(governance);
        factory.grantRole(factory.POOL_ROLE(), deployer);
        vm.stopPrank();

        return factory;
    }

    function _createPool(PoolFactory factory, address reserveToken, address couponToken) internal returns (Pool) {
        vm.startPrank(deployer);
        uint256 reserveAmount = 1e18;
        deal(reserveToken, deployer, reserveAmount);
        IERC20(reserveToken).approve(address(factory), reserveAmount);

        // create pool
        Pool pool = Pool(
            factory.createPool({
                params: PoolFactory.PoolParams({
                    fee: fee,
                    feeBeneficiary: feeBeneficiary,
                    reserveToken: reserveToken,
                    sharesPerToken: 2_500_000,
                    distributionPeriod: 90 days,
                    couponToken: couponToken
                }),
                reserveAmount: reserveAmount,
                bondAmount: 10 ether,
                leverageAmount: 20 ether,
                bondName: "Bond  WETH",
                bondSymbol: "bond WETH",
                leverageName: "Levered  WETH",
                leverageSymbol: "lev WETH",
                pauseOnCreation: false
            })
        );

        // Deploy the mock price feed
        mockPriceFeed = new MockPriceFeed();
        mockPriceFeed.setMockPrice(3000 * int256(10 ** 8), uint8(8));
        OracleFeeds(factory.oracleFeeds()).setPriceFeed(
            address(pool.reserveToken()), address(0), address(mockPriceFeed), 1 days
        );
        vm.stopPrank();

        return pool;
    }
}
```

Run the version without withdrawals:
```solidity
❯ forge test --mt test_FeeCollection -vv

[PASS] test_FeeCollection() (gas: 31368543)
Logs:
  Factory created
  Pool created
  Alice deposits
  Bob deposits
  Fee beneficiary claimed WETH: 50050000000000000000

Suite result: ok. 1 passed; 0 failed; 0 skipped; finished in 7.81ms (6.38ms CPU time)
```

Amount claimed is 5.05 WETH

If we add Alice withdrawal before the claim:
```diff
+      vm.startPrank(alice);
+      uint256 redeem = IERC20(address(pool.bondToken())).balanceOf(alice);
+      IERC20(address(pool.bondToken())).approve(address(pool), redeem);
+      console.log("Alice withdraws");
+      pool.redeem({tokenType: Pool.TokenType.BOND, depositAmount: redeem, minAmount: 0});
+      vm.stopPrank();

        // claim fees
        vm.prank(feeBeneficiary);
        pool.claimFees();
```

And run it:
```solidity
❯ forge test --mt test_FeeCollection -vv

[PASS] test_FeeCollection() (gas: 31520528)
Logs:
  Factory created
  Pool created
  Alice deposits
  Bob deposits
  Alice withdraws
  Fee beneficiary claimed WETH: 33136730500000000000
```

Beneficiary claimed 33.1 WETH

If both Alice and Bob withdraw:
```diff
+        vm.startPrank(alice);
+        uint256 redeem = IERC20(address(pool.bondToken())).balanceOf(alice);
+        IERC20(address(pool.bondToken())).approve(address(pool), redeem);
+        console.log("Alice withdraws");
+        pool.redeem({tokenType: Pool.TokenType.BOND, depositAmount: redeem, minAmount: 0});
+        vm.stopPrank();
+
+        vm.startPrank(bob);
+        uint256 bobRedeem = IERC20(address(pool.bondToken())).balanceOf(bob);
+        IERC20(address(pool.bondToken())).approve(address(pool), bobRedeem);
+        console.log("Bob withdraws");
+        pool.redeem({tokenType: Pool.TokenType.BOND, depositAmount: bobRedeem, minAmount: 0});
+        vm.stopPrank();

        // claim fees
        vm.prank(feeBeneficiary);
        pool.claimFees();

```

And run it:
```solidity
❯ forge test --mt test_FeeCollection -vv

[PASS] test_FeeCollection() (gas: 31670904)
Logs:
  Factory created
  Pool created
  Alice deposits
  Bob deposits
  Alice withdraws
  Bob withdraws
  Fee beneficiary claimed WETH: 1928313764773643168

Suite result: ok. 1 passed; 0 failed; 0 skipped; finished in 10.75ms (7.76ms CPU time)
```

Only 1.92 WETH is collected, compared to ~50 WETH when there was no withdrawals

### Mitigation

Redesign fee collection mechanism. Ie. charge fee on the deposits (not reserves) and put aside the collected fees.