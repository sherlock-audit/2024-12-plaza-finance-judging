Dry Cherry Goose

High

# Wrong handling of current auction variable will prevent auctions from ever succeeding, locking user bids and rewards

https://github.com/sherlock-audit/2024-12-plaza-finance/blob/main/plaza-evm/src/Pool.sol#L577-L583

### Summary



The current auction is fetched by reading the `auctions` mapping of the current period. It will always return `address(0)` because the current period is increased every time a new auction is started.

```solidity
  // Pool.sol

  function startAuction() external whenNotPaused() {
    // [...]

    (uint256 currentPeriod,) = bondToken.globalPool();
    
    // [...]

    // @audit auctions[currentPeriod] was equal to address(0), now equal to the newly created auction address
    auctions[currentPeriod] = Utils.deploy(
      address(new Auction()),
      abi.encodeWithSelector(
        Auction.initialize.selector,
        address(couponToken),
        address(reserveToken),
        couponAmountToDistribute,
        block.timestamp + auctionPeriod,
        1000,
        address(this),
        poolSaleLimit
      )
    );

    // @audit Since we increase bondToken.globalPool().currentPeriod in the same function, 
    //  auctions[currentPeriod] is back to being equal to address(0)
    bondToken.increaseIndexedAssetPeriod(sharesPerToken);

    // [...]
  }

  function transferReserveToAuction(uint256 amount) external virtual {
    (uint256 currentPeriod, ) = bondToken.globalPool();
    // @audit-issue `auctionAddress` is equal to address(0), so the next require will always be false
    address auctionAddress = auctions[currentPeriod];
    require(msg.sender == auctionAddress, CallerIsNotAuction());
    
    IERC20(reserveToken).safeTransfer(msg.sender, amount);
  }
```

As we can see in the previous code, `transferReserveToAuction` will always revert, preventing any auction from ever ending in the `SUCCEEDED` state. Users bid are lost in the auction, and they can't claim their rewards.

### Root Cause

Getting the current auction address using the `currentPeriod` always returns `address(0)`.

### Internal Pre-conditions

_No response_

### External Pre-conditions

_No response_

### Attack Path

1. Creating an auction
2. Having enough bids so the auction would succeed
3. Try to call `Auction::endAuction` => revert

### Impact

As users can't call `Auction::claimBid` or `Auction::claimRefund` (the only functions to receive rewards/bids), they are not able to withdraw their rewards, while they can't get back their coupons either.

### PoC

Add this code in a new file in the `test` folder.
Run with `forge test --mt test_transferReserveToAuction_always_fails -vvv`

```solidity
// SPDX-License-Identifier: GPL-2.0
pragma solidity ^0.8.0;

import "src/PoolFactory.sol";
import {Upgrades} from "@openzeppelin/foundry-upgrades/Upgrades.sol";
import {UpgradeableBeacon} from "@openzeppelin/contracts/proxy/beacon/UpgradeableBeacon.sol";
import {ERC1967Proxy} from "@openzeppelin/contracts/proxy/ERC1967/ERC1967Proxy.sol";
import {Auction} from "src/Auction.sol";
import {console} from "forge-std/console.sol";


import {Test} from "forge-std/Test.sol";

contract PocTest is Test {
    PoolFactory poolFactory;
    Pool pool;
    Token reserveToken;

    // Base Mainnet addresses
    address private couponToken;
    address private constant ethPriceFeed = address(0x71041dddad3595F9CEd3DcCFBe3D1F4b0a16Bb70);

    uint256 private constant distributionPeriod = 7776000; // 3 months in seconds (90 days * 24 hours * 60 minutes * 60 seconds)
    uint256 private constant reserveAmount = 0.001 ether;
    uint256 private constant bondAmount = 0.025 ether;
    uint256 private constant leverageAmount = 0.001 ether;
    uint256 private constant sharesPerToken = 2_500_000;
    uint256 private constant fee = 0;

    function setUp() public {
        reserveToken = new Token("RESERVE", "RESERVE", true);
        couponToken = address(new Token("COUPON", "COUPON", true));
        Token(couponToken).mint(address(this), type(uint128).max);

        address deployerAddress = address(this);
        address contractDeployer = address(new Deployer());
        address mockOracleFeeds = address(new MockOracleFeeds());

        address poolBeacon = address(new UpgradeableBeacon(address(new Pool()), deployerAddress));
        address bondBeacon = address(new UpgradeableBeacon(address(new BondToken()), deployerAddress));
        address levBeacon = address(new UpgradeableBeacon(address(new LeverageToken()), deployerAddress));
        address distributorBeacon = address(new UpgradeableBeacon(address(new Distributor()), deployerAddress));

        poolFactory = PoolFactory(
            address(
                new ERC1967Proxy(
                    address(new PoolFactory()),
                    abi.encodeCall(
                        PoolFactory.initialize,
                        (
                            deployerAddress,
                            contractDeployer,
                            mockOracleFeeds,
                            poolBeacon,
                            bondBeacon,
                            levBeacon,
                            distributorBeacon
                        )
                    )
                )
            )
        );

        poolFactory.grantRole(poolFactory.GOV_ROLE(), address(this));
        poolFactory.grantRole(poolFactory.POOL_ROLE(), address(this));
        poolFactory.grantRole(poolFactory.SECURITY_COUNCIL_ROLE(), address(this));

        Token(reserveToken).mint(address(this), 500000000000000000000000000000);
        Token(reserveToken).approve(address(poolFactory), 500000000000000000000000000000);

        PoolFactory.PoolParams memory params = PoolFactory.PoolParams({
            fee: fee,
            reserveToken: address(reserveToken),
            couponToken: couponToken,
            sharesPerToken: sharesPerToken,
            distributionPeriod: distributionPeriod,
            feeBeneficiary: deployerAddress
        });

        pool = Pool(
            poolFactory.createPool(
                params, reserveAmount, bondAmount, leverageAmount, "Bond ETH", "bondETH", "Levered ETH", "levETH", false
            )
        );

        MockOracleFeeds(mockOracleFeeds).setPriceFeed(params.reserveToken, address(0), ethPriceFeed, 1 days);
        pool.setAuctionPeriod(10 days);
    }

    // forge test --mt test_transferReserveToAuction_always_fails -vvv
    function test_transferReserveToAuction_always_fails() public {
        vm.prank(0x0000000000000000000000000000000000020000);
        uint256 depositAmount = 1e18;

        // Creates some bond tokens
        uint256 minAmount = pool.simulateCreate(Pool.TokenType.BOND, depositAmount);
        IERC20(pool.reserveToken()).approve(address(pool), depositAmount);
        pool.create(Pool.TokenType.BOND, depositAmount, minAmount);

        // You need to set the visibility of `distributionPeriod` to public in Pool
        vm.warp(block.timestamp + distributionPeriod + 1);

        (uint256 currentPeriod,) = pool.bondToken().globalPool();

        // Creates an auction
        vm.prank(0x0000000000000000000000000000000000030000);
        pool.startAuction();

        Auction auction = Auction(pool.auctions(currentPeriod));
        IERC20(auction.buyCouponToken()).approve(address(auction), auction.totalBuyCouponAmount());

        // Bids the max amount users can bid in the auction (to make the auction in the SUCCEEDED state when it ends)
        uint256 maxAmountToBid =
            ((IERC20(auction.sellReserveToken()).balanceOf(address(pool)) * auction.poolSaleLimit()) / 100) - 1;
        auction.bid(maxAmountToBid, auction.totalBuyCouponAmount());

        vm.warp(block.timestamp + auction.endTime() + 1);

        (currentPeriod,) = pool.bondToken().globalPool();

        // The auction cannot end in a SUCCEEDED state
        vm.expectRevert(Pool.CallerIsNotAuction.selector);
        auction.endAuction();

        // This is because the actual auction address doesn't match
        //  with the one in the mapping in Pool::transferReserveToAuction
        assertNotEq(pool.auctions(currentPeriod), address(auction));
        console.log(currentPeriod, pool.auctions(currentPeriod), address(auction));
    }
}

/* Mock contracts */

import {ERC20} from "@openzeppelin/contracts/token/ERC20/ERC20.sol";

contract Token is ERC20 {
    uint8 private tokenDecimals;

    mapping(address => bool) private whitelist;
    bool public restricted;
    address public deployer;

    string private _tokenName;
    string private _tokenSymbol;

    constructor(string memory _nameParam, string memory _symbolParam, bool _restricted)
        ERC20(_nameParam, _symbolParam)
    {
        tokenDecimals = 18;
        _tokenName = _nameParam;
        _tokenSymbol = _symbolParam;
        restricted = _restricted;
        deployer = msg.sender;
        whitelist[deployer] = true;

        // Neeel's address
        whitelist[0x2516115b336E3a5A0790D8B6EfdF5bD8D7d263Dd] = true;
    }

    function mint(address to, uint256 amount) public {
        require(!restricted || whitelist[msg.sender], "Not authorized to mint");
        _mint(to, amount);
    }

    function burn(address account, uint256 amount) public {
        require(!restricted || whitelist[msg.sender], "Not authorized to burn");
        _burn(account, amount);
    }

    function addToWhitelist(address account) public {
        require(whitelist[msg.sender], "Not authorized to add to whitelist");
        whitelist[account] = true;
    }

    function decimals() public view virtual override returns (uint8) {
        return tokenDecimals;
    }

    function setDecimals(uint8 _decimals) external {
        if (totalSupply() > 0) {
            revert("Cannot set decimals after minting");
        }

        tokenDecimals = _decimals;
    }

    function name() public view virtual override returns (string memory) {
        return _tokenName;
    }

    function symbol() public view virtual override returns (string memory) {
        return _tokenSymbol;
    }
}

import "@openzeppelin/contracts/access/AccessControl.sol";
import {AggregatorV3Interface} from "@chainlink/contracts/src/v0.8/shared/interfaces/AggregatorV3Interface.sol";

contract MockOracleFeeds is AccessControl, AggregatorV3Interface {
    bytes32 public constant GOV_ROLE = keccak256("GOV_ROLE");

    constructor() {
        _grantRole(GOV_ROLE, msg.sender);
    }

    function setPriceFeed(address tokenA, address tokenB, address priceFeed, uint256 heartbeat)
        external
        onlyRole(GOV_ROLE)
    {}

    function priceFeeds(address, address) public view returns (address) {
        return address(this);
    }

    function feedHeartbeats(address) public pure returns (uint256) {
        return 1;
    }

    function decimals() external pure override returns (uint8) {
        return 6;
    }

    function description() external pure override returns (string memory) {
        return "desc";
    }

    function version() external pure override returns (uint256) {
        return 1;
    }

    function getRoundData(uint80)
        external
        pure
        override
        returns (uint80 roundId, int256 answer, uint256 startedAt, uint256 updatedAt, uint80 answeredInRound)
    {
        return (1e18, 1e18, 1e18, 1e18, 1e18);
    }

    function latestRoundData()
        external
        pure
        override
        returns (uint80 roundId, int256 answer, uint256 startedAt, uint256 updatedAt, uint80 answeredInRound)
    {
        return (1e18, 1e18, 1e18, 1e18, 1e18);
    }
}
```

The PoC PASS with the following output :

```console
$ forge test --mt test_successful_transferReserveToAuction_always_fails -vvv
[⠰] Compiling...
[⠒] Compiling 1 files with Solc 0.8.27
[⠑] Solc 0.8.27 finished in 15.15s
Compiler run successful!

Ran 1 test for test/pocs.t.sol:PocTest
[PASS] test_successful_transferReserveToAuction_always_fails() (gas: 2393547)
Logs:
  1 0x0000000000000000000000000000000000000000 0x391D1AC749dAB7423B03294C72D8345e4e10d64C

Suite result: ok. 1 passed; 0 failed; 0 skipped; finished in 18.74ms (8.10ms CPU time)

Ran 1 test suite in 30.20ms (18.74ms CPU time): 1 tests passed, 0 failed, 0 skipped (1 total tests)
```

### Mitigation

Get the correct auction address like how it's done in `pool::distribute` :

```solidity
    // [...]

    uint256 previousPeriod = currentPeriod - 1;
    address auctionAddress = auctions[previousPeriod];

    // [...]
```