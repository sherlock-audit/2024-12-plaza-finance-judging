Handsome Mandarin Frog

High

# Pool may lose up to 87% in fees after Auction end

### Summary

The `Pool` contract may lose up to 87% of its claimable fees after an auction ends due to the current implementation of the `claimFees` mechanism.

In order to claim fees, the `Pool:claimFees()` function invokes `Pool:getFeeAmount()` to calculate the amount of fees:

```solidity
    function claimFees() public nonReentrant {
        ...
@>      uint256 feeAmount = getFeeAmount();
        ...
        IERC20(reserveToken).safeTransfer(feeBeneficiary, feeAmount);
        emit FeeClaimed(feeBeneficiary, feeAmount);
    }
```

The `getFeeAmount()` function computes fees as follows:

```solidity
    function getFeeAmount() internal view returns (uint256) {
@>      return (IERC20(reserveToken).balanceOf(address(this)) * fee * (block.timestamp - lastFeeClaimTime))
            / (PRECISION * SECONDS_PER_YEAR);
    }
```

However, when the auction ends, the `Auction:endAuction()` function calls `Pool:transferReserveToAuction()` to transfer `totalSellReserveAmount` (i.e., reserve tokens) from the `Pool` to the `Auction` contract if the auction state is `SUCCEEDED`:

```solidity
    function endAuction() external auctionExpired whenNotPaused {
        if (state != State.BIDDING) revert AuctionAlreadyEnded();

        if (currentCouponAmount < totalBuyCouponAmount) {
            state = State.FAILED_UNDERSOLD;
        } else if (totalSellReserveAmount >= (IERC20(sellReserveToken).balanceOf(pool) * poolSaleLimit) / 100) {
            // @audit this does not takes into account the fees
            state = State.FAILED_POOL_SALE_LIMIT;
        } else {
            state = State.SUCCEEDED;
            Pool(pool).transferReserveToAuction(totalSellReserveAmount);
            // this transfers all the balance in Auction
            IERC20(buyCouponToken).safeTransfer(beneficiary, IERC20(buyCouponToken).balanceOf(address(this)));
        }

        emit AuctionEnded(state, totalSellReserveAmount, totalBuyCouponAmount);
    }
```

The issue arises because:

- `Auction:endAuction()` does not account for the fees claimable in the `Pool` contract.
- `Pool:transferReserveToAuction` does not claim the fees before transferring the `totalSellReserveAmount` to the Auction contract. As a result, a significant portion of the claimable fees may be lost.

```solidity
    function transferReserveToAuction(uint256 amount) external virtual {
        (uint256 currentPeriod,) = bondToken.globalPool();
        address auctionAddress = auctions[currentPeriod];
        require(msg.sender == auctionAddress, CallerIsNotAuction());

        IERC20(reserveToken).safeTransfer(msg.sender, amount);
    }
```

### Root Cause

In [`Auction.sol:139`](https://github.com/sherlock-audit/2024-12-plaza-finance/blob/main/plaza-evm/src/Auction.sol#L341), the `endAuction` function does not account for the claimable fees in its conditions, which may result in excessive reserve tokens being transferred to the auction, significantly decreasing the calculated fee amount in [`getFeeAmount()`](https://github.com/sherlock-audit/2024-12-plaza-finance/blob/main/plaza-evm/src/Pool.sol#L718).

In [`Pool.sol:577`](https://github.com/sherlock-audit/2024-12-plaza-finance/blob/main/plaza-evm/src/Pool.sol#L577), the `transferReserveToAuction` function does not claim the available fees before transferring the `totalSellReserveAmount` to the auction.



### Internal Pre-conditions

1. Fee in `Pool` contract must be greater than zero.

### External Pre-conditions

_No response_

### Attack Path

_No response_

### Impact

This oversight can result in the loss of up to 87% of the fees claimable by the `Pool` contract before the `endAuction` function is executed. This is because the `reserveToken` balance used in `getFeeAmount()` will be reduced by the amount transferred to the auction, significantly decreasing the calculated fee amount.

### PoC

## Proof of Code

This section demonstrates the underlying issue and verifies the impact of losing claimable fees after the auction ends.

1. The `Pool` allows the auction to sell up to 90% of reserveToken by default
   The `Pool` contract sets the `poolSaleLimit` to a minimum of 90% during initialization, as shown below:

```solidity
    uint256 private constant MIN_POOL_SALE_LIMIT = 90; // 90%
```

```solidity
    function initialize(
        ...
    ) public initializer {
        ...
@>      poolSaleLimit = MIN_POOL_SALE_LIMIT;
        ...
    }
```

2. Modify the `getFeeAmount()` function
   To calculate and compare the fees before and after the auction, the `getFeeAmount()` function in the `Pool` contract needs to be changed from `internal` to `public` for testing purposes:

```diff
-   function getFeeAmount() internal view returns (uint256) {
+   function getFeeAmount() public view returns (uint256) {
        return (IERC20(reserveToken).balanceOf(address(this)) * fee * (block.timestamp - lastFeeClaimTime))
            / (PRECISION * SECONDS_PER_YEAR);
    }
```

3. The following test case is added to `Auction.t.sol` to verify that the fees claimable before the auction are significantly greater than the fees claimable after the auction ends:

```solidity
    function test_shiazinho_beforeAuctionFeesGreaterThanAfter() public {
        address[] memory bidders = new address[](10);

        for (uint256 i = 0; i < 10; i++) {
            bidders[i] = address(uint160(i + 11));
        }

        vm.startPrank(governance);
        Pool(pool).setAuctionPeriod(10 days);
        Pool(pool).setFee(5e4);
        vm.stopPrank();

        vm.warp(90 days + 2);
        Pool(pool).startAuction();

        (uint256 currentPeriod,) = Pool(pool).bondToken().globalPool();
        address _auction = Pool(pool).auctions(currentPeriod - 1);

        uint256 totalBuyCouponAmount = Auction(_auction).totalBuyCouponAmount();
        uint256 buyAmount = totalBuyCouponAmount / 10;

        uint256 poolReservers = weth.balanceOf(pool);
        uint256 sellAmount = ((poolReservers * 89) / 100) / 10;

        for (uint256 i = 0; i < 10; i++) {
            vm.startPrank(bidders[i]);
            usdc.mint(bidders[i], buyAmount);
            usdc.approve(address(_auction), buyAmount);
            Auction(_auction).bid(sellAmount, buyAmount);
            vm.stopPrank();
        }

        uint256 beforeAuctionPoolReserves = weth.balanceOf(pool);
        uint256 beforeAuctionFees = Pool(pool).getFeeAmount();

        console2.log("Before Auction Pool Reserves: ", beforeAuctionPoolReserves);
        console2.log("Before Auction Fees: ", beforeAuctionFees);

        vm.warp(block.timestamp + 10 days);
        Auction(_auction).endAuction();

        uint256 afterAuctionPoolReserves = weth.balanceOf(pool);
        uint256 afterAuctionFees = Pool(pool).getFeeAmount();

        console2.log("After Auction Pool Reserves: ", afterAuctionPoolReserves);
        console2.log("After Auction Fees: ", afterAuctionFees);
        console2.log("Diff (Fees): ", beforeAuctionFees - afterAuctionFees);

        uint256 diffInPercentenge = 100 - ((afterAuctionFees * 100) / beforeAuctionFees);
        console2.log("Diff in %: ", diffInPercentenge);

        assertGt(beforeAuctionFees, afterAuctionFees);
    }
```

4. Execute the following command in the terminal to run the test and observe the results:

```bash
    forge test --mt test_shiazinho_beforeAuctionFeesGreaterThanAfter -vv
```

### Mitigation

To address the issue of lost fees after an auction ends, the following mitigation is recommended:

1. Incorporate claimable fees in `endAuction` logic
   Modify the `endAuction()` function to account for the claimable fees when determining whether the `totalSellReserveAmount` exceeds the pool sale limit. This ensures that the calculation considers the reserved fees before transferring reserves to the `Auction` contract. Below is an example implementation:

```diff
    function endAuction() external auctionExpired whenNotPaused {
        ...

+       uint256 claimableFeeAmount = Pool(pool).getFeeAmount();

        if (...) {...}
-       } else if (totalSellReserveAmount >= (IERC20(sellReserveToken).balanceOf(pool) * poolSaleLimit) / 100) {
+       } else if (totalSellReserveAmount >= ((IERC20(sellReserveToken).balanceOf(pool) - claimableFeeAmount) * poolSaleLimit) / 100) {
            state = State.FAILED_POOL_SALE_LIMIT;
        } else {
            ...
        }
        ...
    }
```

> **Note:** The `getFeeAmount()` function must be changed to `public` to allow it to be called by the `Auction` contract.

2. Create an Internal Fee Claiming Function
   Refactor the claimFees logic into an internal function (\_claimFees) within the Pool contract.

```diff
-   function claimFees() public nonReentrant {
+   function claimFees() public {
        require(
            msg.sender == feeBeneficiary || poolFactory.hasRole(poolFactory.GOV_ROLE(), msg.sender), NotBeneficiary()
        );

+       _claimFees();
-       uint256 feeAmount = getFeeAmount();

-       if (feeAmount == 0) {
-           revert NoFeesToClaim();
-       }

-       lastFeeClaimTime = block.timestamp;
-       IERC20(reserveToken).safeTransfer(feeBeneficiary, feeAmount);

-       emit FeeClaimed(feeBeneficiary, feeAmount);
    }

+   function _claimFees() internal nonReentrant {
+       uint256 feeAmount = getFeeAmount();
+
+       if (feeAmount == 0) {
+           revert NoFeesToClaim();
+       }
+
+       lastFeeClaimTime = block.timestamp;
+       IERC20(reserveToken).safeTransfer(feeBeneficiary, feeAmount);
+
+       emit FeeClaimed(feeBeneficiary, feeAmount);
+   }
```

3. Claim Fees Before Transferring Reserves
   Update the `transferReserveToAuction()` function to claim all available fees before transferring the reserves to the `Auction` contract. This ensures that the fees are not inadvertently lost during the transfer. Below is an example implementation:

```diff
    function transferReserveToAuction(uint256 amount) external virtual {
        (uint256 currentPeriod,) = bondToken.globalPool();
        address auctionAddress = auctions[currentPeriod];
        require(msg.sender == auctionAddress, CallerIsNotAuction());

+       _claimFees();

        IERC20(reserveToken).safeTransfer(msg.sender, amount);
    }
```