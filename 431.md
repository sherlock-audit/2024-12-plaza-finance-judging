Soaring Macaroon Tapir

High

# [H-1] Successful auctions can't be ended due to a revert when transfering reserve token from Pool to Auction

### Summary

Due to false logic in fetching the current period in the method that transfers reserve token from a Pool contract to an Auction contract successful auctions will not be able to be finalized.  This means that the state of the Auction is stuck as '**BIDDING**' and bidders funds will be stuck in the Auction contract forever, without them being able to get a refund or claim the reserve token they bid for. 

Also boudETH holders will never be paid coupons which inflicts additional lose of value to protocol users.

### Root Cause

In `Pool.sol:577` we have the following method
https://github.com/sherlock-audit/2024-12-plaza-finance/blob/main/plaza-evm/src/Pool.sol?#L577-L583


This method is called in `Auction.sol:345`:
https://github.com/sherlock-audit/2024-12-plaza-finance/blob/main/plaza-evm/src/Auction.sol#L345

The issues is on the first line of the ```transferReserveToAuction()```  function, where we get the ```currentPeriod```. This logic is incorrect because after we created the Auction from the ```startAuction()``` function located in ```Pool.sol:567``` we called the following method:

https://github.com/sherlock-audit/2024-12-plaza-finance/blob/main/plaza-evm/src/Pool.sol#L567

This method increments the current period of the ```globalPool```:
https://github.com/sherlock-audit/2024-12-plaza-finance/blob/main/plaza-evm/src/BondToken.sol#L217-L229

This in turn means that when we are fetching the ```currentPeriod``` of the ```globalPool``` we are actually fetching a period that dose not have a started auction yet (no record in ```auctions``` mapping) and this will result in our ```auctionAddress``` being the zero address. 
```transferReserveToAuction()``` function will revert on the following line as ```msg.sender```  is different from the zero address.
https://github.com/sherlock-audit/2024-12-plaza-finance/blob/main/plaza-evm/src/Pool.sol#L580

### Internal Pre-conditions

1. Any user starts an auction using the ```startAuction``` function in the ```Pool.sol``` contract
2. The auction expires in a successful state(enough coupon tokens are invested to cover bondETH payouts and the bought reserve tokens does not break the pool stale limit).

### External Pre-conditions

_No response_

### Attack Path

1. Any user starts an auction using the ```startAuction``` function in the ```Pool.sol``` contract
2. We wait for the auction period to expire (```endTime``` of the auction is larger than ```block.timestamp```)
3. We make sure that enough coupon tokens are collected and that the reserve tokens that are bought do not break the stale limit of the pool, this will make sure that the auction should be successful.
4. Ending an auction using the ```endAuction``` method located in ```Auction.sol```  will result in a revert.

### Impact

Users that took part in the biding lose the funds they used for biding as they are stuck inside of the ```Auction``` contract and cannot be refunded. They are also unable to claim the reserve token they are obligated to from the bid.

Holders of bondETH do not get any coupons, to which they are obligated to from holding the asset.

### PoC

The following PoC test can be placed in the ```Auction.t.sol``` file and ran.

```solidity
 function test_PoCAuctionCreatedByPoolRevertOnTransferReserveToAuction () public {
    vm.startPrank(governance);
    //Set the auction period to 10 days
    Pool(pool).setAuctionPeriod(10 days);
    //Set the distribution period to 1 day
    Pool(pool).setDistributionPeriod(1 days);
    vm.stopPrank();
   //We simulate that 2 days are passed so we don't get an exception that the distribution period of a previous auction has not passed yet
    vm.warp(2 days);

    (uint256 currentPeriod,) = Pool(pool).bondToken().globalPool();
    console.log("Current period: %d", currentPeriod);
    console.log("Current period auction address: %s", Pool(pool).auctions(currentPeriod));
    console.log("=======================================================");
    //Start an auction for the current period
    Pool(pool).startAuction();
    console.log("An auction has been started for the current period");
    assert(Pool(pool).auctions(currentPeriod) != address(0));
    console.log("Current period auction address: %s", Pool(pool).auctions(currentPeriod));

    (uint256 newCurrentPeriod,) = Pool(pool).bondToken().globalPool();
    console.log("New Current period: %d", newCurrentPeriod);
    console.log("New current period auction address: %s", Pool(pool).auctions(newCurrentPeriod));
    console.log("=======================================================");

    Auction currentAuction = Auction(Pool(pool).auctions(currentPeriod));

    vm.startPrank(bidder);
    //We mint 25000 ether worth of USDS tokens to a bidder, for testing purposes we want 1 bidder to cover the whole coupon amount requiered by the auction
    usdc.mint(bidder, 25000 ether);
    usdc.approve(address(currentAuction), 25000 ether);


    //This amount is calculated inside of the "startAuction" function and is the amount of coupon tokens required to collect during the auction period
    uint256 couponAmountToBid = 25000 ether;
    //This is the amount of reserve token the bidder wants to get
    uint256 reserveAmountToGet = 100 ether;
    //The bidders bids couponAmountToBid worth of coupon tokens for 100 ether worth of reserve tokens
    currentAuction.bid(reserveAmountToGet, couponAmountToBid);


    //We verify there is a bid in the auction
    assertEq(currentAuction.bidCount(), 1);
    (address bidderAddress, uint256 buyAmount, uint256 sellAmount,,,bool claimed) = currentAuction.bids(1);
    //We verify that the bid is correct and that the bidder has not claimed the reserve token yet
    assertEq(bidderAddress, bidder);
    assertEq(buyAmount, reserveAmountToGet);
    assertEq(sellAmount, couponAmountToBid);
    assertEq(claimed, false);

    vm.stopPrank();

    //We forward the time to the a time when the auction is suppose to be over
    vm.warp(15 days);

    //The bidder tries to end the auction in order to be able to claim the reserve token(or refund the amount he bid if the bid he/she made was too low)
    vm.startPrank(bidder);
    //Ending the auction fails because we are fetcing the wrong address for the current auction when we try to transfer the reserve token from the Pool to the Auction
    vm.expectRevert(Pool.CallerIsNotAuction.selector);
    currentAuction.endAuction();

    //If we try to get a refund we will get a revert as the auction is still stuck in the "bidding" state
    vm.expectRevert(Auction.AuctionFailed.selector);
    currentAuction.claimRefund(1);

    vm.stopPrank();


    console.log("=======================================================");
    console.log("USDC balance of auction contract: %d", usdc.balanceOf(address(currentAuction)));
    console.log("USDC balance of bidder: %d", usdc.balanceOf(bidder));
    console.log("Reserve token balance of auction contract: %d", weth.balanceOf(address(currentAuction)));
    console.log("Reserve token balance of bidder: %d", weth.balanceOf(bidder));
  }
```

Console log output:

```text
  Current period: 0
  Current period auction address: 0x0000000000000000000000000000000000000000
  =======================================================
  An auction has been started for the current period
  Current period auction address: 0x7576e4A0848577a952E1540AB79CF884C44b3a36

  New Current period: 1
  New current period auction address: 0x0000000000000000000000000000000000000000
  =======================================================
  USDC balance of auction contract: 25000000000000000000000
  USDC balance of bidder: 0
  Reserve token balance of auction contract: 0
  Reserve token balance of bidder: 0
```

### Mitigation

Current period could be passed as a varaible to the Auction contract before it is incremented and used to call the transfer function for the reserve token

Or

Similar logic to the one inside of the ```distribute()``` function in the ```Pool.sol``` contract  could be implemented. In this function we get the correct period for the auction by doing:
```solidity
uint256 previousPeriod = currentPeriod - 1;
```
https://github.com/sherlock-audit/2024-12-plaza-finance/blob/main/plaza-evm/src/Pool.sol#L589-L595