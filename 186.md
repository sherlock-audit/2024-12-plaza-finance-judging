Jovial Turquoise Scallop

High

# `endAuction` always reverts

### Summary

When an auction ends by calling `endAuction` function of `Auction` contract, it calls `transferReserveToAuction` function of `Pool` contract to transfer reserve token, but because of incorrect auction address handling in `Pool` contract causes an revert always, thus `endAuction` fails all the time.

### Root Cause

On the line [Pool.sol#L579](https://github.com/sherlock-audit/2024-12-plaza-finance/blob/14a962c52a8f4731bbe4655a2f6d0d85e144c7c2/plaza-evm/src/Pool.sol#L579), the address of current auction contract is incorrectly fetched, because `currentPeriod` is already increased after starting an auction.

### Internal Pre-conditions

An auction is started and fulfilled by bids by users.

### External Pre-conditions

Anyone trigers `endAuction` of `Auction` contract after auction period passes by.

### Attack Path

- Anyone calls `endAuction`.
- `transferReserveToAuction` function of `Pool` contract is called but reverts.
- As a result, auction can't be finished.

### Impact

Because of failure to transfer reserve tokens from `Pool` contract to `Auction` contract, auctions can't be ended, and users' coupon tokens are stuck in the `Auction` contract, causing loss to users.

### PoC

Here's a test case written using Foundry, which can be tested as part of `Pool.t.sol`:

```solidity
  function testAuditAuctionFailure() public {
    Token rToken = Token(params.reserveToken);
    Token usdc = Token(params.couponToken);

    vm.startPrank(governance);
  
    // Mint reserve tokens
    rToken.mint(governance, 10000001000);
    rToken.approve(address(poolFactory), 10000000000);

    // Create pool and approve deposit amount
    Pool _pool = Pool(poolFactory.createPool(params, 10000000000, 10000, 10000, "", "", "", "", false));
    _pool.setDistributionPeriod(90 days);
    _pool.setAuctionPeriod(10 days);

    vm.stopPrank();

    // Pass by distribution period
    vm.warp(block.timestamp + 90 days + 1);
    
    // Start auction
    _pool.startAuction();

    Auction auction = Auction(_pool.auctions(0));

    vm.startPrank(user);

    // Mint USDC to a bidder
    usdc.mint(user, 10e18);
    usdc.approve(address(auction), type(uint256).max);
    auction.bid(1e4, auction.totalBuyCouponAmount());

    vm.stopPrank();

    // End auction
    vm.warp(block.timestamp + 10 days);
    auction.endAuction();
  }
```

When you run the test case, it fails with `CallerIsNotAuction` error.

```bash
forge test --match-test testAuditAuctionFailure -vv

# Output
Ran 1 test for test/Pool.t.sol:PoolTest
[FAIL: CallerIsNotAuction()] testAuditAuctionFailure() (gas: 6459698)
Suite result: FAILED. 0 passed; 1 failed; 0 skipped; finished in 7.49ms (2.68ms CPU time)

Ran 1 test suite in 235.12ms (7.49ms CPU time): 0 tests passed, 1 failed, 0 skipped (1 total tests)

Failing tests:
Encountered 1 failing test in test/Pool.t.sol:PoolTest
[FAIL: CallerIsNotAuction()] testAuditAuctionFailure() (gas: 6459698)

Encountered a total of 1 failing tests, 0 tests succeeded
```

### Mitigation

In `transferReserveToAuction` function, it should fetch the auction address by `currentPeriod - 1`.

```diff
  function transferReserveToAuction(uint256 amount) external virtual {
    (uint256 currentPeriod, ) = bondToken.globalPool();
-   address auctionAddress = auctions[currentPeriod];
+   address auctionAddress = auctions[currentPeriod - 1];
    require(msg.sender == auctionAddress, CallerIsNotAuction());
    
    IERC20(reserveToken).safeTransfer(msg.sender, amount);
  }
```
