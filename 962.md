Joyous Paisley Goblin

Medium

# `reserveAmount` is not validated while creating pool

## Summary

The pre-deposit mechanism allows users to deposit reserve tokens before a pool is created. However, the `reserveAmount` can be either dust (a very small amount) or zero due to two primary reasons: a malicious user exploiting the deposit mechanism or users failing to pre-deposit. This lack of validation during pool creation can lead to the pool becoming insolvent.

## Root Cause

In the [PreDeposit.sol](https://github.com/sherlock-audit/2024-12-plaza-finance/blob/14a962c52a8f4731bbe4655a2f6d0d85e144c7c2/plaza-evm/src/PreDeposit.sol#L118C1-L134C4) `_deposit()` function, there is a check to ensure that `reserveAmount` does not exceed `reserveCap`. However, if a malicious user deposits the maximum `reserveCap`, they can effectively deny other users the opportunity to pre-deposit.

```solidity
  function _deposit(uint256 amount, address onBehalfOf) private checkDepositStarted checkDepositNotEnded {
    if (reserveAmount >= reserveCap) revert DepositCapReached();

    address recipient = onBehalfOf == address(0) ? msg.sender : onBehalfOf;

    // Fill up to cap and add that to reserves
    if (reserveAmount + amount >= reserveCap) {
      amount = reserveCap - reserveAmount;
    }

    balances[recipient] += amount;
    reserveAmount += amount;

    IERC20(params.reserveToken).safeTransferFrom(msg.sender, address(this), amount);

    emit Deposited(recipient, amount);
  }
```

Just before the `depositEndTime`, an attacker can withdraw the remaining dust amount of reserve tokens and invoke `createPool()`. At this point, `reserveAmount` may be very small.

```solidity
  function createPool() external nonReentrant whenNotPaused checkDepositEnded {
    if (reserveAmount == 0) revert NoReserveAmount();
    if (bondAmount == 0 || leverageAmount == 0) revert InvalidBondOrLeverageAmount();
    if (poolCreated) revert PoolAlreadyCreated();
    IERC20(params.reserveToken).approve(address(factory), reserveAmount);
    pool = factory.createPool(params, reserveAmount, bondAmount, leverageAmount, bondName, bondSymbol, leverageName, leverageSymbol, true);

    emit PoolCreated(pool);
    poolCreated = true;
  }
```

The critical issue is that the only validation performed is to ensure that `reserveAmount` is not zero. If `reserveAmount` is very small compared to the fixed amounts of `bondAmount` and `leverageAmount` minted, the pool could become insolvent, leading to financial loss for users who deposit into this pool.

```solidity
  function createPool(
    PoolParams calldata params,
    uint256 reserveAmount,
    uint256 bondAmount,
    uint256 leverageAmount,
    string memory bondName,
    string memory bondSymbol,
    string memory leverageName,
    string memory leverageSymbol,
    bool pauseOnCreation
  ) external whenNotPaused() onlyRole(POOL_ROLE) returns (address) {

    if (reserveAmount == 0) {
      revert ZeroReserveAmount();
    }
    ...

    // Mint seed amounts
    bondToken.mint(msg.sender, bondAmount);
    lToken.mint(msg.sender, leverageAmount);
    ...
  }
```

## Internal Pre-Conditions

The `sharesPerToken` of the pool has been modified.

## External Pre-Conditions


## Attack Path

1. **Exploiting the Pre-Deposit Mechanism:**
    - An attacker creates a `PreDeposit`.
    - They deposit the `reserveCap` amount of reserve tokens with a high gas fee.
    - This action fills the cap, preventing other users from pre-depositing.
    - Just before the `depositEndTime`, the attacker withdraws the remaining dust amount of reserve tokens and invokes `createPool()`.
    - The `createPool()` function only checks that `reserveAmount` is not zero, allowing the fixed amounts of bond and leverage tokens to be minted.
    - If `reserveAmount` is very small compared to the amounts minted, the pool becomes insolvent.

2. **Users Not Pre-Depositing:**
    - Users fail to deposit sufficient reserve tokens, leading to a very small `reserveAmount`.

## Impact

The pool can become insolvent, resulting in financial losses for users who deposit into this pool.

## Mitigation

Implement validation for `reserveAmount` during pool creation. Additionally, mint bond and leverage tokens based on the actual `reserveAmount` to ensure that the pool remains solvent.

### Suggested Validation Logic

```solidity
function createPool(
  PoolParams calldata params,
  uint256 reserveAmount,
  uint256 bondAmount,
  uint256 leverageAmount,
  string memory bondName,
  string memory bondSymbol,
  string memory leverageName,
  string memory leverageSymbol,
  bool pauseOnCreation
) external whenNotPaused() onlyRole(POOL_ROLE) returns (address) {

  if (reserveAmount == 0) {
    revert ZeroReserveAmount();
  }

  // Add validation for minimum reserveAmount
  if (reserveAmount < MINIMUM_RESERVE_AMOUNT) {
    revert InsufficientReserveAmount();
  }

  // Ensure bondAmount and leverageAmount are proportionate to reserveAmount
  if (bondAmount > reserveAmount || leverageAmount > reserveAmount) {
    revert BondOrLeverageAmountTooHigh();
  }

  // Mint seed amounts based on reserveAmount
  bondToken.mint(msg.sender, bondAmount);
  lToken.mint(msg.sender, leverageAmount);
}
```

This approach ensures that the pool is created with a sufficient reserve amount, preventing insolvency and protecting user funds.
