Wide Lava Hedgehog

High

# Users funds will be permanently locked in Auction contract

### Summary

When [`startAuction`](https://github.com/sherlock-audit/2024-12-plaza-finance/blob/14a962c52a8f4731bbe4655a2f6d0d85e144c7c2/plaza-evm/src/Pool.sol#L530C12-L530C24) deploys a new auction contract, it's address is stored inside [`auctions[currentPeriod]`](https://github.com/sherlock-audit/2024-12-plaza-finance/blob/14a962c52a8f4731bbe4655a2f6d0d85e144c7c2/plaza-evm/src/Pool.sol#L552), where currentPeriod's value comes by calling :  
```solidity
function startAuction() external whenNotPaused() {

//...
    
>    (uint256 currentPeriod,) = bondToken.globalPool();

//...
}
```
Then inside the `startAuction` function [`bondToken.increaseIndexedAssetPeriod(sharesPerToken)`](https://github.com/sherlock-audit/2024-12-plaza-finance/blob/14a962c52a8f4731bbe4655a2f6d0d85e144c7c2/plaza-evm/src/Pool.sol#L567) is called, where `curentPeriod` value is incremented.
```solidity
  function increaseIndexedAssetPeriod(uint256 sharesPerToken) public onlyRole(DISTRIBUTOR_ROLE) whenNotPaused() {
    globalPool.previousPoolAmounts.push(
      PoolAmount({
        period: globalPool.currentPeriod,
        amount: totalSupply(),
        sharesPerToken: globalPool.sharesPerToken
      })
    );
>   globalPool.currentPeriod++;
    globalPool.sharesPerToken = sharesPerToken;

    emit IncreasedAssetPeriod(globalPool.currentPeriod, sharesPerToken);
  }

``` 
After the auction period we call `Auction::endAuction()`, and the first two checks pass, so the auction has SUCCEDED, then the [`Pool(pool).transferReserveToAuction(totalSellReserveAmount);`](https://github.com/sherlock-audit/2024-12-plaza-finance/blob/14a962c52a8f4731bbe4655a2f6d0d85e144c7c2/plaza-evm/src/Auction.sol#L345) is called that will transfer reserve tokens to the auction contract for bidders to claim :
 ```solidity
function transferReserveToAuction(uint256 amount) external virtual {
    (uint256 currentPeriod, ) = bondToken.globalPool();
>   address auctionAddress = auctions[currentPeriod];
>   require(msg.sender == auctionAddress, CallerIsNotAuction());
    
    IERC20(reserveToken).safeTransfer(msg.sender, amount);
  }
```
This function will revert due the mismatch  between the `currentPeriod` of the auction and the `currentPeriod` derived from `bondToken.globalPool()`.

This will keep the Auction's state unchanged, (state = State.BIDDING).
Assuming the auction has expired(10 days have passed) and users tries getting a refund using [`claimRefund`](https://github.com/sherlock-audit/2024-12-plaza-finance/blob/14a962c52a8f4731bbe4655a2f6d0d85e144c7c2/plaza-evm/src/Auction.sol#L367C12-L367C23), it will also revert due to this modifier :
```solidity
 modifier auctionFailed() {
    if (state == State.SUCCEEDED || state == State.BIDDING) revert AuctionFailed();
    _;
  }
```


### Root Cause

In `Pool(pool).transferReserveToAuction`, the auction address validation depends on the `currentPeriod` derived from bondToken.globalPool(). The auction's `currentPeriod` is outdated due to the increment caused by `bondToken.increaseIndexedAssetPeriod(sharesPerToken)` during auction creation, resulting in a mismatch.


### Impact

The protocol suffers a High severity impact as users funds are permanently locked in the Auction contract. 

### PoC

Add this test  to /test/Auction.t.sol
```solidity
 function test_incorrectAuctionPeriod() public {
    
    address user1 = makeAddr("user");
    
    // create a pool
    Pool thePool = Pool(createPool(address(weth), address(usdc)));
    

    // assign auction period
    vm.startPrank(governance);
    thePool.setAuctionPeriod(10 days);
    vm.stopPrank();

    //pass the distributionPeriod
    vm.warp(95 days);
    
    // print the auction's currentperiod 
    (uint256 currentPeriod,) = thePool.bondToken().globalPool();
    console.log("currentPeriod before auction starts: ", currentPeriod);

    // start auction
    thePool.startAuction();
    
    // print the next auction's currentperiod 
    (uint256 next_currentPeriod,) = thePool.bondToken().globalPool();
    console.log("currentPeriod after auction starts: ", next_currentPeriod);

    // assign the auction's address 
    address auctionAddress = thePool.auctions(currentPeriod); 
    Auction _auction = Auction(auctionAddress);


    // mint some usdc to user1
    Token usdcToken = Token(thePool.couponToken());
    uint256 amount = 25000000000000000000000 * 2;
    usdcToken.mint(user1, amount);
    vm.startPrank(user1);
    usdcToken.approve(auctionAddress,  amount * 2 );
    
    // bid the totalBuyCouponAmount to trigger "state = State.SUCCEEDED"
    _auction.bid(100 ether, _auction.totalBuyCouponAmount());
    vm.warp(110 days );
    
    // end auction
    vm.expectRevert(Pool.CallerIsNotAuction.selector);
    _auction.endAuction();

    // user tries to get back his USDC
    vm.expectRevert(Auction.AuctionFailed.selector);
    _auction.claimRefund(1);

    vm.stopPrank();
    

  }
```

This is the result , we see clearly the currentPeriod mismatch 
```shell
root@LAPTOP-GJO05O4U:~/sherlock/2024-12-plaza-finance/plaza-evm# forge test --mt test_incorrectAuctionPeriod -vv
[⠢] Compiling...
[⠒] Compiling 1 files with Solc 0.8.26
[⠒] Solc 0.8.26 finished in 24.70s
Compiler run successful!

Ran 1 test for test/Auction.t.sol:AuctionTest
[PASS] test_incorrectAuctionPeriod() (gas: 31037604)
Logs:
  currentPeriod before auction starts:  0
  currentPeriod after auction starts:  1

Suite result: ok. 1 passed; 0 failed; 0 skipped; finished in 118.25ms (9.04ms CPU time)

```

### Mitigation

```diff
function transferReserveToAuction(uint256 amount) external virtual {
    (uint256 currentPeriod, ) = bondToken.globalPool();
-   address auctionAddress = auctions[currentPeriod];
+  address auctionAddress = auctions[currentPeriod - 1];
    require(msg.sender == auctionAddress, CallerIsNotAuction());
    
    IERC20(reserveToken).safeTransfer(msg.sender, amount);
  }
```