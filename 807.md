Suave Vermilion Worm

High

# Balancer LP tokens might be locked inside `BalancerRouter`

### Summary

Not accounting for the deposited tokens from the `BalancerRouter` to the `PreDeposit` contract will lead to tokens being locked, when the `PreDeposit` is overfilled.

### Root Cause

`BalancerRouter` provides a functionality for joining a Balancer pool to retrieve LP tokens and depositing these LP tokens in the `PreDeposit` with a single transaction. 
The `PreDeposit`'s function [_deposit()](https://github.com/sherlock-audit/2024-12-plaza-finance/blob/main/plaza-evm/src/PreDeposit.sol#L118) provides an overfill mechanism. When a user wants to deposit more than the `PreDeposit`'s capacity, the user will deposit just enough to fill the capacity ([ref](https://github.com/sherlock-audit/2024-12-plaza-finance/blob/main/plaza-evm/src/PreDeposit.sol#L123-L126)).

However the `BalancerRouter` function [joinBalancerAndPredeposit()](https://github.com/sherlock-audit/2024-12-plaza-finance/blob/main/plaza-evm/src/BalancerRouter.sol#L23) does not account for that overfill mechanism. So in case of an overfill, the LP tokens will remain in the `BalancerRouter` contract.

### Internal Pre-conditions

1. The `PreDeposit`'s capacity is almost fulfilled.

### External Pre-conditions

_No response_

### Attack Path

1. `PreDeposit` is deployed.
2. Users deposit in the `PreDeposit` without filling the capacity.
3. A user calls `joinBalancerAndPredeposit()`
4. The `PreDeposit` is overfilled.

### Impact

Users will lose funds, because the LP tokens, representing a their position in the Balancer pool, will be locked inside the `BalancerRouter`.

### PoC

```solidity
// SPDX-License-Identifier: UNLICENSED
pragma solidity ^0.8.26;

import "forge-std/Test.sol";
import {Pool} from "src/Pool.sol";
import {Token} from "test/mocks/Token.sol";
import {Utils} from "src/lib/Utils.sol";
import {BalancerRouter} from "src/BalancerRouter.sol";
import {PreDeposit} from "src/PreDeposit.sol";
import {IAsset} from "@balancer/contracts/interfaces/contracts/vault/IVault.sol";
import {Token} from "test/mocks/Token.sol";
import {BondToken} from "src/BondToken.sol";
import {LeverageToken} from "src/LeverageToken.sol";
import {PoolFactory} from "src/PoolFactory.sol";
import {Deployer} from "src/utils/Deployer.sol";
import {Distributor} from "src/Distributor.sol";
import {OracleFeeds} from "src/OracleFeeds.sol";
import {MockPriceFeed} from "test/mocks/MockPriceFeed.sol";
import {MockBalancerVault} from "test/BalancerRouter.t.sol";

contract BalanceRouterPoC is Test {
    Token public balancerPoolToken;
    Token public asset1;
    Token public asset2;

    BalancerRouter public router;
    MockBalancerVault public vault;
    PreDeposit public predeposit;

    address public user = makeAddr("user1");
    address public user2 = makeAddr("user2");
    address public governance = address(0x2);
    address private deployer = address(0x3);
    bytes32 public constant BALANCER_POOL_ID = bytes32(uint256(1));

    function setUp() public {
        vm.startPrank(deployer);

        balancerPoolToken = new Token("Balancer Pool Token", "balancerPoolToken", false);
        vault = new MockBalancerVault(balancerPoolToken);
        asset1 = new Token("Test Token 1", "TT1", false);
        asset2 = new Token("Test Token 2", "TT2", false);

        PoolFactory.PoolParams memory params;
        params.fee = 0;
        params.reserveToken = address(balancerPoolToken);
        params.sharesPerToken = 50 * 10 ** 18;
        params.distributionPeriod = 0;
        params.couponToken = address(new Token("USDC", "USDC", false));
        address poolFactoryAddress = address(0x4); // We don't need deployed factory for this test

        predeposit = PreDeposit(
            Utils.deploy(
                address(new PreDeposit()),
                abi.encodeCall(
                    PreDeposit.initialize,
                    (
                        params,
                        address(poolFactoryAddress),
                        block.timestamp,
                        block.timestamp + 1 hours,
                        1.5 ether,
                        "Bond ETH",
                        "bondETH",
                        "Leveraged ETH",
                        "levETH"
                    )
                )
            )
        );

        router = new BalancerRouter(address(vault), address(balancerPoolToken));
        vm.stopPrank();

        asset1.mint(user, 10 ether);
        asset1.mint(user2, 10 ether);
        asset2.mint(user, 10 ether);
        asset2.mint(user2, 10 ether);
    }
    //3

    function test_AmountsLeftWhenToppingUpPreDeposit() public {
        IAsset[] memory assets = new IAsset[](2);
        assets[0] = IAsset(address(asset1));
        assets[1] = IAsset(address(asset2));

        uint256[] memory maxAmountsIn = new uint256[](2);
        maxAmountsIn[0] = 1 ether;
        maxAmountsIn[1] = 1 ether;
        vm.startPrank(user);
        asset1.approve(address(router), 1 ether);
        asset2.approve(address(router), 1 ether);

        uint256 balancerPoolTokenReceived =
            router.joinBalancerAndPredeposit(BALANCER_POOL_ID, address(predeposit), assets, maxAmountsIn, "");
        vm.stopPrank();

        console.log("--------------------User 1 joined-------------------");
        console.log("PreDeposit reserve token balance: ", balancerPoolToken.balanceOf(address(predeposit)));
        console.log(
            "PreDeposit reserve tokens until cap: ",
            predeposit.reserveCap() - balancerPoolToken.balanceOf(address(predeposit))
        );
        console.log("BalancerRouter reserve token balance: ", balancerPoolToken.balanceOf(address(router)));

        vm.startPrank(user2);
        asset1.approve(address(router), 1 ether);
        asset2.approve(address(router), 1 ether);

        balancerPoolTokenReceived =
            router.joinBalancerAndPredeposit(BALANCER_POOL_ID, address(predeposit), assets, maxAmountsIn, "");
        vm.stopPrank();

        console.log("--------------------User 2 joined-------------------");
        console.log("PreDeposit reserve token balance: ", balancerPoolToken.balanceOf(address(predeposit)));
        console.log(
            "PreDeposit reserve tokens until cap: ",
            predeposit.reserveCap() - balancerPoolToken.balanceOf(address(predeposit))
        );
        console.log("BalancerRouter reserve token balance: ", balancerPoolToken.balanceOf(address(router)));
    }
}
```

Logs:
 ```Logs:
  --------------------User 1 joined-------------------
  PreDeposit reserve token balance:  1000000000000000000
  PreDeposit reserve tokens until cap:  500000000000000000
  BalancerRouter reserve token balance:  0
  --------------------User 2 joined-------------------
  PreDeposit reserve token balance:  1500000000000000000
  PreDeposit reserve tokens until cap:  0
  BalancerRouter reserve token balance:  500000000000000000
```


### Mitigation

Check what amount has been deposited into the `PreDeposit` and return the undeposited LP tokens to the user.