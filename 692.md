Striped Obsidian Ostrich

Medium

# USDC-blacklisted bidder can prevent other bidders from bidding in an auction

### Summary

Due to the USDC mechanics of blacklisting wallet addresses, a Plaza auction bidder can intentionally or unintentionally prevent other bidders from participating in an auction.

### Root Cause

In [auction.bid(...)](https://github.com/sherlock-audit/2024-12-plaza-finance/blob/14a962c52a8f4731bbe4655a2f6d0d85e144c7c2/plaza-evm/src/Auction.sol#L153-L161) function, if the maximum number of bids is exceeded, any excess bids should be refunded. However, if a bidder is blacklisted (intentionally or unintentionally) after their bid has already been processed, it creates a complication: the refund for that bidder may prevent new bidders from entering the auction. This issue arises because refunds are when new bids are submitted.

 `IERC20(buyCouponToken).safeTransfer(currentBid.bidder, amountToRemove);` reverts an thus the whole `auction.bid(...)` call reverts

```solidity
    /**
     * @dev Removes excess bids from the auction.
     */
    function removeExcessBids() internal {
        if (currentCouponAmount <= totalBuyCouponAmount) {
            return;
        }

        uint256 amountToRemove = currentCouponAmount - totalBuyCouponAmount;
        uint256 currentIndex = lowestBidIndex;

        while (currentIndex != 0 && amountToRemove != 0) {
            // Cache the current bid's data into local variables
            Bid storage currentBid = bids[currentIndex];
            uint256 sellCouponAmount = currentBid.sellCouponAmount;
            uint256 prevIndex = currentBid.prevBidIndex;

            if (amountToRemove >= sellCouponAmount) {
                // Subtract the sellAmount from amountToRemove
                amountToRemove -= sellCouponAmount;

                // Remove the bid
                _removeBid(currentIndex);

                // Move to the previous bid (higher price)
                currentIndex = prevIndex;
            } else {
                // Calculate the proportion of sellAmount being removed
                uint256 proportion = (amountToRemove * 1e18) / sellCouponAmount;

                // Reduce the current bid's amounts
                currentBid.sellCouponAmount = sellCouponAmount - amountToRemove;
                currentCouponAmount -= amountToRemove;

                uint256 reserveReduction = ((currentBid.buyReserveAmount * proportion) / 1e18);
                currentBid.buyReserveAmount = currentBid.buyReserveAmount - reserveReduction;
                totalSellReserveAmount -= reserveReduction;

                // Refund the proportional sellAmount
                IERC20(buyCouponToken).safeTransfer(currentBid.bidder, amountToRemove);

                amountToRemove = 0;
                emit BidReduced(
                    currentIndex, currentBid.bidder, currentBid.buyReserveAmount, currentBid.sellCouponAmount
                );
            }
        }
    }


    /**
     * @dev Removes a bid from the linked list.
     * @param bidIndex The index of the bid to remove.
     */
    function _removeBid(uint256 bidIndex) internal {
        Bid storage bidToRemove = bids[bidIndex];
        uint256 nextIndex = bidToRemove.nextBidIndex;
        uint256 prevIndex = bidToRemove.prevBidIndex;

        // Update linked list pointers
        if (prevIndex == 0) {
            // Removing the highest bid
            highestBidIndex = nextIndex;
        } else {
            bids[prevIndex].nextBidIndex = nextIndex;
        }

        if (nextIndex == 0) {
            // Removing the lowest bid
            lowestBidIndex = prevIndex;
        } else {
            bids[nextIndex].prevBidIndex = prevIndex;
        }

        address bidder = bidToRemove.bidder;
        uint256 buyReserveAmount = bidToRemove.buyReserveAmount;
        uint256 sellCouponAmount = bidToRemove.sellCouponAmount;
        currentCouponAmount -= sellCouponAmount;
        totalSellReserveAmount -= buyReserveAmount;

        // Refund the buy tokens for the removed bid
        IERC20(buyCouponToken).safeTransfer(bidder, sellCouponAmount);

        emit BidRemoved(bidIndex, bidder, buyReserveAmount, sellCouponAmount);

        delete bids[bidIndex];
        bidCount--;
    }
```

### Internal Pre-conditions

_No response_

### External Pre-conditions

_No response_

### Attack Path

1. A bidder places a bid
2. At some point before the auction ends, the bidder get blacklisted by USDC (intentionally or unintentionally)
3. New bid continue to be placed and some of the previous bid turn excess and have to be refunded
4. the blacklisted bidder happens to be refunded
5. the incoming bids are failing due to the refunds being reverted because of the blacklisted bidder

### Impact

An auction could potentially be blocked and prevented from compounding enough USDC to allow `bondETH` holder to claim their rewards

### PoC

_No response_

### Mitigation

Instead of automatically refunding bidders, keep a mapping of address => refund amount and prompt bidders claim their refunds on their own

`pull over push` pattern