Fast Concrete Rabbit

Medium

# Paradoxical State in PreDeposit Contract Due to Unvalidated Initialization Time Parameters

### Summary

The PreDeposit contract contains a critical vulnerability where it can be initialized with a `depositStartTime` that is greater than the [depositEndTime] (https://github.com/sherlock-audit/2024-12-plaza-finance/blob/main/plaza-evm/src/PreDeposit.sol#L99) . This creates a paradoxical state where the contract becomes permanently unusable since it is simultaneously "not yet started" and "already ended". This functionality was introduced after the first audit by Zellic Security, introducing a new risk that wasn't present in the originally audited codebase.  

### Root Cause

The Root cause is the lack of validation in the `initialize` function:

```solidity
function initialize(
    PoolFactory.PoolParams memory _params,
    address _factory,
    uint256 _depositStartTime,
    uint256 _depositEndTime,
    uint256 _reserveCap,
    string memory _bondName,
    string memory _bondSymbol,
    string memory _leverageName,
    string memory _leverageSymbol) initializer public {
    if (_params.reserveToken == address(0)) revert InvalidReserveToken();
    __UUPSUpgradeable_init();
    __ReentrancyGuard_init();
    __Ownable_init(msg.sender);
    params = _params;
    depositStartTime = _depositStartTime; // No validation
    depositEndTime = _depositEndTime;     // No validation
    reserveCap = _reserveCap;
    factory = PoolFactory(_factory);
    bondName = _bondName;
    bondSymbol = _bondSymbol;
    leverageName = _leverageName;
    leverageSymbol = _leverageSymbol;
    poolCreated = false;
}
```

### Internal Pre-conditions

_No response_

### External Pre-conditions

_No response_

### Attack Path

_No response_

### Impact

When the contract enters this paradoxical state:

- Users cannot deposit funds because the contract considers deposits not yet started and the modifiers `checkDepositStarted` and `checkDepositNotEnded` create an impossible condition:
```solidity
modifier checkDepositStarted() {
    if (block.timestamp < depositStartTime) revert DepositNotYetStarted();
    _;
}

modifier checkDepositNotEnded() {
    if (block.timestamp >= depositEndTime) revert DepositEnded();
    _;
}
```
When depositStartTime > depositEndTime, these modifiers make every deposit attempt revert.
- Administrative functions become unusable because the contract considers the deposit period ended:
```solidity
function setDepositStartTime(uint256 newDepositStartTime) external onlyOwner {
    if (block.timestamp >= depositStartTime) revert DepositAlreadyStarted();
    if (newDepositStartTime <= depositStartTime) revert DepositStartMustOnlyBeExtended();
    if (newDepositStartTime >= depositEndTime) revert DepositEndMustBeAfterStart();
    depositStartTime = newDepositStartTime;
}
```
- The contract becomes permanently frozen with no recovery mechanism
- The entire predeposit phase must be redeployed, causing significant operational delays and potential loss of user trust




### PoC

The vulnerability can be triggered by initializing the contract with:

```solidity
depositStartTime = currentTime + 2 days;
depositEndTime = currentTime + 1 days;
```

Once in this state, all recovery attempts fail:
- Cannot extend deposit end time (reverts with DepositEnded)
- Cannot move start time earlier (reverts with DepositStartMustOnlyBeExtended)
- Cannot extend start time (reverts with DepositEndMustBeAfterStart)


```solidity
 function test_Paradoxical_Deposit_State() public {
    
    console.log("\n=== Starting Paradoxical State Test ===");
    
    vm.startPrank(governance);
    
    uint256 currentTime = block.timestamp;
    uint256 paradoxEndTime = currentTime + 1 days;
    uint256 paradoxStartTime = currentTime + 2 days;
    
    // Log initial configuration
    console.log("Initial configuration:");
    console.log("- Current time:", currentTime);
    console.log("- End time:", paradoxEndTime);
    console.log("- Start time:", paradoxStartTime);

        PoolFactory.PoolParams memory testParams = PoolFactory.PoolParams({
        fee: 0,
        reserveToken: address(reserveToken),
        couponToken: address(couponToken),
        distributionPeriod: 90 days,
        sharesPerToken: 2 * 10**6,
        feeBeneficiary: address(0)
    });
    
    PreDeposit paradoxPreDeposit = PreDeposit(Utils.deploy(
        address(new PreDeposit()), 
        abi.encodeCall(PreDeposit.initialize, (
            testParams,
            address(poolFactory),
            paradoxStartTime,
            paradoxEndTime,
            RESERVE_CAP,
            "",
            "",
            "",
            ""
        ))
    ));
    vm.stopPrank();

    // Verify the paradoxical initialization
    assertGt(
        paradoxPreDeposit.depositStartTime(),
        paradoxPreDeposit.depositEndTime(),
        "Start time should be greater than end time to demonstrate the vulnerability"
    );

    // Move to demonstrative time
    uint256 testTime = currentTime + 1.5 days;
    vm.warp(testTime);
    
    // Prove the paradoxical state
    bool isBeforeStart = block.timestamp < paradoxPreDeposit.depositStartTime();
    bool isAfterEnd = block.timestamp >= paradoxPreDeposit.depositEndTime();
    assertTrue(isBeforeStart && isAfterEnd, 
        "Contract should be simultaneously before start and after end");

    // Demonstrate all possible recovery attempts
    // 1. Try deposit
    vm.startPrank(user1);
    reserveToken.approve(address(paradoxPreDeposit), DEPOSIT_AMOUNT);
    vm.expectRevert(PreDeposit.DepositNotYetStarted.selector);
    paradoxPreDeposit.deposit(DEPOSIT_AMOUNT);

    // 2. Try administrative fixes
    vm.stopPrank();
    vm.startPrank(governance);
    
    // Try all possible administrative actions
   console.log("\n=== Attempting Recovery Actions ===");

    console.log("\n1. Trying to extend deposit end time...");
    console.log("Current end time:", paradoxPreDeposit.depositEndTime());
    console.log("Attempting to set new end time to:", block.timestamp + 3 days);
    vm.expectRevert(PreDeposit.DepositEnded.selector);
    paradoxPreDeposit.setDepositEndTime(block.timestamp + 3 days); //This will revert
    console.log("-> Failed: Cannot modify end time because deposit period has already ended");

    console.log("\n2. Trying to move start time to current time...");
    console.log("Current start time:", paradoxPreDeposit.depositStartTime());
    console.log("Attempting to set new start time to:", block.timestamp);
    vm.expectRevert(PreDeposit.DepositStartMustOnlyBeExtended.selector);
    paradoxPreDeposit.setDepositStartTime(block.timestamp); // This will revert
    console.log("-> Failed: Start time can only be extended forward, not moved earlier");

    console.log("\n3. Trying to extend start time further...");
    console.log("Current start time:", paradoxPreDeposit.depositStartTime());
    console.log("Attempting to set new start time to:", block.timestamp + 4 days);
    vm.expectRevert(PreDeposit.DepositEndMustBeAfterStart.selector);
    paradoxPreDeposit.setDepositStartTime(block.timestamp + 4 days); //This will revert
    console.log("-> Failed: Cannot extend start time as it must be before end time");

    // Verify contract remains in broken state
    assertEq(
        paradoxPreDeposit.depositStartTime(),
        paradoxStartTime
    );
    assertEq(
        paradoxPreDeposit.depositEndTime(),
        paradoxEndTime
    );

    console.log("\n=== Final State Verification ===");
    console.log("Contract remains in paradoxical state:");
    console.log("- Still before start:", block.timestamp < paradoxPreDeposit.depositStartTime());
    console.log("- Still after end:", block.timestamp >= paradoxPreDeposit.depositEndTime());

  }
```

### Mitigation

Add validation in the initialize function to ensure depositStartTime is less than depositEndTime:

```solidity
function initialize(...) initializer public {
    if (_depositStartTime >= _depositEndTime) revert InvalidDepositTimes();
    // ... rest of the initialization
}
```