Damp Jade Hyena

High

# It's possible to brick `endAuction` and cause all funds to be lost in the Auction contract

### Summary

Under some conditions, it's possible for an attacker to brick a successful auction's conclusion and make all funds in the contract lost.

### Root Cause

In the `Pool` contract, auction is tracked with period in `BondToken`, when a new auction is [created](https://github.com/sherlock-audit/2024-12-plaza-finance/blob/main/plaza-evm/src/Pool.sol#L530C1-L571C4), the period will be incremented:
```solidity
  function startAuction() external whenNotPaused() {
    // Check if distribution period has passed
    require(lastDistribution + distributionPeriod < block.timestamp, DistributionPeriodNotPassed());

    // Check if auction period hasn't passed
    require(lastDistribution + distributionPeriod + auctionPeriod >= block.timestamp, AuctionPeriodPassed());

    // Check if auction for current period has already started
    (uint256 currentPeriod,) = bondToken.globalPool();
    require(auctions[currentPeriod] == address(0), AuctionAlreadyStarted());

    uint8 bondDecimals = bondToken.decimals();
    uint8 sharesDecimals = bondToken.SHARES_DECIMALS();
    uint8 maxDecimals = bondDecimals > sharesDecimals ? bondDecimals : sharesDecimals;

    uint256 normalizedTotalSupply = bondToken.totalSupply().normalizeAmount(bondDecimals, maxDecimals);
    uint256 normalizedShares = sharesPerToken.normalizeAmount(sharesDecimals, maxDecimals);

    // Calculate the coupon amount to distribute
    uint256 couponAmountToDistribute = (normalizedTotalSupply * normalizedShares)
        .toBaseUnit(maxDecimals * 2 - IERC20(couponToken).safeDecimals());

    auctions[currentPeriod] = Utils.deploy(
      address(new Auction()),
      abi.encodeWithSelector(
        Auction.initialize.selector,
        address(couponToken),
        address(reserveToken),
        couponAmountToDistribute,
        block.timestamp + auctionPeriod,
        1000,
        address(this),
        poolSaleLimit
      )
    );

    // Increase the bond token period
    bondToken.increaseIndexedAssetPeriod(sharesPerToken);

    // Update last distribution time
    lastDistribution = block.timestamp;
  }
```

In `Auction::endAuction`, when bids are enough, anyone can end auction by calling `endAuction`, and for a successful auction:
```solidity
    } else {
      state = State.SUCCEEDED;
      Pool(pool).transferReserveToAuction(totalSellReserveAmount);
      IERC20(buyCouponToken).safeTransfer(beneficiary, IERC20(buyCouponToken).balanceOf(address(this)));
    }
```

And in `transferReserveToAuction`:
```solidity
  function transferReserveToAuction(uint256 amount) external virtual {
    (uint256 currentPeriod, ) = bondToken.globalPool();
    address auctionAddress = auctions[currentPeriod];
    require(msg.sender == auctionAddress, CallerIsNotAuction());
    
    IERC20(reserveToken).safeTransfer(msg.sender, amount);
  }
```

We see `bondToken.globalPool` will be fetched for current auction. While the current implementation itself is quite problematic, but we can ignore this first, and assume it works properly. Looking back in `Pool::startAuction`, we see the contract doesn't really check if the previous auction has concluded or not, as long as `auctionPeriod` has passed, a new auction can be created. This creates a window which the new auction is created, but the previous auction has not ended yet. So here, the `currentPeriod` will point this new auction, and the old auction can never be ended, leaving all the bids and prizes to be locked.

### Internal Pre-conditions

1. An auction is created from pool.

### External Pre-conditions

_No response_

### Attack Path

1. After some time, the auction has finished and can be ended
2. Before the `endAuction` call, a malicious user calls `startAuction` again
3. `BondToken.globalPool` will be advanced to new period, old auction's call to `transferReserveToAuction` will fail as `msg.sender` is not from the current period's auction contract address.
4. The old auction cannot be ended, and all funds are locked.

### Impact

All funds in the old auction contract will be locked.

### PoC

_No response_

### Mitigation

In `startAuction` also check the pervious auction has ended, if not, reverts until the pervious auction has concluded.