Rural Burgundy Hawk

High

# Stuck funds in `BalancerRouter` when user exceeds `PreDeposit` deposit cap

### Summary

The `Plaza` protocol offers users the functionality to buy Balancer LP tokens and then directly enter the Plaza pool, all in one transaction, using the `BalancerRouter`. This contract also allows users to get LP tokens and enter the `PreDeposit` contract with them, securing plaza tokens upon pool creation. The `PreDeposit` contract has the functionality to only transfer the required LP tokens to meet the deposit cap, which when used in the `BalancerRouter` context could lead to issues. When a user uses the `BalancerRouter::joinBalancerAndPredeposit(...)`, he/she could end up in a situation where the provided LP tokens to the `PreDeposit::deposit()` are more than what is required to meet the cap, any of the excess ones will be left stuck in the `BalancerRouter` contract as the Balancer vault will have already provided the full amount.

https://github.com/sherlock-audit/2024-12-plaza-finance/blob/main/plaza-evm/src/BalancerRouter.sol#L23-L40
```solidity
function joinBalancerAndPredeposit(
        bytes32 balancerPoolId,
        address _predeposit,
        IAsset[] memory assets,
        uint256[] memory maxAmountsIn,
        bytes memory userData
    ) external nonReentrant returns (uint256) {
        // Step 1: Join Balancer Pool
@>        uint256 balancerPoolTokenReceived = joinBalancerPool(balancerPoolId, assets, maxAmountsIn, userData); // @audit - user gets required LP tokens for maxAmountsIn

        // Step 2: Approve balancerPoolToken for PreDeposit
        balancerPoolToken.safeIncreaseAllowance(_predeposit, balancerPoolTokenReceived);

        // Step 3: Deposit to PreDeposit
@>        PreDeposit(_predeposit).deposit(balancerPoolTokenReceived, msg.sender); // @audit - if balancerPoolTokenReceived exceeds the depositCap, any excess will not be utilized but will be left stuck in Router

        return balancerPoolTokenReceived;
    }
```

https://github.com/sherlock-audit/2024-12-plaza-finance/blob/main/plaza-evm/src/PreDeposit.sol#L124-L126
```solidity
function _deposit(uint256 amount, address onBehalfOf) private checkDepositStarted checkDepositNotEnded {
    if (reserveAmount >= reserveCap) revert DepositCapReached();

    address recipient = onBehalfOf == address(0) ? msg.sender : onBehalfOf;

    // if user would like to put more than available in cap, fill the rest up to cap and add that to reserves
    if (reserveAmount + amount >= reserveCap) {
@>      amount = reserveCap - reserveAmount; // @audit - if this happens in the Router, funds will be stuck
    }

    balances[recipient] += amount;
    reserveAmount += amount;

    IERC20(params.reserveToken).safeTransferFrom(msg.sender, address(this), amount);

    emit Deposited(recipient, amount);
  }
```

### Root Cause

Whenever the user deposits funds through the `BalancerRouter` that exceed the reserve cap of the `PreDeposit`, only the amount needed to meet the reserve cap will be utilized, leaving the rest stuck in the `BalancerRouter.

### Internal Pre-conditions

1. `PreDeposit` to be close to the `reserveCap`.

### External Pre-conditions

N/A

### Attack Path

1. `reserveAmount` in PreDeposit is 0, `reserveCap` is `30e18`.
2. Alice deposits through `BalancerRouter::joinBalancerAndPredeposit(...)` 20e18.
3. `reserveAmount` in PreDeposit is `20e18`, reserve cap is `30e18.`
4. Bob deposits through `BalancerRouter` 20e18.
5. Bob's transaction fills up the rest of the deposit cap, but the extra `10e18` are left stuck in the Router.

### Impact

The user's Balancer LP tokens can end up getting stuck in the `BalancerRouter`.

### PoC

For this PoC, I am using the `MockBalancerVault` provided in the `BalancerRouterTest`, and have changed it as follows:

<details>

<summary>Balancer Vault Set-up</summary>

```solidity
contract MockBalancerVault {
    Token public balancerPoolToken;
    mapping(address => uint256) public joinAmounts;

    struct JoinPoolRequest {
        IAsset[] assets;
        uint256[] maxAmountsIn;
        bytes userData;
        bool fromInternalBalance;
    }

    struct ExitPoolRequest {
        IAsset[] assets;
        uint256[] minAmountsOut;
        bytes userData;
        bool toInternalBalance;
    }

    constructor(Token _balancerPoolToken) {
        balancerPoolToken = _balancerPoolToken;
    }

    function joinPool(bytes32, /*poolId*/ address sender, address recipient, JoinPoolRequest memory request) external {
        for (uint256 i = 0; i < request.assets.length; i++) {
            if (address(request.assets[i]) != address(0)) {
                Token(address(request.assets[i])).transferFrom(sender, address(this), request.maxAmountsIn[i]);
                joinAmounts[address(request.assets[i])] = request.maxAmountsIn[i];
            }
        }
        balancerPoolToken.mint(recipient, 20 ether);
    }

    function exitPool(bytes32, /*poolId*/ address sender, address payable recipient, ExitPoolRequest memory request)
        external
    {
        balancerPoolToken.burn(sender, request.minAmountsOut[0]);
        for (uint256 i = 0; i < request.assets.length; i++) {
            if (address(request.assets[i]) != address(0)) {
                Token(address(request.assets[i])).transfer(recipient, joinAmounts[address(request.assets[i])]);
            }
        }
    }
}
```

</details>


The following is the set-up:

<details>

<summary>Set-Up</summary>

```solidity
function setUp() public {
        vm.startPrank(deployer);

        // Deploy mock tokens
        balancerPoolToken = new Token("Balancer Pool Token", "balancerPoolToken", false);
        asset1 = new Token("Test Token 1", "TT1", true);
        asset2 = new Token("Test Token 2", "TT2", true);
        address contractDeployer = address(new Deployer());
        address oracleFeeds = address(new OracleFeeds());

        address poolBeacon = address(new UpgradeableBeacon(address(new Pool()), governance));
        address bondBeacon = address(new UpgradeableBeacon(address(new BondToken()), governance));
        address levBeacon = address(new UpgradeableBeacon(address(new LeverageToken()), governance));
        address distributorBeacon = address(new UpgradeableBeacon(address(new Distributor()), governance));

        poolFactory = PoolFactory(
            Utils.deploy(
                address(new PoolFactory()),
                abi.encodeCall(
                    PoolFactory.initialize,
                    (governance, contractDeployer, oracleFeeds, poolBeacon, bondBeacon, levBeacon, distributorBeacon)
                )
            )
        );

        params.fee = 2000;
        params.reserveToken = address(balancerPoolToken);
        params.sharesPerToken = 2_500_000;
        params.distributionPeriod = 90 days;
        params.couponToken = address(new Token("USDC", "USDC", false));

        Token(params.couponToken).setDecimals(6);

        OracleFeeds(oracleFeeds).setPriceFeed(params.reserveToken, address(0), ethPriceFeed, 1 days);

        // Deploy the mock price feed
        mockPriceFeed = new MockPriceFeed();

        // Use vm.etch to deploy the mock contract at the specific address
        bytes memory bytecode = address(mockPriceFeed).code;
        vm.etch(ethPriceFeed, bytecode);

        // Set oracle price
        mockPriceFeed = MockPriceFeed(ethPriceFeed);
        mockPriceFeed.setMockPrice(3000 * int256(CHAINLINK_DECIMAL_PRECISION), uint8(CHAINLINK_DECIMAL));

        balancerPoolToken.mint(governance, 1000000 ether);
        vm.stopPrank();

        vm.startPrank(governance);
        poolFactory.grantRole(poolFactory.POOL_ROLE(), governance);

        balancerPoolToken.approve(address(poolFactory), 1000000 ether);

        _pool = Pool(poolFactory.createPool(params, 1000 ether, 25000 ether, 1000 ether, "", "", "", "", false));
        distributor = Distributor(poolFactory.distributors(address(_pool)));
        vm.stopPrank();

        vm.startPrank(deployer);

        // Deploy mock contracts
        vault = new MockBalancerVault(balancerPoolToken);
        predeposit = PreDeposit(
            Utils.deploy(
                address(new PreDeposit()),
                abi.encodeCall(
                    PreDeposit.initialize,
                    (
                        params,
                        address(poolFactory),
                        block.timestamp,
                        block.timestamp + 1 hours,
                        30 ether,
                        "Bond ETH",
                        "bondETH",
                        "Leveraged ETH",
                        "levETH"
                    )
                )
            )
        );
        router = new BalancerRouter(address(vault), address(balancerPoolToken));

        // Setup initial token balances
        asset1.mint(user, 1000 ether);
        asset2.mint(user, 1000 ether);

        asset1.mint(user2, 1000 ether);
        asset2.mint(user2, 1000 ether);

        vm.stopPrank();
    }
```
</details>

And the PoC:

```solidity
function testJoinBalancerAndPredeposit() public {
        vm.startPrank(user);

        assertEq(IERC20(params.reserveToken).balanceOf(address(predeposit)), 0);

        IAsset[] memory assets = new IAsset[](2);
        assets[0] = IAsset(address(asset1));
        assets[1] = IAsset(address(asset2));

        uint256[] memory maxAmountsIn = new uint256[](2);
        maxAmountsIn[0] = 10e18;
        maxAmountsIn[1] = 10e18;

        asset1.approve(address(router), 10 ether);
        asset2.approve(address(router), 10 ether);

        router.joinBalancerAndPredeposit(BALANCER_POOL_ID, address(predeposit), assets, maxAmountsIn, "");
        vm.stopPrank();

        assertEq(IERC20(params.reserveToken).balanceOf(address(router)), 0);

        vm.startPrank(user2);

        maxAmountsIn[0] = 10e18;
        maxAmountsIn[1] = 10e18;

        asset1.approve(address(router), 10 ether);
        asset2.approve(address(router), 10 ether);

        router.joinBalancerAndPredeposit(BALANCER_POOL_ID, address(predeposit), assets, maxAmountsIn, "");
        vm.stopPrank();

        assertEq(IERC20(params.reserveToken).balanceOf(address(router)), 10 ether); // @audit - 10 ether worth of LP tokens are stuck
    }
```

### Mitigation

Check for any excess LP tokens after using the `BalancerRouter::joinBalancerAndPredeposit(...)` function and refund it to the user.