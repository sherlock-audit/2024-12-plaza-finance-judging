Wonderful Candy Manatee

Medium

# `Pool.startAuction()` updates state variable `BondToken.globalPool` incorrectly

## Summary

Whenever auction starts, it updates the state variable `globalPool` of BondToken. However, it updates variable incorrectly and as result, the calculation of coupon tokens is incorrect.

## Root Cause

In the [Pool.sol](https://github.com/sherlock-audit/2024-12-plaza-finance/blob/14a962c52a8f4731bbe4655a2f6d0d85e144c7c2/plaza-evm/src/Pool.sol#L530-L571) :: `startAuction()` function, it updates the state variable in `bondToken` by invoking `bondToken.increaseIndexedAssetPeriod(sharesPerToken)`.

```solidity
  function startAuction() external whenNotPaused() {

    ...

    // Calculate the coupon amount to distribute
    uint256 couponAmountToDistribute = (normalizedTotalSupply * normalizedShares)
        .toBaseUnit(maxDecimals * 2 - IERC20(couponToken).safeDecimals());

    auctions[currentPeriod] = Utils.deploy(
      address(new Auction()),
      abi.encodeWithSelector(
        Auction.initialize.selector,
        address(couponToken),
        address(reserveToken),
        couponAmountToDistribute,
        block.timestamp + auctionPeriod,
        1000,
        address(this),
        poolSaleLimit
      )
    );

    // Increase the bond token period
    bondToken.increaseIndexedAssetPeriod(sharesPerToken);

    // Update last distribution time
    lastDistribution = block.timestamp;
  }
```
In the [BondToken.sol](https://github.com/sherlock-audit/2024-12-plaza-finance/blob/14a962c52a8f4731bbe4655a2f6d0d85e144c7c2/plaza-evm/src/BondToken.sol#L217-L229) :: `increaseIndexedAssetPeriod()` function, it updates the state variable `globalPool`.

It push new struct `PoolAmount` to `globalPool.previousPoolAmounts` setting  `sharesPerToken` as `globalPool.sharesPerToken`. Then update `globalPool.sharesPerToken` with `sharesPerToken`.

This could be correct, while the `sharesPerToken` is not changed. However, there is [Pool.sol](https://github.com/sherlock-audit/2024-12-plaza-finance/blob/14a962c52a8f4731bbe4655a2f6d0d85e144c7c2/plaza-evm/src/Pool.sol#L664-L668) :: `setSharesPerToken()` function and this value could be changed. Also, `globalPool.sharesPerToken` can be only changed while starting auction. 

When acution starts with new `sharesPerToken`, it uses previous value (`globalPool.sharesPerToken`) which is not updated. and as a result, the calculation of coupon tokens which bond holders can claim is incorrect.

```solidity
  function increaseIndexedAssetPeriod(uint256 sharesPerToken) public onlyRole(DISTRIBUTOR_ROLE) whenNotPaused() {
    globalPool.previousPoolAmounts.push(
      PoolAmount({
        period: globalPool.currentPeriod,
        amount: totalSupply(),
@>      sharesPerToken: globalPool.sharesPerToken
      })
    );
    globalPool.currentPeriod++;
@>  globalPool.sharesPerToken = sharesPerToken;

    emit IncreasedAssetPeriod(globalPool.currentPeriod, sharesPerToken);
  }
```

```solidity
  function setSharesPerToken(uint256 _sharesPerToken) external NotInAuction onlyRole(poolFactory.GOV_ROLE()) {
    sharesPerToken = _sharesPerToken;

    emit SharesPerTokenChanged(sharesPerToken);
  }
```

As result, the calculation of coupon tokens which bond holders can claim will be incorrect, because it is based on incorrect value.

```solidity
  function getIndexedUserAmount(address user, uint256 balance, uint256 period) public view returns(uint256) {
    IndexedUserAssets memory userPool = userAssets[user];
    uint256 shares = userPool.indexedAmountShares;

    for (uint256 i = userPool.lastUpdatedPeriod; i < period; i++) {
      shares += (balance * globalPool.previousPoolAmounts[i].sharesPerToken).toBaseUnit(SHARES_DECIMALS);
    }

    return shares;
  }
```


## Internal pre-conditions

When state variable `sharesPerToken` of the pool is modified.

## External pre-conditions


## Attack Path


## Impact

The calculation of coupon tokens which bond holders can claim will be incorrect.

## Mitigation

Update with current value - `sharesPerToken` instead of `globalPool.sharesPerToken`.

```diff
  function increaseIndexedAssetPeriod(uint256 sharesPerToken) public onlyRole(DISTRIBUTOR_ROLE) whenNotPaused() {
    globalPool.previousPoolAmounts.push(
      PoolAmount({
        period: globalPool.currentPeriod,
        amount: totalSupply(),
-       sharesPerToken: globalPool.sharesPerToken
+       sharesPerToken: sharesPerToken
      })
    );
    globalPool.currentPeriod++;
    globalPool.sharesPerToken = sharesPerToken;

    emit IncreasedAssetPeriod(globalPool.currentPeriod, sharesPerToken);
  }
```