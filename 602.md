Itchy Wintergreen Newt

Medium

# Pool's NotInAuction Modifier Checks Wrong Period Making Protected Functions Accessible During Auctions

### Summary

The `NotInAuction` modifier in the Pool contract is ineffective because it checks the wrong auction period, allowing critical parameter changes during active auctions. This occurs because the modifier checks the current period while active auctions are stored in the previous period.

### Root Cause

In `Pool.sol` the `NotInAuction` modifier checks `auctions[currentPeriod]` where `currentPeriod` is fetched from `bondToken.globalPool()`. However, when [starting an auction](https://github.com/sherlock-audit/2024-12-plaza-finance/blob/main/plaza-evm/src/Pool.sol#L530), the period is incremented in [bondToken.increaseIndexedAssetPeriod()](https://github.com/sherlock-audit/2024-12-plaza-finance/blob/main/plaza-evm/src/Pool.sol#L567) as seen in [BondToken:L225](https://github.com/sherlock-audit/2024-12-plaza-finance/blob/main/plaza-evm/src/BondToken.sol#L225), causing the modifier to check the auction in the wrong period.

As a result the NotInAuction modifier will NEVER at ANY time revert because the condition `auctions[currentPeriod] == address(0)` will always be true.
```solidity
modifier NotInAuction() {
    (uint256 currentPeriod,) = bondToken.globalPool();
    // @audit this check below will never revert!
    require(auctions[currentPeriod] == address(0), AuctionIsOngoing());
    _;
}
```

### Internal Pre-conditions

1. An auction must be started via `startAuction()` which:
   - Creates auction at period N
   - Increments period to N+1

### External Pre-conditions

_No response_

### Attack Path

1. An auction is started for period N via `startAuction()`
2. `bondToken.increaseIndexedAssetPeriod()` is called, incrementing period to N+1
3. Attacker calls protected functions like `setDistributionPeriod()`, `setAuctionPeriod()`, or `setSharesPerToken()`
4. The `NotInAuction` modifier checks `auctions[N+1]` which is empty
5. The modifier passes, allowing parameter changes during an active auction

### Impact

Protected functions that should be locked during auctions become accessible, allowing critical parameter changes that could disrupt ongoing auctions:
- `setDistributionPeriod()`
- `setAuctionPeriod()`  
- `setSharesPerToken()`


### PoC

```solidity
// SPDX-License-Identifier: UNLICENSED
pragma solidity ^0.8.26;

import "forge-std/Test.sol";
import {Create3} from "@create3/contracts/Create3.sol";
import {PoolFactory} from "src/PoolFactory.sol";
import {Utils} from "src/lib/Utils.sol";
import {Deployer} from "src/utils/Deployer.sol";
import {BondToken} from "src/BondToken.sol";
import {PoolFactory} from "src/PoolFactory.sol";
import {Distributor} from "src/Distributor.sol";
import {LeverageToken} from "src/LeverageToken.sol";
import {Pool} from "src/Pool.sol";
import {UpgradeableBeacon} from "@openzeppelin/contracts/proxy/beacon/UpgradeableBeacon.sol";
import {IERC20} from "@openzeppelin/contracts/token/ERC20/ERC20.sol";
import {MockERC20} from "./MockERC20.sol";
import {OracleFeeds} from "src/OracleFeeds.sol";
import {MockPriceFeed} from "./mocks/MockPriceFeed.sol";
import {OracleFeeds} from "src/OracleFeeds.sol";
import {Auction} from "src/Auction.sol";

contract MyNotInAuctionTest is Test {
    PoolFactory private poolFactory;
    PoolFactory.PoolParams private params;
    Pool private pool;
    MockPriceFeed private mockPriceFeed;
    OracleFeeds private oracleFeeds;

    address private minter = address(0x2);
    address private governance = address(0x3);
    address private securityCouncil = address(0x4);
    address private alice = address(0x5);
    address private bob = address(0x6);
    address private feeBeneficiary = address(0x7);
    address public constant ethPriceFeed = address(0x71041dddad3595F9CEd3DcCFBe3D1F4b0a16Bb70);
    address public bondPriceFeed = makeAddr("bond_price_feed");
    MockPriceFeed private bondMockPriceFeed;
    uint256 private constant distributionPeriod = 30 days;
    uint256 private constant auctionPeriod = 10 days;

    function setUp() public {
        params.fee = 0;
        params.reserveToken = address(new MockERC20("Wrapped ETH", "WETH"));
        params.distributionPeriod = distributionPeriod;
        params.couponToken = address(new MockERC20("USDC", "USDC"));
        params.sharesPerToken = 1e6;
        params.feeBeneficiary = feeBeneficiary;

        oracleFeeds = new OracleFeeds();
        address contractDeployer = address(new Deployer());

        address poolBeacon = address(new UpgradeableBeacon(address(new Pool()), governance));
        address bondBeacon = address(new UpgradeableBeacon(address(new BondToken()), governance));
        address levBeacon = address(new UpgradeableBeacon(address(new LeverageToken()), governance));
        address distributorBeacon = address(new UpgradeableBeacon(address(new Distributor()), governance));

        poolFactory = PoolFactory(
            Utils.deploy(
                address(new PoolFactory()),
                abi.encodeWithSelector(
                    PoolFactory.initialize.selector,
                    governance,
                    contractDeployer,
                    address(oracleFeeds),
                    poolBeacon,
                    bondBeacon,
                    levBeacon,
                    distributorBeacon
                )
            )
        );

        oracleFeeds.setPriceFeed(params.reserveToken, address(0), ethPriceFeed, 1 days);
        mockPriceFeed = new MockPriceFeed();
        bytes memory bytecode = address(mockPriceFeed).code;
        vm.etch(ethPriceFeed, bytecode);
        mockPriceFeed = MockPriceFeed(ethPriceFeed);

        deal(params.reserveToken, governance, 100e18, true);
        deal(params.reserveToken, alice, 10e18, true);
        deal(params.reserveToken, bob, 10e18, true);
        deal(params.couponToken, alice, 100e18, true);

        vm.startPrank(governance);
        // granting roles
        poolFactory.grantRole(poolFactory.POOL_ROLE(), governance);
        poolFactory.grantRole(poolFactory.SECURITY_COUNCIL_ROLE(), securityCouncil);

        // creating the pool with 100 WETH, and will be minted 10 of bondEth and LevEth each
        IERC20(params.reserveToken).approve(address(poolFactory), 100e18);
        pool = Pool(poolFactory.createPool(params, 100e18, 10e18, 10e18, "Bond ETH", "BE", "Lev ETH", "LE", false));
        vm.stopPrank();
    }

    function testNotInAuction() public {
        vm.prank(governance);
        // governance has GOV_ROLE to set the auctionPeriod
        pool.setAuctionPeriod(auctionPeriod);

        vm.warp(block.timestamp + distributionPeriod + auctionPeriod - 1);
        pool.startAuction();

        (uint256 currentPeriod,) = pool.bondToken().globalPool();
        // this is the address of the auction we started earlier
        address auction = pool.auctions(currentPeriod - 1);

        vm.startPrank(alice);
        IERC20(params.couponToken).approve(address(auction), 5e18);
        Auction(auction).bid(0.05 * 1e18, 5e18);
        vm.stopPrank();
        // the auction is still active going on in its BIDDING state
        assertEq(uint8(Auction(auction).state()), uint8(Auction.State.BIDDING));

        // while auction is going on, the gov can still do the actions below
        // this makes the NotInAuction modifier not effective
        vm.startPrank(governance);
        pool.setDistributionPeriod(5 days);
        pool.setAuctionPeriod(1 days);
        pool.setSharesPerToken(5e6);
        vm.stopPrank();
        // no revert will happen this shows that the NotInAuction was not effective
    }
}
```

### Mitigation

Try to check if the auction has expired instead.
```diff
modifier NotInAuction() {
      (uint256 currentPeriod,) = bondToken.globalPool();
-     require(auctions[currentPeriod] == address(0), AuctionIsOngoing());
+     require(block.timestamp >  Auction(auctions[currentPeriod - 1]).endTime));
        _;
    }
```