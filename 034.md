Shaggy Clay Cobra

High

# Users will lose funds in BalancerRouter when PreDeposit reaches cap

### Summary

Lack of logics to handle unspent funds in the BalancerRouter can cause users to lose funds in the contract

### Root Cause

- The [function `PreDeposit::_deposit()`](https://github.com/sherlock-audit/2024-12-plaza-finance/blob/main/plaza-evm/src/PreDeposit.sol#L118-L126) handles deposits for users. In case the deposit puts more than the available cap, then only the rest up to cap is filled. 
```solidity
  function _deposit(uint256 amount, address onBehalfOf) private checkDepositStarted checkDepositNotEnded {
    if (reserveAmount >= reserveCap) revert DepositCapReached();

    address recipient = onBehalfOf == address(0) ? msg.sender : onBehalfOf;

    // if user would like to put more than available in cap, fill the rest up to cap and add that to reserves
@>    if (reserveAmount + amount >= reserveCap) {
@>      amount = reserveCap - reserveAmount;
    }

    balances[recipient] += amount;
    reserveAmount += amount;

    IERC20(params.reserveToken).safeTransferFrom(msg.sender, address(this), amount);

    emit Deposited(recipient, amount);
  }
```

- The function [`BalancerRouter::joinBalancerAndPredeposit()`](https://github.com/sherlock-audit/2024-12-plaza-finance/blob/main/plaza-evm/src/BalancerRouter.sol#L23-L40) atomically creates Balancer position and then deposits to `PreDeposit` contract using the BPT returned from Balancer. However, there is no logic to handle the case when the amount of BPT is not used fully (when the deposit puts more than the available cap above). As a result, the unspent amount is stuck in the contract
```solidity
    function joinBalancerAndPredeposit(
        bytes32 balancerPoolId,
        address _predeposit,
        IAsset[] memory assets,
        uint256[] memory maxAmountsIn,
        bytes memory userData
    ) external nonReentrant returns (uint256) {
        // Step 1: Join Balancer Pool
@>        uint256 balancerPoolTokenReceived = joinBalancerPool(balancerPoolId, assets, maxAmountsIn, userData);

        // Step 2: Approve balancerPoolToken for PreDeposit
        balancerPoolToken.safeIncreaseAllowance(_predeposit, balancerPoolTokenReceived);

        // Step 3: Deposit to PreDeposit
@>        PreDeposit(_predeposit).deposit(balancerPoolTokenReceived, msg.sender);

        return balancerPoolTokenReceived;
    }
```

### Internal Pre-conditions

_No response_

### External Pre-conditions

_No response_

### Attack Path

_No response_

### Impact

- Users suffer loss of funds. The amount can not be limited

### PoC

Update the test `testJoinBalancerAndPredeposit()` in `BalacerRouter.t.sol` as below:

```solidity
  function testJoinBalancerAndPredeposit() public {
    // @audit PoC: stuck funds in Router when deposit reaches cap
    vm.startPrank(user);

    IAsset[] memory assets = new IAsset[](2);
    assets[0] = IAsset(address(asset1));
    assets[1] = IAsset(address(asset2));

    uint256[] memory maxAmountsIn = new uint256[](2);
    maxAmountsIn[0] = 1 ether;
    maxAmountsIn[1] = 1 ether;

    asset1.approve(address(router), 1 ether);
    asset2.approve(address(router), 1 ether);

    balancerPoolToken.mint(user, 1000000 ether);
    balancerPoolToken.approve(address(predeposit), 1000000 ether);
    predeposit.deposit(predeposit.reserveCap() - predeposit.reserveAmount() - 1);

    uint256 balancerPoolTokenReceived = router.joinBalancerAndPredeposit(
      BALANCER_POOL_ID,
      address(predeposit),
      assets,
      maxAmountsIn,
      ""
    );

    uint routerBPTBalance = balancerPoolToken.balanceOf(address(router));

    assertEq(routerBPTBalance, 0, "stuck funds in the Router");

    vm.stopPrank();
  }
```

Run the test and it failed:
```bash
Failing tests:
Encountered 1 failing test in test/BalancerRouter.t.sol:BalancerRouterTest
[FAIL: stuck funds in the Router: 999999999999999999 != 0] testJoinBalancerAndPredeposit() (gas: 530675)
```

### Mitigation

Consider handle the unspent amount of BPT after deposit