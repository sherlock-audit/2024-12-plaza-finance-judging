Fun Ceramic Mustang

Medium

# Auction related parameters can be modified during an ongoing auction

### Summary

In `Pool.sol` there is a set of admin actions which shouldn't be callable during an ongoing auction. For that reason they have `NotInAuction` modifier. But modifier has an implementation bug and it will let all calls through, even if called during an ongoing auction.

### Root Cause

These are the auction related admin actions:
- [setDistributionPeriod](https://github.com/sherlock-audit/2024-12-plaza-finance/blob/14a962c52a8f4731bbe4655a2f6d0d85e144c7c2/plaza-evm/src/Pool.sol#L642)
- [setAuctionPeriod](https://github.com/sherlock-audit/2024-12-plaza-finance/blob/14a962c52a8f4731bbe4655a2f6d0d85e144c7c2/plaza-evm/src/Pool.sol#L653)
- [setSharesPerToken](https://github.com/sherlock-audit/2024-12-plaza-finance/blob/14a962c52a8f4731bbe4655a2f6d0d85e144c7c2/plaza-evm/src/Pool.sol#L664C12-L664C29)

All of those are protected by [NotInAuction](https://github.com/sherlock-audit/2024-12-plaza-finance/blob/14a962c52a8f4731bbe4655a2f6d0d85e144c7c2/plaza-evm/src/Pool.sol#L750C12-L750C24) modifier. Its implementation contains a bug:
```solidity
  /**
   * @dev Modifier to prevent a function from being called during an ongoing auction.
   */
  modifier NotInAuction() {
    (uint256 currentPeriod,) = bondToken.globalPool();
    require(auctions[currentPeriod] == address(0), AuctionIsOngoing());
    _;
  }
```
 
Period counter `currentPeriod` is increased as soon as new auction is started, [here](https://github.com/sherlock-audit/2024-12-plaza-finance/blob/14a962c52a8f4731bbe4655a2f6d0d85e144c7c2/plaza-evm/src/Pool.sol#L567). For that reason the check in the modifier will always be `true`, because when auction `N` has started, counter is already at `N + 1` and thus looking in the `auctions` array at index `N + 1` will always return zero address. 

### Internal Pre-conditions

No specific internal pre-conditions

### External Pre-conditions

No external pre-conditions

### Attack Path

_No response_

### Impact

Protocol users are supposed to have guarantee that admin (ie. governance) cannot change auction related during an ongoing auction. However, that is not the case because of the implementation error in the `NotInAuction` modifier. For example, new auction can be started while previous one is still ongoing due to the modified `distributionPeriod` and `auctionPeriod`.

### PoC

Here's a complete test that shows the issue:

```solidity
// SPDX-License-Identifier: UNLICENSED
pragma solidity ^0.8.26;

import "forge-std/Test.sol";

import "../src/Pool.sol";
import {Token} from "./mocks/Token.sol";
import {Utils} from "../src/lib/Utils.sol";
import {BondToken} from "../src/BondToken.sol";
import {PoolFactory} from "../src/PoolFactory.sol";
import {Distributor} from "../src/Distributor.sol";
import {OracleFeeds} from "../src/OracleFeeds.sol";
import {LeverageToken} from "../src/LeverageToken.sol";
import {MockPriceFeed} from "./mocks/MockPriceFeed.sol";
import {Deployer} from "../src/utils/Deployer.sol";
import {UpgradeableBeacon} from "@openzeppelin/contracts/proxy/beacon/UpgradeableBeacon.sol";
import "@openzeppelin/contracts/token/ERC20/ERC20.sol";

import "forge-std/console.sol";

contract PoolTest_FeeCollection is Test {
    address deployer = makeAddr("deployer");
    address feeBeneficiary = makeAddr("feeBeneficiary");
    address governance = makeAddr("governance");

    MockPriceFeed mockPriceFeed;

    // 5% fee
    uint256 fee = 50000;

    function test_BrokenModifier() public {
        // create factory
        PoolFactory factory = _createFactory();
        console.log("Factory created");

        // create tokens
        address reserveToken = address(new Token("Wrapped  ETH", " WETH", false));
        address couponToken = address(new Token("USDC", "USDC", false));
        Token(couponToken).setDecimals(6);

        // create pool
        Pool pool = _createPool(factory, reserveToken, couponToken);
        console.log("Pool created");

        // one distribution period has passed
        vm.warp(block.timestamp + pool.getPoolInfo().distributionPeriod + 1);

        // set 10 days auction period
        uint256 auctionPeriod = 10 days;
        vm.prank(governance);
        pool.setAuctionPeriod(auctionPeriod);

        // start auction
        pool.startAuction();

        // change sharesPerToken
        // this function is supposed to be guarded by `NotInAuction` modifier
        vm.prank(governance);
        console.log("Try changing shares per token during the ongoing auction");
        pool.setSharesPerToken(uint256(5453463));
        console.log("sharesPerToken modified, even though it shouldn't be possible");
    }

    function _createFactory() internal returns (PoolFactory) {
        vm.startPrank(deployer);

        // create factory
        address oracleFeedsContract = address(new OracleFeeds());
        PoolFactory factory = PoolFactory(
            Utils.deploy(
                address(new PoolFactory()),
                abi.encodeCall(
                    PoolFactory.initialize,
                    (
                        governance,
                        address(new Deployer()),
                        oracleFeedsContract,
                        address(new UpgradeableBeacon(address(new Pool()), deployer)),
                        address(new UpgradeableBeacon(address(new BondToken()), deployer)),
                        address(new UpgradeableBeacon(address(new LeverageToken()), deployer)),
                        address(new UpgradeableBeacon(address(new Distributor()), deployer))
                    )
                )
            )
        );
        vm.stopPrank();

        vm.startPrank(governance);
        factory.grantRole(factory.POOL_ROLE(), deployer);
        vm.stopPrank();

        return factory;
    }

    function _createPool(PoolFactory factory, address reserveToken, address couponToken) internal returns (Pool) {
        vm.startPrank(deployer);
        uint256 reserveAmount = 1e18;
        deal(reserveToken, deployer, reserveAmount);
        IERC20(reserveToken).approve(address(factory), reserveAmount);

        // create pool
        Pool pool = Pool(
            factory.createPool({
                params: PoolFactory.PoolParams({
                    fee: fee,
                    feeBeneficiary: feeBeneficiary,
                    reserveToken: reserveToken,
                    sharesPerToken: 2_500_000,
                    distributionPeriod: 90 days,
                    couponToken: couponToken
                }),
                reserveAmount: reserveAmount,
                bondAmount: 10 ether,
                leverageAmount: 20 ether,
                bondName: "Bond  WETH",
                bondSymbol: "bond WETH",
                leverageName: "Levered  WETH",
                leverageSymbol: "lev WETH",
                pauseOnCreation: false
            })
        );

        // Deploy the mock price feed
        mockPriceFeed = new MockPriceFeed();
        mockPriceFeed.setMockPrice(3000 * int256(10 ** 8), uint8(8));
        OracleFeeds(factory.oracleFeeds()).setPriceFeed(
            address(pool.reserveToken()), address(0), address(mockPriceFeed), 1 days
        );
        vm.stopPrank();

        return pool;
    }
}
```

Run it:
```solidity
â¯ forge test --mt test_BrokenModifier -vv

[PASS] test_BrokenModifier() (gas: 34373074)
Logs:
  Factory created
  Pool created
  Try changing shares per token during the ongoing auction
  sharesPerToken modified, even though it shouldn't be possible

Suite result: ok. 1 passed; 0 failed; 0 skipped; finished in 17.07ms (8.62ms CPU time)
```

### Mitigation

Change the logic:
```diff
modifier NotInAuction() {
    (uint256 currentPeriod,) = bondToken.globalPool();
-   require(auctions[currentPeriod] == address(0), AuctionIsOngoing());
+   if (currentPeriod > 0) {
+      require(auctions[currentPeriod - 1] == address(0), AuctionIsOngoing());
+   }
    _;
}
```