Damp Jade Hyena

Medium

# It's possible for a whale user to block minting of lToken in the pool

### Summary

Pool contracts are deployed from `predeposit::createPool`, when pool is deployed, some bond and leverage tokens will be minted, and users who pre-deposited will get shares of bond/leverage tokens based on their deposited amount. It's possible for a whale user to DoS the minting of leverage token by pre-depositing large amount and immediately redeeming after pool is redeemed.

### Root Cause

Pools are deployed here in [`createPool`](https://github.com/sherlock-audit/2024-12-plaza-finance/blob/main/plaza-evm/src/PreDeposit.sol#L149):
```solidity
  function createPool() external nonReentrant whenNotPaused checkDepositEnded {
    if (reserveAmount == 0) revert NoReserveAmount();
    if (bondAmount == 0 || leverageAmount == 0) revert InvalidBondOrLeverageAmount();
    if (poolCreated) revert PoolAlreadyCreated();
    IERC20(params.reserveToken).approve(address(factory), reserveAmount);
    pool = factory.createPool(params, reserveAmount, bondAmount, leverageAmount, bondName, bondSymbol, leverageName, leverageSymbol, true);

    emit PoolCreated(pool);
    poolCreated = true;
  }
```

This will mint `bondAmount` and `leverageAmount` to the `Predeposit` contract, and users can `claim` those tokens based on their shares of total pre-deposited amount.
```solidity
  function claim() external nonReentrant whenNotPaused checkDepositEnded {
    if (pool == address(0)) revert ClaimPeriodNotStarted();
    
    uint256 userBalance = balances[msg.sender];
    if (userBalance == 0) revert NothingToClaim();

    address bondToken = address(Pool(pool).bondToken());
    address leverageToken = address(Pool(pool).lToken());

    uint256 userBondShare = (bondAmount * userBalance) / reserveAmount;
    uint256 userLeverageShare = (leverageAmount * userBalance) / reserveAmount;

    balances[msg.sender] = 0;

    if (userBondShare > 0) {
      IERC20(bondToken).safeTransfer(msg.sender, userBondShare);
    }
    if (userLeverageShare > 0) {
      IERC20(leverageToken).safeTransfer(msg.sender, userLeverageShare);
    }

    emit Claimed(msg.sender, userBondShare, userLeverageShare);
  }
```

In the `Pool` contract, we see when leverage tokens are minted, `getCreateAmount` will be called internally to calculate amount to mint. And it reverts when the supply of leverage token is 0:
```solidity
    if (collateralLevel <= COLLATERAL_THRESHOLD) {
      if (tokenType == TokenType.LEVERAGE && assetSupply == 0) {
        revert ZeroLeverageSupply();
      }
      creationRate = (tvl * multiplier) / assetSupply;
    } else if (tokenType == TokenType.LEVERAGE) {
      if (assetSupply == 0) {
        revert ZeroLeverageSupply();
      }
```

We see regardless of the collateral level, if leverage supply is 0, it would revert. Leverage token can only minted by the MINTER role, which is the pool contract, same for burning. When redeeming tokens, there is a division by zero to prevent users from fully redeeming:

```solidity
    uint256 collateralLevel;
    if (tokenType == TokenType.BOND) {
      collateralLevel = ((tvl - (depositAmount * BOND_TARGET_PRICE)) * PRECISION) / ((bondSupply - depositAmount) * BOND_TARGET_PRICE);
    } else {
      multiplier = POINT_TWO;
      assetSupply = levSupply;
      collateralLevel = (tvl * PRECISION) / (bondSupply * BOND_TARGET_PRICE);

      if (assetSupply == 0) {
        revert ZeroLeverageSupply();
      }
    }
```

We see when `bondSupply == depositAmount`, it would be dividing zero, and revert. But it's possible to redeem all leverage supplies, after this, since leverage supply is 0, so no other users can mint leverage token even if it's allowed. So it's possible for a whale user, or some rich attacker to pre-deposit large amount, potentially fulfilling the capped amount, to get himself all the pre-minted bond and leverage tokens, then after the pool is unpaused (the pool is paused after creation due to the flag set in params), this user can redeem all leverage tokens to make supply reach zero, and no other users can mint leverage tokens.

### Internal Pre-conditions

1. Pre-deposit cap is 1000, 100 bond tokens and 100 leverage tokens will be minted for pre-depositors.

### External Pre-conditions

1. Alice pre-deposits 1000 reserve tokens, reaching the cap, for the sake of simplicity we assume admins don't increase cap.
2. The pool is created and Alice gets all 100 bond and leverage tokens.

### Attack Path

1. The GOV role unpauses the pool, the pool is now ready for operations.
2. Alice redeems all leverage tokens, reducing the total supply to 0.
3. Later users cannot mint leverage tokens as the supply is 0, and the transaction reverts.

### Impact

Attacker basically loses nothing as redemption can get his assets back, moreover, holding bond tokens will bring continuous profit for him. The impact of this attack is leverage tokens cannot be minted, breaking the core functionality.

### PoC

_No response_

### Mitigation

It's hard to come up with a sophisticated mitigation suggestion, but the protocol can consider also minting trusted role some dust amount of tokens to prevent such attack.