Refined Ultraviolet Cricket

High

# Funds can be stuck due to `collateralLevel` calculation failling.

### Summary

**Summary**: The possible **underflow** in `Pool.sol::getRedeemAmount()` can block user to withdraw bondToken.

**Note**: if this is a valid issue, please don't report it under my github handler, but with my Watson name **Hurley** or **0xHurley**!

### Root Cause

In [`Pool.sol:498`](https://github.com/sherlock-audit/2024-12-plaza-finance/blob/main/plaza-evm/src/Pool.sol#L498) collateralLevel calculation can revert if `tvl < depositAmount * BOND_TARGET_PRICE`. 

- When a user mint bond and collateralLevel is under 1.2, he will mint bond at a price lower than 100USDC. 
- Then when the same user want to redeem his bond, the collateral level calculation tries to predict what will be the collateral level once the withdraw completed. 
- However, this assume that the bond as been minted at a 100 USDC price, which is not always true. 
- Then in some situation the `depositAmount * BOND_TARGET_PRICE` (corresponding to the total user bond balance) might be higher than the tvl.

- This shouldn't happen as mentionned in the doc "_You can redeem your bondETH tokens for ETH related tokens at any time._" https://docs.plaza.finance/plaza-assets/bondeth-an-ethereum-backed-bond

### Internal Pre-conditions

_No response_

### External Pre-conditions

_No response_

### Attack Path

0. Pre-conditions
  a. 3000 USDC / ETH. 
  b. reserveToken in pool: 1 ETH
  c. bondSupply: 25 ETH.
1. Alice deposit 100 ETH in the pool to create a Bond.
  a. collateralLevel = 3000 / (25 * 100) = 1.2 
  b. creationRate = 3000 * 0.8 / 25 = 96
  c. bondObtained = 100 * 3000 / 96 = 3125 Bond
2. Alice redeem 3125 Bond
  a. tvl = 101 * 3000 = 303000 USDC
  b. depositAmount * BOND_TARGET_PRICE = 3125 * 100 = 312500 USDC > tvl!

### Impact

The user might have a portion of his funds locked. 

### PoC

```solidity
// SPDX-License-Identifier: UNLICENSED
pragma solidity ^0.8.26;

import {Pool} from "../../../src/Pool.sol";
import {Token} from "../../mocks/Token.sol";
import {Utils} from "../../../src/lib/Utils.sol";
import {Deployer} from "../../../src/utils/Deployer.sol";
import {BondToken} from "../../../src/BondToken.sol";
import {OracleFeeds} from "../../../src/OracleFeeds.sol";
import {PoolFactory} from "../../../src/PoolFactory.sol";
import {Distributor} from "../../../src/Distributor.sol";
import {MockPriceFeed} from "../../mocks/MockPriceFeed.sol";
import {LeverageToken} from "../../../src/LeverageToken.sol";
import {UpgradeableBeacon} from "@openzeppelin/contracts/proxy/beacon/UpgradeableBeacon.sol";

abstract contract Setup is Test {
    uint256 public constant ETH_PRICE = 3000;
    uint256 public constant CHAINLINK_DECIMAL = 8;
    uint256 public constant CHAINLINK_DECIMAL_PRECISION = 10 ** 8;
    uint256 public constant INITIAL_LEV_TOKEN = 1 ether;
    uint256 public constant INITIAL_BOND_TOKEN = 25 ether;
    uint256 public constant INITIAL_RESERVE_TOKEN = 1 ether;

    Pool public pool;
    Token public reserveToken;
    Token public couponToken;
    BondToken public bondToken;
    LeverageToken public levToken;
    PoolFactory public poolFactory;

    PoolFactory.PoolParams public params;
    MockPriceFeed public mockPriceFeed;

    address public ethPriceFeed;
    address public oracleFeedsContract;

    mapping(address => string) public names;
    address[] public users;
    address public alice;

    address public governor;
    address public deployer;

    function setUp() public virtual {
        // 1. Setup realistic test environment.
        vm.warp(1000000000);
        vm.roll(1000000000);

        // 2. Create users.
        _createUsers();

        // 3. Deploy contracts.
        _deployContracts();

        // 4. Approvals.
        _approveTokens();

        // 5. Labels.
        _addLabels();
    }

    function _createUsers() internal {
        governor = makeAddr("governor");
        deployer = makeAddr("deployer");

        // EOAs
        alice = makeAddr("alice");

        // Add user to the list
        users.push(alice);

        //
        ethPriceFeed = makeAddr("ethPriceFeed");
    }

    function _deployContracts() internal {
        vm.startPrank(deployer);

        address contractDeployer = address(new Deployer());
        oracleFeedsContract = address(new OracleFeeds());

        address poolBeacon = address(new UpgradeableBeacon(address(new Pool()), governor));
        address bondBeacon = address(new UpgradeableBeacon(address(new BondToken()), governor));
        address levBeacon = address(new UpgradeableBeacon(address(new LeverageToken()), governor));
        address distributorBeacon = address(new UpgradeableBeacon(address(new Distributor()), governor));

        poolFactory = PoolFactory(
            Utils.deploy(
                address(new PoolFactory()),
                abi.encodeCall(
                    PoolFactory.initialize,
                    (
                        governor,
                        contractDeployer,
                        oracleFeedsContract,
                        poolBeacon,
                        bondBeacon,
                        levBeacon,
                        distributorBeacon
                    )
                )
            )
        );

        params.fee = 0;
        params.feeBeneficiary = governor;
        params.reserveToken = address(new Token("Wrapped ETH", "WETH", false));
        params.sharesPerToken = 50 * 10 ** 18;
        params.distributionPeriod = 0;
        params.couponToken = address(new Token("USDC", "USDC", false));

        OracleFeeds(oracleFeedsContract).setPriceFeed(params.reserveToken, address(0), ethPriceFeed, 1 days);

        // Deploy the mock price feed
        mockPriceFeed = new MockPriceFeed();

        // Use vm.etch to deploy the mock contract at the specific address
        bytes memory bytecode = address(mockPriceFeed).code;
        vm.etch(ethPriceFeed, bytecode);

        // Set oracle price
        mockPriceFeed = MockPriceFeed(ethPriceFeed);
        mockPriceFeed.setMockPrice(int256(ETH_PRICE) * int256(CHAINLINK_DECIMAL_PRECISION), uint8(CHAINLINK_DECIMAL));

        vm.stopPrank();

        vm.startPrank(governor);
        poolFactory.grantRole(poolFactory.POOL_ROLE(), governor);
        poolFactory.grantRole(poolFactory.SECURITY_COUNCIL_ROLE(), governor);
        vm.stopPrank();

        reserveToken = Token(params.reserveToken);
        createPool();

        levToken = LeverageToken(pool.lToken());
        bondToken = BondToken(pool.bondToken());
        couponToken = Token(params.couponToken);
        couponToken.setDecimals(6);
    }

    function _approveTokens() internal {
        for (uint256 i; i < users.length; i++) {
            vm.prank(users[i]);
            reserveToken.approve(address(pool), type(uint256).max);
        }
    }

    function _addLabels() internal {
        vm.label(address(reserveToken), "WETH");
        vm.label(address(couponToken), "USDC");
        vm.label(address(bondToken), "bondToken");
        vm.label(address(levToken), "levToken");
    }

    function createPool() public {
        vm.startPrank(governor);
        reserveToken.mint(governor, INITIAL_RESERVE_TOKEN);
        reserveToken.approve(address(poolFactory), INITIAL_RESERVE_TOKEN);

        // Create pool
        pool = Pool(
            poolFactory.createPool(
                params,
                INITIAL_RESERVE_TOKEN,
                INITIAL_BOND_TOKEN,
                INITIAL_LEV_TOKEN,
                "Bond Token",
                "BOND",
                "Leverage Token",
                "LEVE",
                false
            )
        );

        vm.stopPrank();
    }

    function min(uint256 a, uint256 b) internal pure returns (uint256) {
        return a < b ? a : b;
    }

    function max(uint256 a, uint256 b) internal pure returns (uint256) {
        return a > b ? a : b;
    }
}

// SPDX-License-Identifier: MIT
pragma solidity ^0.8.26;

import {console} from "forge-std/Test.sol";
import {Pool} from "../../../src/Pool.sol";
import {TargetFunction} from "./TargetFunction.sol";

contract Unit is TargetFunction {
    function setUp() public override {
        super.setUp();
    }

    function test_unit() public {
        reserveToken.mint(alice, 100 ether);
        vm.prank(alice);
        pool.create(Pool.TokenType.BOND, 100 ether, 0);
        pool.simulateRedeem(Pool.TokenType.BOND, bondToken.balanceOf(alice));
    }
}

```

### Mitigation

Why are we looking at the collateral level? To determine which formula we will use for the creation price. 

In the case of `tvl < depositAmount*BOND_TARGET_PRICE`, we are **sure that the collateralLevel is too low**. So we don't care how low it is, we know that we are in the situation where `collateralLevel <= 1.2`.

So we can return 0 (1 or 1.2 will eith works btw). 

## Current implementation
https://github.com/sherlock-audit/2024-12-plaza-finance/blob/main/plaza-evm/src/Pool.sol#L496-L499


## Proposed mitigation
```solidity
    uint256 collateralLevel;
    if (tokenType == TokenType.BOND) {
      if (tvl < depositAmount * BOND_TARGET_PRICE) {
        collateralLevel = 0;
      } else {
        collateralLevel = ((tvl - (depositAmount * BOND_TARGET_PRICE)) * PRECISION) / ((bondSupply - depositAmount) * BOND_TARGET_PRICE);
      }
    } else {
```