Joyous Paisley Goblin

High

# User funds can be lost during `joinBalancerAndPredeposit()` access

## Summary

Users can participate in the balancer pool by depositing their assets through the `BalancerRouter.joinBalancerAndPredeposit()` function. However, in the `PreDeposit.deposit()` function, there is a check to ensure that the `reserveAmount` does not exceed the `reserveCap`. This oversight can prevent users from fully depositing their desired amount.

When users call `BalancerRouter.joinBalancerAndPredeposit()`, their assets are exchanged for reserve tokens, which are held by the `BalancerRouter`. If not all reserve tokens are used for the deposit, the remaining tokens are not returned to the user due to the lack of proper handling for this scenario.

## Root Cause

In the [BalancerRouter.sol](https://github.com/sherlock-audit/2024-12-plaza-finance/blob/14a962c52a8f4731bbe4655a2f6d0d85e144c7c2/plaza-evm/src/BalancerRouter.sol#L23-L40), the `joinBalancerAndPredeposit()` function allows a user to pre-deposit their assets.

```solidity
    function joinBalancerAndPredeposit(
        bytes32 balancerPoolId,
        address _predeposit,
        IAsset[] memory assets,
        uint256[] memory maxAmountsIn,
        bytes memory userData
    ) external nonReentrant returns (uint256) {
        // Step 1: Join Balancer Pool
        uint256 balancerPoolTokenReceived = joinBalancerPool(balancerPoolId, assets, maxAmountsIn, userData);

        // Step 2: Approve balancerPoolToken for PreDeposit
        balancerPoolToken.safeIncreaseAllowance(_predeposit, balancerPoolTokenReceived);

        // Step 3: Deposit to PreDeposit
        PreDeposit(_predeposit).deposit(balancerPoolTokenReceived, msg.sender);

        return balancerPoolTokenReceived;
    }
```

During this process, the user's assets are converted into `balancerPoolToken` (the reserve token) through the `joinBalancerPool()` call, and the contract holds this token temporarily. After that, it deposits the token into `PreDeposit`.

```solidity
    function joinBalancerPool(
        bytes32 poolId,
        IAsset[] memory assets,
        uint256[] memory maxAmountsIn,
        bytes memory userData
    ) internal returns (uint256) {
        // Transfer assets from user to this contract
        for (uint256 i = 0; i < assets.length; i++) {
            IERC20(address(assets[i])).safeTransferFrom(msg.sender, address(this), maxAmountsIn[i]);
            IERC20(address(assets[i])).safeIncreaseAllowance(address(balancerVault), maxAmountsIn[i]);
        }

        IVault.JoinPoolRequest memory request = IVault.JoinPoolRequest({
            assets: assets,
            maxAmountsIn: maxAmountsIn,
            userData: userData,
            fromInternalBalance: false
        });

        // Join Balancer pool
        uint256 balancerPoolTokenBalanceBefore = balancerPoolToken.balanceOf(address(this));
        balancerVault.joinPool(poolId, address(this), address(this), request);
        uint256 balancerPoolTokenBalanceAfter = balancerPoolToken.balanceOf(address(this));

        return balancerPoolTokenBalanceAfter - balancerPoolTokenBalanceBefore;
    }
```

In the [PreDeposit.sol](https://github.com/sherlock-audit/2024-12-plaza-finance/blob/14a962c52a8f4731bbe4655a2f6d0d85e144c7c2/plaza-evm/src/PreDeposit.sol#L125), the `_deposit()` function checks if the `reserveAmount` has reached the `reserveCap`, only allowing users to deposit the remaining amount up to the limit.

This creates a scenario where the full amount of the user's reserve tokens might not be deposited if they attempt to contribute more than what is available in the cap.

When the entire amount of reserve tokens is not deposited, the leftover reserve tokens are retained by the `BalancerRouter`, leading to a loss of funds for the user.

```solidity
  function _deposit(uint256 amount, address onBehalfOf) private checkDepositStarted checkDepositNotEnded {
@>  if (reserveAmount >= reserveCap) revert DepositCapReached();

    address recipient = onBehalfOf == address(0) ? msg.sender : onBehalfOf;

    // if user would like to put more than available in cap, fill the rest up to cap and add that to reserves
    if (reserveAmount + amount >= reserveCap) {
@>    amount = reserveCap - reserveAmount;
    }

    balances[recipient] += amount;
    reserveAmount += amount;

    IERC20(params.reserveToken).safeTransferFrom(msg.sender, address(this), amount);

    emit Deposited(recipient, amount);
  }
```

## Internal Pre-Conditions

Occurs when the reserve tokens swapped with the user's assets exceed the `reserveCap`.

## External Pre-Conditions


## Attack Path


## Impact

Users may not receive the remaining reserve tokens during the deposit process, resulting in a loss of their funds.

## Mitigation

Implement a mechanism to return the leftover reserve tokens to the user or facilitate swapping assets for only the amount of available reserve tokens that can be deposited.
