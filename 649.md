Furry Walnut Mandrill

High

# Pool fee is unaccounted when Auction settles

### Summary

The pool keeps a % of fee from reserves. This is calculated as below. It checks current reserveToken balance and calculates fee owed to the **feeBeneficiary**.

```solidity
  function getFeeAmount() public view returns (uint256) {
    return (IERC20(reserveToken).balanceOf(address(this)) * fee * (block.timestamp - lastFeeClaimTime)) / (PRECISION * SECONDS_PER_YEAR);
  }
```

 When Auction settles the part of **reserveTokens** as sent to the `Auction.sol` contract. The settlement process doesn't trigger fee distribution before reserveToken transfers.

```solidity
  function transferReserveToAuction(uint256 amount) external virtual {
    (uint256 currentPeriod, ) = bondToken.globalPool();
    address auctionAddress = auctions[currentPeriod];
    require(msg.sender == auctionAddress, CallerIsNotAuction());
    
@=> IERC20(reserveToken).safeTransfer(msg.sender, amount);
  }
```

### Root Cause

- https://github.com/sherlock-audit/2024-12-plaza-finance/blob/main/plaza-evm/src/Pool.sol#L718C3-L720C4
- https://github.com/sherlock-audit/2024-12-plaza-finance/blob/main/plaza-evm/src/Pool.sol#L582

### Internal Pre-conditions

_No response_

### External Pre-conditions

_No response_

### Attack Path

1. Initially users deposit **reservetokens** to `Pool.sol`. The total **reserveTokens** in pool is 100 Ether and total protocol fee is 10 Ether.
2. Auction is initiated and ends. Now Auction pulls 90 Ether of **reserveTokens** from the `Pool.sol` contract.
3. This will result to `Pool.sol` left with **reserveTokens** 10 Ether and total new protocol fee left will be 1 Ether. The protocol fee decreased drastically because it was not claimed.

### Impact

The protocol fee will be cut from 0% to max of 90% as that is **MIN_POOL_SALE_LIMIT**. 

### PoC

Make `getFeeAmount()` public in `plaza-evm/src/Pool.sol`
```solidity
  function getFeeAmount() public view returns (uint256) {
    return (IERC20(reserveToken).balanceOf(address(this)) * fee * (block.timestamp - lastFeeClaimTime)) / (PRECISION * SECONDS_PER_YEAR);
  }

```

Add the follwing changes in `plaza-evm/test/Auction.t.sol`

1. Update `createPool()`

```solidity
  function createPool(address reserve, address coupon) public returns (address) {
    vm.startPrank(governance);
    address deployer = address(new Deployer());
    address oracleFeeds = address(new OracleFeeds());

    address poolBeacon = address(new UpgradeableBeacon(address(new Pool()), governance));
    address bondBeacon = address(new UpgradeableBeacon(address(new BondToken()), governance));
    address levBeacon = address(new UpgradeableBeacon(address(new LeverageToken()), governance));
    address distributorBeacon = address(new UpgradeableBeacon(address(new Distributor()), governance));

    PoolFactory poolFactory = PoolFactory(Utils.deploy(address(new PoolFactory()), abi.encodeCall(
      PoolFactory.initialize, 
      (governance, deployer, oracleFeeds, poolBeacon, bondBeacon, levBeacon, distributorBeacon)
    )));

    PoolFactory.PoolParams memory params;
    params.fee = 50000;
    params.reserveToken = reserve;
    params.sharesPerToken = 2500000;
    params.distributionPeriod = 90 days;
    params.couponToken = coupon;
    
    poolFactory.grantRole(poolFactory.GOV_ROLE(), governance);
    poolFactory.grantRole(poolFactory.POOL_ROLE(), governance);
    poolFactory.grantRole(poolFactory.SECURITY_COUNCIL_ROLE(), securityCouncil);
    
    Token(reserve).mint(governance, 500000000000000000000000000000);
    Token(reserve).approve(address(poolFactory), 500000000000000000000000000000);
    
    return poolFactory.createPool(params, 500000000000000000000000000000, 10000*10**18, 10000*10**18, "Bond ETH", "bondETH", "Leverage ETH", "levETH", false);
  }
```
2. Add test
```solidity
  function testEndAuctionCorruptPoolFee() public {
    vm.startPrank(bidder);
    usdc.mint(bidder, 1000000000000 ether);
    usdc.approve(address(auction), 1000000000000 ether);
    auction.bid(100000000000 ether, 1000000000000);
    vm.stopPrank();

    vm.warp(block.timestamp + 15 days);
    vm.prank(pool);
    console.log("Pool fee before End Auction: ", Pool(pool).getFeeAmount());
    console.log("Reserve Token balance of Pool: ", IERC20(Pool(pool).reserveToken()).balanceOf(pool));
    auction.endAuction();
    console.log("Pool fee after End Auction: ", Pool(pool).getFeeAmount());
    console.log("Reserve Token balance of Pool: ", IERC20(Pool(pool).reserveToken()).balanceOf(pool));

    assertEq(uint256(auction.state()), uint256(Auction.State.SUCCEEDED));
  }
```


## Output
As pool reserve decreased the protocol fee decreased.
```diff
+++  Pool fee before End Auction:  1027397260273972602739726027
+++  Reserve Token balance of Pool:  500000000000000000000000000000
+++  Pool fee after End Auction:  821917808219178082191780821
+++  Reserve Token balance of Pool:  400000000000000000000000000000
```

### Mitigation

Distribute the protocol fee in `Pool.startAuction()` flow or update `Auction.endAuction()` to include fee in reserves calculation and distribute protocol fees in `Pool.transferReserveToAuction()`.