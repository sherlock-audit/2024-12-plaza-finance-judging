Merry Shadow Dinosaur

Medium

# Division by Zero in Collateral Level Calculation for Final Bond Redemption

### Summary

The calculation of `collateralLevel` using `bondSupply - depositAmount` without validating that `bondSupply > depositAmount` in `Pool::getRedeemAmount` will cause a **Denial of Service (DoS)** for the **last bond redeemer** as the contract will **revert due to a division by zero when the final bond holder attempts to redeem their full balance**.

### Root Cause


In the calculation for **`collateralLevel`** when the `tokenType` is `TokenType.BOND`, the formula:

https://github.com/sherlock-audit/2024-12-plaza-finance/blob/14a962c52a8f4731bbe4655a2f6d0d85e144c7c2/plaza-evm/src/Pool.sol#L498

```solidity
collateralLevel = ((tvl - (depositAmount * BOND_TARGET_PRICE)) * PRECISION) / ((bondSupply - depositAmount) * BOND_TARGET_PRICE);
```

can result in a division by zero error **if `bondSupply` equals `depositAmount`**, which occurs when the last user attempts to redeem their entire bond balance. In this scenario, `(bondSupply - depositAmount)` becomes zero, causing the division to revert the transaction and preventing the last redeemer from successfully redeeming their full amount.



### Impact

This issue will result in a **Denial of Service (DoS)** for the **last bond redeemer**, as the contract will revert due to a division by zero if they attempt to redeem their full balance. This effectively prevents the final bond holder from completing the redemption process, leaving their funds locked indefinitely. While the likelihood of this scenario occurring is low, the potential impact could be significant.

### PoC

run in Pool.t.sol

```solidity
  function testLastRedeem_failed() public {
    initializeTestCasesFixedEth();

    vm.startPrank(governance);
    Token rToken = Token(params.reserveToken);

    CalcTestCase memory calcTests = CalcTestCase({
      assetType: Pool.TokenType.LEVERAGE,
      inAmount: 3 ether,
      ethPrice: 3000 * 10**8,
      TotalUnderlyingAssets: 1000000 ether,
      DebtAssets: 25000000 ether,
      LeverageAssets: 1000000 ether,
      expectedCreate: 15000000000000000000,
      expectedRedeem: 600000000000000000,
      expectedSwap: 18750011328131844079
    });

      
      rToken.mint(governance, calcTests.TotalUnderlyingAssets);
      rToken.approve(address(poolFactory), calcTests.TotalUnderlyingAssets);

      // Create pool and approve deposit amount
      // some bondToken are mind to governance
      Pool _pool = Pool(poolFactory.createPool(params, calcTests.TotalUnderlyingAssets, calcTests.DebtAssets, calcTests.LeverageAssets, "", "10", "", "", false));


      address bob = address(100);
      rToken.mint(bob, 10 ether);
      vm.startPrank(bob);
      rToken.approve(address(_pool), 10 ether);
      _pool.create(Pool.TokenType.BOND, 10 ether, 0);
      vm.stopPrank();

      // governance redeem his full amount
      vm.startPrank(governance);
      _pool.redeem(Pool.TokenType.BOND, BondToken(_pool.bondToken()).balanceOf(governance), 0);
      vm.stopPrank();

      
      vm.startPrank(bob);
      uint256 fullBobBalance = BondToken(_pool.bondToken()).balanceOf(bob);
      assertGt(fullBobBalance,1 ether);
      
      // bob can't redeem his full amount because he is the last holder
      vm.expectRevert(stdError.divisionError);
      _pool.redeem(Pool.TokenType.BOND,fullBobBalance , 0);
      vm.stopPrank();

  }
```

### Mitigation

To address this issue, you need to handle the edge case where `bondSupply == depositAmount`. You can introduce a conditional check before performing the division:

```solidity
if (bondSupply == depositAmount) {
    // set an appropriate value
} else {
    collateralLevel = ((tvl - (depositAmount * BOND_TARGET_PRICE)) * PRECISION) / ((bondSupply - depositAmount) * BOND_TARGET_PRICE);
}
```

This will ensure that the last redeemer can successfully redeem their bonds without causing a revert due to division by zero.