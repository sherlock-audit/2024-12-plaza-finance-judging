Crazy Carmine Corgi

High

# BondOracleAdapter#getPool methodology can be abused to cause oracle to reference low liquidity pool

### Summary

When getting the address of the dex pool, BondOracleAdapter#getPool will select the first pool that return from the getPool array. In the event that the main liquidity for the pool is not in the first pool check then a malicious user can create a new pool with very little liquidity and the oracle will utilize that pool instead. Since this pool will have very little liquidity the attacker can manipulate the price to steal funds from withdrawing bond token holders.

[BondOracleAdapter.sol#L48-L64](https://github.com/sherlock-audit/2024-12-plaza-finance/blob/main/plaza-evm/src/BondOracleAdapter.sol#L48-L64)

        function initialize(
            address _bondToken,
            address _liquidityToken,
            uint32 _twapInterval,
            address _dexFactory,
            address _owner
        ) initializer external {
            __Ownable_init(_owner);
            __Pausable_init();
            bondToken = _bondToken;
            liquidityToken = _liquidityToken;
            dexFactory = _dexFactory;
    @>      (dexPool,,) = getPool(bondToken, liquidityToken);

            decimals = IERC20(_bondToken).safeDecimals();
            twapInterval = _twapInterval;
        }

We see above that during initialization that getPool is used to select which pool is used for the oracle TWAP.

[BondOracleAdapter.sol#L120-L138](https://github.com/sherlock-audit/2024-12-plaza-finance/blob/main/plaza-evm/src/BondOracleAdapter.sol#L120-L138)

        function getPool(address tokenA, address tokenB) private view returns (address, uint24, int24) {
            // this only works for Aerodrome, they decided to break compatibility with getPool mapping
    @>      int24[5] memory spacing = [int24(1), int24(50), int24(100), int24(200), int24(2000)];

            for (uint24 i = 0; i < spacing.length; i++) {
    @>      try ICLFactory(dexFactory).getPool(tokenA, tokenB, spacing[i]) returns (address _pool) {
                if (_pool == address(0)) continue;
                
                // Aerodrome CL specific
                (bool success, bytes memory data) = dexFactory.staticcall(abi.encodeWithSignature("tickSpacingToFee(int24)", spacing[i]));
                if (!success) continue;
                
    @>          return (_pool, abi.decode(data, (uint24)), spacing[i]);
            } catch {}
            }

            revert NoPoolFound();
        }

Notice here that we return the first pool that is successfully returned from the factory. Looking at our spacing array this means that the pool with the lowest spacing for the pair will be used. This enabled a malicious user to hijack the oracle by creating a pool with a lower tick spacing than the intended pool. Once the oracle has been compromised they can manipulate the low liquidity to exploit bond holders.

[Pool.sol#L443-L462](https://github.com/sherlock-audit/2024-12-plaza-finance/blob/main/plaza-evm/src/Pool.sol#L443-L462)

            uint256 marketRate;
            address feed = OracleFeeds(oracleFeeds).priceFeeds(address(bondToken), USD);
            if (feed != address(0)) {
    @>      marketRate = getOraclePrice(address(bondToken), USD)
                .normalizeAmount(
                getOracleDecimals(address(bondToken), USD), 
                oracleDecimals // this is the decimals of the reserve token chainlink feed
                );
            }

            return getRedeemAmount(
            tokenType,
            depositAmount,
            bondSupply,
            levSupply,
            poolReserves,
            getOraclePrice(reserveToken, USD),
            oracleDecimals,
    @>      marketRate
            ).normalizeAmount(COMMON_DECIMALS, IERC20(reserveToken).safeDecimals());

[Pool.sol#L477-L525](https://github.com/sherlock-audit/2024-12-plaza-finance/blob/main/plaza-evm/src/Pool.sol#L477-L525)

        function getRedeemAmount(

        ... SNIP

    @>      uint256 marketRate
        ) public pure returns(uint256) {
 
        ... SNIP

    @>      if (marketRate != 0 && marketRate < redeemRate) {
                redeemRate = marketRate;
            }
            
            // Calculate and return the final redeem amount
            return ((depositAmount * redeemRate).fromBaseUnit(oracleDecimals) / ethPrice) / PRECISION;
        }

Looking at the above snippets we see that redeem rate for bond tokens are capped at the current market price returned by the oracle. By manipulating the oracle the attacker can cause any number of issues to occur, almost all of which cause loss to other users.

### Root Cause

[BondOracleAdapter#getPool](https://github.com/sherlock-audit/2024-12-plaza-finance/blob/main/plaza-evm/src/BondOracleAdapter.sol#L120-L135) returns the first pool found rather than allowing the fee to be specified.

### Internal preconditions

None

### External preconditions

Token liquidity is not in the first pool check

### Attack Path

1. Bond token is created and liquidity accumulates on the 100 spacing pair
2. Just before the oracle is added a malicious user create a pool with the 1 spacing
3. BondOracleAdapter is created and automatically selects the 1 tick pool 
4. The 1 tick pool is very low liquidity allowing the attacker to manipulate the price very low
5. Users withdraw bond tokens and are only paid the manipulated rate causing massive loss

### Impact

Potential impacts:
1. Loss of funds to withdrawing users
2. Users are forced to sell to liquidity pool at a loss because they can't withdraw at the proper price
3. Attacker can bypass market price control on bond price (i.e. it is currently $90 on secondary markets but they can withdraw at $100) causing loss to the pool

### POC

N/A

### Mitigation

BondOracleAdapter should allow the caller to specify which spacing to be used when selecting the pool