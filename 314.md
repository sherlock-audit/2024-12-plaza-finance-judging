Dazzling Goldenrod Orangutan

Medium

# Unclaimed bonds will lead to stuck rewards in `Predeposit` contracts

### Summary

A `PreDeposit` contract is designed to raise funds for creating bond and leverage tokens associated with reserve assets. After`PreDeposit.depositEndtimes`, users will be able to claim these tokens. However, if a user decides not to or cannot claim the tokens before the end of the first period, they will lose the bond-associated coupon tokens. Since these tokens cannot be withdrawn from the `PreDeposit` contract, they will simply remain stuck there.

### Root Cause

https://github.com/sherlock-audit/2024-12-plaza-finance/blob/main/plaza-evm/src/PreDeposit.sol#L163-L185

### Internal Pre-conditions

_No response_

### External Pre-conditions

_No response_

### Attack Path

_No response_

### Impact

**MEDIUM**
Loss of yield (coupon tokens) due to late claims from the `PreDeposit` contract. Consider that period durations are updatable; therefore, this could become a much more serious concern in the future.

### PoC

This POC relays on recommended mitigation for issue: `Auction.endAuction()` will always revert, locking gathered funds


```solidity
// SPDX-License-Identifier: UNLICENSED
pragma solidity ^0.8.13;

import {Test, console} from "forge-std/Test.sol";
import {Pool} from "../src/Pool.sol";
import {PreDeposit} from "../src/PreDeposit.sol";
import {Token} from "./mocks/Token.sol";
import {Utils} from "../src/lib/Utils.sol";
import {Auction} from "../src/Auction.sol";
import {MockPool} from "./mocks/MockPool.sol";
import {MockPriceFeed} from "./mocks/MockPriceFeed.sol";
import {BondToken} from "../src/BondToken.sol";
import {LeverageToken} from "../src/LeverageToken.sol";
import {PoolFactory} from "../src/PoolFactory.sol";
import {Distributor} from "../src/Distributor.sol";
import {OracleFeeds} from "../src/OracleFeeds.sol";
import {LeverageToken} from "../src/LeverageToken.sol";
import {Deployer} from "../src/utils/Deployer.sol";
import {IERC20} from "@openzeppelin/contracts/token/ERC20/IERC20.sol";
import {UpgradeableBeacon} from "@openzeppelin/contracts/proxy/beacon/UpgradeableBeacon.sol";

struct Implementations{
    PoolFactory poolFactoryImpl;
    Pool poolImpl;
    BondToken bondTokenImpl;
    LeverageToken leverageTokenImpl;
    Distributor distributorImpl;
    PreDeposit preDepositImpl;
}

uint8 constant ORACLE_DECIMALS = 8;
uint256 constant SHARES_PER_TOKEN = 2_500_000;
uint256 constant DISTRIBUTION_PERIOD = 90 days; // 3 months aprox
uint256 constant AUCTION_PERIOD = 10 days;
uint256 constant RESERVE_CAP = 100 * 1e18;
uint256 constant BOND_AMOUNT = 50 * 4_000 * 1e18 / 100; // 80% backup bonds valued at 100 USD each one
uint256 constant LEV_AMOUNT = 50 * 1e18; // 20% backup lev ETH
uint256 constant EXPECTED_PERIOD_YIELD = 0.25 * 10e18; // 1% anual


// This POC relays on recommended mitgation for issue: `Auction.endAuction()` will always revert, locking gathered funds
contract LoseOfCouponsForLateClaimPOC is Test {
    Token USDC;
    Token WETH;

    address GOVERNANCE          = address(bytes20(bytes("GOVERNANCE")));
    address SECURITY_COUNCIL    = address(bytes20(bytes("SECURITY_COUNCIL")));
    address CHAIN_MINTER        = address(bytes20(bytes("CHAIN_MINTER"))); // mints USDC and WETH
    address FEE_BENEFICIARY     = address(bytes20(bytes("FEE_BENEFICIARY"))); // mints USDC and WETH
    
    uint256 PREDEPOSIT_START_TIME = block.timestamp + 1 days;
    uint256 PREDEPOSIT_END_TIME = PREDEPOSIT_START_TIME + 7 days;

    address[] users;
    address[] bidders;


    // implementations
    Implementations implementations;
    Pool pool;
    Auction lastAuction;
    PoolFactory poolFactory;
    OracleFeeds oracleFeeds;
    BondToken bondToken;
    LeverageToken leverageToken;
    Deployer deployer;
    IERC20 token;
    PreDeposit preDeposit;
    MockPriceFeed ETH_USDC_ORACLE;

    PoolFactory.PoolParams _poolParams;


    function _setupBidders() private{
        bidders.push(address(bytes20(bytes("BIDDER_0"))));
        bidders.push(address(bytes20(bytes("BIDDER_1"))));
        bidders.push(address(bytes20(bytes("BIDDER_2"))));
        bidders.push(address(bytes20(bytes("BIDDER_3"))));
        bidders.push(address(bytes20(bytes("BIDDER_4"))));
        bidders.push(address(bytes20(bytes("BIDDER_5"))));
        bidders.push(address(bytes20(bytes("BIDDER_6"))));
        bidders.push(address(bytes20(bytes("BIDDER_7"))));
        bidders.push(address(bytes20(bytes("BIDDER_8"))));
        bidders.push(address(bytes20(bytes("BIDDER_9"))));
    }

    function _setupImplementations() private {
        vm.label(GOVERNANCE, "GOVERNANCE");
        vm.label(SECURITY_COUNCIL, "SECURITY_COUNCIL");
        vm.label(CHAIN_MINTER, "CHAIN_MINTER");

        implementations.poolFactoryImpl = new PoolFactory();
        implementations.poolImpl = Pool(address(new UpgradeableBeacon(address(new Pool()), GOVERNANCE)));
        implementations.bondTokenImpl = BondToken(address(new UpgradeableBeacon(address(new BondToken()), GOVERNANCE)));
        implementations.leverageTokenImpl = LeverageToken(address(new UpgradeableBeacon(address(new LeverageToken()), GOVERNANCE)));
        implementations.distributorImpl = Distributor(address(new UpgradeableBeacon(address(new Distributor()), GOVERNANCE)));
        implementations.preDepositImpl = new PreDeposit();

        deployer = new Deployer();
        ETH_USDC_ORACLE = new MockPriceFeed();
        USDC = new Token("USDC", "USDC", false);
        USDC.setDecimals(6);
        WETH = new Token("WETH", "WETH", false);
        
        ETH_USDC_ORACLE = new MockPriceFeed();
        ETH_USDC_ORACLE.setMockPrice(int256(3000 * 10**ORACLE_DECIMALS), ORACLE_DECIMALS); // 

        vm.prank(GOVERNANCE);
        oracleFeeds = new OracleFeeds();

        _poolParams.fee = 0;
        _poolParams.reserveToken = address(WETH);
        _poolParams.couponToken = address(USDC);
        _poolParams.sharesPerToken = SHARES_PER_TOKEN;
        _poolParams.distributionPeriod = DISTRIBUTION_PERIOD;
        _poolParams.feeBeneficiary = FEE_BENEFICIARY;
        

        vm.label(address(oracleFeeds), "OracleFeeds");
        vm.label(address(implementations.poolFactoryImpl), "PoolFactory_Impl");
        vm.label(address(implementations.poolImpl), "Pool_Impl");
        vm.label(address(implementations.bondTokenImpl), "BondToken_Impl");
        vm.label(address(implementations.leverageTokenImpl), "LeverageToken_Impl");
        vm.label(address(implementations.distributorImpl), "Distributor_Impl");
        vm.label(address(deployer), "Deployer");
        vm.label(address(ETH_USDC_ORACLE), "ETH_USDC_ORACLE");
        vm.label(address(USDC), "USDC");
        vm.label(address(WETH), "WETH");
    }

    function _setupInstances() private{
        poolFactory = PoolFactory(Utils.deploy(
            address(implementations.poolFactoryImpl),
            abi.encodeCall(
                PoolFactory.initialize,
                (
                    GOVERNANCE,
                    address(deployer),
                    address(oracleFeeds),
                    address(implementations.poolImpl),
                    address(implementations.bondTokenImpl),
                    address(implementations.leverageTokenImpl),
                    address(implementations.distributorImpl)
                )
            )
        ));
        vm.label(address(poolFactory), "PoolFactory");

        preDeposit = PreDeposit(Utils.deploy(
            address(implementations.preDepositImpl),
            abi.encodeCall(
                PreDeposit.initialize,
                (
                    _poolParams,
                    address(poolFactory),
                    PREDEPOSIT_START_TIME,
                    PREDEPOSIT_END_TIME,
                    RESERVE_CAP,
                    "bondETH",
                    "bondETH",
                    "levETH",
                    "levETH"
                )
            )
        )); 
        preDeposit.setBondAndLeverageAmount(BOND_AMOUNT, LEV_AMOUNT);

    }

    function _setupUsers() private{
        users.push(address(bytes20(bytes("USER_0"))));
        users.push(address(bytes20(bytes("USER_1"))));
        users.push(address(bytes20(bytes("USER_2"))));
        users.push(address(bytes20(bytes("USER_3"))));
        users.push(address(bytes20(bytes("USER_4"))));
        users.push(address(bytes20(bytes("USER_5"))));
        users.push(address(bytes20(bytes("USER_6"))));
        users.push(address(bytes20(bytes("USER_7"))));
        users.push(address(bytes20(bytes("USER_8"))));
        users.push(address(bytes20(bytes("USER_9"))));
    }

    function setUp() public {
        _setupUsers();
        _setupBidders();
        _setupImplementations();
        _setupInstances();
    }

    function _getLastAuction() private returns(Auction){
        (uint256 currentPeriod, ) = bondToken.globalPool();
        return Auction(pool.auctions( currentPeriod - 1));
    }

    function step_startPredeposit() public {
        vm.warp(PREDEPOSIT_START_TIME);
    }

    function step_fullfillPredeposit() public {
        uint256 defaultDepositAmount = RESERVE_CAP / users.length;
        uint256 remainingToFullfillCap = RESERVE_CAP;

        uint256 depositAmount;


        for(uint i; i < users.length; i++){
            if (i == users.length - 1 ){
                depositAmount = remainingToFullfillCap;
            } else {
                depositAmount = defaultDepositAmount;
                remainingToFullfillCap -= depositAmount;
            }

            WETH.mint(users[i], depositAmount);

            vm.startPrank(users[i]);
            WETH.approve(address(preDeposit), depositAmount);
            preDeposit.deposit(depositAmount);
            vm.stopPrank();
        }
    }

    function step_endPredeposit() public {
        
        vm.startPrank(GOVERNANCE);
        poolFactory.grantRole(poolFactory.POOL_ROLE(),address(preDeposit));
        poolFactory.grantRole(poolFactory.SECURITY_COUNCIL_ROLE(),SECURITY_COUNCIL);
        vm.stopPrank();

        vm.warp(PREDEPOSIT_END_TIME);
        preDeposit.createPool();

        pool = Pool(preDeposit.pool());
        vm.prank(SECURITY_COUNCIL);
        pool.unpause();
        

        // Get bond, leverage and pool instances
        pool = Pool(preDeposit.pool());
        bondToken = pool.bondToken();
        leverageToken = pool.lToken();

        // Set auction period
        vm.prank(GOVERNANCE);
        pool.setAuctionPeriod(AUCTION_PERIOD);
    }



    function step_startAuction() private {
        Pool.PoolInfo memory poolInfo = pool.getPoolInfo();
        uint256 lastDistributionPeriod = poolInfo.lastDistribution;
        uint256 distributionPeriod = poolInfo.distributionPeriod;

        // Time pass and we create an auction
        uint256 auctionStartTime = lastDistributionPeriod + distributionPeriod + 1;
        vm.warp(auctionStartTime);
        pool.startAuction();
        lastAuction = _getLastAuction();
    }

    function step_completeAuction() private {
        address bidder0 = bidders[0];
        uint256 auctionAmount = lastAuction.totalBuyCouponAmount();
        USDC.mint(bidder0, auctionAmount);
        

        vm.startPrank(bidder0);
        USDC.approve(address(lastAuction), auctionAmount);
        lastAuction.bid(1 ether,auctionAmount);
        vm.stopPrank();

        vm.warp(lastAuction.endTime());

        lastAuction.endAuction();
    }

    
    
    function test_poc_rewardsForNotClaimedBondsAreLost() public {
        step_startPredeposit();
        step_fullfillPredeposit();
        step_endPredeposit();
        pool = Pool(preDeposit.pool());

        uint256 snapshot = vm.snapshot();

        vm.prank(users[0]);
        preDeposit.claim();

        step_startAuction();
        step_completeAuction();


        (uint256 currentPeriod, ) = bondToken.globalPool();
        uint256 rewardsFromFirstAuctionForClaimBeforeAuctionStarted = bondToken.getIndexedUserAmount(
            users[0],
            bondToken.balanceOf(users[0]),
            currentPeriod
        );

        vm.revertTo(snapshot);

        // First auction
        step_startAuction();
        step_completeAuction();

        // get coupon tokens before claim
        (uint256 currentPeriodAux, ) = bondToken.globalPool();

        vm.prank(users[0]);
        preDeposit.claim();
        uint256 rewardsFromFirstAuctionForClaimAfterAuctionStarted = bondToken.getIndexedUserAmount(
            users[0],
            bondToken.balanceOf(users[0]),
            currentPeriodAux
        );

        require(currentPeriod == currentPeriodAux, "Period mismatch");
        require(rewardsFromFirstAuctionForClaimBeforeAuctionStarted == rewardsFromFirstAuctionForClaimAfterAuctionStarted, "Reward difference because of claiming after an auction started");
    }
}
```


### Mitigation

Add a state variable which keep track the amount of coupon tokens per bond token that have been claimed so far, and when calling claim function transfer the corresponding amount

```diff
// PreDeposit.sol 
+       uint256 remainingCouponRewardsDueToClaims;

        function claim() external nonReentrant whenNotPaused checkDepositEnded {
            if (pool == address(0)) revert ClaimPeriodNotStarted();
            
            uint256 userBalance = balances[msg.sender];
            if (userBalance == 0) revert NothingToClaim();

            address bondToken = address(Pool(pool).bondToken());
            address leverageToken = address(Pool(pool).lToken());

            uint256 userBondShare = (bondAmount * userBalance) / reserveAmount;
            uint256 userLeverageShare = (leverageAmount * userBalance) / reserveAmount;

            balances[msg.sender] = 0;

            if (userBondShare > 0) {
+               uint256 couponsToClaimByPredeposit = bondToken.getIndexedUserAmount(bondToken.globalPool().currentPeriod) 
+               if(couponsToClaimByPredeposit >= 0){ // if 0 no need to claim nothing
+                   remainingCouponRewardsDueToClaims += couponsToClaimByPredeposit; // ! This might require handling decimals, this solution assumes that shares and coupon tokens are in same units
+               }
+               couponsToClaim = userBondShare * accruedCouponRewards / bondToken.balanceOf(this);

                IERC20(bondToken).safeTransfer(msg.sender, userBondShare);
                
+               if (couponsToClaim != 0){
+                   remainingCouponRewardsDueToClaims -= couponsToClaim;
+                   IERC20(params.couponToken).safeTransfer(msg.sender, couponsToClaim);
+                   // Would be nice to add an event emission here about this claim or modify current Claim event
+               }
            }
            // ..
        }
```